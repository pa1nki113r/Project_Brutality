//All Map
ACTOR NewAllmap : Allmap Replaces Allmap
{
	Inventory.PickUpSound "misc/map_PickUp"
	+FLOORCLIP
	+DONTGIB
	+COUNTITEM
}

// Invulnerability Sphere ---------------------------------------------------

ACTOR InvulnerabilitySphere_  : PowerupGiver Replaces InvulnerabilitySphere
{
	Game Doom
	SpawnID 133
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.BIGPOWERUP
	+FloatBob
	+DONTGIB
	Inventory.MaxAmount 0
	Powerup.Type Invulnerable
	Powerup.Color White //InverseMap
	Inventory.PickupMessage "$GOTINVUL"
	Inventory.PickupSound "INVUL"
	States
	{
	Spawn:
		PINV AAABBBCCCDDD 2 Bright A_SpawnItem("GreenFlareMedium",0,35)
		Loop
	Pickup:
        TNT1 AAAAAAAAAAAAA 0 A_CustomMissile ("ShinnySmoke", 0, 0, random (0, 360), 2, random (0, 360))
		Stop
	}
}

// Soulsphere --------------------------------------------------------------

ACTOR LifeSphere : CustomInventory Replaces SoulSphere
{
	//$Title Life Sphere
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+DONTGIB
	+FloatBob
	Inventory.PickupMessage "Soulsphere"
	Inventory.PickupSound "SSPH"
	States
	{
	Spawn:
		TNT1 A 0 A_CheckSight("Spawn2")
		SOUL AAABBBCCCDDDCCCBBB 2 Bright A_SpawnItem("BlueFlareMedium2",0,30)
		Loop
	Spawn2:
		SOUL ABCDCB 2 BRIGHT
		Goto Spawn
	Pickup:
        TNT1 AAAAAAAAAAAAA 0 A_CustomMissile ("ShinnySmoke", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_GiveInventory("SoulSphereHealth", 1)
		Stop
	}
}

ACTOR SoulSphereHealth : Health	
{
	Inventory.Amount 100
	Inventory.MaxAmount 200
	+INVENTORY.ALWAYSPICKUP
}

ACTOR EnergySphere : CustomInventory Replaces Megasphere
{
	//$Title Energy Sphere
	+COUNTITEM
	+DONTGIB
	+FloatBob
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage "Megasphere"
	Inventory.PickupSound "MEGASPH"
	States
	{
	Spawn:
		TNT1 A 0 A_CheckSight("Spawn2")
		MEGA AABBCCDD 2 BRIGHT A_SpawnItem("LensFlare",0,35)
		Loop
	Spawn2:	
		MEGA AABBCCDD 2 BRIGHT
		Goto Spawn
	Pickup:
        TNT1 AAAAAAAAAAAAA 0 A_CustomMissile ("ShinnySmoke", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_GiveInventory("HeavyArmorForMegaSphere", 1)
		TNT1 AA 0 A_GiveInventory("SoulsphereHealth", 1)
		Stop
	}
}	


ACTOR NightVision : Infrared Replaces Infrared
{
	//$Title Night Vision Goggles
	Inventory.PickUpSound "RA1IF1"
	+FLOORCLIP
	+COUNTITEM
	+DONTGIB
	+Inventory.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Inventory.MaxAmount 0
	Powerup.Type LightAmp
	Inventory.PickUpMessage "$GOTVISOR"
	States
	{
	Spawn:
		PVIS AAAAA 2 BRIGHT A_SpawnItem("RedFlareVerySmall",0,10)
		PVIS B 10
		Loop
	}
}

Actor HasInfrared : Inventory
{
inventory.maxamount 1
}

ACTOR NormalBerserk : CustomInventory //Replaces Berserk
{
	Game Doom
    Scale 1.0
	SpawnID 134
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+DONTGIB
	Inventory.PickupMessage "$GOTBERSERK"
    Inventory.PickupSound "BERSPKUP"
	States
	{
	Spawn:
		PSTR A 2 
		Loop
	Pickup:
		TNT1 A 0 A_GiveInventory("PowerStrength")
		TNT1 A 0 HealThing(100, 0)
		TNT1 A 20
		TNT1 A 0 ACS_NamedExecute("BerserkPrompt",0)
		Stop
	}
}

ACTOR NewBlurSphere: BlurSphere
{
Inventory.pickupsound "INVISIBL"
+Floatbob
}

//From AGITATION.dec
//This was copied to prevent an issue with ledge grabbing removing invulnerability
//from the Invulnerability power-up.
ACTOR PowerInvulnerable2 : PowerInvulnerable
{
  Powerup.Duration -10
}