ACTOR NewRocketBox : CustomInventory
{
	Game Doom
	Scale 1.0
	-COUNTITEM
	+DONTGIB
	+FLOORCLIP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteAlways("SmartScavengerAmmoScript",0)
		Deciding:
			TNT1 A 1
			Loop
		SpawnNormal:
			TNT1 A 1 A_SpawnItemEx("NormalRocketBox",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
		SpawnSmartScav:
			TNT1 A 1 A_SpawnItemEx("SmartRocketBox",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
	}
}

ACTOR SmartRocketBox : SwitchableDecoration
{
	+LOOKALLAROUND
	+USESPECIAL
	+DONTGIB
	+FLOORCLIP
	Activation THINGSPEC_Activate
	Radius 5
	States
	{
	Spawn:
		BR0K A 1 A_Look
		Loop
	Pickup:
		BR0K A 1
		Fail
	See:
		BR0K A 1 A_JumpIfCloser(36,"CheckPlayerPack")
		Loop
	CheckPlayerPack:
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack_h",1,"CheckSkill_Pack_h")
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack",1,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("RocketAmmo",0,"FullFail")
		Goto CheckSkill_NoPack
	CheckSkill_Pack:
		TNT1 A 0 A_JumpIfInTargetInventory("RocketAmmo",100,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("RocketAmmo",95,"SpawnRockets")
		Goto SpawnBox
	CheckSkill_NoPack:
		TNT1 A 0 A_JumpIfInTargetInventory("RocketAmmo",50,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("RocketAmmo",45,"SpawnRockets")
		Goto SpawnBox
	CheckSkill_Pack_h:
		TNT1 A 0 A_JumpIfInTargetInventory("RocketAmmo",125,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("RocketAmmo",120,"SpawnRockets")
		Goto SpawnBox
	SpawnBox:
		TNT1 A 0 A_SpawnItemEx("NormalRocketBox_SS")
		Stop
	Active:
	SpawnRockets:
		TNT1 A 0 A_ChangeFlag("USESPECIAL", 0)
		BR0K B 15 A_StartSound("Ammocase/Open")
		BR0K C 10 A_SpawnItemEx("NewRocketAmmo",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		BR0K D 10 A_SpawnItemEx("NewRocketAmmo",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		BR0K E 10 A_SpawnItemEx("NewRocketAmmo",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		BR0K F 10 A_SpawnItemEx("NewRocketAmmo",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		BR0K G 10 A_SpawnItemEx("NewRocketAmmo",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		BR0K G 10
		TNT1 A 0 ACS_NamedExecuteAlways("SmartScavContainerStay",0)
		BR0K A 70
	 FadeOut:
		BR0K A 5 A_FadeOut(0.1,1)
		Loop
	 RemainOpen:
		BR0K G -1
		Stop
	FullFail:
		BR0K A 1
		Goto See
	}
}

ACTOR NewRocketAmmo : RocketAmmo
{
	Inventory.PickupMessage "Picked up an explosive round."
	Inventory.PickUpSound "misc/rocket_PickUp"
	+FLOORCLIP
	+DONTGIB
	Scale 0.45
	States
	{
		Spawn:
			R0CK A -1
			Stop
	}
}

ACTOR NormalRocketBox_SS : CustomInventory
{
	Inventory.PickupMessage "Picked up a Box of Explosive Ordinance."
	Inventory.PickupSound "misc/rockboxa"
	Inventory.Amount 5
	+DONTGIB
	+FLOORCLIP
	States
	{
	Spawn:
		BR0K A -1
		Stop
	Pickup:
		TNT1 A 0 A_GiveInventory("NewRocketAmmo",6) 		
		TNT1 A 20
		Stop
	}
}

ACTOR NormalRocketBox : CustomInventory
{
	Inventory.PickupMessage "Picked up a Box of Explosive Ordinance."
	Inventory.PickupSound "misc/rockboxa"
	Inventory.Amount 5
	+DONTGIB
	+FLOORCLIP
	States
	{
	Spawn:
		BROK A -1
		Stop
	Pickup:
		TNT1 A 0 A_GiveInventory("NewRocketAmmo",6) 		
		TNT1 A 20
		Stop
	}
}