ACTOR NewClipBox : CustomInventory
{
	Game Doom
	Scale 1.0
	-COUNTITEM
	+DONTGIB
	+FLOORCLIP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteAlways("SmartScavengerAmmoScript",0)
		Deciding:
			TNT1 A 1
			Loop
		SpawnNormal:
			TNT1 A 1 A_SpawnItemEx("NormalClipBox",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
		SpawnSmartScav:
			TNT1 A 1 A_SpawnItemEx("SmartClipBox",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
	}
}

ACTOR SmartClipBox : SwitchableDecoration
{
    Scale 0.75
	+LOOKALLAROUND
	+USESPECIAL
	+DONTGIB
	+FLOORCLIP
	Activation THINGSPEC_Activate
	Radius 5
	States
	{
	Spawn:
		AMM0 A 1 A_Look
		Loop
	Pickup:
		AMM0 A 1
		Fail
	See:
		AMM0 A 1 A_JumpIfCloser(36,"CheckPlayerPack")
		Loop
	CheckPlayerPack:
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack_h",1,"CheckSkill_Pack_h")
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack",1,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("NewClip",0,"FullFail")
		Goto CheckSkill_NoPack
	CheckSkill_Pack:
		TNT1 A 0 A_JumpIfInTargetInventory("NewClip",600,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("NewClip",575,"SpawnClips")
		Goto SpawnBox
	CheckSkill_NoPack:
		TNT1 A 0 A_JumpIfInTargetInventory("NewClip",301,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("NewClip",275,"SpawnClips")
		Goto SpawnBox 
	CheckSkill_Pack_h:
		TNT1 A 0 A_JumpIfInTargetInventory("NewClip",800,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("NewClip",775,"SpawnClips")
		Goto SpawnBox
	SpawnBox:
		TNT1 A 0 A_SpawnItemEx("NormalClipBox2")
		Stop
	Active:
	SpawnClips:
		TNT1 A 0 A_ChangeFlag("USESPECIAL", 0)
		AMM0 B 15 A_StartSound("Ammocase/Open")
		AMM0 C 10 A_SpawnItemEx("NewClip",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		AMM0 D 10 A_SpawnItemEx("NewClip",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		AMM0 E 10 A_SpawnItemEx("NewClip",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		AMM0 F 10 A_SpawnItemEx("NewClip",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		AMM0 G 10 A_SpawnItemEx("NewClip",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		AMM0 G 10
		TNT1 A 0 ACS_NamedExecuteAlways("SmartScavContainerStay",0)
		AMM0 H 70
	 FadeOut:
		AMM0 H 5 A_FadeOut(0.1,1)
		Loop
	RemainOpen:
		AMM0 G -1
		Stop
	FullFail:
		AMM0 A 1
		Goto See
	}
}

ACTOR NewClip : Ammo //Replaces Clip
{
    Game Doom
	Scale 0.75
	+DONTGIB
	+FLOORCLIP
	Inventory.PickUpMessage "Picked up a High Caliber Magazine"
	Inventory.PickUpSound "misc/clip_PickUp"
	Inventory.Amount 10
    Inventory.MaxAmount 300
    Ammo.BackpackAmount 10
    Ammo.BackpackMaxAmount 600
    Inventory.Icon "CLIPA0"
    States
    {
    Spawn:
      CLIP A -1
      Stop
	}
}

ACTOR NormalClipBox2 : NewClip
{
	Scale 0.75
	Inventory.PickupMessage "Picked up a Box of High Caliber Bullets."
	Inventory.PickupSound "CBOXPKUP"
	Inventory.Amount 50
    Inventory.MaxAmount 300
    Ammo.BackpackAmount 50
    Ammo.BackpackMaxAmount 600
	Inventory.Icon "CLIPA0"
	+DONTGIB
	+FLOORCLIP
	States
	{
	Spawn:
		AMM0 A -1 
		Stop
	}
}

ACTOR NormalClipBox : NewClip
{
	Scale 0.75
	Inventory.PickupMessage "Picked up a Box of High Caliber Bullets."
	Inventory.PickupSound "CBOXPKUP"
	Inventory.Amount 35
    Inventory.MaxAmount 300
    Ammo.BackpackAmount 50
    Ammo.BackpackMaxAmount 600
	Inventory.Icon "CLIPA0"
	+DONTGIB
	+FLOORCLIP
	States
	{
	Spawn:
		AMMO A -1 
		Stop
	}
}