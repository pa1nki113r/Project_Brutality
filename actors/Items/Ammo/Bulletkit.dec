ACTOR BulletKit : CustomInventory
{
	Game Doom
	Scale 1.0
	-COUNTITEM
	+DONTGIB
	+FLOORCLIP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteAlways("SmartScavengerAmmoScript",0)
		Deciding:
			TNT1 A 1
			Loop
		SpawnNormal:
			TNT1 A 1 A_SpawnItemEx("NormalBulletKit",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
		SpawnSmartScav:
			TNT1 A 1 A_SpawnItemEx("SmartBulletKit",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
	}
}

ACTOR SmartBulletKit : SwitchableDecoration
{
	+LOOKALLAROUND
	+USESPECIAL
	+DONTGIB
	+FLOORCLIP
	Activation THINGSPEC_Activate
	Radius 5
	States
	{
	Spawn:
		AMO7 A 1 A_Look
		Loop
	Pickup:
		AMO7 A 1
		Fail
	See:
		AMO7 A 1 A_JumpIfCloser(36,"CheckPlayerPack")
		Loop
	CheckPlayerPack:
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack_h",1,"CheckSkill_Pack_h")
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack",1,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",0,"FullFail")
		Goto CheckSkill_NoPack
	CheckSkill_Pack:
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",400,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",380,"SpawnClips")
		Goto SpawnBox
	CheckSkill_NoPack:
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",201,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",180,"SpawnClips")
		Goto SpawnBox
	CheckSkill_Pack_h:
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",600,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",580,"SpawnClips")
		Goto SpawnBox
	SpawnBox:
		TNT1 A 0 A_SpawnItemEx("NormalBulletKit")
		Stop
	Active:
	SpawnClips:
		TNT1 A 0 { bUSESPECIAL = false; }
		AMO7 B 15 A_PlaySound("Ammocase/Open")
		AMO7 C 10 A_SpawnItemEx("BulletCartridge",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		AMO7 D 10 A_SpawnItemEx("BulletCartridge",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		AMO7 E 10 A_SpawnItemEx("BulletCartridge",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		AMO7 E 10
		TNT1 A 0 ACS_NamedExecuteAlways("SmartScavContainerStay",0)
		AMO7 A 70
	 FadeOut:
		AMO7 A 5 A_FadeOut(0.1,1)
		Loop
	 RemainOpen:
		AMO7 E -1
		Stop
	FullFail:
		AMO7 A 1
		Goto See
	}
}

ACTOR BulletCartridge : PistolBullets
{
	Game Doom
	Inventory.Amount 15
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 55
	Ammo.BackpackMaxAmount 400
	Inventory.PickupMessage "Picked up a Low Caliber Magazine."
	Inventory.PickupSound "misc/clip_PickUp"
	States
	{
	Spawn:
		MBLK A -1
		Stop
	}
}

ACTOR NormalBulletKit : PistolBullets
{
	Game Doom
	Scale 1.15
	Inventory.PickupMessage "Picked up a Box of Low Caliber rounds."
	Inventory.PickupSound "CBOXPKUP"
	Inventory.Amount 30
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 30
	Ammo.BackpackMaxAmount 400
	+DONTGIB
	+FLOORCLIP
	States
	{
	Spawn:
		AMOK A -1 
		Stop
	}
}