ACTOR BulletKitBig : CustomInventory
{
	Game Doom
	Scale 1.0
	-COUNTITEM
	+DONTGIB
	+FLOORCLIP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteAlways("SmartScavengerAmmoScript",0)
		Deciding:
			TNT1 A 1
			Loop
		SpawnNormal:
			TNT1 A 1 A_SpawnItemEx("NormalBulletKitBig",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
		SpawnSmartScav:
			TNT1 A 1 A_SpawnItemEx("SmartBulletKitBig",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
	}
}

ACTOR SmartBulletKitBig : SwitchableDecoration
{
	+LOOKALLAROUND
	+USESPECIAL
	+DONTGIB
	+FLOORCLIP
	Activation THINGSPEC_Activate
	Radius 5
	States
	{
	Spawn:
		4M07 A 1 A_Look
		Loop
	Pickup:
		4M07 A 1
		Fail
	See:
		4M07 A 1 A_JumpIfCloser(36,"CheckPlayerPack")
		Loop
	CheckPlayerPack:
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack_h",1,"CheckSkill_Pack_h")
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack",1,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",0,"FullFail")
		Goto CheckSkill_NoPack
	CheckSkill_Pack:
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",400,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",350,"SpawnClips")
		Goto SpawnBox
	CheckSkill_NoPack:
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",201,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",150,"SpawnClips")
		Goto SpawnBox
	CheckSkill_Pack_h:
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",600,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("PistolBullets",550,"SpawnClips")
		Goto SpawnBox
	SpawnBox:
		TNT1 A 0 A_SpawnItemEx("NormalBulletKitBig")
		Stop
	Active:
	SpawnClips:
		TNT1 A 0 A_ChangeFlag("USESPECIAL", 0)
		4M07 B 10 A_StartSound("Ammocase/Open")
		4M07 B 10 A_SpawnItemEx("BulletCartridge",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		4M07 C 10 A_SpawnItemEx("BulletCartridge",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		4M07 D 10 A_SpawnItemEx("BulletCartridge",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		4M07 E 10 A_SpawnItemEx("BulletCartridge",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		4M07 F 10 A_SpawnItemEx("BulletCartridge",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		4M07 G 10 A_SpawnItemEx("BulletCartridge",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		4M07 H 10 A_SpawnItemEx("BulletCartridge",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		4M07 I 10
		TNT1 A 0 ACS_NamedExecuteAlways("SmartScavContainerStay",0)
		4M07 A 70
	 FadeOut:
		4M07 A 5 A_FadeOut(0.1,1)
		Loop
	 RemainOpen:
	    4M07 I -1
		Stop
	FullFail:
		4M07 A 1
		Goto See
	}
}

ACTOR NormalBulletKitBig : PistolBullets
{
	Scale 1.15
	Inventory.PickupMessage "Picked up a Large Case of Low Caliber rounds."
	Inventory.PickupSound "CBOXPKUP"
	Inventory.Amount 75
	+DONTGIB
	+FLOORCLIP
	States
	{
	Spawn:
		4M0K A -1 
		Stop
	}
}

