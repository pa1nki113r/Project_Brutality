ACTOR ShellBoxBig : CustomInventory
{
	Game Doom
	Scale 1.0
	-COUNTITEM
	+DONTGIB
	+FLOORCLIP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteAlways("SmartScavengerAmmoScript",0)
		Deciding:
			TNT1 A 1
			Loop
		SpawnNormal:
			TNT1 A 1 A_SpawnItemEx("NormalShellBoxBig",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
		SpawnSmartScav:
			TNT1 A 1 A_SpawnItemEx("SmartShellBoxBig",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
	}
}

ACTOR SmartShellBoxBig : SwitchableDecoration
{
	+LOOKALLAROUND
	+USESPECIAL
	+DONTGIB
	+FLOORCLIP
	Activation THINGSPEC_Activate
	Radius 5
	Scale .8
	States
	{
	Spawn:
		LSBX A 1 A_Look
		Loop
	Pickup:
		LSBX A 1
		Fail
	See:
		LSBX A 1 A_JumpIfCloser(36,"CheckPlayerPack")
		Loop
	CheckPlayerPack:
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack_h",1,"CheckSkill_Pack_h")
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack",1,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("NewShell",0,"FullFail")
		Goto CheckSkill_NoPack
	CheckSkill_Pack:
		TNT1 A 0 A_JumpIfInTargetInventory("NewShell",100,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("NewShell",73,"SpawnShells")
		Goto SpawnBox
	CheckSkill_NoPack:
		TNT1 A 0 A_JumpIfInTargetInventory("NewShell",51,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("NewShell",23,"SpawnShells")
		Goto SpawnBox
	CheckSkill_Pack_h:
		TNT1 A 0 A_JumpIfInTargetInventory("NewShell",150,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("NewShell",123,"SpawnShells")
		Goto SpawnBox
	SpawnBox:
		TNT1 A 0 A_SpawnItemEx("NormalShellBoxBig")
		Stop
	Active:
	SpawnShells:
		TNT1 A 0 A_ChangeFlag("USESPECIAL", 0)
		LSBX B 15 A_StartSound("Ammocase/Open")
		LSBX B 6 A_SpawnItemEx("SmartShell",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		LSBX C 6 A_SpawnItemEx("SmartShell",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		LSBX D 6 A_SpawnItemEx("SmartShell",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		LSBX E 6 A_SpawnItemEx("SmartShell",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		LSBX F 6 A_SpawnItemEx("SmartShell",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		LSBX G 6 A_SpawnItemEx("SmartShell",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		LSBX H 6 A_SpawnItemEx("SmartShell",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		LSBX I 6 A_SpawnItemEx("SmartShell",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		LSBX J 15
		TNT1 A 0 ACS_NamedExecuteAlways("SmartScavContainerStay",0)
		LSBX A 70
	 FadeOut:
		LSBX A 5 A_FadeOut(0.1,1)
		Loop
	RemainOpen:
		LSBX J -1
		Stop
	FullFail:
		LSBX A 1
		Goto See
	}
}

ACTOR NewShell : Ammo Replaces Shell
{
	Inventory.PickUpSound "misc/shell_PickUp"
	Inventory.PickupMessage "Picked up some Shotgun Shells."
	+FLOORCLIP
	+DONTGIB
	Inventory.Amount 4
	Inventory.MaxAmount 50
	Inventory.Icon "SHELA0"
	Ammo.BackpackAmount 4
	Ammo.BackpackMaxAmount 100
	States
	{
	Spawn:
		SHEL A -1
		Stop
	}
}

ACTOR NormalShellBoxBig : NewShell
{
	Scale .8
	Inventory.PickupMessage "Picked up a Large Case of Shells."
	Inventory.PickupSound "misc/shellbox_PickUp"
	Inventory.Amount 32
	+DONTGIB
	+FLOORCLIP
	States
	{
	Spawn:
		TNT1 A 0 ACS_ExecuteAlways(796, 0, 0, 0, 0)//Check if Vanilla Mode is activated
		LSBX A -1
		Stop
	ReplaceVanilla:
		TNT1 A 0 A_SpawnItemEx("NewShellBox",0,0,0,0,0,0,0,288)
		TNT1 A 1
		Stop
	}
}

ACTOR SmartShell : NewShell
{
	+FLOORCLIP
	+DONTGIB
	Scale .6
	States
	{
	Spawn:
		SH3L A -1
		Stop
	}
}