ACTOR GrenadeBox : CustomInventory
{
	Game Doom
	SpawnID 7300
	Scale 1.0
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+DONTGIB
	+FLOORCLIP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteAlways("SmartScavengerAmmoScript",0)
		Deciding:
			TNT1 A 1
			Loop
		SpawnNormal:
			TNT1 A 1 A_SpawnItemEx("NormalGrenadeBox",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
		SpawnSmartScav:
			TNT1 A 1 A_SpawnItemEx("SmartGrenadeBox",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
			Stop
	}
}

ACTOR SmartGrenadeBox : SwitchableDecoration
{
	+LOOKALLAROUND
	+USESPECIAL
	-COUNTITEM
	+DONTGIB
	+FLOORCLIP
	Activation THINGSPEC_Activate
	Radius 5
	States
	{
	Spawn:
		9TCK A 1 A_Look
		Loop
	Pickup:
		9TCK A 1
		Fail
	See:
		9TCK A 1 A_JumpIfCloser(36,"CheckPlayerPack")
		Loop
	CheckPlayerPack:
		TNT1 A 0 A_JumpIfInTargetInventory("MineAmmo",0,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("HandGrenadeAmmo",0,"FullFail")
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack",1,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("GotAPack_h",1,"CheckSkill_Pack_h")
		Goto CheckSkill_NoPack
	CheckSkill_Pack_h:
		TNT1 A 0 A_JumpIfInTargetInventory("MineAmmo",17,"SpawnNadesAndMines")
		TNT1 A 0 A_JumpIfInTargetInventory("HandGrenadeAmmo",17,"SpawnNadesAndMines")
		Goto SpawnBox
	CheckSkill_Pack:
		TNT1 A 0 A_JumpIfInTargetInventory("MineAmmo",17,"SpawnNadesAndMines")
		TNT1 A 0 A_JumpIfInTargetInventory("HandGrenadeAmmo",10,"SpawnNadesAndMines")
		Goto SpawnBox
	CheckSkill_NoPack:
		TNT1 A 0 A_JumpIfInTargetInventory("MineAmmo",10,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("MineAmmo",7,"SpawnNadesAndMines")
		TNT1 A 0 A_JumpIfInTargetInventory("HandGrenadeAmmo",6,"CheckSkill_Pack")
		TNT1 A 0 A_JumpIfInTargetInventory("HandGrenadeAmmo",4,"SpawnNadesAndMines")
		Goto SpawnBox
	SpawnBox:
		TNT1 A 0 A_SpawnItemEx("NormalGrenadeBox")
		Stop
	Active:
	SpawnNadesAndMines:
		TNT1 A 0 A_ChangeFlag("USESPECIAL", 0)
		9TCK B 15 A_StartSound("Ammocase/Open")
		9TCK B 10 A_SpawnItemEx("SingleHandGrenade",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		9TCK B 10 A_SpawnItemEx("SingleHandGrenade",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		9TCK B 10 A_SpawnItemEx("SingleHandGrenade",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		9TCK B 10 A_SpawnItemEx("SingleLandMine",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		9TCK B 10 A_SpawnItemEx("SingleLandMine",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		9TCK B 10 A_SpawnItemEx("SingleLandMine",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
        9TCK B 10 A_SpawnItemEx("SingleLandMine",0,0,0,frandom(2,4),0,frandom(2,4),random(1,360))
		TNT1 A 0 ACS_NamedExecuteAlways("SmartScavContainerStay",0)
		9TCK A 70
	 FadeOut:
		9TCK A 5 A_FadeOut(0.1,1)
		Loop
	RemainOpen:
		9TCK B -1
		Stop
	FullFail:
		9TCK A 1
		Goto See
	}
}

ACTOR NormalGrenadeBox : CustomInventory
{
	Game Doom
	Scale 1.0
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+DONTGIB
	+FLOORCLIP
	Inventory.pickupsound "OPNGRN"
	Inventory.PickupMessage "Picked up a box of 3 Frag Grenades and 4 Land Mines."
	States
	{
		Spawn:
			9TCK A 2 
			Loop		
		Pickup:
			TNT1 A 0 A_GiveInventory("HandGrenadeAmmo",3) 
			TNT1 A 0 A_GiveInventory("MineAmmo",4) 				
			TNT1 A 20
			Stop
	}
}

ACTOR SingleHandGrenade : CustomInventory
{
	Game Doom
	SpawnID 7310
	Scale 0.5
	+DONTGIB
	+FLOORCLIP
	Inventory.PickupMessage "Picked up a UAC-M26 Frag Grenade."
	Inventory.PickupSound "GRNPIN"
	States
	{
		Spawn:
			PGRN D 2 
			Loop
			
		Pickup:
			TNT1 A 0 A_GiveInventory("HandGrenadeAmmo",1) 		
			TNT1 A 20
			Stop	
	}
}

ACTOR SingleLandMine : CustomInventory
{
    Game Doom
	Scale 0.4
	+DONTGIB
	+FLOORCLIP
	Inventory.PickupMessage "Picked up a Land Mine."
    Inventory.PickUpSound "misc/rocket_PickUp"
	Inventory.Amount 1
	States
	{
		Spawn:
			LMIN A -1
			Stop
		Pickup:
			TNT1 A 0 A_GiveInventory("MineAmmo",1) 		
			TNT1 A 20
			Stop	
	}
}