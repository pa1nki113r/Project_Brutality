// --------------------------------------------------------------------------
//
// Sawed-Off (SSG)
//
// --------------------------------------------------------------------------

Actor PBSSGHasUnloaded: Inventory
{
Inventory.maxamount 1
}

Actor RespectSSG: Inventory
{
Inventory.maxamount 1
}

ACTOR DualWieldingSSG: Inventory
{
Inventory.MaxAmount 1
}


Actor PB_SSGFireAnimation1: Inventory
{
	Inventory.maxamount 1
}

Actor PB_SSGFireAnimation2: Inventory
{
	Inventory.maxamount 1
}

ACTOR LeftSSGAmmo : Ammo 
{
   Inventory.Amount 0
   Inventory.MaxAmount 2 
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 2
   Inventory.Icon "SGN2A0"
}


ACTOR SSGAmmoCounter: Ammo
{
	Inventory.Amount 0
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 2
	Inventory.Icon "SGN2A0"
}


Actor SSGBarrelHeat: Inventory
{Inventory.maxamount 200}

ACTOR SSG : PB_weapon Replaces SuperShotgun
{
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Game Doom
    Weapon.Kickback 50
	Weapon.SelectionOrder 400
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 2
	Weapon.AmmoGive2 0
	Weapon.AmmoType "NewShell"
	Weapon.AmmoType2 "SSGAmmoCounter"
	+FLOORCLIP
	+DONTGIB
    +WEAPON.NOAUTOAIM
	Tag "Sawed-Off Shotgun"
	+WEAPON.NOAUTOFIRE
    +WEAPON.MELEEWEAPON
	Inventory.MaxAmount 3
	Inventory.PickupMessage "You got the Sawed-Off Shotgun! (Slot 3)"
	Obituary "%o was splattered by %k's SSG"
    Inventory.PickupSound "CLIPINQS"
	Scale 0.5
	Inventory.AltHUDIcon "SGN2A0"
	States
	{
		
	Steady:
		TNT1 A 1
		Goto Ready

	Spawn.V5:
		VGN2 A 10 {
			IF( A_CHECKRANGE( V5_MDist , "NULL", TRUE) ) {A_SetSpawnSprite("SGN2");}
			}
	Spawn:
		TNT1 A 0 NoDelay A_jumpif( V5_MODELS == 1 && V5_Cull == 1, "Spawn.V5")
		SGN2 A 10 {
				if ( GetCvar( "V5_MODELS" ) == 1 && GetCVar( "V5_Cull" ) == 0 ) {A_SetSpawnSprite("VGN2");}
		}
		LOOP
		
	Deselect:
		TNT1 A 0 {
			A_ClearOverlays(10,11);
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(92);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_Takeinventory("SSGSelected",1) 
		TNT1 A 0 A_JumpIfInventory("GotMeatShield", 1, "GrabEnemy")
		TNT1 A 0 A_JumpIfInventory("Quad_SSG",1,"DeselectUpgrade")
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1,"DeselectAnimationDualWield")
		SG02 CBA 1
        SG01 Z 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectAnimationDualWield:
		P6SS FED 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		P6SS CBA 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectUpgrade:
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait

	Select:
		TNT1 A 0 {
			A_ClearOverlays(10,11);
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(92);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		Goto SelectFirstPersonLegs
		SelectContinue:
		TNT1 A 0 A_SelectWeapon("Quad_SSG")
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_TakeInventory("RifleSelected",1)
		TNT1 A 0 A_TakeInventory("CarbineSelected",1)
		TNT1 A 0 A_TakeInventory("FistsSelected",1)
		TNT1 A 0 A_TakeInventory("SawSelected",1)
		TNT1 A 0 A_TakeInventory("SledgeSelected",1)
		TNT1 A 0 A_TakeInventory("HandgunSelected",1)
		TNT1 A 0 A_TakeInventory("DualHandgunSelected",1)
		TNT1 A 0 A_TakeInventory("RevolverSelected",1)
		TNT1 A 0 A_TakeInventory("ShotgunSelected",1)
		TNT1 A 0 A_GiveInventory("SSGSelected",1)
		TNT1 A 0 A_TakeInventory("ASGSelected",1)
		TNT1 A 0 A_TakeInventory("QSGSelected",1)
		TNT1 A 0 A_TakeInventory("MinigunSelected",1)
		TNT1 A 0 A_TakeInventory("NewChaingunSelected",1)	
		TNT1 A 0 A_TakeInventory("RocketLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("GrenadeLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("SGLSelected",1)
		TNT1 A 0 A_TakeInventory("PlasmaGunSelected",1)
		TNT1 A 0 A_TakeInventory("M2Selected",1)	
		TNT1 A 0 A_TakeInventory("FreezerSelected",1)
		TNT1 A 0 A_TakeInventory("RailGunSelected",1)
		TNT1 A 0 A_TakeInventory("BFGSelected",1)
		TNT1 A 0 A_TakeInventory("BFGBeamSelected",1)
		TNT1 A 0 A_TakeInventory("BHCSelected",1)
		TNT1 A 0 A_TakeInventory("RevenantLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("FlameCannonSelected",1)
		TNT1 A 0 A_TakeInventory("HellRifleSelected",1)
		TNT1 A 0 A_TakeInventory("LandMineSelected",1)
		TNT1 A 0 A_TakeInventory("UACSMGSelected",1)
		TNT1 A 0 A_TakeInventory("SubMachineGunSelected",1)
		TNT1 A 0 A_TakeInventory("LostSoulSelected",1)
		TNT1 A 0 A_TakeInventory("HasBarrel",1)
		TNT1 A 0 A_TakeInventory("HasBurningBarrel",1)
		TNT1 A 0 A_TakeInventory("HasIncendiaryWeapon", 1)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait

	Ready:
		TNT1 A 1 A_SelectWeapon("Quad_SSG")
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0 A_JumpIfInventory("RespectSSG", 1, "SelectAnimation")
	WeaponRespect:
		TNT1 A 0 {
			A_Giveinventory("RespectSSG",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_StartSound("Ironsights", CHAN_AUTO);
			A_SetCrosshair(5);
			}
		SG00 ABCD 1 {
			A_SetRoll(roll+0.8);
			A_DoPBWeaponAction;
		}
		TNT1 A 0 A_StartSound("weapons/ssg/inspect1", CHAN_AUTO)
		SG00 EFGH 1 {
			A_SetRoll(roll-0.8);
			A_DoPBWeaponAction;
		}
		SG00 IIIIIIIJKLMNO 1 A_DoPBWeaponAction
		SG00 PQRRRSTU 1 {
			A_SetRoll(roll-1.0);
			A_DoPBWeaponAction;
		}
		SG00 VWXY 1 {
			A_SetRoll(roll+2.0);
			A_DoPBWeaponAction;
		}
		SG00 ZZZZ 1
		SG01 ABCCCCCC 1 {
			A_SetRoll(roll+0.5);
			A_DoPBWeaponAction;
		}
		SG01 DEFG 1 {
			A_SetRoll(roll-1.0);
			A_DoPBWeaponAction;
		}
		TNT1 A 0 A_StartSound("weapons/ssg/inspect2", CHAN_AUTO)
		SG01 H 1 A_DoPBWeaponAction
		SG03 ABCCCBA 1 A_DoPBWeaponAction
		SG01 H 1 A_DoPBWeaponAction
		SG01 IJKKKK 1 A_DoPBWeaponAction
		TNT1 A 0 A_StartSound("weapons/ssg/inspect2", CHAN_AUTO)
		SG01 L 1 A_DoPBWeaponAction
		SG04 ABC 1 A_DoPBWeaponAction
		SG01 MMMM 1 A_DoPBWeaponAction
		SG04 CBA 1 A_DoPBWeaponAction
		SG01 NO 1 A_DoPBWeaponAction
		TNT1 A 0 A_StartSound("weapons/ssg/inspect3", CHAN_AUTO)
		SG01 PQRST 1 {
			A_SetRoll(roll+1.0);
			A_DoPBWeaponAction;
		}
		SG01 UVWXXXX 1 
		TNT1 A 0 A_StartSound("Ironsights", CHAN_AUTO)
		SG01 YZ 1 {
			A_SetRoll(roll-1.0);
			A_DoPBWeaponAction;
		}
		SG02 ABC 1 {
			A_SetRoll(roll-1.0);
			A_DoPBWeaponAction;
		}
		Goto Ready3
	SelectAnimation:
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "SelectAnimationDualWield")
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
        SG01 Z 1
		SG02 ABC 1
	Ready3:
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "ReadyDualWield")
		TNT1 A 0 {
			A_ClearOverlays(10,11);
			A_Takeinventory("PB_LockScreenTilt",1);
			PB_HandleCrosshair(92);
			A_SetRoll(0);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1,"ReadyToFireDualWield")
	RealReady:
		TNT1 A 0 A_SelectWeapon("Quad_SSG")
		SHT2 A 1 {
			if (CountInv("SSGBarrelHeat") >= 60) {
					A_Gunflash("BarrelSmoke1");
			}
			if (PressingFire() && CountInv("SSGAmmoCounter") > 0 ){
					return state("Fire");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD,CheckUnloaded("PBSSGHasUnloaded"));
		}
		Loop
	BarrelSmoke1:
		TNT1 A 1 {
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, -0.6, 0, 0, 0);
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0.6, 0, 0, 0);
			A_Takeinventory("SSGBarrelHeat",1);
		}
		Stop
	SelectAnimationDualWield:
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
		P6SS ABC 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
		P6SS DEF 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
	ReadyDualWield:
		TNT1 A 0 A_SelectWeapon("Quad_SSG")
		TNT1 A 0 {
			A_SetRoll(0);
			PB_HandleCrosshair(92);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_SetFiringRightWeapon(False);
			A_SetFiringLeftWeapon(False);
			A_TakeInventory("DualFiring",1);
			if(CountInv("LeftSSGAmmo") < CountInv("SSGAmmoCounter")){
				A_GiveInventory("DualFiring",1);
			}
			if(CountInv("NewShell")>0){
				if(CountInv("LeftSSGAmmo")<=0){
					A_GiveInventory("DualFireReload",1);
				}
				if(CountInv("SSGAmmoCounter")<=0){
					A_GiveInventory("DualFireReload",1);
				}
			}
			A_Overlay(10, "IdleLeft_Overlay", true);
			A_Overlay(11, "IdleRight_Overlay", true);
		}
	DualFireCheck:
		TNT1 A 0 {
			if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() || ((PressingAltfire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringRightWeapon())){ 
				Return State("FireDualBlank");
				}
				Return State("");
			}
	ReadyToFireDualWield:
		TNT1 A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD | WRF_NOFIRE)
		Goto DualFireCheck
// FireDualBlank state made to prevent strange issues with dual SSGs.
	FireDualBlank:
		TNT1 A 0 A_JumpIfInventory("DualFireReload",2,"AltFireDualBlank")
		TNT1 A 12 A_WeaponReady(WRF_NOSWITCH | WRF_NOFIRE)
		Goto DualFireCheck
// State to prevent strange issues with left fire going straight to reload, only initiated from the jump state above.
	AltFireDualBlank: 
		TNT1 A 0
		Goto ReloadDualWield

	IdleLeft_Overlay:
		P6SS H 1 {
			if(CountInv("LeftSSGAmmo")<=0 && CountInv("SSGAmmoCounter")>0){
				A_GiveInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==0 || (CountInv("DualFiring")==0 && CountInv("SSGAmmoCounter")<=0) || GetCvar("SingleDualFire")==1){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon()){
					if(CountInv("LeftSSGAmmo") > 0){
						return state("FireLeft_Overlay");
					}
					else {
						A_StartSound("weapons/empty", CHAN_AUTO);
						return state("");
					}
				}
			}
			return state("");
		}
		Loop
	IdleRight_Overlay:
		P6SS G 1 {
			if(CountInv("LeftSSGAmmo")>0 && CountInv("SSGAmmoCounter")<=0){
				A_TakeInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==1 || (CountInv("DualFiring")==1 && CountInv("LeftSSGAmmo")<=0)){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire")==0){
					if(CountInv("SSGAmmoCounter") > 0){
						return state("FireRight_Overlay");
					}
					else {
						A_StartSound("weapons/empty", CHAN_AUTO);
						return state("");
					}
				}
			}
			if((PressingAltfire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringRightWeapon()){
				if(CountInv("SSGAmmoCounter")>0 && GetCvar("SingleDualFire")==1){
					return state("FireRight_Overlay");
				}
				if(CountInv("SSGAmmoCounter")>0 && CountInv("LeftSSGAmmo")>0 && GetCvar("SingleDualFire")==0){
					A_Overlay(10, "FireLeft_Overlay", false);
					return state("FireRight_Overlay");
				}
				else {
					A_StartSound("weapons/empty", CHAN_AUTO);
					return state("");
				}
			}

			return state("");
		}
		Loop
	FireLeft_Overlay:
        TNT1 A 0 {
			if(CountInv("LeftSSGAmmo") == 2){
				A_FireBullets(0, 0, 1, 35, "shotpuff", 0, 220);
			}
		}
		P6W2 A 1 BRIGHT {
			A_GunFlash;
			if(CountInv("LeftSSGAmmo") == 1) {
				A_AlertMonsters;
				A_FireBullets(9, 6, 10, 7, "ShotgunPuff");
				A_StartSound("weapons/shh2", CHAN_WEAPON);
				A_FireCustomMissile("GunFireSmokE", 0, 0, -3, 0, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, -5, 0, 0, 0);
				A_ZoomFactor(0.95);
				A_GiveInventory("PB_SSGFireAnimation2",1); 
				A_TakeInventory("PB_SSGFireAnimation1",1); 
				A_TakeInventory("LeftSSGAmmo", 1);
			}
			else {
				A_AlertMonsters;
				A_FireBullets (12, 6, 20, 7, "ShotgunPuff");
				A_FireCustomMissile("GunFireSmokE", 0, 0, 3, 0, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 5, 0, 0, 0);
				A_StartSound("SSHFIRE", CHAN_WEAPON);
				A_ZoomFactor(0.95);
				A_GiveInventory("PB_SSGFireAnimation2",1); 
				A_TakeInventory("PB_SSGFireAnimation1",1); 
				A_TakeInventory("LeftSSGAmmo", 2);
			}
		}
		P6W2 B 1 BRIGHT {
			A_FireCustomMissile("GunFireSmokE", 0, 0, 3, 0, 0, 0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 5, 0, 0, 0);
			}
		P6W2 C 1 {
			A_ZoomFactor(0.975);
            PB_WeaponRecoil(-3.80,+2.40);
		}
		P6W2 D 1 {
			A_ZoomFactor(1.0);
            PB_WeaponRecoil(-3.80,+2.40);
		}
		P6W2 E 1 {
            PB_WeaponRecoil(-3.80,+2.40);
		}
		P6W2 F 1 {
			//CountInv("DualFiring")==0
			if(CountInv("LeftSSGAmmo")<=0 || CountInv("SSGAmmoCounter")>0){
				A_GiveInventory("DualFiring",1);
			}
		}
		P6W2 GHIJK 1
		TNT1 A 0 {
			if(CountInv("LeftSSGAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleLeft_Overlay
		
	FireRight_Overlay:
        TNT1 A 0 {
			if(CountInv("SSGAmmoCounter") == 2){
				A_FireBullets(0, 0, 1, 35, "shotpuff", 0, 220);
			}
		}
		P6W1 A 1 BRIGHT {
			A_GunFlash;
			if(CountInv("SSGAmmoCounter") == 1){
				A_AlertMonsters;
				A_FireBullets(9, 6, 10, 7, "ShotgunPuff");
				A_StartSound("weapons/shh2", CHAN_WEAPON);
				A_FireCustomMissile("GunFireSmokE", 0, 0, -3, 0, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, -5, 0, 0, 0);
				A_ZoomFactor(0.95);
				A_GiveInventory("PB_SSGFireAnimation1",1);
				A_TakeInventory("SSGAmmoCounter", 1);
			}
			else {
				A_AlertMonsters;
				A_FireBullets (12, 6, 20, 7, "ShotgunPuff");
				A_FireCustomMissile("GunFireSmokE", 0, 0, -3, 0, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, -5, 0, 0, 0);
				A_StartSound("SSHFIRE", CHAN_WEAPON);
				A_ZoomFactor(0.95);
				A_GiveInventory("PB_SSGFireAnimation1",1);
				A_TakeInventory("SSGAmmoCounter", 2);
			}
		}
		P6W1 B 1 BRIGHT {
			A_FireCustomMissile("GunFireSmokE", 0, 0, -3, 0, 0, 0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, -5, 0, 0, 0);
			}
		P6W1 C 1 {
			A_ZoomFactor(0.975);
            PB_WeaponRecoil(-3.80,-2.40);
		}
		P6W1 D 1 {
			A_ZoomFactor(1.0);
            PB_WeaponRecoil(-3.80,-2.40);
		}
		P6W1 E 1 {
            PB_WeaponRecoil(-3.80,-2.40);
		}
		P6W1 F 1 {
			if(CountInv("LeftSSGAmmo")>0 || CountInv("SSGAmmoCounter")<=0){
				A_TakeInventory("DualFiring",1);
			}
		}
		P6W1 GHIJK 1
		TNT1 A 0 {
			if(CountInv("SSGAmmoCounter")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleRight_Overlay
		
	WeaponSpecial:
		TNT1 A 0 {
			A_Takeinventory("GoWeaponSpecialAbility",1);
			A_GiveInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
			PB_HandleCrosshair(92);
			}
		TNT1 A 0 A_JumpIfInventory("SSG", 2,"SwitchToDualWield")
		TNT1 A 0 A_Print("You need two sawed-offs to dual wield!")
		Goto Ready3
		
	SwitchToDualWield:
		TNT1 A 0 {
			A_StartSound("Ironsights", CHAN_AUTO);
			if (A_CheckAkimbo()) {
				A_SetAkimbo(False);
				A_TakeInventory("DualWieldingSSG", 1);
				return state("SwitchFromDualWield");
			}
			else {
				A_SetAkimbo(True);
				A_GiveInventory("DualWieldingSSG", 1);
				return state("");
			}
		}
		SG02 CB 1 A_Setroll(roll+0.5, SPF_INTERPOLATE)
		SG02 A 1 A_Setroll(roll-0.5, SPF_INTERPOLATE)
        SG01 Z 1 A_Setroll(roll-0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
		P6SS ABC 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
		P6SS DEF 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
		Goto ReadyDualWield
		
	SwitchFromDualWield:
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
		P6SS FED 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
		P6SS CBA 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_StartSound("Ironsights", CHAN_AUTO)
        SG01 Z 1 A_Setroll(roll-0.5, SPF_INTERPOLATE)
		SG02 A 1 A_Setroll(roll-0.5, SPF_INTERPOLATE)
		SG02 BC 1 A_Setroll(roll+0.5, SPF_INTERPOLATE)
		Goto Ready3
		
	
	Fire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(92);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "DualFireCheck")
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter", 2, 2)
		TNT1 A 0 A_StartSound("weapons/empty", CHAN_WEAPON)
		Goto AltFire2
        TNT1 A 0 A_FireBullets(0, 0, 1, 35, "shotpuff", 0, 220)
        SHO9 A 1 BRIGHT {
			A_GunFlash;
			A_FireBullets (12, 6, 20, 7, "ShotgunPuff");
			A_FireCustomMissile("GunFireSmokE", 0, 0, 2, 0, 0, 0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 0, 0, 0);
			A_StartSound("SSHFIRE", CHAN_WEAPON);
			A_ZoomFactor(0.95);
			A_TakeInventory("SSGAmmoCounter", 2);
			A_Giveinventory("SSGBarrelHeat",120);
			A_AlertMonsters;
			}
        SHO9 B 1 BRIGHT {
			A_FireCustomMissile("GunFireSmokE", 0, 0, 2, 0, 0, 0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 0, 0, 0);
			}
		SHO8 C 1 {
			A_ZoomFactor(0.975);
            PB_WeaponRecoil(-2.20,+0.40);
			}
		SHO8 D 1 {
			A_ZoomFactor(1.0);
            PB_WeaponRecoil(-2.20,+0.40);
			}
		SHO8 EFGGHIJ 1 
		TNT1 A 0 A_JumpIfInventory("NewShell", 1, "Reload")
		Goto Ready3
		
	AltFire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(92);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "DualFireCheck")
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter", 2, 1)
		Goto AltFire2
		SHTA A 1 BRIGHT {
			A_GunFlash;
			A_FireBullets(9, 6, 10, 7, "ShotgunPuff");
			A_FireCustomMissile("GunFireSmoke", 0, 0, 2, 0, 0, 0);
			A_StartSound("weapons/shh2", CHAN_WEAPON);
			A_TakeInventory("SSGAmmoCounter", 1);
			A_ZoomFactor(0.98);
			}
        SHTA A 1 BRIGHT {
			A_ZoomFactor(0.99);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 2, 0, 0, 0);
			PB_WeaponRecoil(-1.50,+1.0);
		}
	    SHO8 C 1 {
			A_ZoomFactor(1.0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 2, 0, 0, 0);
			PB_WeaponRecoil(-1.50,+1.0);
		}
	    SHO8 JK 1
		SHT2 A 1 Offset(0,40)
		SHT2 A 1 Offset(0,36)
		SHT2 A 1 Offset(0,32)
        Goto Ready3
		
	AltFire2:
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FireDualBlank")
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter", 1, 1)
		Goto Reload
		SHTA B 1 BRIGHT {
			A_GunFlash;
			A_FireBullets(9, 6, 10, 7, "ShotgunPuff");
			A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 0, 0, 0);
			A_StartSound("weapons/shh2", CHAN_WEAPON);
			A_TakeInventory("SSGAmmoCounter", 1);
			A_ZoomFactor(0.98);
			}
        SHTA B 1 BRIGHT {
			A_ZoomFactor(0.99);
			A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 0, 0, 0);
			PB_WeaponRecoil(-1.50,+1.0);
		}
	    SHO8 C 1 {
			A_ZoomFactor(1.0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 0, 0, 0);
			PB_WeaponRecoil(-1.50,+1.0);
		}
	    SHO8 JK 1
		SHT2 A 1 Offset(0,40)
		SHT2 A 1 Offset(0,36)
		SHT2 A 1 Offset(0,32)
		Goto Reload
		
	Reload:
		TNT1 A 0 A_TakeInventory("PBSSGHasUnloaded",1)//So that the unload function won't break when in dual wield mode
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "ReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter", 2, "Ready3")
		TNT1 A 0 A_JumpIfInventory("NewShell", 1, 2)
		TNT1 A 0 A_TakeInventory("SSGAlt", 1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("PB_LockScreenTilt",1);
			A_Takeinventory("PB_SSGFireAnimation1",1);
			A_Takeinventory("PB_SSGFireAnimation2",1);
			A_SetCrosshair(5);
			}
		SG10 ABCDEF 1 A_SetRoll(roll+0.5)
        TNT1 A 0 A_StartSound("weapons/ssg/open", CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter", 1, 1)
		Goto FullReload
	HalfReload:
		TNT1 A 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-10)
        SG12 ABC 1 A_SetRoll(roll-1.0)
		SG12 DEFGH 1
		TNT1 A 0 A_StartSound("weapons/ssg/inspect2", CHAN_AUTO)
        SG10 QRRS 1 A_SetRoll(roll+2.0)
		SG10 TTTU 1 A_SetRoll(roll-1.0)
		TNT1 A 0 A_StartSound("weapons/ssg/inspect3", CHAN_AUTO)
		SG10 VWXY 1 A_SetRoll(roll-1.0)
		SG10 Z 1
		SG11 ABCDEFGH 1
		Goto InsertBullets
	FullReload:
		TNT1 AA 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-10)
        SG10 GHI 1 A_SetRoll(roll-1.0)
        SG10 JKLMNOP 1 
		TNT1 A 0 A_StartSound("weapons/ssg/inspect2", CHAN_AUTO)
        SG10 QRRS 1 A_SetRoll(roll+2.0)
		SG10 TTTU 1 A_SetRoll(roll-1.0)
		TNT1 A 0 A_StartSound("weapons/ssg/inspect3", CHAN_AUTO)
		SG10 VWXY 1 A_SetRoll(roll-1.0)
		SG10 Z 1
		SG11 ABBCDEFGH 1
		Goto InsertBullets
		
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",2,"FinishReload")
		TNT1 A 0 A_JumpIfInventory("NewShell",1,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("SSGAmmoCounter",1);
			A_Takeinventory("NewShell",1);
			}
		Loop
	FinishReload:
		SHT2 A 1 A_Refire
		Goto Ready3
		
	ReloadDualWield:
		TNT1 A 0 A_JumpIf((CountInv("SSGAmmoCounter")==2 && CountInv("LeftSSGAmmo")==2), "ReadyDualWield")
		TNT1 A 0 A_JumpIfInventory("NewShell", 1, 1)
		Goto ReadyDualWield
		TNT1 A 0 A_ClearOverlays(10,11)
		P6SS FED 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
		P6SS CBA 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
	ReloadRight:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",2,"ReloadDualWieldContinued")
		R6FR ABCDEFGHIJ 1
        TNT1 A 0 A_StartSound("weapons/ssg/open")
		TNT1 A 0 {
			if(CountInv("SSGAmmoCounter") == 0) {
				A_FireCustomMissile("SSGCaseSpawner",0,0,10);
				A_FireCustomMissile("SSGCaseSpawner",0,0,10);
			}
			else {
				A_FireCustomMissile("SSGCaseSpawner",0,0,10);
			}
		}
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",1,"HalfReloadRight")
		R6FR K 4
        R6FR LMNO 1
	    R6FR P 1
        TNT1 A 0 A_StartSound("weapons/ssg/inspect2")
	    R6FR QRST 1 
        TNT1 A 0 A_StartSound("weapons/ssg/inspect3")
        R6FR UVWX 1

	RightInsertBullets:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",2,"ReloadDualWieldContinued")
		TNT1 A 0 A_JumpIfInventory("NewShell",1,1)
		Goto FinishReloadDualWield
		TNT1 A 0 {
			A_Giveinventory("SSGAmmoCounter",1);
			A_Takeinventory("NewShell",1);
			}
		Loop
	HalfReloadRight: 
        SHT9 EEFFGGHH 1
	    R6FR OOPP 1
        TNT1 A 0 A_StartSound("weapons/ssg/inspect2", 8)
	    R6FR QRST 1 
        TNT1 A 0 A_StartSound("weapons/ssg/inspect3", 8)
        R6FR UV 1
		Goto RightInsertBullets
	ReloadDualWieldContinued:
		TNT1 A 0 A_JumpIfInventory("LeftSSGAmmo",2,"FinishReloadDualWield")
	ReloadLeft:
		R6FL ABCDEFGHIJ 1
	    TNT1 A 0 A_StartSound("weapons/ssg/open")
		TNT1 A 0 {
			if(CountInv("SSGAmmoCounter") == 0) {
				A_FireCustomMissile("SSGCaseSpawner",0,0,-10);
				A_FireCustomMissile("SSGCaseSpawner",0,0,-10);
			}
			else {
				A_FireCustomMissile("SSGCaseSpawner",0,0,-10);
			}
		}
		TNT1 A 0 A_JumpIfInventory("LeftSSGAmmo",1,"HalfReloadLeft")
		R6FL K 4
        R6FL LMNO 1
        R6FL P 1 
		TNT1 A 0 A_StartSound("weapons/ssg/inspect2")
	    R6FL QRST 1 
        TNT1 A 0 A_StartSound("weapons/ssg/inspect3")
        R6FL UVWX 1
		
	LeftInsertBullets:
		TNT1 A 0 A_JumpIfInventory("LeftSSGAmmo",2,"FinishReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("NewShell",1,1)
		Goto FinishReloadDualWield
		TNT1 A 0 {
			A_Giveinventory("LeftSSGAmmo",1);
			A_Takeinventory("NewShell",1);
			}
		Loop
	HalfReloadLeft:
        SHT9 AABBCCDD 1
        R6FL OOPP 1
		TNT1 A 0 A_StartSound("weapons/ssg/inspect2", 8)
	    R6FL QRST 1 
        TNT1 A 0 A_StartSound("weapons/ssg/inspect3", 8)
        R6FL UV 1
		Goto LeftInsertBullets
	FinishReloadDualWield:
		TNT1 A 0 A_TakeInventory("DualFireReload",2)
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
		P6SS ABC 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
		P6SS DEF 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
		Goto ReadyDualWield
		
	Unload:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_JumpIfInventory("PBSSGHasUnloaded", 1, "Ready3")
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "UnloadDualWield")
        TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",1,1)
        Goto Ready3
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",1,1)
		Goto FinishUnload
		TNT1 A 0 {
			A_Takeinventory("SSGAmmoCounter",1);
			A_Giveinventory("NewShell",1);
		}
		Goto RemoveBullets
	FInishUnload:
		TNT1 A 0 A_ClearOverlays(10,11)
		SHT2 B 1 Offset(20,48)
        SHT2 B 1 Offset(10,36)
		SHT2 BCDEF 1
		SHT2 GH 1
		TNT1 A 0 A_StartSound("weapons/sshotc")
		SHT2 IJ 1
        SHT2 K 1
		TNT1 A 5
        TNT1 A 0 A_StartSound("weapons/sshotc")
        SHT2 KJIHGFEDCBA 1
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_GiveInventory("PBSSGHasUnloaded",1)
		Goto Ready3
	
	UnloadDualWield:
		TNT1 A 0 A_ClearOverlays(10,11)
		P6SS FED 1
		TNT1 A 0 A_StartSound("weapons/ssg/inspect4", CHAN_AUTO)
		P6SS CBA 1
	RemoveBullets_Right:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",1,1)
		Goto FinishUnload_Right
		TNT1 A 0 {
			A_Takeinventory("SSGAmmoCounter",1);
			A_Giveinventory("NewShell",1);
		}
		Goto RemoveBullets_Right
	
	FinishUnload_Right:
		R6FR ABCDEF 1
		R6FR GH 1
		TNT1 A 0 A_StartSound("weapons/sshotc")
		R6FR IJ 1
		TNT1 A 7
        TNT1 A 0 A_StartSound("weapons/sshotc")
        R6FR JIHGFEDCBA 1
	RemoveBullets_Left:
		TNT1 A 0 A_JumpIfInventory("LeftSSGAmmo",1,1)
		Goto FinishUnload_Left
		TNT1 A 0 {
			A_Takeinventory("LeftSSGAmmo",1);
			A_Giveinventory("NewShell",1);
			A_Giveinventory("PBSSGHasUnloaded",1);
		}
		Goto RemoveBullets_Left
	FInishUnload_Left:
		R6FL ABCDEF 1
		R6FL GH 1
		TNT1 A 0 A_StartSound("weapons/sshotc")
		R6FL IJ 1
		TNT1 A 7
        TNT1 A 0 A_StartSound("weapons/sshotc")
        R6FL JIHGFEDCBA 1
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto Ready3
	
	DualWieldFlashPunching:
		TNT1 A 15
		Stop
	DualWieldFlashKicking:
		P6SK ABCDEFGGGFEDCBA 1 
		Stop
	DualWieldFlashAirKicking:
		P6SK ABCDEFGGGFEDCBA 1
		P6SK AAA 1
		Stop
	DualWieldFlashSlideKicking:
		P6SK ABCDEFGGGGGGGGGGGGGFEDCBA 1
		Stop
	DualWieldFlashSlideKickingStop:
		P6SK FEDCA 1 
		P6SK AAA 1
		Stop
		
	FlashPunching:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "DualWieldFlashPunching")
		SG21 ABCDEFGGGFEDCBA 1 
		Stop
    FlashKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "DualWieldFlashKicking")
		SG20 ABCDEFGGGFEDCBA 1 
		Stop
	FlashAirKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "DualWieldFlashAirKicking")
		SG20 ABCDEFGGGFEDCBA 1
		SHT2 AAA 1
		Stop
	FlashSlideKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "DualWieldFlashSlideKicking")
		SG20 ABCDEFGGGGGGGGGGGGGFEDCBA 1
		Stop
	FlashSlideKickingStop:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "DualWieldFlashSlideKickingStop")
		SG20 FEDCA 1 
		SHT2 AAA 1
		Stop
	}
}
