// --------------------------------------------------------------------------
//
// Sawed-Off (SSG)
//
// --------------------------------------------------------------------------

Actor PBSSGHasUnloaded: Inventory
{
Inventory.maxamount 1
}

Actor RespectSSG: Inventory
{
Inventory.maxamount 1
}

ACTOR DualWieldingSSG: Inventory
{
Inventory.MaxAmount 1
}


Actor PB_SSGFireAnimation1: Inventory
{
	Inventory.maxamount 1
}

Actor PB_SSGFireAnimation2: Inventory
{
	Inventory.maxamount 1
}

ACTOR LeftSSGAmmo : PB_WeaponAmmo 
{
	Inventory.Amount 0
	Inventory.MaxAmount 2 
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 2
	+INVENTORY.IGNORESKILL
	Inventory.Icon "SGN2A0"
}


ACTOR SSGAmmoCounter: PB_WeaponAmmo
{
	Inventory.Amount 0
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 2
	+INVENTORY.IGNORESKILL
	Inventory.Icon "SGN2A0"
}


Actor SSGBarrelHeat: Inventory
{Inventory.maxamount 200}

Actor SSGBarrelHeat2: Inventory
{Inventory.maxamount 200}


ACTOR PB_SSG : PB_weapon //Replaces SuperShotgun
{
	//$Title Double Barreled Shotgun
	//$Category Project Brutality - Weapons
	//$Sprite SGN2A0
	Game Doom
	Weapon.Kickback 50
	Weapon.SelectionOrder 400
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 2
	Weapon.AmmoGive2 0
	weapon.slotnumber 3
	Weapon.AmmoType "PB_Shell"
	Weapon.AmmoType2 "SSGAmmoCounter"
	PB_WeaponBase.AmmoTypeLeft "LeftSSGAmmo"
	+FLOORCLIP
	+DONTGIB
	+WEAPON.NOAUTOAIM
	Tag "Sawed-Off Shotgun"
	+WEAPON.NOAUTOFIRE
	Inventory.MaxAmount 3
	Inventory.PickupMessage "Sawed-Off Shotgun (Slot 3)"
	Obituary "%o was splattered by %k's SSG"
	Inventory.PickupSound "CLIPINQS"
	Scale 0.5
	Inventory.AltHUDIcon "SGN2A0"
	PB_WeaponBase.respectItem "RespectSSG"
	FloatBobStrength 0.5
	PB_WeaponBase.DualWieldToken "DualWieldingSSG"
	States
	{
		
	Steady:
		TNT1 A 1
		Goto Ready

	Spawn:
		VGN2 A 0 NoDelay
		SGN2 A 10 A_PbvpFramework("VGN2")
		"####" "#" 0 A_PbvpInterpolate()
		LOOP
		
	Deselect:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "PlaceBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "PlaceFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "PlaceIceBarrel")
		TNT1 A 0 {
			A_ClearOverlays(10,11);
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(92);
			A_SetInventory("PB_LockScreenTilt",0);
		}
		TNT1 A 0 A_SetInventory("Unloading",0)
		TNT1 A 0 A_SetInventory("SSGSelected",0) 
		
		TNT1 A 0 //A_JumpIfInventory("PB_QuadSG",1,"DeselectUpgrade")
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1,"DeselectAnimationDualWield")
		SG1S ABCD 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectAnimationDualWield:
		P6SS ED 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		P6SS CBA 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	//DeselectUpgrade:
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait

	Select:
		TNT1 A 0 {
			A_ClearOverlays(10,11);
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(92);
			A_SetInventory("PB_LockScreenTilt",0);
		}
		
		TNT1 A 0 PB_ResetBarrelTokens
		
		Goto SelectFirstPersonLegs
	SelectContinue:
		TNT1 A 0 PB_SelectIfUpgrade("PB_QuadSG") //A_SelectWeapon("PB_QuadSG")
		TNT1 A 0 PB_WeapTokenSwitch("SSGSelected")
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait

	Ready:
		TNT1 A 1 PB_SelectIfUpgrade("PB_QuadSG") //A_SelectWeapon("PB_QuadSG")
		TNT1 A 0 A_SetInventory("HasNotPickedUpSSG",0)
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0 PB_RespectIfNeeded
	WeaponRespect:
		TNT1 A 0 {
			A_SetInventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("Ironsights", "Auto");
			A_SetCrosshair(5);
			}
		SG50 EFGHIJKLMNO 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect1", "Auto")
		SG50 PQRSTUVWXYZ 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/open", "Auto")
		SG51 ABCDEFGHIJKLMNOPQRST 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect2", "Auto")
		SG51 UVWXYZ 1 A_DoPBWeaponAction
		SG52 ABCDEFGHIJK 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect2", "Auto")
		SG52 LMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
		SG53 ABCDE 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect3", "Auto")
		SG53 FGHIJKLMNOPQRST 1 A_DoPBWeaponAction
		Goto Ready3
	SelectAnimation:
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "SelectAnimationDualWield")
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect4", "Auto")
		SG1S DCBA 1
	Ready3:
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "ReadyDualWield")
		TNT1 A 0 {
			A_ClearOverlays(10,11);
			A_SetInventory("PB_LockScreenTilt",0);
			PB_HandleCrosshair(92);
			A_SetRoll(0);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1,"ReadyToFireDualWield")
	RealReady:
		TNT1 A 0 PB_SelectIfUpgrade("PB_QuadSG") //A_SelectWeapon("PB_QuadSG")
		SHT2 A 1 {
			PB_CoolDownBarrel(2, 0, 3);
			PB_CoolDownBarrel(-2, 0, 3);
			if (PressingFire() && CountInv("SSGAmmoCounter") > 0 ){
					return state("Fire");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD,CheckUnloaded("PBSSGHasUnloaded"));
		}
		Loop
	BarrelSmoke1:
		TNT1 A 1 {
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, -0.8, 0, 0, 0);
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0.8, 0, 0, 0);
			A_Takeinventory("SSGBarrelHeat",1);
		}
	BarrelSmokeDual1:
		TNT1 A 1 {
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2.4, 0, 0, 0);
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, 4, 0, 0, 0);
			A_Takeinventory("SSGBarrelHeat",1);
		}
	BarrelSmokeDual2:
		TNT1 A 1 {
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, -4, 0, 0, 0);
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, -2.4, 0, 0, 0);
			A_Takeinventory("SSGBarrelHeat2",1);
		}
		Stop
	SelectAnimationDualWield:
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect4", "Auto")
		P6SS ABC 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect4", "Auto")
		P6SS DE 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
	ReadyDualWield:
		TNT1 A 0 PB_SelectIfUpgrade("PB_QuadSG") //A_SelectWeapon("PB_QuadSG")
		TNT1 A 0 {
			A_SetRoll(0);
			PB_HandleCrosshair(92);
			A_SetInventory("PB_LockScreenTilt",0);
			A_SetFiringRightWeapon(False);
			A_SetFiringLeftWeapon(False);
			A_TakeInventory("DualFiring",1);
			if(CountInv("LeftSSGAmmo") < CountInv("SSGAmmoCounter")){
				A_GiveInventory("DualFiring",1);
			}
// 			if(CountInv("PB_Shell")>0){
// 				if(CountInv("LeftSSGAmmo")<=0){
// 					A_GiveInventory("DualFireReload",1);
// 				}
// 				if(CountInv("SSGAmmoCounter")<=0){
// 					A_GiveInventory("DualFireReload",1);
// 				}
// 			}
			A_Overlay(10, "IdleLeft_Overlay", false);
			A_Overlay(11, "IdleRight_Overlay", false);
		}
	DualFireCheck:
		TNT1 A 0 {
			if(CountInv("PB_Shell")>0){
				if(CountInv("LeftSSGAmmo")<=0 || CountInv("SSGAmmoCounter")<=0){
					if(CountInv("LeftSSGAmmo")<=0 && CountInv("SSGAmmoCounter")<=0){
						A_SetInventory("DualFireReload",2);
					} 
					else{
						A_SetInventory("DualFireReload",1);
					}
				}
			}
			
			if(!PB_CanDualWield())
			{
				A_ClearOverlays(10,11);
				A_SetAkimbo(false);
				return state("StopDualWield");
			}
			
			if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() || ((PressingAltfire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringRightWeapon())){ 
				Return State("FireDualBlank");
				}
				Return A_DoPBWeaponAction(WRF_ALLOWRELOAD | WRF_NOFIRE);
			}
	ReadyToFireDualWield:
		TNT1 A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD | WRF_NOFIRE)
		Goto DualFireCheck
// FireDualBlank state made to prevent strange issues with dual SSGs.
	FireDualBlank:
		TNT1 A 0 A_JumpIfInventory("DualFireReload",2,"AltFireDualBlank")
		TNT1 A 12 A_DoPBWeaponAction(WRF_NOSWITCH | WRF_NOFIRE)
		Goto DualFireCheck
// State to prevent strange issues with left fire going straight to reload, only initiated from the jump state above.
	AltFireDualBlank: 
		TNT1 A 0
		Goto ReloadDualWield

	IdleLeft_Overlay:
		P6SS H 1 {
			PB_CoolDownBarrel(13, 0, 3);
			PB_CoolDownBarrel(8, 0, 3);
			if(CountInv("LeftSSGAmmo")<=0 && CountInv("SSGAmmoCounter")>0){
				A_GiveInventory("DualFiring",1);
			}
			if((PressingAltfire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire") >=2){
					if(CountInv("LeftSSGAmmo") > 0){
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			if(CountInv("DualFiring")==0 || (CountInv("DualFiring")==0 && CountInv("SSGAmmoCounter")<=0) || GetCvar("SingleDualFire")==1){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire") <=1){
					if(CountInv("LeftSSGAmmo") > 0){
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			return state("");
		}
		Loop
	IdleRight_Overlay:
		P6SS G 1 {
			PB_CoolDownBarrel(-13, 0, 3);
			PB_CoolDownBarrel(-8, 0, 3);
			if(CountInv("LeftSSGAmmo")>0 && CountInv("SSGAmmoCounter")<=0){
				A_TakeInventory("DualFiring",1);
			}
			if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringRightWeapon() && GetCvar("SingleDualFire") == 2){
				if(CountInv("SSGAmmoCounter") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			if(CountInv("DualFiring")==1 || (CountInv("DualFiring")==1 && CountInv("LeftSSGAmmo")<=0)){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire")==0){
					if(CountInv("SSGAmmoCounter") > 0){
						return state("FireRight_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			if((PressingAltfire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringRightWeapon()){
				if(CountInv("SSGAmmoCounter")>0 && GetCvar("SingleDualFire")==1){
					return state("FireRight_Overlay");
				}
				if(CountInv("SSGAmmoCounter")>0 && CountInv("LeftSSGAmmo")>0 && GetCvar("SingleDualFire")==0){
					A_Overlay(10, "FireLeft_Overlay", false);
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}

			return state("");
		}
		Loop
	FireLeft_Overlay:
		P6W2 A 1 BRIGHT {
			PB_IncrementHeat(10, true);
			A_GunFlash;
			if(CountInv("LeftSSGAmmo") == 1) {
				A_AlertMonsters;
				//A_FireBullets(9, 6, 10, 7, "ShotgunPuff");
				PB_FireBullets("PB_10GAPellet",10,8,0,0,6);
				A_StartSound("weapons/shh2", CHAN_Weapon, CHANF_DEFAULT, 1.0);
		  PB_DynamicTail("shotgun", "shotgun");
				//A_FireCustomMissile("GunFireSmokE", 0, 0, -3, 0, 0, 0);
				//A_FireCustomMissile("GunFireSmoke", 0, 0, -5, 0, 0, 0);
				PB_GunSmoke(3,0,0);
				PB_GunSmoke(5,0,0);
				A_ZoomFactor(0.95);
				A_SetInventory("PB_SSGFireAnimation2",1); 
				A_SetInventory("PB_SSGFireAnimation1",0); 
				A_TakeInventory("LeftSSGAmmo", 1);
				A_FireProjectile("ShotgunWad",random(-1,1),0,-10,-2,FPF_NOAUTOAIM,random(-1,1));
			}
			else {
				A_AlertMonsters;
				//A_FireBullets (12, 6, 20, 7, "ShotgunPuff");
				PB_FireBullets("PB_10GAPellet_LP",1,0,0,0,0);
				PB_FireBullets("PB_10GAPellet",20,8,0,0,6);
				//A_FireCustomMissile("GunFireSmokE", 0, 0, 3, 0, 0, 0);
				//A_FireCustomMissile("GunFireSmoke", 0, 0, 5, 0, 0, 0);
				PB_GunSmoke(3,0,0);
				PB_GunSmoke(5,0,0);
				A_StartSound("SSHFIRE", CHAN_Weapon, CHANF_DEFAULT, 1.0);
		  PB_DynamicTail("shotgun", "shotgun");
				A_ZoomFactor(0.95);
				A_SetInventory("PB_SSGFireAnimation2",1); 
				A_SetInventory("PB_SSGFireAnimation1",0); 
				A_TakeInventory("LeftSSGAmmo", 2);
				A_FireProjectile("ShotgunWad",random(-1,1),0,-10,-2,FPF_NOAUTOAIM,random(-1,1));
				A_FireProjectile("ShotgunWad",random(-1,1),0,-6,-2,FPF_NOAUTOAIM,random(-1,1));
			}
		}
		P6W2 B 1 BRIGHT {
			//A_FireCustomMissile("GunFireSmokE", 0, 0, 3, 0, 0, 0);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, 5, 0, 0, 0);
			PB_GunSmoke(-3,0,0);
			PB_GunSmoke(-5,0,0);
			}
		P6W2 C 1 {
			A_ZoomFactor(0.975);
			PB_WeaponRecoil(-3.80,+2.40);
		}
		P6W2 D 1 {
			A_ZoomFactor(1.0);
			PB_WeaponRecoil(-3.80,+2.40);
		}
		P6W2 E 1 {
			PB_WeaponRecoil(-3.80,+2.40);
		}
		TNT1 A 0 A_Giveinventory("SSGBarrelHeat2",120)
		P6W2 F 1 {
			//CountInv("DualFiring")==0
			if(CountInv("LeftSSGAmmo")<=0 || CountInv("SSGAmmoCounter")>0){
				A_GiveInventory("DualFiring",1);
			}
		}
		P6W2 GHIJK 1
		TNT1 A 0 {
			if(CountInv("LeftSSGAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleLeft_Overlay
		
	FireRight_Overlay:
		P6W1 A 1 BRIGHT {
			PB_IncrementHeat(10);
			A_GunFlash;
			if(CountInv("SSGAmmoCounter") == 1){
				A_AlertMonsters;
				//A_FireBullets(9, 6, 10, 7, "ShotgunPuff");
				PB_FireBullets("PB_10GAPellet",10,8,0,0,6);
				A_StartSound("weapons/shh2", CHAN_Weapon, CHANF_DEFAULT, 1.0);
		  PB_DynamicTail("shotgun", "shotgun");
				//A_FireCustomMissile("GunFireSmokE", 0, 0, -3, 0, 0, 0);
				//A_FireCustomMissile("GunFireSmoke", 0, 0, -5, 0, 0, 0);
				PB_GunSmoke(-3,0,0);
				PB_GunSmoke(-5,0,0);
				A_ZoomFactor(0.95);
				A_SetInventory("PB_SSGFireAnimation1",1);
				A_TakeInventory("SSGAmmoCounter", 1);
				A_FireProjectile("ShotgunWad",random(-1,1),0,6,-2,FPF_NOAUTOAIM,random(-1,1));
			}
			else {
				A_AlertMonsters;
				//A_FireBullets (12, 6, 20, 7, "ShotgunPuff");
				PB_FireBullets("PB_10GAPellet_LP",1,0,0,0,0);
				PB_FireBullets("PB_10GAPellet",20,8,0,0,6);
				//A_FireCustomMissile("GunFireSmokE", 0, 0, -3, 0, 0, 0);
				//A_FireCustomMissile("GunFireSmoke", 0, 0, -5, 0, 0, 0);
				PB_GunSmoke(-3,0,0);
				PB_GunSmoke(-5,0,0);
				A_StartSound("SSHFIRE", CHAN_Weapon, CHANF_DEFAULT, 1.0);
		  PB_DynamicTail("shotgun", "shotgun");
				A_ZoomFactor(0.95);
				A_SetInventory("PB_SSGFireAnimation1",1);
				A_TakeInventory("SSGAmmoCounter", 2);
				A_FireProjectile("ShotgunWad",random(-1,1),0,10,-2,FPF_NOAUTOAIM,random(-1,1));
				A_FireProjectile("ShotgunWad",random(-1,1),0,6,-2,FPF_NOAUTOAIM,random(-1,1));
			}
		}
		P6W1 B 1 BRIGHT {
			//A_FireCustomMissile("GunFireSmokE", 0, 0, -3, 0, 0, 0);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, -5, 0, 0, 0);
			PB_GunSmoke(-3,0,0);
			PB_GunSmoke(-5,0,0);
			}
		P6W1 C 1 {
			A_ZoomFactor(0.975);
			PB_WeaponRecoil(-3.80,-2.40);
		}
		P6W1 D 1 {
			A_ZoomFactor(1.0);
			PB_WeaponRecoil(-3.80,-2.40);
		}
		P6W1 E 1 {
			PB_WeaponRecoil(-3.80,-2.40);
		}
		P6W1 F 1 {
			if(CountInv("LeftSSGAmmo")>0 || CountInv("SSGAmmoCounter")<=0){
				A_TakeInventory("DualFiring",1);
			}
		}
		P6W1 GHIJK 1
		TNT1 A 0 A_Giveinventory("SSGBarrelHeat",120)
		TNT1 A 0 {
			if(CountInv("SSGAmmoCounter")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleRight_Overlay
		
	WeaponSpecial:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "IdleBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "IdleFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "IdleIceBarrel")
		TNT1 A 0 {
			A_SetInventory("GoWeaponSpecialAbility",0);
			A_SetInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
			PB_HandleCrosshair(92);
			}
		TNT1 A 0 A_JumpIfInventory("PB_SSG", 2,"SwitchToDualWield")
		TNT1 A 0 A_Print("You need two sawed-offs to dual wield!")
		Goto Ready3
		
	SwitchToDualWield:
		TNT1 A 0 {
			A_PlaySoundEx("Ironsights", "Auto");
			if (A_CheckAkimbo()) {
				A_SetAkimbo(False);
				A_SetInventory("DualWieldingSSG", 0);
				return state("SwitchFromDualWield");
			}
			else {
				A_SetAkimbo(True);
				A_SetInventory("DualWieldingSSG", 1);
				return state("");
			}
		}
		SG3S ABCD 1 
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect4", "Auto")
		SG3S EFGHIJ 1
		Goto ReadyDualWield
	
	StopDualWield:
		TNT1 A 0 A_SetAkimbo(False)
		TNT1 A 0 A_SetInventory("DualWieldingSSG", 0)
	SwitchFromDualWield:
		SG3S JIHGFE 1
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect4", "Auto")
		SG3S DCBA 1 
		Goto Ready3
		
	
	Fire:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "ThrowBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "ThrowFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "ThrowIceBarrel")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(92);
			A_SetInventory("PB_LockScreenTilt",0);
		}
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "DualFireCheck")
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter", 2, 2)
		TNT1 A 0 A_PlaySoundEx("weapons/empty", "Weapon")
		Goto AltFire2
		TNT1 A 0 PB_FireBullets("PB_10GAPellet_LP",1,0,0,0,0)
		SHO9 A 1 BRIGHT {
			PB_IncrementHeat(10);
			PB_IncrementHeat(10, true);
			A_GunFlash;
			//A_FireBullets (12, 6, 20, 7, "ShotgunPuff");
			PB_FireBullets("PB_10GAPellet",20,8,0,0,6);
			//A_FireCustomMissile("GunFireSmokE", 0, 0, 2, 0, 0, 0);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 0, 0, 0);
			PB_GunSmoke(-2,0,0);
			PB_GunSmoke(2,0,0);
			A_StartSound("SSHFIRE", CHAN_Weapon, CHANF_DEFAULT, 1.0);
		  PB_DynamicTail("shotgun", "dbshotgun");
			A_ZoomFactor(0.95);
			A_TakeInventory("SSGAmmoCounter", 2);
			A_Giveinventory("SSGBarrelHeat",120);
			A_AlertMonsters;
			}
		SHO9 B 1 BRIGHT {
			//A_FireCustomMissile("GunFireSmokE", 0, 0, 2, 0, 0, 0);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 0, 0, 0);
			PB_GunSmoke(2,0,0);
			PB_GunSmoke(-2,0,0);
			A_FireProjectile("ShotgunWad",random(-2,2),0,3,-4,FPF_NOAUTOAIM,random(-2,2));
			A_FireProjectile("ShotgunWad",random(-2,2),0,-3,-4,FPF_NOAUTOAIM,random(-2,2));
			}
		SHO8 C 1 {
			A_ZoomFactor(0.975);
			PB_WeaponRecoil(-2.20,+0.40);
			}
		SHO8 D 1 {
			A_ZoomFactor(1.0);
			PB_WeaponRecoil(-2.20,+0.40);
			}
		SHO8 EFGHIJJKLM 1 
		TNT1 A 0 A_JumpIfInventory("PB_Shell", 1, "Reload")
		Goto Ready3
		
	AltFire:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "PlaceBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "PlaceFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "PlaceIceBarrel")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(92);
			A_SetInventory("PB_LockScreenTilt",0);
		}
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "DualFireCheck")
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter", 2, 1)
		Goto AltFire2
		SHTA A 1 BRIGHT {
			A_GunFlash;
			PB_IncrementHeat(10);
			//A_FireBullets(9, 6, 10, 7, "ShotgunPuff");
			PB_FireBullets("PB_10GAPellet",10,7,0,0,6);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, 2, 0, 0, 0);
			PB_GunSmoke(-2,0,0);
			A_StartSound("weapons/shh2", CHAN_Weapon, CHANF_DEFAULT, 1.0);
		  PB_DynamicTail("shotgun", "shotgun");
			A_TakeInventory("SSGAmmoCounter", 1);
			A_ZoomFactor(0.98);
			}
		SHTA B 1 BRIGHT {
			A_ZoomFactor(0.99);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, 2, 0, 0, 0);
			PB_GunSmoke(-2,0,0);
			A_FireProjectile("ShotgunWad",random(-2,2),0,3,-4,FPF_NOAUTOAIM,random(-2,2));
			PB_WeaponRecoil(-1.50,+1.0);
		}
		SHO8 C 1 {
			A_ZoomFactor(1.0);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, 2, 0, 0, 0);
			PB_GunSmoke(-2,0,0);
			PB_WeaponRecoil(-1.50,+1.0);
		}
		SHO8 LCM 1
		SHT2 A 2
		TNT1 A 0 PB_ReFire("AltFire2")
		Goto Ready3
		
	AltFire2:
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FireDualBlank")
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter", 1, 1)
		Goto Reload
		SHTA C 1 BRIGHT {
			A_GunFlash;
			PB_IncrementHeat(10, true);
			//A_FireBullets(9, 6, 10, 7, "ShotgunPuff");
			PB_FireBullets("PB_10GAPellet",10,7,0,0,6);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 0, 0, 0);
			PB_GunSmoke(2,0,0);
			A_StartSound("weapons/shh2", CHAN_Weapon, CHANF_DEFAULT, 1.0);
		  PB_DynamicTail("shotgun", "shotgun");
			A_TakeInventory("SSGAmmoCounter", 1);
			A_ZoomFactor(0.98);
			}
		SHTA D 1 BRIGHT {
			A_ZoomFactor(0.99);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 0, 0, 0);
			PB_GunSmoke(2,0,0);
			A_FireProjectile("ShotgunWad",random(-2,2),0,-4,-4,FPF_NOAUTOAIM,random(-2,2));
			PB_WeaponRecoil(-1.50,-1.0);
		}
		SHO8 C 1 {
			A_ZoomFactor(1.0);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 0, 0, 0);
			PB_GunSmoke(2,0,0);
			PB_WeaponRecoil(-1.50,-1.0);
		}
		SHO8 LCM 1
		Goto Reload
		
	Reload:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "IdleBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "IdleFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "IdleIceBarrel")
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "ReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter", 2, "Ready3")
		TNT1 A 0 A_JumpIfInventory("PB_Shell", 1, 2)
		TNT1 A 0 A_SetInventory("SSGAlt", 0)
		Goto Ready3
		TNT1 A 0 {
			A_SetInventory("PB_LockScreenTilt",1);
			A_SetInventory("PB_SSGFireAnimation1",0);
			A_SetInventory("PB_SSGFireAnimation2",0);
			A_SetCrosshair(5);
			}
		TNT1 A 0 A_DoPBWeaponAction(WRF_NOFIRE)
		SG10 ABCDEFG 1 A_SetRoll(roll+0.5)
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/open", "Auto")
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter", 1, 1)
		Goto FullReload
	HalfReload:
		TNT1 A 0 PB_SpawnCasing("ShotgunCasing",15,3,30,-1,4,4)
		SG12 AB 1 A_SetRoll(roll-1.0)
		SG12 CDEFGHIJ 1
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect2", "Auto")
		SG12 KL 1 A_SetRoll(roll+2.0)
		SG12 MNO 1 A_SetRoll(roll-1.0)
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect3", "Auto")
		SG10 WXY 1 A_SetRoll(roll-1.0)
		SG10 Z 1
		Goto InsertBullets
	FullReload:
		TNT1 A 0 A_JumpIfInventory("PBSSGHasUnloaded",1,3)
		TNT1 A 0 PB_SpawnCasing("ShotgunCasing",15,3,30,-1,4,4)
		TNT1 A 0 PB_SpawnCasing("ShotgunCasing",14,3,30,-1,5,4)
		SG10 HI 1 A_SetRoll(roll-1.0)
		SG10 JKLMNOPQ 1 
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect2", "Auto")
		SG10 RS 1 A_SetRoll(roll+2.0)
		SG10 TUV 1 A_SetRoll(roll-1.0)
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect3", "Auto")
		SG10 WXY 1 A_SetRoll(roll-1.0)
		SG10 Z 1
		Goto InsertBullets
		
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",2,"FinishReload")
		TNT1 A 0 A_JumpIfInventory("PB_Shell",1,1)
		Goto FinishReloadOneShell
		TNT1 A 0 {
			A_Giveinventory("SSGAmmoCounter",1);
			A_Takeinventory("PB_Shell",1);
			}
		Loop
	FinishReload:
		TNT1 A 0 A_SetInventory("PBSSGHasUnloaded",0)
		SG11 ABCDEFGHIJKLMNOPQRST 1
		SHT2 A 1 PB_ReFire
		Goto Ready3
	FinishReloadOneShell:
		TNT1 A 0 A_SetInventory("PBSSGHasUnloaded",0)
		SG11 ABC 1
		SG12 YZ 1
		SG11 FGHIJKLMNOPQRST 1
		SHT2 A 1 PB_ReFire
		Goto Ready3
	
	NoAmmoDualWield:
		TNT1 A 1
		Goto Ready3
		
	ReloadDualWield:
		TNT1 A 0 A_JumpIf((CountInv("SSGAmmoCounter")==2 && CountInv("LeftSSGAmmo")==2), "ReadyDualWield")
		TNT1 A 0 A_JumpIfInventory("PB_Shell", 1, 1)
		Goto NoAmmoDualWield
		TNT1 A 0 A_ClearOverlays(10,11)
		P6SS ED 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect4", "Auto")
		P6SS CBA 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
	ReloadRight:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",2,"ReloadDualWieldContinued")
		SGAR ABCDEFGHIJLMNOP 1
		TNT1 A 0 A_PlaySound("weapons/ssg/open")
		TNT1 A 0 PB_SpawnCasing("ShotgunCasing",15,3,30,-1,4,4)
		TNT1 A 0 A_JumpIfInventory("PBSSGHasUnloaded",1,4)
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",1,"HalfReloadRight")
		TNT1 A 0 PB_SpawnCasing("ShotgunCasing",14,3,30,-1,5,4)
		SG10 HIJKLMNO 1 
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect2", "Auto")
		SG10 PQ 1 A_SetRoll(roll+2.0)
		SG10 RSTUVW 1 A_SetRoll(roll-1.0)
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect3", "Auto")
		SG10 XYZ 1 A_SetRoll(roll-1.0)
		SG11 CDEFGHIJKLMNOPQRST 1

	RightInsertBullets:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",2,"ReloadDualWieldContinued")
		TNT1 A 0 A_JumpIfInventory("PB_Shell",1,1)
		Goto FinishReloadDualWield
		TNT1 A 0 {
			A_Giveinventory("SSGAmmoCounter",1);
			A_Takeinventory("PB_Shell",1);
			}
		Loop
	ReloadDualWieldContinued:
		TNT1 A 0 A_JumpIf(CountInv("PB_Shell")<=0,"FinishReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("LeftSSGAmmo",2,"FinishReloadDualWield")
	ReloadLeft:
		SGAL ABCDEFGHIJLMNOP 1
		TNT1 A 0 A_PlaySound("weapons/ssg/open")
		TNT1 A 0 PB_SpawnCasing("ShotgunCasing",15,-3,30,-1,-4,4)
		TNT1 A 0 PB_SpawnCasing("ShotgunCasing",14,-3,30,-1,-5,4)
		TNT1 A 0 A_JumpIfInventory("PBSSGHasUnloaded",1,4)
		TNT1 A 0 A_JumpIfInventory("LeftSSGAmmo",1,"HalfReloadLeft")
		SGAL QRSTUVWX 1 
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect2", "Auto")
		SGAL YZ 1 A_SetRoll(roll+2.0)
		S1AL ABCDEF 1 A_SetRoll(roll-1.0)
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect3", "Auto")
		S1AL GHI 1 A_SetRoll(roll-1.0)
		S1AL JKLMNOPQRSTUVWXYZ 1
		
	LeftInsertBullets:
		TNT1 A 0 A_JumpIfInventory("LeftSSGAmmo",2,"FinishReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("PB_Shell",1,1)
		Goto FinishReloadDualWield
		TNT1 A 0 {
			A_Giveinventory("LeftSSGAmmo",1);
			A_Takeinventory("PB_Shell",1);
			}
		Loop
	FinishReloadDualWield:
		TNT1 A 0 A_SetInventory("DualFireReload",0)
		TNT1 A 0 A_SetInventory("PBSSGHasUnloaded",0)//So that the unload function won't break when in dual wield mode
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect4", "Auto")
		P6SS ABC 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect4", "Auto")
		P6SS DE 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
		Goto ReadyDualWield
		
	Unload:
		TNT1 A 0 A_SetInventory("Unloading",0)
		TNT1 A 0 A_JumpIfInventory("PBSSGHasUnloaded", 1, "Ready3")
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "UnloadDualWield")
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",1,1)
		Goto Ready3
		SG10 A 1
		SG10 B 1
		SG10 C 1
		SG10 DEF 1
		TNT1 A 0 A_PlaySound("weapons/ssg/open", 0)
		SG10 Y 1
		SG10 X 1 
		SG10 W 1 
		SG10 V 1 
		TNT1 A 0 A_PlaySound("weapons/ssg/inspect2", 0)
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",2,2)
		SG10 USRQPONMLK 1
		SG10 J 5
		SG10 I 1
		TNT1 A 0 A_PlaySound("weapons/ssg/inspect3", 0)
		SG10 HGFEA 1
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",1,1)
		Goto FinishUnload
		TNT1 A 0 {
			A_Takeinventory("SSGAmmoCounter",1);
			A_Giveinventory("PB_Shell",1);
		}
		Goto RemoveBullets
	FInishUnload:
		TNT1 A 0 A_SetInventory("Unloading",0)
		TNT1 A 0 A_SetInventory("PBSSGHasUnloaded",1)
		Goto Ready3
	
	UnloadDualWield:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",1,2)
		TNT1 A 0 A_JumpIfInventory("LeftSSGAmmo",1,1)
		Goto Ready3
		TNT1 A 0 A_ClearOverlays(10,11)
		P6SS ED 1 A_SetPitch(pitch-0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("weapons/ssg/inspect4", "Auto")
		P6SS CBA 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
	RemoveBullets_Right:
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",1,1)
		Goto FinishUnload_Right
		TNT1 A 0 {
			A_Takeinventory("SSGAmmoCounter",1);
			A_Giveinventory("PB_Shell",1);
		}
		Goto RemoveBullets_Right
	
	FinishUnload_Right:
		SGAR ABCDEFGHIJKLMN 1 
		SG11 D 1
		TNT1 A 0 A_PlaySound("weapons/ssg/open", 0)
		SG10 YXWV 1
		TNT1 A 0 A_PlaySound("weapons/ssg/inspect2", 0)
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",2,2)
		SG10 UTSRQPONML 1
		SG10 KJIH 1
		SGAR BCDDCBA 1
		TNT1 A 0 A_PlaySound("weapons/ssg/inspect3", 0)
	RemoveBullets_Left:
		TNT1 A 0 A_JumpIfInventory("LeftSSGAmmo",1,1)
		Goto FinishUnload_Left
		TNT1 A 0 {
			A_Takeinventory("LeftSSGAmmo",1);
			A_Giveinventory("PB_Shell",1);
			A_SetInventory("PBSSGHasUnloaded",1);
		}
		Goto RemoveBullets_Left
	FinishUnload_Left:
		SGAL ABCDEFGHIJKLMN 1 
		S1AL M 1
		TNT1 A 0 A_PlaySound("weapons/ssg/open", 0)
		S1AL HGFE 1
		TNT1 A 0 A_PlaySound("weapons/ssg/inspect2", 0)
		TNT1 A 0 A_JumpIfInventory("SSGAmmoCounter",2,2)
		S1AL DCBA 1
		SGAL ZYXWVU 1
		SGAL TSRQBCDDCBA 1
		TNT1 A 0 A_PlaySound("weapons/ssg/inspect3", 0)
		TNT1 A 0 A_SetInventory("Unloading",0)
		Goto Ready3
	
	DualWieldFlashPunching:
		TNT1 A 15
		STOP
	DualWieldFlashKicking:
		P6SK ABCDEFGGGFEDCBA 1
		Goto Ready3
	DualWieldFlashAirKicking:
		P6SK ABCDEFGGGFEDCBA 1
		P6SK AAA 1
		Goto Ready3
	DualWieldFlashSlideKicking:
		P6SK ABCDEFGGGGGGGGGGGGGFEDCBA 1
		Goto Ready3
	DualWieldFlashSlideKickingStop:
		P6SK FEDCA 1 
		P6SK AAA 1
		Goto Ready3
		
	FlashPunching:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelPunching")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelPunching")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelPunching")
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "DualWieldFlashPunching")
		SG21 ABCDEFGGGFEDCBA 1 
		Stop
	FlashKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "DualWieldFlashKicking")
		SG20 ABCDEFGGGFEDCBA 1 
		Goto Ready3
	FlashAirKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelAirKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelAirKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelAirKicking")	
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "DualWieldFlashAirKicking")
		SG20 ABCDEFGGGFEDCBA 1
		SHT2 AAA 1
		Goto Ready3
	FlashSlideKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "DualWieldFlashSlideKicking")
		SG20 ABCDEFGGGGGGGGGGGGGFEDCBA 1
		Goto Ready3
	FlashSlideKickingStop:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSSG", 1, "DualWieldFlashSlideKickingStop")
		SG20 FEDCA 1 
		SHT2 AAA 1
		Goto Ready3
	}
}
