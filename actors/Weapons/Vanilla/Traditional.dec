ACTOR Traditional_Fist : Weapon //replaces Fist
{
	Game Doom
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	//Weapon.SelectionOrder 3700
	Obituary "$OB_MPFIST"
	+WEAPON.WIMPY_WEAPON
    +WEAPON.NOAUTOAIM
	+WEAPON.MELEEWEAPON
	Tag "Fists"
	States
	{
	Ready:
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		0UNG A 1 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		Loop
	Deselect:
		TNT1 A 0 A_TakeInventory("FistsSelected",1)
		0UNG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		TNT1 A 0 A_GiveInventory("FistsSelected",1)
		0UNG A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	
	Steady:
		TNT1 A 1
		Goto Ready

	Fire:
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"SuperFire")
		0UNS A 1 
		TNT1 A 0 A_PlaySound("weapons/fistwhoosh",0)
		TNT1 A 0 A_TakeInventory("Kicking",1)
		TNT1 A 0 A_TakeInventory("Punching",1)
		TNT1 A 0 A_TakeInventory("FistHit",1)
		TNT1 A 0 A_Recoil(-2)
		TNT1 A 0 A_ZoomFactor(0.99)
		0UNG AB 1 
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0)
		TNT1 AA 0 A_FireCustomMissile("MeleeStrike1", 0, 0, 0, 0)
		TNT1 A 0 //A_SetPitch(+3 + pitch)
		0UNG CD 1 
		TNT1 A 0 A_ZoomFactor(0.985)
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0 //A_SetPitch(-3 + pitch)
		0UNG F 2 A_ZoomFactor(0.98)
		TNT1 A 0 A_PlaySound("weapons/fistwhoosh",0)
		0UN4 AB 1 
		TNT1 A 0 A_ZoomFactor(0.985)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0)
		TNT1 AA 0 A_FireCustomMissile("MeleeStrike1", 0, 0, 0, 0)
		TNT1 A 0 //A_SetPitch(+3 + pitch)
		0UN4 CD 1 
		TNT1 A 0 A_ZoomFactor(0.99)
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0// A_SetPitch(-3 + pitch)
		0UN4 E 1 
		0UN4 FG 1 
		0UNS B 1 A_ZoomFactor(1.0)
		0UNS A 4 
		0UNS A 4 A_Refire
		Goto Ready
	
	SuperFire:
		0UNS A 1 
		TNT1 A 0 A_PlaySound("weapons/ultrwhoosh",0)
		TNT1 A 0 A_TakeInventory("Kicking",1)
		TNT1 A 0 A_TakeInventory("Punching",1)
		TNT1 A 0 A_TakeInventory("FistHit",1)
		TNT1 A 0 A_Recoil(-2)
		TNT1 A 0 A_ZoomFactor(0.99)
		0UNG AB 1 
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0)
		TNT1 AAA 0 A_FireCustomMissile("MeleeStrike1Smash", 0, 0, 0, 0)
		PUNG AA 0 A_FireCustomMissile("Traditional_MeleeStrike", 0, 0, 0, 0)
		TNT1 A 0 //A_SetPitch(+3 + pitch)
		0UNG CD 1 
		TNT1 A 0 A_ZoomFactor(0.985)
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0 //A_SetPitch(-3 + pitch)
		0UNG F 2 A_ZoomFactor(0.98)
		TNT1 A 0 A_PlaySound("weapons/ultrwhoosh",0)
		0UN4 AB 1 
		TNT1 A 0 A_ZoomFactor(0.985)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0)
		PUNG AAA 0 A_FireCustomMissile("Traditional_MeleeStrike", 0, 0, 0, 0)
		TNT1 A 0 A_Jump(192, 4) //Occassionally knock back enemies
		TNT1 AAA 0 A_FireCustomMissile("MeleeStrike1Smash", 0, 0, 0, 0)
		TNT1 AAA 0 //A_SetPitch(+3 + pitch)
		0UN4 CD 1 
		TNT1 A 0 A_ZoomFactor(0.99)
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0// A_SetPitch(-3 + pitch)
		0UN4 E 1 
		0UN4 FG 1 
		0UNS B 1 A_ZoomFactor(1.0)
		0UNS A 4 
		0UNS A 4 A_Refire
		Goto Ready
	}
}

ACTOR Traditional_Pistol : Weapon
{
	Game Doom
	Scale 0.8
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "NewClip"
	//Weapon.SelectionOrder 1900
	Obituary "$OB_MPPISTOL"
	+WEAPON.WIMPY_WEAPON
    +WEAPON.NOAUTOAIM
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	Decal "BulletChip"
	Tag "9mm Pistol"
	States
	{
	Ready:
		0PIG A 1 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		Loop
	Deselect:
		0PIG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		0PIG A 0 bright A_PlaySound("TRPSEL")
		0PIG A 1 A_Raise
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_WeaponOffset(0,32)
		0PIG A 1
		0PIF A 0 bright //A_GunFlash
		0PIF A 0 bright A_FireBullets (2.0, 1.6, 1, 15, "HitPuff")
		TNT1 A 0 A_ZoomFactor(0.983)
		0PIF A 2 bright A_PlaySound("TRPFIRE", CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("Tracer", random(-2,2), 0, -1, 0)
		RIFF A 0 A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 2, 0, 0)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0)
		TNT1 A 0 A_FireCustomMissile("Mp40CaseSpawnRight", 0,0,-1,-2)
		TNT1 AA 0 A_FireCustomMissile("SmokeSpawner",0,0,0,5)
		0PIF CB 2
		0PIG A 3 A_ZoomFactor(1.0)
		0PIG AAA 1 A_ReFire
		Goto Ready
		
 	Spawn:
		DEPI A -1
		Stop
	}
}

ACTOR Traditional_Shotgun : Shotgun //replaces Shotgun
{
	Game Doom
	Scale 0.8
	+FLOORCLIP
	+DONTGIB
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "NewShell"
	//Weapon.SelectionOrder 1300
	Inventory.PickupMessage "$GOTSHOTGUN"
	Obituary "$OB_MPSHOTGUN"
	Decal "BulletChip"
    +WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	Tag "Shotgun"
	States
	{
	Ready:
		0SGG A 1 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		Loop
	Deselect:
		0SGG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
        TNT1 A 0 A_PlaySound("weapons/sgmvpump")
		0SGG A 1 A_Raise
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 0 A_ZoomFactor(0.97)
        TNT1 A 0 A_PlaySound("Weapons/AutoShotgun", 5)
		TNT1 A 0 A_FireCustomMissile("YellowFlareSpawn", 0, 0, 0, 0)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer", Random(-5, 5), 0, -1, Random(-5, 5))
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssDouble", 0, 0, 0, 0)
		TNT1 AAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-17,17), 0, -1, random(-17,17))
		RIFF A 0 A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0)
		0SGF A 1 bright A_FireBullets(3.65, 1.85, 10, 12, "ShotgunPuff")
		0SGF A 1 
		TNT1 A 0 A_ZoomFactor(1.0)
		0SGF BCDE 1 bright
		0SGG A 5 //A_PlaySound("H4SGCOCK", 1)
		0SGG BCD 2
		TNT1 A 0 A_PlaySound("H4SGCOCK", 1)
		0SGG E 5 A_FireCustomMissile("ShotCaseSpawn",0,0,9,-15)
		0SGG DCB 2
		0SGG A 1 
		0SGG A 1 A_WeaponReady
		0SGG AAAA 1 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 2 Bright A_Light2
		TNT1 A 2 Bright A_Light1
		Goto LightDone
	Spawn:
		0ESG A -1
		Stop
	}
}

ACTOR Traditional_SSG : DoomWeapon //replaces SuperShotgun
{
	Game Doom
	Scale 0.8
	+FLOORCLIP
	+DONTGIB
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse 2
	Weapon.AmmoGive 8
	Weapon.AmmoType "NewShell"
	//Weapon.SelectionOrder 400
	Inventory.PickupMessage "$GOTSHOTGUN2"
	Obituary "$OB_MPSSHOTGUN"
	Decal "BulletChip"
    +WEAPON.NOAUTOAIM
	Tag "Super Shotgun"
	States
	{
	Ready:
		0SSG A 1 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		Wait
	Deselect:
		0SSG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
        TNT1 A 0 A_PlaySound("CLIPINSS")
		0SSG A 1 A_Raise
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 0 A_PlaySound("SSHFIRE", 8)
        SHO9 A 0 A_FireBullets(0, 0, 1, 50, "shotpuff", 0, 180)
        TNT1 AAAAAAAAAAA 0 A_FireCustomMissile("Tracer", Random(-7, 7), 0, -1, Random(-7, 7))
		TNT1 AA 0 A_FireCustomMissile("YellowFlareSpawn", 15, 0, 0, 0)
		RIFF A 0 A_FireCustomMissile("GunFireSmoke", 0, 0, -2, 1, 0, 0)
		RIFF A 0 A_FireCustomMissile("GunFireSmoke", 0, 0, 2, 1, 0, 0)
		TNT1 A 0 A_ZoomFactor(0.96)
		0SSF A 1 bright A_FireBullets(11, 6, 22, 10, "ShotgunPuff")
		TNT1 A 0 A_ZoomFactor(0.80)
		0SSF B 2 bright
		TNT1 A 0 A_ZoomFactor(1.0)
		0SSF CJKL 2
		0SSG B 2 A_CheckReload
		0SSG CD 2
		0SSG E 2 A_PlaySound("weapons/ssg/open", CHAN_AUTO)
		TNT1 AA 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-15,-23)
		0SSG FGIJK 2
		0SSG L 2 A_PlaySound("weapons/ssg/in", CHAN_WEAPON)
		0SSG MNOP 2
		0SSG Q 2 A_PlaySound("weapons/ssg/in", CHAN_WEAPON)
		0SSG RS 2
		0SSG T 2 A_PlaySound("weapons/ssg/close", CHAN_WEAPON)
		0SSG UC 2
		0SSG AAAAAA 1 A_ReFire
		0SSG AAAAAA 1 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 1 Bright A_Light1
		TNT1 A 2 Bright A_Light2
		TNT1 A 2 Bright A_Light1
		Goto LightDone
	Spawn:
		0ESS A -1
		Stop
	}
}

ACTOR Traditional_ChainGun : Chaingun
{
	Game Doom
	Scale 0.8
	+FLOORCLIP
	+DONTGIB
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "NewClip"
    +WEAPON.NOAUTOAIM
	//Weapon.SelectionOrder 700
	Inventory.PickupMessage "You got the Chain Gun!"
	Obituary "$OB_MPGATLINGGUN"
	Decal "BulletChip"
	Tag "Chain Gun"
	States
	{
	Ready:
		0GTG C 1 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		Loop
	Deselect:
		TNT1 C 0 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_Takeinventory("RandomHeadExploder",1)
		0GTG C 1 A_Lower
		TNT1 A 0 A_Lower
		Wait
	Select:
        TNT1 A 0 A_PlaySound("CHAINUP")
		TNT1 A 0 A_Giveinventory("RandomHeadExploder",1)
		0GTG C 1 A_Raise
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_WeaponOffset(0,32)
		0GTG D 2 A_PlaySound("CHA1NSTA", CHAN_BODY)
		0GTG AB 1
		0GTG CD 1
	F1:
		0GTG ABCD 1
		0GTF A 0 A_PlaySound("CHA1NSPI", CHAN_BODY, 1.0, 1)
	Hold:
		0GTF A 0 A_GunFlash
		0GTF A 0 A_PlaySound("weapons/gatlingfire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)
		TNT1 A 0 A_FireCustomMissile("YellowFlareSpawn",0,0,0,0)
		0GTF A 1 bright A_FireBullets(2.75, 2.5, 1, 18, "MachineGunBulletPuff", 1)
		TNT1 A 0 A_Zoomfactor(0.98)
		CHAF A 0 BRIGHT A_FireCustomMissile("MinigunTracer", random(-2,2), 0, -1, random(-3,3))
		RIFF A 0 A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,-12,-18)
		0GTG BCD 1
		TNT1 A 0 A_Zoomfactor(1.0)
		0GTG D 0 A_JumpIfNoAmmo("WindDown")
		0GTG D 0 A_ReFire
	WindDown:
		0GTG A 0 A_PlaySound("CHA1NSTO", CHAN_BODY)
		0GTG AA 1 A_ReFire("Hold")
		RIFF A 0 A_FireCustomMissile("SmokeSpawner11", 0, 0, 0, 1, 0, 0)
		0GTG BB 1 A_ReFire("Hold")
		RIFF A 0 A_FireCustomMissile("SmokeSpawner11", 0, 0, 0, 1, 0, 0)
		0GTG CC 1 A_ReFire("Hold")
		RIFF A 0 A_FireCustomMissile("SmokeSpawner11", 0, 0, 0, 1, 0, 0)
		0GTG A 0 A_JumpIfNoAmmo(2)
		0GTG A 0 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH)
		RIFF A 0 A_FireCustomMissile("SmokeSpawner11", 0, 0, 0, 1, 0, 0)
		0GTG D 1 A_ReFire("F1")
		RIFF A 0 A_FireCustomMissile("SmokeSpawner11", 0, 0, 0, 1, 0, 0)
		0GTG D 1 A_ReFire("F1")
		0GTG AAABBBCCC 1 A_ReFire("F1")
		0GTG A 0 A_JumpIfNoAmmo(2)
		0GTG A 0 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH)
		0GTG DDDDAAAABBBB 1 A_ReFire("F1")
		0GTG CC 1 A_ReFire("Fire")
		Goto Ready
	Flash:
		TNT1 A 1 Bright A_Light2
		TNT1 A 1 Bright A_Light1
		Goto LightDone
	Spawn:
		0EGT A -1
		Stop
	}
}

ACTOR Traditional_RocketLauncher : DoomWeapon
{
	Game Doom
	Scale 1.15
	+FLOORCLIP
	+DONTGIB
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
    +WEAPON.NOAUTOAIM
	//Weapon.SelectionOrder 2500
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$GOTLAUNCHER"
	Tag "Rocket Launcher"
	States
	{
	Ready:
		0RLG A 1 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		Loop
	Deselect:
		0RLG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
        TNT1 A 0 A_PlaySound("RLANDRAW")
		0RLG A 1 A_Raise
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_WeaponOffset(0,32)
		0RLG A 0 A_GunFlash
		0RLF A 1 bright
		TNT1 A 0 A_ZoomFactor(0.980)
		TNT1 A 0 A_FireCustomMissile("RedFlareSpawn",-5,0,0,0)
        TNT1 A 0 A_PlaySound ("0SRFIRE", 7)
		TNT1 A 0 A_FireCustomMissile("Rocket", 0, 1, 0, -5)
		0RLF B 2 bright
		TNT1 A 0 A_ZoomFactor(0.985)
		0RLF C 2 BRIGHT 
		TNT1 A 0 A_ZoomFactor(0.99)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssDouble", 0, 0, 0, 0)
		0RLF DE 2
		TNT1 A 0 A_ZoomFactor(1.0)
		0RLG B 1 A_FireCustomMissile("SmokeSpawner11",0,0,0,0)
		0RLG C 3
		TNT1 A 0 A_PlaySound("RLCYC", 5)
		0RLG A 4 
		0RLG A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 2 Bright A_Light2
		TNT1 A 2 Bright A_Light(3)
		TNT1 A 2 Bright A_Light2
		TNT1 A 2 Bright A_Light1
		Goto LightDone
	Spawn:
		0ERL A -1
		Stop
	}
}

ACTOR Traditional_PlasmaRifle : DoomWeapon
{
	Game Doom
	Scale 0.95
	+FLOORCLIP
	+DONTGIB
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
    +WEAPON.NOAUTOAIM
	//Weapon.SelectionOrder 100
	Inventory.PickupMessage "$GOTPLASMA"
	Tag "Plasma Rifle"
	States
	{
	Ready:
		0P1G A 1 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		Loop
	Deselect:
		TNT1 A 0 A_Takeinventory("HasPlasmaWeapon",1)
		0P1G A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
        TNT1 A 0 A_PlaySound("PLSDRAW")
		TNT1 A 0 A_Giveinventory("HasPlasmaWeapon",1)
		0P1G A 1 A_Raise
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_WeaponOffset(0,32)
        TNT1 A 0 A_PlaySound("P1SM9",3)
		0P1G A 0 A_FireCustomMissile("Plasma_Ball", 0, 1, 0, 0)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0)
		0P1G A 0 A_Jump(128,"Fire2")
	Fire1:
		0P1F A 1 bright A_ZoomFactor(.980)
		0P1F B 1 bright A_ZoomFactor(.990)
		Goto FireEnd
	Fire2:
		0P1F C 1 bright A_ZoomFactor(.980)
		0P1F D 1 bright A_ZoomFactor(.990)
	FireEnd:
		0P1G A 1 A_ZoomFactor(1.0)
		0P1G B 2 A_ReFire
		0P1S FGHIJK 1
        TNT1 A 0 A_PlaySound("PLSCOOL",1)
		TNT1 A 0 A_Playsound("PLSCO0L",2)
		0P1S LLLLL 3 A_FireCustomMissile("SmokeSpawner",0,0,0,5)
		0P1S KJIHGF 1
		Goto Ready
	Spawn:
		0P1L A -1
		Stop
	}
}

ACTOR Traditional_BFG : DoomWeapon
{
	Game Doom
	+FLOORCLIP
	+DONTGIB
	Scale 1.05
	Height 20
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse 40
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
    +WEAPON.NOAUTOAIM
	//Weapon.SelectionOrder 2800
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$GOTBFG9000"
	Tag "BFG 9000"
	States
	{
	Ready:
		0BFG A 1 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		Loop
	Deselect:
		0BFG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Playsound("GENREADY")
		0BFG A 1 A_Raise
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 0 A_PlaySound("TRBFGC", 1)
		0BFG B 10 
		0BFC A 2 BRIGHT A_FireCustomMissile("GreenFlareSpawnEvenSmaller",0,0,0,0)
		0BFC B 2 BRIGHT A_FireCustomMissile("GreenFlareSpawnJustSmaller",0,0,0,0)
		0BFC C 2 BRIGHT A_FireCustomMissile("GreenFlareSpawnSmallClose",0,0,0,0)
		TNT1 A 0 A_PlaySound("TRBFGF", 2)
		0BFC DEFEFE 2 BRIGHT A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
		0BFC DEFEFEF 1 BRIGHT A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
		0BFC F 1 Bright A_GunFlash
		TNT1 A 0 A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
		0BFC GHIJ 1 Bright A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
		TNT1 A 0 A_PlaySound("BFG_FIRE", 3)
		TNT1 A 0 A_Zoomfactor(0.94)
		TNT1 AAAAA 0 A_FireCustomMissile("GreenTrailSparks", 0, 0, 0, 0)
		0BFF A 2 Bright A_FireBFG
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssDouble", 0, 0, 0, 0)
		TNT1 A 0 A_Zoomfactor(0.975)
		0BFF B 2 Bright 
		TNT1 A 0 A_Zoomfactor(1.0)
		0BFG CDA 2 Bright
		0BFG A 20 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 5 Bright A_Light1
		TNT1 A 5 Bright A_Light2
		TNT1 A 2 Bright A_Light(3)
		TNT1 A 2 Bright A_Light2
		TNT1 A 2 Bright A_Light1
		Goto LightDone
	Spawn:
		0EBF A -1
		Stop
	}
}
//Code modified from Weapons of Saturn's Saw
ACTOR Traditional_Chainsaw : Weapon
{
	Inventory.PickupMessage "A Chain saw! Find some meat!"
	Inventory.PickupSound "weapons/chainsaw/draw"
	Obituary "%o was disemboweled by %k's Chain saw"
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	+FLOORCLIP
	+DONTGIB
	Decal "ChainsawPuff"
	+WEAPON.MELEEWEAPON
	+WEAPON.WIMPY_WEAPON
	+WEAPON.NOALERT
    +WEAPON.NOAUTOAIM
	//Weapon.SelectionOrder 2200
	Tag "Chainsaw"
	States
	{
	Deselect:
		TNT1 A 0 A_StopsoundEx("Weapon")
		TNT1 A 0 A_Takeinventory("HasCutingWeapon",1)
		KICK A 0 A_Takeinventory("PowerBloodOnVisor",1)
		54S7 G 0 A_PlayWeaponSound ("weapons/chainsaw/lower")
	ReallyDeselect:
		54S7 FEDCBA 1
		TNT1 A 1 A_Lower
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		Wait
	Ready:
        TNT1 A 0 A_PlaySound("DSSAWZIP",2)
		54S7 ABCDEF 1
		TNT1 A 0 A_PlaySound("weapons/chainsaw/start",3)
		TNT1 A 0  //A_Playsound("weapons/chainsaw/stop",8)
	ReadyII:
        TNT1 A 0 A_JumpIfInventory("PowerBloodOnVisor",1,"ReadyII.Blood")
		TNT1 A 0 A_Takeinventory("HasCutingWeapon",1)
		TNT1 A 0 A_FireCustomMissile("SmokeSpawner",0,0,0,-5)
	    TNT1 A 0 A_PlayWeaponSound ("weapons/chainsaw/idle")
		54W7 AB 2 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		54W7 CD 2 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		54W7 EF 2 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		Loop
	ReadyII.Blood:
		TNT1 A 0 A_Takeinventory("HasCutingWeapon",1)
		TNT1 A 0 A_FireCustomMissile("SmokeSpawner",0,0,0,-5)
	    TNT1 A 0 A_PlayWeaponSound ("weapons/chainsaw/idle")
		54B7 AB 2 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		54B7 CD 2 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		54B7 EF 2 
			{
				A_WeaponReady;
				A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"),WOF_KEEPY);
			}
		Loop
	Fire:
		TNT1 A 0 A_WeaponOffset(0,32)
        TNT1 A 0 A_JumpIfInventory("PowerBloodOnVisor",1,"Fire.Blood")
		TNT1 A 0 A_Giveinventory("HasCutingWeapon",1)
		TNT1 A 0 A_StopsoundEx("Weapon")
		TNT1 A 0 A_PlaySound("weapons/chainsaw/start", 2)
		54G7 ABCD 1
		TNT1 A 0 A_AlertMonsters
	Hold:
        TNT1 A 0 A_JumpIfInventory("PowerBloodOnVisor",1,"Hold.Blood")
		TNT1 A 0 A_playsoundEx("weapons/chainsaw/loop","Weapon")
		TNT1 AAAA 0 A_FireCustomMissile("SmokeSpawner",0,0,0,-5)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54G7 E 1 A_Saw("","DSSAWHIT",4,"SSawPuff2",6)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54G7 F 1 
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54G7 G 1 
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54G7 H 1 A_Saw("","DSSAWHIT",4,"SSawPuff2",6)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54G7 I 1 A_Saw("","DSSAWHIT",4,"SSawPuff2",6)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54G7 J 1 A_Saw("","DSSAWHIT",4,"SSawPuff2",6)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54G7 K 1 A_Saw("","DSSAWHIT",4,"SSawPuff2",6)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		TNT1 A 0 A_Refire
		TNT1 A 0 A_StopsoundEx("Weapon")
		TNT1 A 0 A_Playsound("weapons/chainsaw/stop",7)
		54G7 DCBA 1
		Goto ReadyII
	Fire.Blood:
		TNT1 A 0 A_Giveinventory("HasCutingWeapon",1)
		TNT1 A 0 A_StopsoundEx("Weapon")
		TNT1 A 0 A_PlaySound("weapons/chainsaw/start", 2)
		54B8 ABCD 1
		TNT1 A 0 A_AlertMonsters
	Hold.Blood:
		TNT1 A 0 A_playsoundEx("weapons/chainsaw/loop","Weapon")
		TNT1 AAAA 0 A_FireCustomMissile("SmokeSpawner",0,0,0,-5)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54B8 E 1 A_Saw("","DSSAWHIT",4,"SSawPuff2",6)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54B8 F 1 
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54B8 G 1 
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54B8 H 1 A_Saw("","DSSAWHIT",4,"SSawPuff2",6)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54B8 I 1 A_Saw("","DSSAWHIT",4,"SSawPuff2",6)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54B8 J 1 A_Saw("","DSSAWHIT",4,"SSawPuff2",6)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		54B8 K 1 A_Saw("","DSSAWHIT",4,"SSawPuff2",6)
		TNT1 A 0 A_JumpIfCloser(119, "Slice")
		TNT1 A 0 A_Refire
		TNT1 A 0 A_StopsoundEx("Weapon")
		TNT1 A 0 A_Playsound("weapons/chainsaw/stop",7)
		54B8 DCBA 1
		Goto ReadyII
	Slice:
		TNT1 A 0 A_Playsound("DSSAWHIT", 6)
		54H7 ABCDE 1
	SliceContinue:
		54H7 F 1 A_Saw("weapons/chainsaw/loop", "Machete/Yum", 4, "SSawPuff2", 0, 120, 3)
		54H7 GH 1
		54H7 G 1
		54H7 H 1 A_Saw("weapons/chainsaw/loop", "Machete/Yum", 4, "SSawPuff", 0, 120, 3)
		TNT1 A 0 A_JumpIfCloser(119, "SliceContinue")
		TNT1 A 0 A_PlaySound("weapons/chainsaw/draw", 4)
		54H7 IJKL 1
		TNT1 A 0 A_StopsoundEx("Weapon")
		TNT1 A 0 A_Playsound("weapons/chainsaw/stop", 7)
		Goto ReadyII
	Spawn:
		0SAW Z -1
		Stop
	}
}