ACTOR FireModeAcidGun : Inventory
{
Inventory.MaxAmount 1
}

ACTOR HasIncendiaryWeapon : Inventory
{
Inventory.MaxAmount 1
}

Actor CausticCharge : Inventory
{
inventory.maxamount 20
}

ACTOR TossedHellRifle
{
	SpawnID 9900
    Radius 8
    Height 8
	Scale 0.48
	Speed 8
	Mass 1
    gravity 0.5
	Decal BrutalBloodSplat
	BounceFactor 0.4
	BounceCount 3
	+DOOMBOUNCE
    +MOVEWITHSECTOR
    //+CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
    +MISSILE
	-EXPLODEONWATER
	+DONTSPLASH
	+Rollsprite
    States
    {
	Spawn.V5:
		TNT1 A 0 A_jumpif( V5_MODELS == 0 , "Spawn")
		VRPU A 2 A_SetRoll(roll+30)
	Spawn:
		TNT1 A 0 NoDelay A_jumpif( V5_MODELS == 1 , "Spawn.V5")
		HRPU A 2 A_SetRoll(roll+30)
		LOOP		
	Death:
		TNT1 A 0 A_jumpif( V5_MODELS == 1 , "Death.V5")
	    HRPU A 1 A_SetRoll(0)
        TNT1 A 0 A_SpawnItemEx("PB_Demontech",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | 288)
        Stop
	Death.V5:
		VRPU A 1 A_SetRoll(0)
        TNT1 A 0 A_SpawnItemEx("PB_Demontech",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | 288)
        Stop
		
	
	ReplaceVanilla:
		TNT1 A 1
		TNT1 A 0 A_SpawnItemEx("NewShell",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | 288)
		Stop
    }
}

ACTOR PB_Demontech : PB_UpgradeItem
{
	//$Title Demon Tech Rifle
	//$Category Weapons
	//$Sprite HRPUA0
	Inventory.PickupSound "HRPickup"
	Inventory.PickupMessage "You got the Demon Tech Rifle! (Slot 0)"
	Scale 0.48
	FloatBobStrength 0.5
	States
	{
	Spawn:
		VRPU A 0 NoDelay
		HRPU A 10 A_PbvpFramework("VRPU")
		"####" "#" 0 A_PbvpInterpolate()
		LOOP
		pickup:
        TNT1 A 0 A_GiveInventory ("Hell_rifle")
		Stop
	}
}

ACTOR HellAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 60
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 60
   +INVENTORY.IGNORESKILL
   Inventory.Icon "HRPUA0"
}

Actor RespectDemonTech : Inventory
{
Inventory.MaxAmount 1
}

// --------------------------------------------------------------------------
//
// DemonTech Rifle
//
// --------------------------------------------------------------------------

ACTOR Hell_rifle : PB_Weapon
{
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.SelectionOrder 400
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 20
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	+FLOORCLIP
	+DONTGIB
    Inventory.PickupSound "HRPickup"
	Obituary "%o was set ablaze by %k's hellish rifle."
	Weapon.AmmoType1 "Demonpower"
	Weapon.AmmoType2 "HellAmmo"
	Tag "Demon-Tech Rifle"
	Inventory.PickupMessage "You got the Demon Tech Rifle! (Slot 0)"
    +WEAPON.NOAUTOAIM
	Scale 0.48
	Inventory.AltHUDIcon "HRPUA0"
	PB_WeaponBase.respectItem "RespectDemonTech"
	FloatBobStrength 0.5
	States
	{
		Steady:
			TNT1 A 1
			Goto Ready
		
		Ready:
			TNT1 A 0 PB_RespectIfNeeded
		WeaponRespect:
			TNT1 A 0 {
				A_SetCrosshair(5);
				A_GiveInventory("RespectDemonTech");
				A_PlaySoundEx("weapons/carbine/up", "Auto");
			}
			D0T0 ABCDEFGHIJKLMNOPQRSSSSS 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect1", "Auto")
			D0T1 ABCDEF 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect2", "Auto")
			D0T1 GHIJKLMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			D0T2 ABCDE 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect3", "Auto")
			D0T2 FGHIJKLMNOPQRSTUVW 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
			D0T2 XYZ 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("HRSteam", "Auto")
			D0T3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			Goto Ready3
			
		SelectAnimation:	
			TNT1 A 0 A_StartSound("HRReady")
			TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
			TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"SelectAnimation2")
			D2T0 ABCDE 1
		Ready3:
			TNT1 A 0 {
				A_TakeInventory("PB_LockScreenTilt",1);
				PB_HandleCrosshair(39);
				}
		ReadyToFire1:
			TNT1 A 0 A_PlaySound("DTCHHUM", 1, 1,1)
			TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"ReadyToFire2")
			D5T0 ABCDEFGHIJKLMAABMBAAABAA 3 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
			Goto ReadyToFire1+2
		
		SelectAnimation2:
			TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
			D2T1 ABCDE 1
		ReadyToFire2:
			TNT1 A 0 A_PlaySound("DTCHGUM", 1, 1,1)
			D5T1 ABCDEFGHIJKLMAABMBAAABAA 3 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
			Goto ReadyToFire2+1
		
		WeaponSpecial:
			TNT1 A 0 A_StopSound(1)
			TNT1 A 0 A_Takeinventory("GoWeaponSpecialAbility",1)
			TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"SwitchFireMode")
			TNT1 A 0 A_GiveInventory("FireModeAcidGun", 1)
			TNT1 A 0 A_Print("Firemode: \cdCaustic")
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/weaponspecial1", "Auto")
			D1T0 ABCD 1 
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/weaponspecial2", "Auto")
			D1T0 EFGHIJKLMN 2
			Goto ReadyToFire2
		SwitchFireMode:
			TNT1 A 0 A_StopSound(1)
			TNT1 A 0 A_TakeInventory("FireModeAcidGun", 1)
			TNT1 A 0 A_Print("Firemode: \cgInferno")
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/weaponspecial1", "Auto")
			D1T1 ABCD 1 
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/weaponspecial2", "Auto")
			D1T1 EFGHIJKLMN 2
			Goto ReadyToFire1
			
		Deselect:
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_SetRoll(0);
				PB_HandleCrosshair(39);
				A_TakeInventory("PB_LockScreenTilt",1);
			}
			TNT1 A 0 A_StopSound(1)
			TNT1 A 0 A_TakeInventory("HasIncendiaryWeapon", 1)
			TNT1 A 0 A_JumpIfInventory("GotMeatShield", 1, "GrabEnemy")
			TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,6)
			D2T0 DCBA 1
			TNT1 A 0 A_Jump(256, 5)
			D2T1 DCBA 1
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
			Wait
		Select:
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_SetRoll(0);
				PB_HandleCrosshair(39);
				A_TakeInventory("PB_LockScreenTilt",1);
			}
			Goto SelectFirstPersonLegs
			SelectContinue:
			TNT1 A 0 
			TNT1 A 0 PB_WeapTokenSwitch("HellRifleSelected")
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 AAAAAAAA 1 A_Raise
			wait
	
		Fire:
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_SetRoll(0);
				PB_HandleCrosshair(39);
				A_TakeInventory("PB_LockScreenTilt",1);
			}
			TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"Fire2")
			TNT1 A 0 A_JumpIfInventory("HellAmmo",1,1)
			Goto Reload
			D3T0 A 1 BRIGHT {
				A_Giveinventory("HasIncendiaryWeapon",1);
				A_FireCustomMissile("Hellbullet", 0, 0, 0, 0, 0, random(-1,1));
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				A_PlaySoundEx("HRFire", "Weapon");
				A_Takeinventory("HellAmmo",1);
				A_AlertMonsters;
				A_ZoomFactor(0.99);
				A_GunFlash;
				A_Overlay(-4, "InfernoFlash", true);
				PB_WeaponRecoil(-0.52,-0.2);
			}
			
			D3T0 B 1 BRIGHT {
				A_ZoomFactor(0.995);
				PB_WeaponRecoil(-0.52,-0.2);
			}
			D3T0 C 1 A_ZoomFactor(1.0)
			D3T0 D 1 
			TNT1 A 0 A_ReFire
			TNT1 A 0 A_PlaySoundEx("HRSteam", "Auto")
			D3T0 EFGHIJKLMNOPQRS 1
			TNT1 A 0 A_Takeinventory("HasIncendiaryWeapon",1)
			Goto ReadyToFire1
			
		Fire2:
			TNT1 A 0 A_JumpIfInventory("HellAmmo",1,1)
			Goto Reload
			D3T1 A 1 BRIGHT {
				A_Takeinventory("HasIncendiaryWeapon",1);
				A_FireCustomMissile("Hellbullet2", 0, 0, 0, 0, 0, random(-1,1));
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				A_PlaySoundEx("8FGGFIRE", "Weapon");
				A_Takeinventory("HellAmmo",1);
				A_AlertMonsters;
				A_ZoomFactor(0.99);
				A_GunFlash;
				A_Overlay(-4, "CausticFlash", true);
				PB_WeaponRecoil(-0.68,-0.36);
			}
			D3T1 B 1 BRIGHT {
				A_ZoomFactor(0.995);
				PB_WeaponRecoil(-0.68,-0.36);
			}
			D3T1 C 1 A_ZoomFactor(1.0)
			D3T1 D 1 
			D2T1 E 0 A_ReFire
			TNT1 A 0 A_PlaySoundEx("HRSteam", "Auto")
			D3T1 EFGHIJKLMNOPQRS 1
			Goto ReadyToFire2
		
		InfernoFlash:
			TNT1 A 0 A_Jump(256, "FMuzzle1", "FMuzzle2", "FMuzzle3")
		FMuzzle1:
			D3T2 AB 1 BRIGHT
			TNT1 A 0 A_Jump(100, "ThirdFMuzzle1")
			Stop
		FMuzzle2:
			D3T2 DE 1 BRIGHT
			TNT1 A 0 A_Jump(100, "ThirdFMuzzle2")
			Stop
		FMuzzle3:
			D3T2 GH 1 BRIGHT
			TNT1 A 0 A_Jump(100, "ThirdFMuzzle3")
			STOP
			
		ThirdFMuzzle1:
			D3T2 C 1 BRIGHT
			STOP
		ThirdFMuzzle2:
			D3T2 F 1 BRIGHT
			STOP
		ThirdFMuzzle3:
			D3T2 I 1 BRIGHT
			Stop
			
		CausticFlash:
			TNT1 A 0 A_Jump(256, "CMuzzle1", "CMuzzle2", "CMuzzle3", "CMuzzle4", "CMuzzle5", "CMuzzle6")
		CMuzzle1:
			D3T3 AB 1 BRIGHT
			TNT1 A 0 A_Jump(100, "ThirdCMuzzle1")
			Stop
		CMuzzle2:
			D3T3 DE 1 BRIGHT
			TNT1 A 0 A_Jump(100, "ThirdCMuzzle2")
			Stop
		CMuzzle3:
			D3T3 GH 1 BRIGHT
			TNT1 A 0 A_Jump(100, "ThirdCMuzzle3")
			STOP
		CMuzzle4:
			D3T3 JK 1 BRIGHT
			TNT1 A 0 A_Jump(100, "ThirdCMuzzle4")
			STOP
		CMuzzle5:
			D3T3 MN 1 BRIGHT
			TNT1 A 0 A_Jump(100, "ThirdCMuzzle5")
			STOP
		CMuzzle6:
			D3T3 PQ 1 BRIGHT
			TNT1 A 0 A_Jump(100, "ThirdCMuzzle6")
			STOP
			
		ThirdCMuzzle1:
			D3T3 C 1 BRIGHT
			STOP
		ThirdCMuzzle2:
			D3T3 F 1 BRIGHT
			STOP
		ThirdCMuzzle3:
			D3T3 I 1 BRIGHT
			Stop
		ThirdCMuzzle4:
			D3T3 L 1 BRIGHT
			Stop
		ThirdCMuzzle5:
			D3T3 O 1 BRIGHT
			Stop
		ThirdCMuzzle6:
			D3T3 R 1 BRIGHT
			Stop
			
		AltFire:
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_SetRoll(0);
				PB_HandleCrosshair(39);
				A_TakeInventory("PB_LockScreenTilt",1);
			}
			TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"AltFire2")
			TNT1 A 0 A_JumpIfInventory("HellAmmo",1,1)
			Goto Reload
			TNT1 A 0 A_JumpIfInventory("HellAmmo",20,1)
			Goto Reload
			TNT1 A 0 A_PlaySound("HRCharge")
			D5T0 ABCDABCDABCDABCD 1 BRIGHT {
				A_WeaponOffset(random(-1,1),random(32,34));
				A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			}
				
			TNT1 A 0 A_PlaySoundEx("unmbal", "Weapon")
			TNT1 A 0 A_FireCustomMissile("RedFlareSpawn",0,0,0,0)
			TNT1 A 0 A_FireCustomMissile("PossessionGhost")
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_Overlay(-4, "InfernoFlash", true)
			D3T0 AB 1 BRIGHT
			D3T0 CD 1
			TNT1 A 0 A_Takeinventory("HellAmmo",20)
			TNT1 A 0 A_PlaySoundEx("HRSteam", "Auto")
			D3T0 EFGHIJKLNOPQRS 1
			Goto ReadyToFire1
			
		Reload:
			TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"Reload2")
			D4T0 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			TNT1 A 0 A_JumpIfInventory("HellAmmo",60,"Ready3")
			TNT1 A 0 A_JumpIfInventory("Demonpower",1,1)
			Goto Ready3
			TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto")
			D4T0 ABCDEFGHIJK 1 
			TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
			D4T0 LMN 1 
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect1", "Auto")
			D4T0 OPQRSTUVWXYZ 1
			D4T1 ABCDEFGH 1 
			TNT1 A 0 A_PlaySound("weapons/fistwhoosh2")
			D4T1 IJKLM 1 
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect4", "Auto")
			D4T1 N 1 
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect2", "Auto")
			TNT1 A 0 A_PlaySoundEx("HRPickup", "Auto")
			D4T1 OPQRSTUVWXYZ 1
			D4T2 ABCDEFGHIJKLMNOPQRST 1 
			TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto")
			D4T2 UVWXYZ 1
			D4T3 ABCDEFGH 1 
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect3", "Auto")
			D4T3 IJKLMNOPQRSTUV 1 
			TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
			D4T3 WXYZ 1
			TNT1 A 0 A_PlaySoundEx("HRSteam", "Auto")
			D4T4 ABCDEFGHIJKLMNOPQRST 1
		InsertBullets:
			TNT1 A 0 A_JumpIfInventory("HellAmmo",60,"Ready3")
			TNT1 A 0 A_JumpIfInventory("Demonpower",1,1)
			Goto Ready3
			TNT1 A 0 {
				A_Giveinventory("HellAmmo",1);
				A_Takeinventory("Demonpower",1);
				}
			Goto InsertBullets
			
		Reload2:
			D6T0 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			TNT1 A 0 A_JumpIfInventory("HellAmmo",60,"Ready3")
			TNT1 A 0 A_JumpIfInventory("Demonpower",1,1)
			Goto Ready3
			TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto")
			D6T0 ABCDEFGHIJK 1 
			TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
			D6T0 LMN 1 
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect1", "Auto")
			D6T0 OPQRSTUVWXYZ 1
			D6T1 ABCDEFGH 1 
			TNT1 A 0 A_PlaySound("weapons/fistwhoosh2")
			D6T1 IJKLM 1 
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect4", "Auto")
			D6T1 N 1 
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect2", "Auto")
			TNT1 A 0 A_PlaySoundEx("HRPickup", "Auto")
			D6T1 OPQRSTUVWXYZ 1
			D6T2 ABCDEFGHIJKLMNOPQRST 1 
			TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto")
			D6T2 UVWXYZ 1
			D6T3 ABCDEFGH 1 
			TNT1 A 0 A_PlaySoundEx("weapons/demontech/respect3", "Auto")
			D6T3 IJKLMNOPQRSTUV 1 
			TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
			D6T3 WXYZ 1
			TNT1 A 0 A_PlaySoundEx("HRSteam", "Auto")
			D6T4 ABCDEFGHIJKLMNOPQRST 1
			Goto InsertBullets
		
		Altfire2:
			TNT1 A 0 A_JumpIfInventory("HellAmmo",5,1)
			Goto Reload
		CausticCharging:
			TNT1 A 0 A_PlaySound("CNTCTBM", 6)
			TNT1 A 0 A_PlaySound("Weapons/StachanovCharge",5,1.0,1)
		CausticChargingLoop:
			TNT1 A 0 A_JumpIfInventory("CausticCharge",20,2) //Gun has reached Max charge, skip 2 frames
			TNT1 A 0 A_JumpIfInventory("HellAmmo",5,8)
			TNT1 A 0
			Goto CausticChargedBlast
			TNT1 A 0
			//Determine Sound Level based on charged
			TNT1 A 0 A_JumpIfInventory("CausticCharge",20,"ChargeUp5")
			TNT1 A 0 A_JumpIfInventory("CausticCharge",15,"ChargeUp4")
			TNT1 A 0 A_JumpIfInventory("CausticCharge",10,"ChargeUp3")
			TNT1 A 0 A_JumpIfInventory("CausticCharge",5,"ChargeUp2")
		ChargeUp1:
			TNT1 A 0 A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)
			TNT1 A 0 A_PlaySound("HRCharge",3)
			TNT1 A 0 A_SetBlend("Green",0.1,16)
			Goto ChargingContinue
		ChargeUp2:
			TNT1 A 0 A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)
			TNT1 A 0 A_PlaySound("HRCharge",3)
			TNT1 A 0 A_SetBlend("Green",0.15,16)
			Goto ChargingContinue
		ChargeUp3:
			TNT1 A 0 A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)
			TNT1 A 0 A_PlaySound("HRCharge",3)
			TNT1 A 0 A_SetBlend("Green",0.2,16)
			Goto ChargingContinue
		ChargeUp4:
			TNT1 A 0 A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)
			TNT1 A 0 A_PlaySound("HRCharge",3)
			TNT1 A 0 A_SetBlend("Green",0.25,16)
			Goto ChargingContinue
		ChargeUp5:
			TNT1 A 0 A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)
			TNT1 A 0 A_PlaySound("HRCharge",3)
			TNT1 A 0 A_SetBlend("Green",0.3,16)
			Goto ChargingContinue
			
		ChargingContinue:
			D5T1 ABCD 1 BRIGHT {
					A_GiveInventory("CausticCharge", 1);
					A_Takeinventory("HellAmmo",1);
					A_WeaponOffSet(random(-1,1), random(32,34));
					A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			}
			D5T1 BCDABCDABCD 1 BRIGHT {
					A_WeaponOffSet(random(-1,1), random(32,34));
					A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			}
			TNT1 A 0 A_Refire("CausticChargingLoop")
			Goto CausticChargedBlast
			
		CausticChargedBlast:
			TNT1 A 0 A_StopSound(5)
			TNT1 A 0 A_StopSound(6)
			TNT1 A 0 A_JumpIfInventory("CausticCharge",20,"CausticBlast4")
			TNT1 A 0 A_JumpIfInventory("CausticCharge",15,"CausticBlast3")
			TNT1 A 0 A_JumpIfInventory("CausticCharge",10,"CausticBlast2")
			TNT1 A 0 A_JumpIfInventory("CausticCharge",5,"CausticBlast1")
			goto FireUnCharged
		
		FireUncharged:
			D3T1 A 1 BRIGHT {
				A_AlertMonsters;
				A_PlaySoundEx("Weapons/StachanovAddFire","Weapon");
				A_FireCustomMissile("GreenFlareSpawn",0,0,0,0);
				A_FireCustomMissile("ShrinkBeam", 0, 1);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				A_Takeinventory("CausticCharge",20);
				A_Overlay(-4, "CausticFlash", true);
			}
			D3T1 B 1 BRIGHT A_ZoomFactor(0.96)
			D3T1 C 1 BRIGHT A_ZoomFactor(0.98)
			D3T1 D 1 BRIGHT A_ZoomFactor(1.0)
			TNT1 A 0 A_PlaySoundEx("HRSteam", "Auto")
			D3T1 EFGHIJKLMNOPQRS 1
			goto ReadyToFire2
			
		CausticBlast1:
		CausticBlast2:
			D3T1 A 1 BRIGHT {
				A_AlertMonsters;
				A_PlaySoundEx("Weapons/StachanovAddFire","Weapon");
				A_FireCustomMissile("GreenFlareSpawn",0,0,0,0);
				A_FireCustomMissile("ShrinkBeam", 0, 1);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				A_Takeinventory("CausticCharge",20);
				A_GunFlash;
				A_Overlay(-4, "CausticFlash", true);
			}
			D3T1 B 1 BRIGHT A_ZoomFactor(0.96)
			D3T1 C 1 BRIGHT A_ZoomFactor(0.98)
			D3T1 D 1 BRIGHT A_ZoomFactor(1.0)
			TNT1 A 0 A_PlaySoundEx("HRSteam", "Auto")
			D3T1 EFGHIJKLMNOPQRS 1
			goto ReadyToFire2
		
			
		CausticBlast3:
		CausticBlast4:
			D3T1 A 1 BRIGHT {
				A_AlertMonsters;
				A_PlaySoundEx("Weapons/StachanovAddFire","Weapon");
				A_FireCustomMissile("GreenFlareSpawn",0,0,0,0);
				A_FireCustomMissile("CausticGreenPlasmaBall", 0, 1);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				A_Takeinventory("CausticCharge",20);
				A_GunFlash;
				A_Overlay(-4, "CausticFlash", true);
			}
			D3T1 B 1 BRIGHT A_ZoomFactor(0.96)
			D3T1 C 1 BRIGHT A_ZoomFactor(0.98)
			D3T1 D 1 BRIGHT A_ZoomFactor(1.0)
			TNT1 A 0 A_PlaySoundEx("HRSteam", "Auto")
			D3T1 EFGHIJKLMNOPQRS 1
			goto ReadyToFire2

	Flash:
		TNT1 A 4
        Goto LightDone
	Spawn:
		VRPU A 0 NoDelay
		HRPU A 10 A_PbvpFramework("VRPU")
		"####" "#" 0 A_PbvpInterpolate()
		LOOP
     FlashKicking:
		TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"FlashKickingAcid")
		D7T0 ABCDEFFFGHIJKLM 1
		Goto Ready3
		
	FlashKickingAcid:
		D7T1 ABCDEFFFGHIJKLM 1
		Goto Ready3
		
	FlashAirKicking:
		TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"FlashAirKickingAcid")
		D7T0 ABCDEFFFFFGHIJKLM 1
		Goto Ready3
		
	FlashAirKickingAcid:
		D7T1 ABCDEFFFFFGHIJKLM 1
		Goto Ready3
		
	FlashSlideKicking:
		TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"FlashSlideKickingAcid")
		D7T2 ABCDEFGHIJKLMNOPPPPPQRST 1
		D5T0 AA 1
		Goto Ready3
		
	FlashSlideKickingAcid:
		D7T3 ABCDEFGHIJKLMNOPPPPPQRST 1
		D5T1 AA 1
		Goto Ready3
		
	FlashSlideKickingStop:
		TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"FlashSlideKickingStopAcid")
		D7T2 QRST 1
		D5T0 AAA 1
		Goto Ready3
		
	FlashSlideKickingStopAcid:
		D7T3 QRST 1
		D5T1 AAA 1
		Goto Ready3

	FlashPunching:
		TNT1 A 0 A_JumpIfInventory("FireModeAcidGun",1,"FlashPunchingAcid")
		D7T4 ABCDEFFFFFGHI 1
		D5T0 AA 1
		Stop
		
	FlashPunchingAcid:
		D7T5 ABCDEFFFFFGHI 1
		D5T1 AA 1
		Stop
	}
}

Actor Hellbullet: FastProjectile
{
	+FORCEXYBILLBOARD
   +BLOODSPLATTER 
   +NOEXTREMEDEATH
damage 12
damagetype fire
radius 2
height 1
renderstyle ADD
//alpha 0.8
scale .3
speed 130
Decal "Scorch"
Deathsound "Weapons/Demontech/Crash"
states
	{
	Spawn:
		PBAL H 1 BRIGHT 
		TNT1 A 0 A_CustomMissile ("RedTracerSmall", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_SpawnItemEx("ObeliskTrailSpark", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0)
		PBAL I 1 BRIGHT 
		TNT1 A 0 A_SpawnItem("RedFlare",0)
		Loop
Xdeath:
   
	TNT1 A 0 //A_CustomMissile ("Flametrails", 0, 0, random (0, 180), 2, random (0, 360))
	TNT1 A 0 A_SpawnItem("HellRifle_Puff",0)
	TNT1 A 0 BRIGHT A_SpawnItem("RedFlare",0)
	TNT1 AAA 0 A_SpawnItemEx("ExplosionParticleVerySlow", random(-8, 8), random(-8, 8), random(-2,2), 0, 0, 0, 0, 128, 0)
	TNT1 AAA 0 A_SpawnItemEx("ObeliskTrailSpark", random(-8, 8), random(-8, 8), random(-2,2), 0, 0, 0, 0, 128, 0)
	TNT1 A 4
	TNT2 AAA 3 A_CustomMissile ("PlasmaSmoke", 1, 0, random (0, 360), 2, random (0, 160))
		Stop

	Death:
	TNT1 A 0 A_SpawnItem("HellRifle_Puff2",0)
	TNT1 A 0 BRIGHT A_SpawnItem("RedFlare",0)
	TNT1 AAA 0 A_SpawnItemEx("ExplosionParticleVerySlow", random(-8, 8), random(-8, 8), random(-2,2), 0, 0, 0, 0, 128, 0)
	TNT1 AA 0 A_SpawnItemEx("BurningEmberParticlesFloating_Bigger", random(19,-19), random(19,-19), random(4,-4), 0, 0, 0, 0, 128, 0)
	TNT1 AAA 0 A_SpawnItemEx("ObeliskTrailSpark", random(-8, 8), random(-8, 8), random(-2,2), 0, 0, 0, 0, 128, 0)
	TNT1 A 4	
    TNT2 AAAAA 4 A_CustomMissile ("PlasmaSmoke", 1, 0, random (0, 360), 2, random (0, 160))
		Stop
	}
}
//GreenPlasma_Puff

Actor Hellbullet2: FastProjectile
{
	+FORCEXYBILLBOARD
   +BLOODSPLATTER 
   +NOEXTREMEDEATH
damage 13
damagetype desintegrate
radius 2
height 1
renderstyle ADD
//alpha 0.7
scale .19
speed 100
Decal "Scorch"
Deathsound "Weapons/Demontech/Crash"
states
	{
	Spawn:
		TNT1 AA 0 A_SpawnItem("GreenFlareSmall",0)
		DB57 AB 1 BRIGHT Light("CausticProjectile")
		TNT1 A 0 A_CustomMissile ("GreenTracerSmall", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_SpawnItemEx("GreenTrailSparks", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0)
		DB57 C 1 BRIGHT
		Loop
Xdeath:
	TNT1 A 0 A_SpawnItem("GreenPlasma_Puff_Medium",0)
	TNT1 A 0 A_CustomMissile ("PlasmaParticleSpawner", 0, 0, random (0, 180), 2, random (0, 360))
	TNT1 A 4
	//TNT2 AAA 3 A_CustomMissile ("PlasmaSmoke", 1, 0, random (0, 360), 2, random (0, 160))
		
		Stop

	Death:
	TNT1 A 0 A_SpawnItem("GreenPlasma_Puff_Medium",0)
	TNT1 A 0 A_CustomMissile ("PlasmaParticleSpawner", 0, 0, random (0, 180), 2, random (0, 360))
	TNT1 A 4	
    //TNT2 AAAAA 4 A_CustomMissile ("PlasmaSmoke", 1, 0, random (0, 360), 2, random (0, 160))
		Stop
	}
}



ACTOR DemonSoulHeal: CustomInventory //23167
{
	Game Doom
	Radius 80
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage "Your Demon Spirits have granted you health!"
	Inventory.PickupSound "DemonHeal"
	States
	{
		Spawn:
			TNT1 A 80
			Stop
			
		Pickup:
			TNT1 A 0 ACS_NamedExecute("CrueltyBonus10", 0)	
			Stop
	}
}

// Demon Power Pickups ---------------------------------------------------------------

// lesser ammo item
actor DemonPower : Ammo
{
	//$Title Lesser Demon Energy
	//$Category Ammunition
	//$Sprite MSP2A0
	Inventory.PickupMessage "Lesser demon energy"
	Inventory.PickupSound "HellPickup"
	Inventory.Amount 20 
	Inventory.MaxAmount 300
	Inventory.Icon "MSP2A0"
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 600
	renderstyle translucent
	alpha 0.55
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.IGNORESKILL
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 ACS_ExecuteAlways(796, 0, 0, 0, 0)//Check if Vanilla Mode is activated
			MSP2 AB 4 bright //A_Fadeout(0.001)
			loop
			
		ReplaceVanilla:
			TNT1 A 0 
			TNT1 A 1 
			Stop
		}
}
//greater ammo item
actor LargeDemonPower : DemonPower
{
	//$Title Greater Demon Energy
	//$Category Ammunition
	//$Sprite MSP2A0
	Inventory.PickupMessage "Greater demon energy"
	Inventory.Amount 40
	scale 1.1
	states
	{
		Spawn:
			TNT1 A 1 
			TNT1 A 0 ACS_ExecuteAlways(796, 0, 0, 0, 0)//Check if Vanilla Mode is activated
			MSP2 ABCD 4 bright
			loop
	}
}

Actor RedTracerSmall
{
  Height 0
  Radius 0
  Mass 0
  +Missile
  +NoBlockMap
  -NoGravity
  //+LowGravity
  +DontSplash
  +DoomBounce
  +FORCEXYBILLBOARD
  BounceFactor 0.2
  Gravity 0.8
  RenderStyle Add
  Scale 0.035
  //Speed 2
  Speed 0
    +NoGravity
  -DOOMBOUNCE
  RenderStyle Add
  Scale 0.25
  Alpha 0.6
  States
  {
  Spawn:
  Death:
    SHOQ A 2 Bright A_FadeOut(0.04)
	TNT1 A 0 A_SpawnItemEx("ExplosionParticleVerySlow", random(19,-19), random(19,-19), random(4,-4), 0, 0, 0, 0, 128, 0)
	SHOQ B 2 Bright A_FadeOut(0.04)
	SHOQ C 2 Bright A_FadeOut(0.04)
	TNT1 A 0 A_SpawnItemEx("BurningEmberParticlesFloating", random(19,-19), random(19,-19), random(4,-4), 0, 0, 0, 0, 128, 0)
	SHOQ D 2 Bright A_FadeOut(0.04)
	SHOQ E 2 Bright A_FadeOut(0.04)
	TNT1 A 0 A_SpawnItemEx("ExplosionParticleVerySlow", random(19,-19), random(19,-19), random(4,-4), 0, 0, 0, 0, 128, 0)
	SHOQ F 2 Bright A_FadeOut(0.04)
	SHOQ G 2 Bright A_FadeOut(0.04)
	TNT1 A 0 A_SpawnItemEx("ObeliskTrailSpark", random(19,-19), random(19,-19), random(4,-4), 0, 0, 0, 0, 128, 0)
    Stop
  }
}

Actor GreenTracerSmall
{
  Height 0
  Radius 0
  Mass 0
  +Missile
  +NoBlockMap
  -NoGravity
  //+LowGravity
  +DontSplash
  +DoomBounce
  +FORCEXYBILLBOARD
  BounceFactor 0.2
  Gravity 0.8
  RenderStyle Add
  Scale 0.035
  //Speed 2
  Speed 0
    +NoGravity
  -DOOMBOUNCE
  RenderStyle Add
  Scale 0.25
  Alpha 0.6
  States
  {
  Spawn:
  Death:
    SH0Q ABCDEFG 1 Bright A_FadeOut(0.04)
    Stop
  }
}

ACTOR ShrinkBeam : MageWandMissile
{
  Speed 25
  Radius 16
  Height 16
  Damage 3
  Decal "Scorch"
  damagetype "Shrink"
  //-RIPPER
	+NOBOSSRIP
  -CANNOTPUSH
  -NODAMAGETHRUST
  +Bloodlessimpact
  +NOBLOOD
  states
  {
  
  Spawn:
    APBX B 1 BRIGHT A_SpawnItem("GreenFlareSmall",0,0)
	TNT1 A 0 A_SpawnItemEx("GreenTrailSparks", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0)
	APBX C 1 BRIGHT A_SpawnItem("GreenFlareSmall",0,0)
	TNT1 A 0 A_SpawnItemEx("GreenTrailSparks", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0)
	APBX D 1 BRIGHT A_SpawnItem("GreenFlareSmall",0,0)
	TNT1 A 0 A_SpawnItemEx("GreenTrailSparks", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0)
	TNT1 A 0 A_SpawnItemEx("SmallGreenFlameTrails", 0, 0, 0, 0, 0, 0, 0, 128)
    loop
  
  Death:
	TNT1 A 0 A_Explode(30,70,1)
	TNT1 A 0 A_Explode(10,90,1)
    APBX B 1 bright A_SpawnItem("GreenFlare",0,0)
	TNT1 A 0 A_SpawnItemEx("GreenTrailSparks", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0)
	APBX C 1 bright A_SpawnItem("GreenFlare",0,0)
	TNT1 A 0 A_SpawnItemEx("GreenTrailSparks", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0)
	APBX D 1 bright A_SpawnItem("GreenFlare",0,0)
	TNT1 A 0 A_SpawnItemEx("GreenTrailSparks", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0)
	APBX E 1 bright A_SpawnItem("GreenFlare",0,0)
	TNT1 A 0 A_SpawnItemEx("GreenTrailSparks", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0)
	TNT1 A 0 A_SpawnItem("BFGAltShockWave",0,0)
    stop
  }
}

ACTOR CausticGreenPlasmaBall: BaronBall
{
	Radius 10
	Height 16
	Speed 30
	FastSpeed 30
	Projectile 
	+RANDOMIZE
	+FORCEXYBILLBOARD
    +THRUGHOST
    Damage 120
	RenderStyle Add
	Alpha 0.9
	Scale 1.45
	SeeSound "baron/attack"
	DeathSound "belphegor/missile"
	Decal "Scorch"
	DamageType "Disintegrate"

	States
	{
	Spawn:
	    TNT1 A 0 A_SpawnItem("GreenFlare22",0,0)
		FRPG K 1 BRIGHT A_SpawnItemEx("GreenExplosionFlameTrail", 0, 0, 0, 0, 0, 0, 0, 128) 
		Loop

	Death:
    BFE1 A 0 A_PlaySound("FAREXPL", 3)
	EXPL A 0 Radius_Quake (2, 54, 0, 15, 0)
    BFE1 A 0 Bright A_Explode(27, 135, 1)
        TNT1 A 0 A_SpawnItem ("GreenExplosionMushroom", 0)
        TNT1 AAAAAAAAA 0 A_CustomMissile ("GreenExplosionFire", 2, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_PlaySound ("barlp1",3)
		TNT1 A 0 A_SpawnItem("ACIDFOGSHRINK", 0, 0)
        EXPL AA 0 A_CustomMissile ("BigNeoSmoke", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 19 A_CustomMissile("PlasmaSmoke", 1, 0, random (0, 360), 2, random (0, 160))
    TNT1 BCDEF 2 Bright
	TNT1 AAAAAAA 2 A_CustomMissile ("BigNeoSmoke", 2, 0, random (0, 360), 2, random (0, 360))
	Stop
	}
}

ACTOR ACIDFOGSHRINK
{
    Radius 2
    Height 1
	//Alpha .8
	Decal "Scorch"
	//RenderStyle Translucent
	Damage 5
	DamageType "Disintegrate"
	Scale 1.32
	Speed 0
	Gravity 0
    +NOBLOCKMAP
    +NOTELEPORT
	+NOEXTREMEDEATH
	-EXTREMEDEATH
	+FORCEXYBILLBOARD
	+ALLOWPARTICLES
    +CLIENTSIDEONLY
	+DONTSPLASH
	+PAINLESS
	-CAUSEPAIN
	-FORCEPAIN
    States
    {
    Spawn:
		TNT1 A 0 A_SpawnItemEx("PlasmaParticleSpawner", 0, 0, 0, 6, 6, 6, 0, 128)
		TNT1 A 0 A_Explode ( 50, 190 )
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GreenCloudLarge", random(3,-3), random(3,-3), random(3,-3), random(1,-1), random(1,-1), 0, 0, SXF_NOCHECKPOSITION, 0)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GreenCloudMedium", random(3,-3), random(3,-3), random(3,-3), random(1,-1), random(1,-1), 0, 0, SXF_NOCHECKPOSITION, 0)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GreenCloudMediumShrink", random(3,-3), random(3,-3), random(3,-3), random(1,-1), random(1,-1), 0, 0, SXF_NOCHECKPOSITION, 0)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GreenCloudSmallShrink", random(3,-3), random(3,-3), random(3,-3), random(1,-1), random(1,-1), 0, 0, SXF_NOCHECKPOSITION, 0)
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("GreenTrailSparks", random(10,-10), random(10,-10), random(10,-10), random(-2, 2), random(-2, 2), random(-2, 2), 0, SXF_NOCHECKPOSITION, 0)
		
		TNT1 A 0 A_SpawnItem("NewAcidExplosionSmoke", 0, 0)
		TNT1 AAAA 2 BRIGHT Light("BARONBALL") A_SpawnItem("GreenFlare",0,20)
        Goto Death
		
	Death:
		TNT1 A 0 A_SpawnItemEx("PlasmaParticleSpawner", 0, 0, 0, 6, 6, 6, 0, 128)
		Stop
    }
}

ACTOR GreenCloudMediumShrink : GreenCloudSmall
{
	Scale 1.3
	DamageType "Shrink"
	States
	{
	Spawn:
		GTXL KLMNOPQRST 1 
		TNT1 A 0 A_Explode ( 8, 100 )
		GTXL TSRQPONMLK 1 
		TNT1 A 0 A_Explode ( 8, 100 )
		GTXL KLMNOPQRST 1 
		TNT1 A 0 A_Explode ( 8, 100 )
		GTXL TSRQPONMLK 1 
		TNT1 A 0 A_Explode ( 8, 100 )
		GTXL KLMNOPQRST 1 
		TNT1 A 0 A_Explode ( 8, 100 )
		GTXL TSRQPONMLK 1 
		TNT1 A 0 A_Explode ( 8, 100 )
		GTXL KLMNOPQRST 1 
		TNT1 A 0 A_Explode ( 8, 100 )
		GTXL TSRQPONMLK 1 
		TNT1 A 0 A_Explode ( 8, 100 )
		GTXL KLMNOPQRST 1 
		TNT1 A 0 A_Explode ( 8, 100 )
		GTXL TSRQPONMLK 1 
		TNT1 A 0 A_Explode ( 8, 100 )
		GTXL KLMNOPQRST 1  A_FadeOut( 0.10)
		Stop
	}
}

ACTOR GreenCloudSmallShrink : GreenCloudSmall
{
	Scale 1.05
	DamageType "Shrink"
	States
	{
	
	Spawn:
		GTXL KLMNOPQRST 1 
		TNT1 A 0 A_Explode ( 6, 100 )
		GTXL TSRQPONMLK 1 
		TNT1 A 0 A_Explode ( 6, 100 )
		GTXL KLMNOPQRST 1 
		TNT1 A 0 A_Explode ( 6, 100 )
		GTXL TSRQPONMLK 1 
		TNT1 A 0 A_Explode ( 6, 100 )
		GTXL KLMNOPQRST 1 
		TNT1 A 0 A_Explode ( 6, 100 )
		GTXL TSRQPONMLK 1 
		TNT1 A 0 A_Explode ( 6, 100 )
		GTXL KLMNOPQRST 1 
		TNT1 A 0 A_Explode ( 6, 100 )
		GTXL TSRQPONMLK 1 
		TNT1 A 0 A_Explode ( 6, 100 )
		GTXL KLMNOPQRST 1 
		TNT1 A 0 A_Explode ( 6, 100 )
		GTXL TSRQPONMLK 1 
		TNT1 A 0 A_Explode ( 6, 100 )
		GTXL KLMNOPQRST 1  A_FadeOut( 0.10)
		Stop
	}
}

//////////////////////////////////////////Possession

// Homing ghost projectile fired upon the item's use ----------------------------------------------

ACTOR PossessionGhost
{
  Alpha 0.85
  Height 2
  MaxTargetRange 32
  Projectile
  Radius 2
  RenderStyle "Add"
  SeeSound "HRReady"
  Speed 20
  Scale 0.1
  +BRIGHT
  +SCREENSEEKER
  +SEEKERMISSILE
  +THRUACTORS

  const int PSGH_JITC_DISTANCE = 20; // A_JumpIfTracerCloser distance parameter

  States
  {
	Spawn:
		TNT1 A 0 A_PlaySound("SpiritSeeker", 3, 1.0, 1)
	SpawnLoop:
		BLHS ABCD 2 BRIGHT {
			A_SpawnItem("RedFlareSpawn",0,0,0,0);
			A_SpawnItemEx("ObeliskTrailSpark", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0);
			A_GiveInventory("PossessionGhostRoutine", 1);
			A_SeekerMissile (4,8);
			if(ScaleX <= 1.50){
				A_SetScale(ScaleX+0.08, ScaleY+0.08);
			}
			if(A_JumpIfTracerCloser(PSGH_JITC_DISTANCE, "Possess")){
				return A_Jump(256, "Possess");
			}
			return state("");
		}
		loop
	Possess:
		TNT1 A 0 {
			A_StopSound(3);
			A_SpawnItem("HellRifle_Puff2",0);
			A_Playsound("weapons/demontech/respect2");
			A_GiveInventory("PossessMonster", 1 , AAPTR_TRACER);
		}
		Stop
	Death:
		TNT1 A 0 A_SpawnItem("HellRifle_Puff2",0)
		Goto Null
	}
}

ACTOR PossessionGhostTrail
{
  Alpha 0.5
  RenderStyle "Add"
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 2
    BLHS ABCD 2 Bright A_FadeOut(0.08)
    Wait
  }
}

ACTOR PossessionGhostRoutine : CustomInventory
{
  States
  {
  Pickup:
    TNT1 A 0 A_SeekerMissile(5.0, 10.0, SMF_LOOK, 256)
    //TNT1 A 0 A_SpawnItemEx("PossessionGhostTrail", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

// Possession state indicators (the things that spawn on top of the possessed monster) ------------

ACTOR PossessionWarperA
{
  RenderStyle "Add"
  Scale 0.75
  +ISMONSTER
  +NOINTERACTION

  var int user_angle;
  var int user_xoffset;
  var int user_yoffset;
  var int user_zoffset;

  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_SetUserVar("user_xoffset", 32)
  SetVars:
    TNT1 A 0 A_SetUserVar("user_yoffset", 0)
    TNT1 A 0 A_SetUserVar("user_zoffset", CallACS("Pos - Adjust height"))
    TNT1 A 0 A_SetUserVar("user_angle", 0)
  AnimInit:
    TNT1 A 1 A_Warp(AAPTR_MASTER, user_xoffset, user_yoffset, user_zoffset, user_angle,
                    WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE|WARPF_ABSOLUTEANGLE)
  AnimLoop:
    MSP2 A 1 Bright A_Warp(AAPTR_MASTER, user_xoffset, user_yoffset, user_zoffset, user_angle,
                           WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE|WARPF_ABSOLUTEANGLE)
    TNT1 A 0 A_SetUserVar("user_angle", user_angle + 8)
    TNT1 A 0 A_SpawnItemEx("PossessionWarperTrail", 0, 0, 0, 0, 0, 0, 0,
                           SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
    TNT1 A 0 A_JumpIf(CallACS("Pos - Check possession state") == TRUE, "Destroy")
	MSP2 B 1
    Loop
  Destroy:
    MSP2 A 1 Bright A_Warp(AAPTR_MASTER, user_xoffset, user_yoffset, user_zoffset, user_angle,
                           WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE|WARPF_ABSOLUTEANGLE)
    TNT1 A 0 A_SetUserVar("user_angle", user_angle + 8)
    TNT1 A 0 A_SpawnItemEx("PossessionWarperTrail", 0, 0, 0, 0, 0, 0, 0,
                           SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
    TNT1 A 0 A_FadeOut(0.02)
    Loop
  }
}

ACTOR PossessionWarperB : PossessionWarperA
{
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_SetUserVar("user_xoffset", -32)
    Goto SetVars
  }
}

ACTOR PossessionWarperTrail
{
  RenderStyle "Add"
  Scale 0.3
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 1
    POBL B 1 Bright A_FadeOut(0.15)
    Wait
  }
}

// Possession setup "scripts" ---------------------------------------------------------------------

ACTOR PossessMonster : CustomInventory
{
  States
  {
  Pickup: // "Forces" the to-be-possessed monster to enter the Possession state
    TNT1 A 0 ACS_NamedExecuteAlways("Pos - Execute Possession state")
    Stop
  }
}

ACTOR UnSkullFly : CustomInventory
{
  States
  {
  Pickup:
    // The Lost Soul or any monster that charges at its target using A_SkullAttack or variant of
    // the function gets the following exectued on it in case it was charging at the moment when it
    // was struck by the possession projectile.
    TNT1 A 0 A_ChangeFlag("SKULLFLY", FALSE) // Free the monster from the shackles of SKULLFLY...
    TNT1 A 0 A_Stop // ... and stop it in its tracks.
    Stop
  }
}

// Effect timer
ACTOR PossessionEffect : Powerup { Powerup.Duration -45 }

ACTOR SpawnPossessionWarpers : CustomInventory
{
  States
  {
  Pickup:
    TNT1 A 0 A_GiveInventory("PossessionEffect", 1)
    TNT1 A 0 A_SpawnItemEx("PossessionWarperA", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER|SXF_NOCHECKPOSITION)
    TNT1 A 0 A_SpawnItemEx("PossessionWarperB", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER|SXF_NOCHECKPOSITION)
    Stop
  }
}
