ACTOR FireModeFreezerBeam : Inventory
{
Inventory.MaxAmount 1
}

Actor FreezerAmmo : Ammo
{
inventory.maxamount 60
+INVENTORY.IGNORESKILL
Inventory.Icon "FRPKA0"
}

ACTOR RespectFreezer : Inventory
{
Inventory.MaxAmount 1
}

ACTOR HasUnloadedFreezer : Inventory
{
Inventory.MaxAmount 1
}

Actor FreezerOverCooling: Inventory
{
Inventory.maxamount 420
}

Actor WasPistolEquipped : Inventory {}

Actor PB_Freezer: PB_Weapon
{
	//$Title Cryogenic Rifle
	//$Category Weapons
	//$Sprite FRPKA0
	SpawnID 9700
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.SelectionOrder 100
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 60
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
    Inventory.PickupSound "weapons/freezer/respect1"
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoType2 "FreezerAmmo"
   	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	DamageType "Freeze"
	Inventory.PickupMessage "You got the UAC Cryogenic Dispersion Rifle! (Slot 8)"
	Obituary "%o was iced by %k"
	Tag "Cryo Rifle"
	Scale 0.48
	PB_WeaponBase.respectItem "RespectFreezer"
  States
  {
  	
	Spawn:
		FRPK A 1
		Loop
		
	Steady: 
		TNT1 A 1
		Goto Ready
		
		Ready:
			TNT1 A 0 PB_RespectIfNeeded
		WeaponRespect:
			TNT1 A 0 {
				A_SetCrosshair(5);
				A_GiveInventory("RespectFreezer");
			}
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/up", "Auto")
			FR00 ABCDEFGHIJKLMNO 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/respect2", "Auto")
			FR00 PQRSTUVWXYZ 1 A_DoPBWeaponAction
			FR01 ABCD 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/respect3", "Auto")
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/respect1", "Auto")
			FR01 EFGHIJKLMNOPQRSTUVWXYZ 1 {
				A_FireCustomMissile("GunFireSmoke", 0, 0, -2, -5, 0, 0);
				return A_DoPBWeaponAction;
			}
			FR02 ABCDEFGH 1 {
				A_FireCustomMissile("GunFireSmoke", 0, 0, -2, -5, 0, 0);
				return A_DoPBWeaponAction;
			}
			FR02 IJKL 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySound("weapons/freezer/idle", 1, 1.0, 1)
			FR02 MNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			FR60 ABCDEFGHIJKLMNOPQ 1 A_DoPBWeaponAction
			Goto Ready3
			
		SelectAnimationPistolEquip:
			Goto AltFire+3
		SelectAnimation:
			TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/respect1", "Auto")
			FR03 ABCD 1
			TNT1 A 0 A_JumpIfInventory("FreezerPistolToken",1,"SelectAnimationPistolEquip")
		Ready3:
			TNT1 A 0 {
				A_TakeInventory("PB_LockScreenTilt",1);
				PB_HandleCrosshair(79);
				A_PlaySound("weapons/freezer/idle", 1, 1.0, 1);
				A_PlaySound("PLSIDLE", 6, 0.7, 1);
				}
			TNT1 A 0 A_JumpIfInventory("FreezerPistolToken", 1, "Ready2")
			TNT1 A 0 A_JumpIfInventory("HasUnloadedFreezer", 1, "GunEmpty")
		ReadyToFire1:
			FR03 EFGHIJKLMMLKJIHGFE 1 {
				A_Overlay(7, "BigTubeGlow");
				A_Overlay(8, "SmallTubeGlow");
				A_Overlay(9, "MuzzleGlow");
				A_TakeInventory("FreezerOverCooling",1);
				A_FireCustomMissile("TinyGunSmoker", 0, 0, 0, -3, 0, 0);
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
			}
			FR03 EFGHIJKLMMLKJIHGFE 1 {
				A_Overlay(7, "BigTubeGlow");
				A_Overlay(8, "SmallTubeGlow");
				A_Overlay(9, "MuzzleGlow");
				A_TakeInventory("FreezerOverCooling",1);
				A_FireCustomMissile("TinyGunSmoker", 0, 0, 0, -3, 0, 0);
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
			}
			Loop
		GunEmpty:
			TNT1 A 0 A_StopSound(1)
			TNT1 A 0 A_StopSound(6)
			FR03 EE 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
			Goto GunEmpty+3
		
		//Pistol Drawn
		Ready2:
			FR30 I 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR39");}
			
				A_FireCustomMissile("TinyGunSmoker", 0, 0, -4, -6, 0, 0);
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
			}
			Goto Ready2
		
		WeaponSpecial:
		SwitchToBeam:
			TNT1 A 0
			{
				if (CountInv("FreezerPistolToken") >= 1)
				{
					A_GiveInventory("WasPistolEquipped",1);
					return state("UnequipPistol");
				}
				return state("");
			}
			TNT1 A 0 A_Takeinventory("GoWeaponSpecialAbility",1)
			TNT1 A 0 A_JumpIfInventory("FireModeFreezerBeam",1,"SwitchToMissile")
			TNT1 A 0 {
				A_GiveInventory("FireModeFreezerBeam", 1);
				A_Print("Firemode: Freeze Ray");
				A_PlaySoundEx("weapons/freezer/up", "Auto");
				A_PlaySoundEx("weapons/freezer/reload1", "Auto");
			}
			FR20 ABCDEDFGHIJKL 1
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/respect2", "Auto")
			FR20 MNOPQRSTUU 1 A_FireCustomMissile("GunFireSmoke", 0, 0, -5, -5, 0, 0)
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/respect3", "Auto")
			FR20 VWXYZ 1 A_FireCustomMissile("GunFireSmoke", 0, 0, -5, -5, 0, 0)
			FR21 ABCDEDFGHIJKLMNOPQRS 1
			TNT1 A 0
			{
				if (CountInv("WasPistolEquipped") >= 1)
				{
					A_TakeInventory("WasPistolEquipped",1);
					return state("AltFire");
				}
				return state("");
			}			
			Goto Ready3
		SwitchToMissile:
			TNT1 A 0 {
				A_TakeInventory("FireModeFreezerBeam", 1);
				A_Print("Firemode: Ice Missile");
				A_PlaySoundEx("weapons/freezer/up", "Auto");
				A_PlaySoundEx("weapons/freezer/reload1", "Auto");
			}
			FR22 ABCDEFGHIJ 1 
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/respect2", "Auto")
			FR22 KLMNOPQRSTT 1 A_FireCustomMissile("GunFireSmoke", 0, 0, -5, -5, 0, 0)
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/respect3", "Auto")
			FR22 UVWXYZ 1 A_FireCustomMissile("GunFireSmoke", 0, 0, -5, -5, 0, 0)
			FR23 ABCDEFGHIJKLMNOPQ 1			
			TNT1 A 0
			{
				if (CountInv("WasPistolEquipped") >= 1)
				{
					A_TakeInventory("WasPistolEquipped",1);
					return state("AltFire");
				}
				return state("");
			}
			Goto Ready3

		Select:
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_SetRoll(0);
				PB_HandleCrosshair(79);
				A_TakeInventory("PB_LockScreenTilt",1);
			}
			Goto SelectFirstPersonLegs
			SelectContinue:
			TNT1 A 0 A_Takeinventory("HasUnloadedFreezer",1)
			TNT1 A 0 PB_WeapTokenSwitch("FreezerSelected")
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 AAAAAAAA 1 A_Raise
			Wait

		Deselect:
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_SetRoll(0);
				PB_HandleCrosshair(79);
				A_TakeInventory("PB_LockScreenTilt",1);
			}
			TNT1 A 0 A_Takeinventory("HasUnloadedFreezer",1)
			TNT1 A 0 A_Takeinventory("Unloading",1)
			TNT1 A 0 A_StopSound(1)
			TNT1 A 0 A_StopSound(6)
			TNT1 A 0 A_JumpIfInventory("GotMeatShield", 1, "GrabEnemy")
			TNT1 A 0 A_JumpIfInventory("FreezerPistolToken",1,1)
			Goto FinalDeselect
			FR30 JKLMNOPQ 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR39");}
			}
		FinalDeselect:
			FR93 NOPQ 0
			FR03 NOPQ 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR93");}
			}
			TNT1 A 0 A_Takeinventory("FreezerOverCooling",420)
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
			Wait

		Fire:
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_SetRoll(0);
				PB_HandleCrosshair(79);
				A_TakeInventory("PB_LockScreenTilt",1);
			}
			TNT1 A 0 A_JumpIfInventory("FreezerPistolToken",1,"FirePistol")
			TNT1 A 0 A_JumpIfInventory("FireModeFreezerBeam",1,"FireBeam")
			TNT1 A 0 A_JumpIfInventory("FreezerAmmo",1,1)
			Goto Reload
			FR11 ABCDEFGHI 0 // Initialize sprites into virtual memory
			FR10 A 1 Bright {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				A_GunFlash;
				A_Takeinventory("FreezerAmmo",6);
				A_Giveinventory("FreezerOverCooling",60);
				A_AlertMonsters;
				A_Playsound("weapons/freezer/fire");
				A_FireCustomMissile("FreezerBall", 0, 1, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
				
				A_ZoomFactor(0.98);
				PB_WeaponRecoil(-1.84,+0.9);
			}
			FR10 B 1 Bright {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				
				A_ZoomFactor(0.99);
				PB_WeaponRecoil(-1.84,+0.9);
			}
			FR10 C 1 Bright {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				A_ZoomFactor(1.0);
			} 
			FR10 D 1 Bright {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
			}
			FR10 EFGHI 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
			}
			TNT1 A 0 A_Refire
			Goto Ready3

		BeamMuzzleFlash:
			TNT1 A 0 A_Jump(256, "Muzzle1", "Muzzle2", "Muzzle3", "Muzzle4", "Muzzle5", "Muzzle6", "Muzzle7")
		Muzzle1:
			FR12 A 1 BRIGHT
			Stop
		Muzzle2:
			FR12 B 1 BRIGHT
			Stop
		Muzzle3:
			FR12 C 1 BRIGHT
			Stop
		Muzzle4:
			FR12 D 1 BRIGHT
			Stop
		Muzzle5:
			FR12 E 1 BRIGHT
			Stop
		Muzzle6:
			FR12 F 1 BRIGHT
			Stop
		Muzzle7:
			FR12 G 1 BRIGHT
			Stop

		FireBeam:
			TNT1 A 0 A_JumpIfInventory("FreezerPistolToken",1,"FirePistol")
			TNT1 A 0 A_JumpIfInventory("FreezerAmmo",2,1)
			Goto Reload
			FR11 ABCDEFGHI 0 // Initialize sprites into virtual memory
			FR14 ABC 0 
		BeamCharge:
			TNT1 A 0 A_Playsound("weapons/railgun/deselectred",2,1,1)
			FR11 IIHGFEDCBAII 1
		BeamHold:
			TNT1 A 0 A_JumpIfInventory("FreezerAmmo",2,1)
			Goto Reload
			TNT1 A 0 A_Playsound("weapons/cryobowflyby",2,1,1)
			FR13 ABCB 1 BRIGHT {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR14");}
				A_GunFlash;
				A_Overlay(-2, "BeamMuzzleFlash");
				A_AlertMonsters;
				A_FireCustomMissile("BlueFlareSpawn",0,0,0,0);
				PB_FireFreezerLaser();
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
				A_Giveinventory("FreezerOverCooling",8);
				PB_WeaponRecoil(-0.40,+frandom(-1.4,1.4));
			}
			TNT1 A 0 A_Takeinventory("FreezerAmmo",2)
			TNT1 A 0 A_JumpIfInventory("FreezerAmmo",2,1)
			Goto EndBeam
			TNT1 A 0 A_Refire ("BeamHold")
		EndBeam:
			TNT1 A 0 A_StopSound(2)
			TNT1 A 0 A_StopSound(6)
			TNT1 A 0 A_Playsound("Weapons/Freezer/Beamstop")
			FR10 CDEFGHI 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
			}
			Goto Ready3
	
		FirePistol:
			TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo", 1, 1)
			Goto ReloadPistol
			TNT1 A 0 A_GunFlash
			FR30 Z 4 {
				A_GunFlash("FlashFirePistol");
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR39");}
			}
			FR30 IIII 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR39");}
				A_DoPBWeaponAction(WRF_ALLOWRELOAD);
			}
			TNT1 A 0 A_Refire
			Goto Ready3
		
		FlashFirePistol:
			D2FS W 1 BRIGHT {
				A_FireBullets (0.1, 0.1, -1, 15, "HitPuff", FBF_NORANDOM,8192,"Tracer", -1,4);
				A_ZoomFactor(0.985);
				A_PlaySound("weapons/pistol");
				A_AlertMonsters;
				A_FireCustomMissile("GunFireSmoke", 0, 0, 1, 1, 0, 0);
				A_FireCustomMissile("YellowFlareSpawn", 0, 0, 1, 1, 0, 0);
				A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
				A_FireCustomMissile("PistolCasingSpawner", 0,0,0,2);
				A_Takeinventory("PrimaryPistolAmmo",1);
				PB_WeaponRecoil(-0.18,-0.08);
			}
			D2FS X 1 BRIGHT {
				A_ZoomFactor(0.99);
				PB_WeaponRecoil(-0.18,-0.08);
			}
			D2FS Y 1 A_ZoomFactor(1.0)
			D2FS Z 1
			Stop


		AltFire: //drawing pistol with full freezer
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_SetRoll(0);
				PB_HandleCrosshair(79);
				A_TakeInventory("PB_LockScreenTilt",1);
			}
			TNT1 A 0 A_JumpIfInventory("FreezerPistolToken",1,"UnequipPistol")
			TNT1 A 0 {	
				A_GiveInventory("FreezerPistolToken", 1);
				A_PlaySoundEx("Ironsights", "Auto");
				A_PlaySoundEx("weapons/pistolup", "Auto");
			}
			FR39 ABCDEFGHIJKLMNOPQZ 0
			FR30 ABCDEFGH 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR39");}
			}
			Goto Ready3
		
		UnequipPistol:
			TNT1 A 0 {
				A_WeaponOffset(0,32);
				A_TakeInventory("FreezerPistolToken", 1);
				A_PlaySoundEx("Ironsights", "Auto");
			}
			FR30 JKLMNOPQ 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR39");}
			}
			Goto Ready3
		
		Reload:
			TNT1 A 0 A_StopSound(1)
			TNT1 A 0 A_StopSound(6)
			TNT1 A 0 A_JumpIfInventory("FreezerPistolToken",1,"ReloadPistol")
			TNT1 A 0 A_JumpIfInventory("FreezerAmmo",60,"Ready3")
			TNT1 A 0 A_JumpIfInventory("Cell",1,1)
			Goto Ready3
			FR94 ABCDEFGHIJKLMNOPQRSTUVWXYZ 0
			FR95 ABCDEFGHIJKLMNOPQRSTUVWXYZ 0
			FR96 ABCDEFGHIJKLMN 0
			//TNT1 A 0 A_TakeInventory("FreezerOverCooling", 420)
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/reload1", "Auto")
			FR04 ABCDEFGHIJ 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR94");}
			}
			FR04 KLM 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR94");}
				A_FireCustomMissile("GunFireSmoke", 0, 0, 5, -5, 0, 0);
			}
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/reload3", "Auto")
			FR04 NOPQRSTUVW 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR94");}
				A_FireCustomMissile("GunFireSmoke", 0, 0, 5, -5, 0, 0);
			}
			TNT1 A 0 A_PlaySoundEx("Weapons/CellEject", "Auto")
			FR04 XYZ 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR94");}
				A_FireCustomMissile("GunFireSmoke", 0, 0, 5, -6, 0, 0);
			}
			FR05 ABCDEFGHIJKLM 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR95");}
				A_FireCustomMissile("GunFireSmoke", 0, 0, 5, -5, 0, 0);
			}
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/reload2", "Auto")
			FR05 NOOOPQRS 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR95");}
			}
			TNT1 A 0 A_PlaySoundEx("weapons/plasma/cellin", "Auto")
			FR05 TUVWXYZ 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR95");}
			}
			FR06 ABCDEFGHI 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR96");}
			}
			TNT1 A 0 A_PlaySoundEx("weapons/nailgun/inspect4", "Auto")
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/up", "Auto")
			FR06 JKLMN 1 {
				if (CountInv("FreezerOverCooling") >= 100 ) { A_SetWeaponSprite("FR96");}
			}
			TNT1 A 0 A_Takeinventory("HasUnloadedFreezer", 1)
		InsertBullets:
			TNT1 A 0 A_JumpIfInventory("FreezerAmmo",60,"Ready3")
			TNT1 A 0 A_JumpIfInventory("Cell",1,1)
			Goto Ready3
			TNT1 A 0 {
				A_Giveinventory("FreezerAmmo",1);
				A_Takeinventory("Cell",1);
				}
			Goto InsertBullets	
			
		
		ReloadPistol:
			TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo", 16, "Ready3")
			TNT1 A 0 A_JumpIfInventory("PistolBullets", 1, 1)
			Goto Ready2
			TNT1 A 0 {
				A_SetCrosshair(5);
				A_Giveinventory("PB_LockScreenTilt",1);
				}
			TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto")
			D5GB A 1 A_SetRoll(roll+.5, SPF_INTERPOLATE) 
			TNT1 A 0 A_FireCustomMissile("EmptyClipMP40",-80,0,3,-4,0,-45)
			D5GB BCD 1 A_SetRoll(roll+.5, SPF_INTERPOLATE)
			D5GB EFGHI 1 A_SetRoll(roll-.5, SPF_INTERPOLATE)
			TNT1 A 0 A_PlaySoundEx("PSRLIN", "Auto")
			D5GB KKLMNOPQRS 1 A_SetRoll(roll-.1, SPF_INTERPOLATE)
			D5GB TUVWX 1 A_SetRoll(roll+0.2, SPF_INTERPOLATE)
			TNT1 A 0 A_SetRoll(0, SPF_INTERPOLATE)
			TNT1 A 0 A_Takeinventory("PB_PistolHasUnloaded",1)
		InsertBulletsChamber:
			TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",16, "Ready3")
			TNT1 A 0 A_JumpIfInventory("PistolBullets", 1, 1)
			Goto Ready3
			TNT1 A 0 {	
				A_Giveinventory("PrimaryPistolAmmo",1);
				A_Takeinventory("PistolBullets",1);
				}
			Loop
		
		Unload:
			TNT1 A 0 A_Takeinventory("Unloading",1)
			FR03 E 1 A_JumpIfInventory("HasUnloadedFreezer", 1, "Ready3")
			TNT1 A 0 A_StopSound(6)
			TNT1 A 0 A_StopSound(1)
			TNT1 A 0 A_JumpIfInventory("FreezerAmmo",1,1)
			Goto Ready3
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/reload1", "Auto")
			FR06 NMLKJIHGFEDCBA 1
			TNT1 A 0 A_PlaySoundEx("weapons/freezer/reload3", "Auto")
			FR05 ZYXWVUTSTRQPONMLKJIH 1
			FR04 KJIHGFEDCBA 1
		RemoveBullets:
			TNT1 A 0 A_JumpIfInventory("FreezerAmmo",1,1)
			Goto FinishUnload
			TNT1 A 0 A_Takeinventory("FreezerAmmo",1)
			TNT1 A 0 A_Giveinventory("Cell",1)
			Goto RemoveBullets
		FInishUnload:
			TNT1 A 0 A_GiveInventory("HasUnloadedFreezer", 1)
			TNT1 A 0 A_Takeinventory("Unloading",1)
			Goto Ready3

		Flash:
			TNT1 A 1
			Goto LightDone
			
		FlashPunching:
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling", 100, "FlashPunchingFrost")
			FR42 ABCDEFGGHIJKLM 1
			Stop
		FlashPunchingFrost:
			FR72 ABCDEFGGHIJKLM 1
			Stop
		FlashKicking:
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling", 100, "FlashKickingFrost")
			FR40 ABCDEFGGHIJKLM 1
			Goto Ready3
		FlashKickingFrost:
			FR70 ABCDEFGGHIJKLM 1
			Goto Ready3
		FlashAirKicking:
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling", 100, "FlashAirKickingFrost")
			FR40 ABCDEFGGGHIJKLM 1
			Goto Ready3
		FlashAirKickingFrost:
			FR70 ABCDEFGGGHIJKLM 1
			Goto Ready3
		FlashSlideKicking:
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling", 100, "FlashSlideKickingFrost")
			FR41 ABCDEFGHIJKLMNOPQRSSSTUVWX 1
			Goto Ready3
		FlashSlideKickingFrost:
			FR71 ABCDEFGHIJKLMNOPQRRRRRSTUV 1
			Goto Ready3
		FlashSlideKickingStop:
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling", 100, "FlashSlideKickingStopFrost")
			FR41 TTTUVWX 1 
			Goto Ready3
		FlashSlideKickingStopFrost:
			FR71 RRRSTUV 1 
			Goto Ready3
			
		
		//Tube/Muzzle Components Effects
		BigTubeGlow:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",360, "BigTubeGlow7")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",300, "BigTubeGlow6")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",240, "BigTubeGlow5")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",180, "BigTubeGlow4")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",120, "BigTubeGlow3")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",60, "BigTubeGlow2")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",40, "BigTubeGlow1")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",35, "BigTubeGlowFade1")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",30, "BigTubeGlowFade2")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",25, "BigTubeGlowFade3")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",20, "BigTubeGlowFade4")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",15, "BigTubeGlowFade5")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",10, "BigTubeGlowFade6")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",5, "BigTubeGlowFade7")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",3, "BigTubeGlowFade8")
			FR50 A 1
			Stop
		BigTubeGlow1:
			FR50 B 1 BRIGHT //BRIGHT
			Stop
		BigTubeGlow2:
			FR50 B 1 BRIGHT //BRIGHT
			Stop
		BigTubeGlow3:
			FR50 C 1 BRIGHT
			Stop
		BigTubeGlow4:
			FR50 D 1 BRIGHT
			Stop
		BigTubeGlow5:
			FR50 E 1 BRIGHT
			Stop
		BigTubeGlow6:
			FR50 F 1 BRIGHT
			Stop
		BigTubeGlow7:
			FR50 G 1 BRIGHT
			Stop
		BigTubeGlowFade1:
			FR50 O 1 BRIGHT //BRIGHT
			Stop
		BigTubeGlowFade2:
			FR50 N 1 BRIGHT //BRIGHT
			Stop
		BigTubeGlowFade3:
			FR50 M 1 BRIGHT //BRIGHT
			Stop
		BigTubeGlowFade4:
			FR50 L 1 BRIGHT //BRIGHT
			Stop
		BigTubeGlowFade5:
			FR50 K 1 BRIGHT //BRIGHT
			Stop
		BigTubeGlowFade6:
			FR50 J 1 BRIGHT //BRIGHT
			Stop
		BigTubeGlowFade7:
			FR50 I 1 BRIGHT //BRIGHT
			Stop
		BigTubeGlowFade8:
			FR50 H 1 BRIGHT //BRIGHT
			Stop
		SmallTubeGlow:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",360, "SmallTubeGlow7")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",300, "SmallTubeGlow6")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",240, "SmallTubeGlow5")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",180, "SmallTubeGlow4")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",120, "SmallTubeGlow3")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",60, "SmallTubeGlow2")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",40, "SmallTubeGlow1")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",35, "SmallTubeGlowFade1")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",30, "SmallTubeGlowFade2")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",25, "SmallTubeGlowFade3")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",20, "SmallTubeGlowFade4")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",15, "SmallTubeGlowFade5")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",10, "SmallTubeGlowFade6")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",5, "SmallTubeGlowFade7")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",3, "SmallTubeGlowFade8")
			FR51 A 1
			Stop
		SmallTubeGlow1:
			FR51 B 1 BRIGHT //BRIGHT
			Stop
		SmallTubeGlow2:
			FR51 B 1 BRIGHT //BRIGHT
			Stop
		SmallTubeGlow3:
			FR51 C 1 BRIGHT
			Stop
		SmallTubeGlow4:
			FR51 D 1 BRIGHT
			Stop
		SmallTubeGlow5:
			FR51 E 1 BRIGHT
			Stop
		SmallTubeGlow6:
			FR51 F 1 BRIGHT
			Stop
		SmallTubeGlow7:
			FR51 G 1 BRIGHT
			Stop
		SmallTubeGlowFade1:
			FR51 O 1 BRIGHT //BRIGHT
			Stop
		SmallTubeGlowFade2:
			FR51 N 1 BRIGHT //BRIGHT
			Stop
		SmallTubeGlowFade3:
			FR51 M 1 BRIGHT //BRIGHT
			Stop
		SmallTubeGlowFade4:
			FR51 L 1 BRIGHT //BRIGHT
			Stop
		SmallTubeGlowFade5:
			FR51 K 1 BRIGHT //BRIGHT
			Stop
		SmallTubeGlowFade6:
			FR51 J 1 BRIGHT //BRIGHT
			Stop
		SmallTubeGlowFade7:
			FR51 I 1 BRIGHT //BRIGHT
			Stop
		SmallTubeGlowFade8:
			FR51 H 1 BRIGHT //BRIGHT
			Stop
		MuzzleGlow:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",360, "MuzzleGlow7")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",300, "MuzzleGlow6")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",240, "MuzzleGlow5")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",180, "MuzzleGlow4")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",120, "MuzzleGlow3")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",60, "MuzzleGlow2")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",40, "MuzzleGlow1")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",35, "MuzzleGlowFade1")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",30, "MuzzleGlowFade2")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",25, "MuzzleGlowFade3")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",20, "MuzzleGlowFade4")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",15, "MuzzleGlowFade5")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",10, "MuzzleGlowFade6")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",5, "MuzzleGlowFade7")
			TNT1 A 0 A_JumpIfInventory("FreezerOverCooling",3, "MuzzleGlowFade8")
			FR52 A 1
			Stop
		MuzzleGlow1:
			FR52 B 1 BRIGHT //BRIGHT
			Stop
		MuzzleGlow2:
			FR52 B 1 BRIGHT //BRIGHT
			Stop
		MuzzleGlow3:
			FR52 C 1 BRIGHT
			Stop
		MuzzleGlow4:
			FR52 D 1 BRIGHT
			Stop
		MuzzleGlow5:
			FR52 E 1 BRIGHT
			Stop
		MuzzleGlow6:
			FR52 F 1 BRIGHT
			Stop
		MuzzleGlow7:
			FR52 G 1 BRIGHT
			Stop
		MuzzleGlowFade1:
			FR52 O 1 BRIGHT //BRIGHT
			Stop
		MuzzleGlowFade2:
			FR52 N 1 BRIGHT //BRIGHT
			Stop
		MuzzleGlowFade3:
			FR52 M 1 BRIGHT //BRIGHT
			Stop
		MuzzleGlowFade4:
			FR52 L 1 BRIGHT //BRIGHT
			Stop
		MuzzleGlowFade5:
			FR52 K 1 BRIGHT //BRIGHT
			Stop
		MuzzleGlowFade6:
			FR52 J 1 BRIGHT //BRIGHT
			Stop
		MuzzleGlowFade7:
			FR52 I 1 BRIGHT //BRIGHT
			Stop
		MuzzleGlowFade8:
			FR52 H 1 BRIGHT //BRIGHT
			Stop
		
	}
}

///////////////// Code taken from Cryobow - THANKS GUYS!!!
ACTOR FreezerBall : FastProjectile
{
	Radius 4
	Height 8
	Speed 52
	Damage 0
	Projectile
	DamageType "Freeze"
	RenderStyle Add
	//Translation "0:255=%[0,0,0]:[0,1,1]"
	Alpha 0.95
	+NODAMAGETHRUST
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+BLOODLESSIMPACT
	DeathSound "weapons/cryobowhit"
	//MissileHeight 8
	Decal "FreezerBurn"
	States
	{
	Spawn:
		FRPJ ABC 1 Bright A_SpawnItemEx("FreezerTrailSparksSmall", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0) 
		TNT1 A 0 A_CustomMissile("BlueFlareSpawn",0,0,0,0)
		TNT1 A 0 A_CustomMissile ("Icetracer", 0, 0, random (0, 360), 2, random (0, 360))
		Loop
	Death:	
		TNT1 A 0 A_SpawnItemEx ("DetectFloorIce",0,0,1,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilIce",0,0,1,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 Bright A_StopSound(CHAN_BODY)
		TNT1 A 0 Bright A_ChangeFlag("ICEDAMAGE", 1)
		TNT1 A 0 Bright A_ChangeFlag("NODAMAGETHRUST", 0)
		TNT1 A 0 Bright A_Explode(180,60, 0)  
		TNT1 A 0 Bright {
			if(GetCvar("pb_realdamage")) {A_Explode(60, 120);}
			else{
				A_Explode(60, 120, 0);
				}
			}
	    TNT1 AAAAAAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke", 0, 0, 0, random(10, 30)*0.1, 0, random(0, 10)*0.1, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		TNT1 AAAAA 0 Bright A_SpawnItemEx("CryoSmoke3", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
		TNT1 AAAAAAA 0 Bright A_SpawnItemEx("FreezerTrailSparksSmall", random(5,-5), random(5,-5), random(5,-5), random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
		TNT1 AAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke2", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
		TNT1 A 0 Bright A_SpawnItemEx("IceExplosionImpact", 0, 0, 0, 0, 0, 0, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		BXPL ABCDEFGH 1 Bright
		BXPL IJKLLM 1 Bright A_FadeOut(0.1)
		Stop
	}
}

ACTOR CryoSmoke
{
    Radius 2
    Height 2
    +NOINTERACTION
    +FORCEXYBILLBOARD
    RenderStyle Shaded
    StencilColor "A0 FF FF"
    Alpha 0.8
    Scale 0.85
    States
    {
    Spawn:
		SMKO A 0
        SMK2 A 2 A_FadeOut (0.05)
        Wait
    }
}

Actor CryoSmoke2 : CryoSmoke { StencilColor "DD DD DD" Scale 0.7 Alpha 0.5 VSpeed 2.5
	States {
	Spawn:
		TNT1 A 0
		SMK2 A 6 A_FadeOut(0.04)
		Wait
	}
}

Actor CryoSmoke3 : CryoSmoke { StencilColor "AA FF FF" Scale 0.7 Alpha 0.4 VSpeed 2.25
	States {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("Weapons/FreezerRecharge")
		SMK2 A 6 A_FadeOut(0.04)
		Wait
	}
}

Actor MiniCryoSmoke1 : CryoSmoke { StencilColor "AA FF FF" Scale 0.7 Alpha 0.4 VSpeed 2.25
	Scale 0.25
	States {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("Weapons/FreezerRecharge")
		SMK2 A 6 A_FadeOut(0.04)
		Wait
	}
}

Actor IceTracer
{
  Height 0
  Radius 0
  Mass 0
  +Missile
  +NoBlockMap
 // -NoGravity
  +LowGravity
  +DontSplash
  +DoomBounce
  +FORCEXYBILLBOARD
  BounceFactor 0.2
  Gravity 0.1
  RenderStyle Add
  StencilColor "A0 FF FF"
  Scale 1
  //Speed 2
  Speed 0
  -DOOMBOUNCE
  RenderStyle Add
  Alpha 1
  States
  {
  Spawn:
  Death:
    SHEX AABBCCDDEE 1 Bright A_FadeOut(0.04)
    Stop
  }
}

ACTOR CryoTrail
{
	+NOINTERACTION
	Radius 4
	Height 8
	Renderstyle Add
	Alpha 0.5
	YScale 0.3
	XScale 0.6
	States
	{
	Spawn:
		X027 ABCDEFGHIJK 1 Bright
		Stop
	}
}

Actor FreezerPistolToken : Inventory
{inventory.maxamount 1}
