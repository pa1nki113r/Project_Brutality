Actor LaserSightActivated : Inventory
{
inventory.maxamount 1
}

Actor UACSMGHasUnloaded: Inventory
{
Inventory.maxamount 1
}

Actor UACSMGWasEmpty: Inventory
{
Inventory.maxamount 1
}

ACTOR RespectSMG : Inventory
{
Inventory.MaxAmount 1
}

ACTOR DualWieldingSMGs: Inventory
{
Inventory.MaxAmount 1
}

Actor SelectDualWieldSMG : Inventory
{inventory.maxamount 1}
Actor SelectLaserSight : SelectDualWieldSMG{}

ACTOR Laserdot: BulletPuff
{ 
	Decal "None"
    Mass 0
    Scale 0.10
    Radius 1
    Height 2
	Projectile
	VSpeed 0
    +NOBLOCKMAP
    +NOGRAVITY
    +BLOODLESSIMPACT
    +ALWAYSPUFF
	+FIXMAPTHINGPOS
    +PUFFONACTORS
    +DONTSPLASH
    +FORCEXYBILLBOARD
	-ALLOWPARTICLES
    Renderstyle Add
    Alpha 0.75
    States
    {
	Melee:
	Spawn:
		LEYS R 1 BRIGHT //A_AlertMonsters(500)
	Death:
	XDeath:
	Crash:
		LEYS R 1 
		TNT1 A 0 A_SpawnItemEx("LaserAlertNearbyMonsters",0,0,0,0,0,0,0,288)
		LEYS R 1 BRIGHT
		Stop
	}
}

Actor LaserDotPerma : LaserDot
{
	States
	{
	Melee:
	Spawn:
		LEYS R 2 BRIGHT
		TNT1 A 0 A_SpawnItemEx("LaserAlertNearbyMonsters",0,0,0,0,0,0,0,288)
		Stop
	}
}
ACTOR LaserAlertNearbyMonsters : CustomInventory
{	
	Radius 600
    +DONTSPLASH
	+INVENTORY.QUIET
	Inventory.MaxAmount 1
	States
	{
	Spawn:
		TNT1 A 0 A_ALERTMONSTERS(750)
		TNT1 A 5
		Stop
	Pickup:
		TNT1 A 0 A_ALERTMONSTERS(750)
		TNT1 A 0 A_SetInventory("LaserAlertNearbyMonsters", 0)
		Stop
	}
}
ACTOR KeepLaserDeactivated : Inventory {}

ACTOR PB_SMG : PB_Weapon
{
	//$Category Project Brutality - Weapons
	//$Sprite ATFLA0
	SpawnID 9200
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 50
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "PB_LowCalMag"
	Weapon.AmmoType2 "SMGAmmo"
	Inventory.MaxAmount 3
	Inventory.Amount 1
	+FLOORCLIP
	+DONTGIB
	Obituary "%o was shot down by %k's UAC-17 Compact Sub-Machine Gun."
    Inventory.PickupSound "CLIPIN"
	Inventory.Pickupmessage "UAC Compact Sub-Machine Gun (Slot 2)"
	Tag "UAC-17 SMG"
    +WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
    +WEAPON.NOAUTOFIRE
    Scale 0.4
	PB_WeaponBase.UnloaderToken "UACSMGHasUnloaded"
	Inventory.AltHUDIcon "ATFLA0"
	PB_WeaponBase.RespectItem "RespectSMG"
	FloatBobStrength 0.5
	PB_WeaponBase.OffsetRecoilX 3.0
	PB_WeaponBase.OffsetRecoilY 2.5
	PB_WeaponBase.UsesWheel 1
	PB_WeaponBase.WheelInfo "PB_SMGWheel"
	States
	{
	Steady:
		TNT1 A 1
		Goto Ready
	
	LaserOverlay:
		TNT1 A 1
		{
				if (CountInv("LaserSightActivated")==1 && CountInv("KeepLaserDeactivated") != 1)
				{
					A_SpawnLaserPuff(0,0,"LaserDotPerma");
				}
				else 
				{
					A_DestroyLaserPuff;
				}
		}
		TNT1 A 0 A_DestroyLaserPuff()
		Loop
		
	Ready:
		TNT1 A 0
//        TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "UnloadedReady")
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0 PB_RespectIfNeeded
	WeaponRespect:
		TNT1 A 0 {
			A_SetInventory("RespectSMG",1);
			A_SetInventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("weapons/smg_up", "Auto");
			A_SetCrosshair(5);
			}
		A1F5 ABCDEFGHIJKKKKKKLMNOPQ 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/smg_click", "Auto")
		A1F5 RSTTUVWXYYY 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/smg_magfly1", "Auto")
		A1F5 Z 1 A_DoPBWeaponAction
		A1F6 ABCDEF 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/smg_magfly2", "Auto")
		A1F6 GHIJ 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("IronSights", "Auto")
		A1F6 KLMNOPQRS 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/smg_in", "Auto")
		A1F6 TTUV 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("IronSights", "Auto")
		A1F6 WXYZ 1 A_DoPBWeaponAction
		A1F7 XY 1 A_DoPBWeaponAction
		Goto Ready3
	
	SelectAnimation:
		TNT1 A 0 A_SetInventory("Zoomed",0)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "SelectAnimationDualWield")
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded",1, "SelectAnimationUnloaded")
        A1F1 ABCD 1
	Ready3:
		TNT1 A 0 A_SetInventory("KeepLaserDeactivated",0)
		TNT1 A 0 A_JumpIfInventory("UACSMGWasEmpty", 1, "LoadChamber")
		TNT1 A 0 {
			A_SetRoll(0);
			A_SetInventory("PB_LockScreenTilt",0);
			if(CountInv("LaserSightActivated") == 1) {
				PB_HandleCrosshair(5);
				}
				else {
				PB_HandleCrosshair(44);
				}	
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "ReadyDualWield")
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "UnloadedReady")
	ReadyToFire:
		A1F1 E 1 {
			A_Overlay(-16,"LaserOverlay");
			if (PressingFire() && PressingAltfire() && CountInv("SMGAmmo") > 0){
					return state("Fire");
			}
			if (PressingFire() && CountInv("SMGAmmo") > 0){
					return state("Fire");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD, CheckUnloaded("UACSMGHasUnloaded"));	
		}
		Loop
		
    Ready2:
		TNT1 A 0 {
			A_SetRoll(0);
			A_SetCrosshair(5);
			A_SetInventory("PB_LockScreenTilt",0);
			}
	ReadyToFire2:
        A1F3 D 1 {
			//Updated code for far superior smooth gameplay
			if(GetCvar("pb_toggle_aim_hold")) {
				if(!PressingAltfire() || JustReleased(BT_ALTATTACK)){
					return state("Zoomout");
				}
				if (PressingFire() && PressingAltfire() && CountInv("SMGAmmo") > 0){
						return state("Fire2");
				}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOSECONDARY, CheckUnloaded("UACSMGHasUnloaded"));
			}
			else {
				if (PressingFire() && CountInv("SMGAmmo") > 0 ){
					return state("Fire2");
				}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD, CheckUnloaded("UACSMGHasUnloaded"));
			}
			return state("");
		}
		Loop
	SelectAnimationUnloaded:
		A1F1 A 1
		A0F1 BCD 1
		Goto Ready3
	UnloadedReady:
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "UnloadedReadyDualWield")
		A1F1 Z 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD, PBWEAP_UNLOADED)
		Loop
	
	SelectAnimationDualWield:
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "SelectAnimationDWUnloaded")
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
		A2F3 GFEDCBA 1
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
		Goto ReadyDualWield
	SelectAnimationDWUnloaded:
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
		A0F2 CDEFGHI 1
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
	ReadyDualWield:
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "UnloadedReadyDualWield")
		TNT1 A 0 {
			A_SetRoll(0);
			A_SetInventory("PB_LockScreenTilt",0);
			if(CountInv("LeftSMGAmmo") < CountInv("SMGAmmo")){
				A_GiveInventory("DualFiring",1);
			}
// 			if(CountInv("PB_LowCalMag")>0){
// 				if(CountInv("LeftSMGAmmo")<=0){
// 					A_GiveInventory("DualFireReload",1);
// 				}
// 				if(CountInv("SMGAmmo")<=0){
// 					A_GiveInventory("DualFireReload",1);
// 				}
// 			}
			A_Overlay(10, "IdleLeft_Overlay", false);
			A_Overlay(11, "IdleRight_Overlay", false);
			}
	ReadyToFireDualWield:
		TNT1 A 0 A_Overlay(-16,"LaserOverlay")
		TNT1 A 1 {
			if(CountInv("PB_LowCalMag")>0){
				if(CountInv("LeftSMGAmmo")<=0 || CountInv("SMGAmmo")<=0){
					if(CountInv("LeftSMGAmmo")<=0 && CountInv("SMGAmmo")<=0){
						A_SetInventory("DualFireReload",2);
					}
					else{
						A_SetInventory("DualFireReload",1);
					}
				}
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE);
		}
		Loop
		
		
		
	IdleLeft_Overlay:
		A2F2 I 1 {
			if(CountInv("LeftSMGAmmo")<=0 && CountInv("SMGAmmo")>0){
				A_GiveInventory("DualFiring",1);
			}
			if((PressingAltFire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire") == 2){
					if(CountInv("LeftSMGAmmo") > 0){
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			if(CountInv("DualFiring")==0 || (CountInv("DualFiring")==0 && CountInv("SMGAmmo")<=0) || GetCvar("SingleDualFire")==1){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire") < 2){
					if(CountInv("LeftSMGAmmo") > 0){
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			return state("");
		}
		Loop
	IdleRight_Overlay:
		A2F2 H 1 {
			if(CountInv("LeftSMGAmmo")>0 && CountInv("SMGAmmo")<=0){
				A_TakeInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==1 || (CountInv("DualFiring")==1 && CountInv("LeftSMGAmmo")<=0)){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire")==0){
					if(CountInv("SMGAmmo") > 0){
						return state("FireRight_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			if((PressingAltfire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringRightWeapon() && GetCvar("SingleDualFire")==1){
				if(CountInv("SMGAmmo") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringRightWeapon() && GetCvar("SingleDualFire")==2){
				if(CountInv("SMGAmmo") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			return state("");
		}
		Loop
		
	FireLeft_Overlay:
		A2F1 D 1 BRIGHT {
			A_AlertMonsters;
			A_PlaySoundEx("ZSpecOps/MGun", "SoundSlot7");
			//A_FireBullets (0.1, 0.1, -1, 12, "HitPuff", FBF_NORANDOM,8192,"Tracer", -4, -8);
			A_FireProjectile("PB_45ACP", frandom(-0.1,0.1),0,0,0, FPF_NOAUTOAIM, frandom(-0.1,0.1));
			A_FireCustomMissile("YellowFlareSpawn",0,0,-4,0);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, -4, 0, 0, 0);
			PB_GunSmoke(4,0,0);
			A_FireCustomMissile("SMGCasingSpawner",5,0,-14,-6);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			PB_LowAmmoSoundWarning("smg", "LeftSMGAmmo");
			A_Takeinventory("LeftSMGAmmo",1);
			A_ZoomFactor(0.985);
			A_GunFlash;
            PB_WeaponRecoil(-1.1,+0.76);
			}
		A2F1 E 1 {
			A_ZoomFactor(0.99);
			if(CountInv("LeftSMGAmmo")<=0 || CountInv("SMGAmmo")>0 ){
				A_GiveInventory("DualFiring",1);
			}
            PB_WeaponRecoil(-1.1,+0.76);
			}
		A2F1 F 1 A_ZoomFactor(1.0)
		TNT1 A 0 {
			if(CountInv("LeftSMGAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleLeft_Overlay
		
	FireRight_Overlay:
		A2F1 A 1 BRIGHT {
			A_AlertMonsters;
			A_PlaySoundEx("ZSpecOps/MGun", "SoundSlot6");
			//A_FireBullets (0.1, 0.1, -1, 12, "HitPuff", FBF_NORANDOM,8192,"Tracer", -4, 8);
			A_FireProjectile("PB_45ACP", frandom(-0.1,0.1),0,0,0, FPF_NOAUTOAIM, frandom(-0.1,0.1));
			A_FireCustomMissile("YellowFlareSpawn",0,0,4,0);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, 4, 0, 0, 0);
			PB_GunSmoke(-4,0,0);
			A_FireCustomMissile("SMGCasingSpawner",5,0,16,-5);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			PB_LowAmmoSoundWarning("smg");
			A_Takeinventory("SMGAmmo",1);
			A_ZoomFactor(0.985);
			A_GunFlash;
            PB_WeaponRecoil(-1.1,-0.76);
			}
		A2F1 B 1 {
			A_ZoomFactor(0.99);
			if(CountInv("LeftSMGAmmo")>0 || CountInv("SMGAmmo")<=0 ){
				A_TakeInventory("DualFiring",1);
			}
            PB_WeaponRecoil(-1.1,-0.76);
		}
		A2F1 C 1 A_ZoomFactor(1.0)
		TNT1 A 0 {
			if(CountInv("SMGAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleRight_Overlay
		
		
		
	UnloadedReadyDualWield:
		A2F3 Z 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD, PBWEAP_UNLOADED)
		Loop
		
	WheelCancel:
		TNT1 A 0 A_SetInventory("SelectLaserSight",0)
		TNT1 A 0 A_SetInventory("SelectDualWieldSMG",0)
		Stop
		
	WeaponSpecial:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "IdleBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "IdleFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "IdleIceBarrel")
		TNT1 A 0 {
			A_SetInventory("GoWeaponSpecialAbility",0);
			A_SetInventory("Zoomed",0);
			A_SetInventory("ADSmode",0);
			A_SetInventory("PB_LockScreenTilt",1);
			//PB_HandleCrosshair(44);
			A_ZoomFactor(1.0);
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 {
			if(CountInv("SelectLaserSight") == 1) {	
				A_Overlay(-12, "WheelCancel");
				return state ("ToggleLaser");
				}
			if(CountInv("SelectDualWieldSMG") == 1) {
				A_Overlay(-12, "WheelCancel");
				return state ("DualHandler");
				}
			return state("");
			}
			Goto Ready3
	ToggleLaser:
		TNT1 A 0 {
				if(CountInv("LaserSightActivated")){A_SetInventory("LaserSightActivated",0); 
				A_Print("\cgLaser Sight \c-Deactivated");
				PB_HandleCrosshair(44);
				}
				else{
				A_SetInventory("LaserSightActivated",1); 
				A_Print("\cgLaser Sight \c-Activated");
				PB_HandleCrosshair(5);
				}
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "ToggleLaserDualWield")
		ATFW B 1 Offset(-18, 55)
		ATFW BB 1 A_WeaponOffset(-10, 8, WOF_ADD)
		ATFW B 1
		ATFW B 3 A_StartSound("LIGHTON", CHAN_AUTO)
		ATFW BBB 1 A_WeaponOffset(10, -8, WOF_ADD)
		TNT1 A 0 A_WeaponOffset(0,32)
		Goto Ready3
	
	ToggleLaserDualWield:
		A2F3 ABCDE 1
		A2F3 E 3 A_StartSound("LIGHTON", CHAN_AUTO)
		A2F3 DCBA 1
		Goto Ready3
		
	DualHandler:
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "StopDualWield")
		TNT1 A 0 A_JumpIfInventory("PB_SMG", 2,"SwitchToDualWield")
		TNT1 A 0 A_Print("You need two submachine guns to dual wield!")
		Goto Ready3
	SwitchToDualWield:
		TNT1 A 0 {
			A_SetInventory("DualWieldingSMGs", 1);
			A_PlaySoundEx("weapons/smg_up", "Auto");
			}
		A0F2 A 0 A_JumpIfInventory("UACSMGHasUnloaded",1,2) //Load the unloaded frames into memory
		A1F2 A 0
		"####" ABCDEF 1 A_Setroll(roll-0.5, SPF_INTERPOLATE)
		"####" GHI 1 A_Setroll(roll+1.0, SPF_INTERPOLATE)
		Goto ReadyDualWield
	StopDualWield:
		TNT1 A 0 {
			A_SetInventory("DualWieldingSMGs", 0);
			A_PlaySoundEx("weapons/smg_up", "Auto");
			}
		A0F2 A 0 A_JumpIfInventory("UACSMGHasUnloaded",1,2) //Load the unloaded frames into memory
		A1F2 A 0
		"####" IHGFED 1 A_Setroll(roll+0.5, SPF_INTERPOLATE)
		"####" CBA 1 A_Setroll(roll-1.0, SPF_INTERPOLATE)
		Goto Ready3
	
	Deselect:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "PlaceBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "PlaceFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "PlaceIceBarrel")
		TNT1 A 0 {
			A_DestroyLaserPuff();
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			A_SetInventory("PB_LockScreenTilt",0);
			A_ClearOverlays(10,11);
		}
		TNT1 A 0 A_SetInventory("Unloading",0)
		TNT1 A 0 A_SetInventory("Zoomed",0)
		TNT1 A 0 A_SetInventory("ADSmode",0)
		TNT1 A 0 A_Overlay(-9, "Null")
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "DeselectDualWield")
        TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded",1,"DeselectUnloaded")
		A1F1 DCBA 1
		TNT1 A 0 A_DestroyLaserPuff
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectDWUnloaded:
		A0F2 IHGF 1 A_Lower
		A0F2 EDC 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectUnloaded:
		A0F1 DCB 1
		A1F1 A 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectDualWield:
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded",1,"DeselectDWUnloaded")
		A2F3 ABCDEFG 1
		TNT1 A 0 A_DestroyLaserPuff
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait

	Select:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			A_SetInventory("PB_LockScreenTilt",0);
			A_ClearOverlays(10,11);
		}
		
		TNT1 A 0 PB_ResetBarrelTokens
		
		Goto SelectFirstPersonLegs
		SelectContinue:
		TNT1 A 0
		TNT1 A 0 PB_WeapTokenSwitch("UACSMGSelected")
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait
		
    Fire:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "ThrowBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "ThrowFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "ThrowIceBarrel")
		TNT1 A 0 A_JumpIfInventory ("DualWieldingSMGs", 1, "ReadyDualWield")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			A_SetInventory("PB_LockScreenTilt",0);
			if(CountInv("LaserSightActivated") == 1) {
				PB_HandleCrosshair(5);
				}
				else {
				PB_HandleCrosshair(44);
				}	
			}
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Fire2")
        TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
        Goto Reload
		TNT1 A 0 A_Jump(256, "Flash_Variant1", "Flash_Variant2", "Flash_Variant3", "Flash_Variant4", "Flash_Variant5")
	ActualFire:
		"####" "#" 1 BRIGHT {
			PB_FireOffset;
			A_AlertMonsters;
			A_PlaySoundEx("ZSpecOps/MGun", "Weapon");
			//A_FireBullets (0.1, 0.1, -1, 12, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireProjectile("PB_45ACP", frandom(-0.1,0.1),0,0,0, FPF_NOAUTOAIM, frandom(-0.1,0.1));
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			PB_GunSmoke(0,0,0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_FireCustomMissile("SMGCasingSpawner",5,0,0,-9);
			PB_LowAmmoSoundWarning("smg");
			A_Takeinventory("SMGAmmo",1);
			A_ZoomFactor(0.985);
			A_GunFlash;
            PB_WeaponRecoil(-0.24,+0.22);
			}
		A1F1 G 1 BRIGHT {
			A_ZoomFactor(0.99);
            PB_WeaponRecoil(-0.24,+0.22);
			}
		A1F1 H 1 {
			A_ZoomFactor(1.0);
			A_WeaponReady(WRF_NOPRIMARY);
		}
		TNT1 A 0 A_Refire("Fire")
		Goto Ready3
	Flash_Variant1:
		A1F1 F 0
		Goto ActualFire
	Flash_Variant2:
		A1F1 I 0
		Goto ActualFire
	Flash_Variant3:
		A1F1 J 0
		Goto ActualFire
	Flash_Variant4:
		A1F1 K 0
		Goto ActualFire
	Flash_Variant5:
		A1F1 L 0
		Goto ActualFire
	
      Fire2:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetCrosshair(5);
			}
	    TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
        Goto Reload
		A1F3 E 1 BRIGHT {
			A_AlertMonsters;
			A_PlaySoundEx("ZSpecOps/MGun", "Weapon");
			//A_FireBullets (0.1, 0.1, -1, 12, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireProjectile("PB_45ACP", frandom(-0.1,0.1),0,0,0, FPF_NOAUTOAIM, frandom(-0.1,0.1));
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 3, 0, 0);
			PB_GunSmoke(0,0,0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_FireCustomMissile("SMGCasingSpawner",-2,0,2,0);
			PB_LowAmmoSoundWarning("smg");
			A_Takeinventory("SMGAmmo",1);
			A_ZoomFactor(1.19);
            PB_WeaponRecoil(-0.2,+0.18);
			}
		A1F3 F 1 {
			A_ZoomFactor(1.195);
            PB_WeaponRecoil(-0.2,+0.18);
			}
		A1F3 G 1 A_ZoomFactor(1.2)
   		TNT1 A 0 {
			if(GetCvar("pb_toggle_aim_hold")) {
				if(JustReleased(BT_ALTATTACK)){
					return state("Zoomout");
				}
		
				if (JustPressed(BT_ATTACK) && PressingAltfire()){
						return state("Fire2");
				}
			}
			else {
				if(PressingAltfire()){
					return state("Zoomout");
				}
				
				if (JustPressed(BT_ATTACK)){
						return state("Fire2");
				}
				A_Refire("Fire2");
				
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE);
		}
		Goto Ready2
	
    AltFire:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "PlaceBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "PlaceFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "PlaceIceBarrel")
		TNT1 A 0 A_JumpIfInventory ("DualWieldingSMGs", 1, "ReadyDualWield")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(44);
			A_SetInventory("PB_LockScreenTilt",0);
		}
        TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "UnloadedReady")
		TNT1 A 0 A_PlaySound("IronSights", 0)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Zoomout")
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_SetInventory("Zoomed",1);
			A_ZoomFactor(1.2);
			A_SetInventory("ADSmode",1);
		}
        A1F3 ABC 1
        Goto Ready2
		
    Zoomout:
		TNT1 A 0 {
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
			A_ZoomFactor(1.0);
			if(CountInv("LaserSightActivated") == 1) {
				PB_HandleCrosshair(5);
				}
				else {
				PB_HandleCrosshair(44);
				}	
			}
        A1F3 CBA 1
        Goto Ready3
		
	Reload:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "IdleBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "IdleFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "IdleIceBarrel")
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
		}
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "ReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "ReloadUnloaded")
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",51,"Ready3")
        TNT1 A 0 A_JumpIfInventory("PB_LowCalMag",1,2)
        TNT1 A 0 A_PlaySoundEx("weapons/empty", "Weapon")
        Goto Ready3
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
			A_SetInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
		Goto Reload2
        A1F4 ABCD 1
		A1F4 E 1 Offset(0,34)
		TNT1 A 0 A_PlaySoundEx("weapons/smg_out", "Auto")
		A1F4 F 1 Offset(0,34)
		A1F4 F 2 Offset(0,35)
		A1F4 F 2 Offset(0,36)
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
		A1F4 F 1 Offset(0,34)
    ReloadUnloaded:
		TNT1 A 0 A_WeaponOffset(0,32)
		A1F4 GHIJ 1 A_SetRoll(roll+0.4, SPF_INTERPOLATE)
		A1F4 KLMNO 1 A_SetRoll(roll-0.4, SPF_INTERPOLATE)
		A1F4 O 1
		A1F4 PQRST 1 A_SetRoll(roll-0.5, SPF_INTERPOLATE)
		TNT1 A 0 {
			A_PlaySoundEx("weapons/smg_in", "Auto");
			A_SetInventory("UACSMGHasUnloaded",0);
		}
		A1F4 UVWXY 1 A_WeaponOffset(0,32,SPF_INTERPOLATE)
		A1F4 Z 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		Goto InsertBullets2 //30+1 effect

	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",50,"Ready3")
		TNT1 A 0 A_JumpIfInventory("PB_LowCalMag",1,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("SMGAmmo",1);
			A_Takeinventory("PB_LowCalMag",1);
			}
		Loop
	InsertBullets2:
		A2F5 ABCD 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		A2F5 EFGH 1 A_SetRoll(roll-0.5, SPF_INTERPOLATE)
	InsertBullets2Loop:
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",51,"Ready3")
		TNT1 A 0 A_JumpIfInventory("PB_LowCalMag",1,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("SMGAmmo",1);
			A_Takeinventory("PB_LowCalMag",1);
			}
		Loop
	Reload2: //Reload and Chamber
		A1F0 ABCDE 1
		TNT1 A 0 A_StartSound("RIFCL_CK", CHAN_AUTO)
		A1F0 FGHIJ 1 A_SetRoll(roll+0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_StartSound("weapons/smg_out", CHAN_AUTO)
		A1F0 KL 2
		TNT1 A 0 A_StartSound("weapons/smg_up", CHAN_AUTO)
        A1F0 MN 1 A_SetRoll(roll-0.4, SPF_INTERPOLATE)
//     	TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded",1,2)
		TNT1 A 0 PB_SpawnCasing("SMGEmptyMagazine",28,0,30,3,Frandom(-8,-5),Frandom(1,4))
	ReloadUnloaded:
        A1F0 OPQRSTU 1 A_SetRoll(roll-0.4, SPF_INTERPOLATE)
		A1F0 U 2
		A1F0 VWXYZ 1 A_SetRoll(roll-0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_SetInventory("UACSMGHasUnloaded",0)
		A2F4 A 1 A_StartSound("weapons/smg_in", CHAN_AUTO) //A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		A2F4 BCCCDE 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_SetInventory("UACSMGWasEmpty", 1)	
		Goto InsertBullets
		
	LoadChamber:
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
			A_SetInventory("UACSMGWasEmpty",0);
			A_SetInventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("IronSights", "Auto");
			}
		A2F4 FFGHI 1
		TNT1 A 0 A_PlaySoundEx("weapons/smg_click", "Auto")
		A2F4 JK 1 A_SetRoll(roll+0.4, SPF_INTERPOLATE)
		A2F4 L 1 {
			A_WeaponOffset(-1,32);
			A_SetRoll(roll+0.2, SPF_INTERPOLATE);
			}
		A2F4 M 1 A_SetRoll(roll+0.2, SPF_INTERPOLATE)
		A2F4 M 1 {
			A_WeaponOffset(-2,32);
			A_SetRoll(roll+0.2, SPF_INTERPOLATE);
			}
		TNT1 A 0 A_WeaponOffset(0,32)
		A2F4 NOPQR 1 A_SetRoll(roll+0.3, SPF_INTERPOLATE)
		A2F4 STUV 1 A_SetRoll(roll -0.4, SPF_INTERPOLATE)
		Goto Ready3
		
	ReloadLeftGunOnly:
		TNT1 A 0 A_PlaySoundEx("weapons/smg_out", "Auto")
		TNT1 A 0 A_JumpIfInventory("LeftSMGAmmo",1,2)
		TNT1 A 0 A_FireCustomMissile("SMGEmptyMagazine",-5,0,-6,-20)
		A2F3 ABCDEFG 1
		TNT1 A 5
		Goto ReloadLeftGun
		
	ReloadDualWield:
		TNT1 A 0 {
			if (CountInv("SMGAmmo") >= 51 && CountInv("LeftSMGAmmo") >= 51) {
				return state("Ready3");
			}
			return state("");
		}
        TNT1 A 0 A_JumpIfInventory("PB_LowCalMag",1,2)
		TNT1 A 0 A_PlaySoundEx("weapons/empty", "Auto")
		Goto Ready3
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
			A_SetInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 A_JumpIfInventory("SMGAmmo", 51, "ReloadLeftGunOnly")
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, 9)
		A3F4 A 1 A_PlaySoundEx("weapons/smg_out", "Auto")
		A3F4 B 1
		A3F4 C 1 A_PlaySoundEx("weapons/smg_out", "Auto")
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,2)
		TNT1 A 0 A_FireCustomMissile("SMGEmptyMagazine",5,0,6,-20)
		TNT1 A 0 A_JumpIfInventory("LeftSMGAmmo",1,2)
		TNT1 A 0 A_FireCustomMissile("SMGEmptyMagazine",-5,0,-6,-20)
		A3F4 DEFGHI 1
		// Normal Reload Frames
		A1F4 I 1 Offset (22,36)
		A1F4 JKL 1 Offset (6,32)
        A1F4 MNN 1 A_WeaponOffset(0,32)
		A1F4 OPQRST 1 
		TNT1 A 0 {
			A_PlaySoundEx("weapons/smg_in", "Auto");
			A_SetInventory("UACSMGHasUnloaded",0);
			}
		A1F4 UV 1
		A1F4 WXYZ 1
		A2F5 ABC 1
		A2F5 D 1 Offset(-2,32)
		// Normal Reload Frames
		A3F4 KLMN 1 A_WeaponOffset(0,32)
		TNT1 A 0 A_JumpIf(CountInv("LeftSMGAmmo") < 51,2)
		TNT1 A 0 A_JumpIfInventory("UACSMGWasEmpty",1,"InsertBullets3")
		Goto InsertBullets_Right
	ReloadLeftGun:
		TNT1 A 3
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
		A3F4 UTSROPQ 1
		// Normal Reload Frames
		A1F4 J 1
        A1F4 KLMNNN 1
		A1F4 OPQRS 1 
		TNT1 A 0 {
			A_PlaySoundEx("weapons/smg_in", "Auto");
			A_SetInventory("UACSMGHasUnloaded",0);
			}
		A1F4 T 1 Offset(0,32)
		A1F4 T 1 Offset(2,34)
		A1F4 T 1 Offset(4,36)
		A1F4 T 1 Offset(5,40)
		A1F4 UV 1
		A1F4 WXYZ 1 
		// Normal Reload Frames
		A1F2 EFGHI 1
	InsertBullets_Right:
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",51,"InsertBullets_Left")
		TNT1 A 0 A_JumpIfInventory("PB_LowCalMag",2,1)
		Goto InsertBullets_Left
		TNT1 A 0 {
			A_Giveinventory("SMGAmmo",1);
			A_Takeinventory("PB_LowCalMag",1);
		}
		Goto InsertBullets_Right
		
	InsertBullets_Left:
		TNT1 A 0 A_JumpIfInventory("LeftSMGAmmo",51,"FinishInsertBullets")
		TNT1 A 0 A_JumpIfInventory("PB_LowCalMag",1,1)
		Goto FinishInsertBullets
		TNT1 A 0 {
			A_Giveinventory("LeftSMGAmmo",1);
			A_Takeinventory("PB_LowCalMag",1);
		}
		Goto InsertBullets_Left
	FinishInsertBullets:
		TNT1 A 0
		TNT1 A 0 A_SetInventory("UACSMGWasEmpty",0)
		Goto Ready3
		
	Unload:
		ATFG A 0 A_SetInventory("Unloading",0)
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "Ready3")
        TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
        Goto Ready3
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
			A_SetInventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("weapons/smg_up", "Auto");
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs",1,"UnloadDual")
		A4F1 ABCDEF 1
		A1F4 ZYXWVU 1
		A1F4 T 1 A_PlaySoundEx("weapons/smg_out", "Auto")
		A1F4 SRQPONMLKJIH 1
		A4F1 GH 1
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
		Goto FinishUnload
		TNT1 A 0 {
			A_Takeinventory("SMGAmmo",1);
			A_Giveinventory("PB_LowCalMag",1);
			}
		Loop
	
	FInishUnload:
		TNT1 A 0 {
			A_SetInventory("UACSMGHasUnloaded", 1);
			A_SetInventory("Unloading",0);
			}
		Goto Ready3
	UnloadDual:
		TNT1 A 0 A_StopSound(CHAN_AUTO)
		A3F4 A 1 A_PlaySoundEx("weapons/smg_out", "Auto")
		A3F4 B 1
		A3F4 C 1 A_PlaySoundEx("weapons/smg_out", "Auto")
		A3F4 DEFGHI 1
		A1F4 J 5
		A3F4 IHGFE 1
	RemoveBullets_Right:
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
		Goto RemoveBullets_Left
		TNT1 A 0 {
			A_Takeinventory("SMGAmmo",1);
			A_Giveinventory("PB_LowCalMag",1);
			}
		Loop
	RemoveBullets_Left:
		TNT1 A 0 A_JumpIfInventory("LeftSMGAmmo",1,1)
		Goto FinishUnloadDual
		TNT1 A 0 {
			A_Takeinventory("LeftSMGAmmo",1);
			A_Giveinventory("PB_LowCalMag",1);
			}
		Loop
	
	FInishUnloadDual:
		TNT1 A 0 {
			A_SetInventory("UACSMGHasUnloaded", 1);
			A_SetInventory("Unloading",0);
			}
		Goto Ready3
//////////////////////////////////////////Unloading
		
	Spawn:
		VTFL A 0 NoDelay
		ATFL A 10 A_PbvpFramework("VTFL")
		"####" A 0 A_PbvpInterpolate()
		LOOP
		
    FlashKicking:
    TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "FlashKickingDW")
		A1F7 ABCDEFGGFEDCBA 1 A_DoPBWeaponAction
		Goto Ready3


	FlashPunching:
		TNT1 A 0 A_ClearOverlays(10,11)
    TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelPunching")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelPunching")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelPunching")
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "FlashPunchingDW")
		A3F1 ABCDEFGGFEDCBA 1
		Stop
   FlashKicking:

		

		
	FlashAirKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelAirKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelAirKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelAirKicking")	
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "FlashAirKickingDW")
		A1F7 ABCDEFGGGGFEDCBA 1 A_DoPBWeaponAction
		Goto Ready3
		
	FlashKickingDW:
		A2F3 ABCDEFGGGFEDCBA 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Goto Ready3
		
	FlashAirKickingDW:
		A2F3 ABCDEFGGGFEDCBAAA 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Goto Ready3

	FlashPunchingDW:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 15
		Stop
		
	FlashSlideKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "FlashSlideKickingDW")
		A1F8 ABCDEFGHIJKLLMLKJIHGFEDCBA 1 A_DoPBWeaponAction
		A1F1 E 1 A_DoPBWeaponAction
		Goto Ready3
		
	FlashSlideKickingDW:
		A2F3 ABCDEFGGGGGGGGGGGGGFEDCBA 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		A2F3 A 1 A_DoPBWeaponAction
		Goto Ready3
		
	FlashSlideKickingStop:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "FlashSlideKickingStopDW")
		A1F8 GFEDCBA 1 A_DoPBWeaponAction
		A1F1 E 1 A_DoPBWeaponAction
		Goto Ready3
		
	FlashSlideKickingStopDW:
		A2F3 GFEDCBA 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		A2F3 A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Goto Ready3
	}
}

ACTOR SMGAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 51
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 51
   +INVENTORY.IGNORESKILL
   Inventory.Icon "ATFLA0"
}


ACTOR LeftSMGAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 51
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 51
   +INVENTORY.IGNORESKILL
   Inventory.Icon "ATFLA0"
}
