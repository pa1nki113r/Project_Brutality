Actor LaserSightActivated : Inventory
{
inventory.maxamount 1
}

Actor UACSMGHasUnloaded: Inventory
{
Inventory.maxamount 1
}

Actor UACSMGWasEmpty: Inventory
{
Inventory.maxamount 1
}

ACTOR RespectSMG : Inventory
{
Inventory.MaxAmount 1
}

ACTOR DualWieldingSMGs: Inventory
{
Inventory.MaxAmount 1
}

Actor PB_SMGFireAnimation1: Inventory
{
	Inventory.maxamount 1
}

ACTOR Laserdot: BulletPuff
{ 
	Decal "None"
    Mass 0
    Scale 0.10
    Radius 1
    Height 2
	Projectile
	VSpeed 0
    +NOBLOCKMAP
    +NOGRAVITY
    +BLOODLESSIMPACT
    +ALWAYSPUFF
	+FIXMAPTHINGPOS
    +PUFFONACTORS
    +DONTSPLASH
    +FORCEXYBILLBOARD
	-ALLOWPARTICLES
    Renderstyle Add
    Alpha 0.75
    States
    {
	Melee:
	Spawn:
		LEYS R 1 BRIGHT //A_AlertMonsters(500)
	Death:
	XDeath:
	Crash:
		LEYS R 1 
		TNT1 A 0 A_SpawnItemEx("LaserAlertNearbyMonsters",0,0,0,0,0,0,0,288)
		LEYS R 1 BRIGHT
		Stop
	}
}

Actor LaserDotPerma : LaserDot
{
	States
	{
	Melee:
	Spawn:
		LEYS R 2 BRIGHT
		TNT1 A 0 A_SpawnItemEx("LaserAlertNearbyMonsters",0,0,0,0,0,0,0,288)
		Loop
	}
}
ACTOR LaserAlertNearbyMonsters : CustomInventory
{	
	Radius 600
    +DONTSPLASH
	+INVENTORY.QUIET
	Inventory.MaxAmount 1
	States
	{
	Spawn:
		TNT1 A 0 A_ALERTMONSTERS(750)
		TNT1 A 5
		Stop
	Pickup:
		TNT1 A 0 A_ALERTMONSTERS(750)
		TNT1 A 0 A_TakeInventory("LaserAlertNearbyMonsters", 1)
		Stop
	}
}
ACTOR KeepLaserDeactivated : Inventory {}

ACTOR CompactSMG : PB_Weapon
{
	SpawnID 9200
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 50
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "PistolBullets"
	Weapon.AmmoType2 "SMGAmmo"
	+FLOORCLIP
	+DONTGIB
	Obituary "%o was shot down by %k's UAC17 Compact Sub-Machine Gun."
    Inventory.PickupSound "CLIPIN"
	Inventory.Pickupmessage "You found the UAC Compact Sub-Machine Gun! (Slot 2)"
	Tag "UAC-17 SMG"
    +WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
    +WEAPON.NOAUTOFIRE
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
    Scale 0.9
	States
	{
	Steady:
		TNT1 A 1
		Goto Ready
	
	LaserOverlay:
		TNT1 A 1
		{
				if (CountInv("LaserSightActivated")==1 && CountInv("KeepLaserDeactivated") != 1)
				{
					A_SpawnLaserPuff;
				}
				else 
				{
					A_DestroyLaserPuff;
				}
		}
		Loop
		
	Ready:
		TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "UnloadedReady")
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0 A_JumpIfInventory("RespectSMG", 1, "SelectAnimation")
	WeaponRespect:
		TNT1 A 0 {
			A_Giveinventory("RespectSMG",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("weapons/smg_up", "Auto");
			A_SetCrosshair(5);
			}
		A1F5 ABCDEFGHIJKKKKKKLMNOPQ 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/smg_click", "Auto")
		A1F5 RSTTUVWXYYY 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/smg_magfly1", "Auto")
		A1F5 Z 1 A_DoPBWeaponAction
		A1F6 ABCDEF 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/smg_magfly2", "Auto")
		A1F6 GHIJ 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("IronSights", "Auto")
		A1F6 KLMNOPQRS 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/smg_in", "Auto")
		A1F6 TTUV 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("IronSights", "Auto")
		A1F6 WXYZ 1 A_DoPBWeaponAction
		A1F7 AB 1 A_DoPBWeaponAction
		Goto Ready3
		
	SelectAnimation:
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "SelectAnimationDualWield")
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
        A1F1 ABCD 1
	Ready3:
		TNT1 A 0 A_JumpIfInventory("UACSMGWasEmpty", 1, "LoadChamber")
		TNT1 A 0 {
			A_SetRoll(0);
			A_SetCrosshair(0);
			A_TakeInventory("PB_LockScreenTilt",1);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "ReadyDualWield")
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "UnloadedReady")
	ReadyToFire:
		A1F1 E 1 A_DoPBWeaponAction
		Loop
		
    Ready2:
		TNT1 A 0 {
			A_SetRoll(0);
			A_SetCrosshair(5);
			A_TakeInventory("PB_LockScreenTilt",1);
			}
	ReadyToFire2:
        A1F3 D 1 A_DoPBWeaponAction
		Loop
		
	UnloadedReady:
		A1F1 Z 1 A_DoPBWeaponAction
		Loop
	
	SelectAnimationDualWield:
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
		A2F3 GFEDCBA 1
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
	ReadyDualWield:
		TNT1 A 0 {
			A_SetRoll(0);
			A_SetCrosshair(0);
			A_TakeInventory("PB_LockScreenTilt",1);
			}
	TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "UnloadedReadyDualWield")
	ReadyToFireDualWield:
		A2F1 G 1 A_DoPBWeaponAction
		Loop
		
	UnloadedReadyDualWield:
		A2F3 Z 1 A_DoPBWeaponAction
		Loop
		
	WeaponSpecial:
		TNT1 A 0 {
			A_Takeinventory("GoWeaponSpecialAbility",1);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_GiveInventory("PB_LockScreenTilt",1);
			A_SetCrosshair(0);
			A_ZoomFactor(1.0);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "StopDualWield")
		TNT1 A 0 {
			A_GiveInventory("DualWieldingSMGs", 1);
			A_PlaySoundEx("weapons/smg_up", "Auto");
			}
		A1F2 ABCDEF 1 A_Setroll(roll-0.5)
		A1F2 GHI 1 A_Setroll(roll+1.0)
		Goto ReadyDualWield
	StopDualWield:
		TNT1 A 0 {
			A_TakeInventory("DualWieldingSMGs", 1);
			A_PlaySoundEx("weapons/smg_up", "Auto");
			}
		A1F2 IHGFED 1 A_Setroll(roll+0.5)
		A1F2 CBA 1 A_Setroll(roll-1.0)
		Goto Ready3
	
	Deselect:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_DestroyLaserPuff
		TNT1 A 0 A_Overlay(-9, "Null")
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("GotMeatShield", 1, "GrabEnemy")
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "DeselectDualWield")
        A1F1 DCBA 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectDualWield:
		A2F3 ABCDEFG 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait

	Select:
		TNT1 A 0 A_Overlay(-10, "FirstPersonLegsStand")
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_TakeInventory("RifleSelected",1)
		TNT1 A 0 A_TakeInventory("CarbineSelected",1)
		TNT1 A 0 A_TakeInventory("FistsSelected",1)
		TNT1 A 0 A_TakeInventory("SawSelected",1)
		TNT1 A 0 A_TakeInventory("SledgeSelected",1)
		TNT1 A 0 A_TakeInventory("HandgunSelected",1)
		TNT1 A 0 A_TakeInventory("DualHandgunSelected",1)
		TNT1 A 0 A_TakeInventory("RevolverSelected",1)
		TNT1 A 0 A_TakeInventory("ShotgunSelected",1)
		TNT1 A 0 A_TakeInventory("SSGSelected",1)
		TNT1 A 0 A_TakeInventory("ASGSelected",1)
		TNT1 A 0 A_TakeInventory("QSGSelected",1)
		TNT1 A 0 A_TakeInventory("MinigunSelected",1)
		TNT1 A 0 A_TakeInventory("NewChaingunSelected",1)	
		TNT1 A 0 A_TakeInventory("RocketLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("GrenadeLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("SGLSelected",1)
		TNT1 A 0 A_TakeInventory("PlasmaGunSelected",1)
		TNT1 A 0 A_TakeInventory("M2Selected",1)	
		TNT1 A 0 A_TakeInventory("FreezerSelected",1)
		TNT1 A 0 A_TakeInventory("RailGunSelected",1)
		TNT1 A 0 A_TakeInventory("BFGSelected",1)
		TNT1 A 0 A_TakeInventory("BFGBeamSelected",1)
		TNT1 A 0 A_TakeInventory("BHCSelected",1)
		TNT1 A 0 A_TakeInventory("RevenantLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("FlameCannonSelected",1)
		TNT1 A 0 A_TakeInventory("HellRifleSelected",1)
		TNT1 A 0 A_TakeInventory("LandMineSelected",1)
		TNT1 A 0 A_TakeInventory("SubMachineGunSelected",1)
		TNT1 A 0 A_TakeInventory("LostSoulSelected",1)
		TNT1 A 0 A_TakeInventory("HasBarrel",1)
		TNT1 A 0 A_TakeInventory("HasBurningBarrel",1)
		TNT1 A 0 A_GiveInventory("UACSMGSelected",1)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait
		
    Fire:
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "FireDualWield")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Fire2")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetCrosshair(0);
			}
        TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
        Goto Reload
		A1F1 F 1 BRIGHT {
			A_AlertMonsters;
			A_PlaySoundEx("ZSpecOps/MGun", "Weapon");
			A_FireBullets (0.1, 0.1, -1, 12, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_FireCustomMissile("Mp40CaseSpawnRight",5,0,0,-9);
			A_Takeinventory("SMGAmmo",1);
			A_ZoomFactor(0.985);
			
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.12);
				A_SetAngle(angle+0.11);
				}
			else {
				A_SetPitch(pitch-0.24);
				A_SetAngle(angle+0.22);
				}
			}
		A1F1 G 1 {
			A_ZoomFactor(0.99);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.12);
				A_SetAngle(angle+0.11);
				}
			else {
				A_SetPitch(pitch-0.24);
				A_SetAngle(angle+0.22);
				}
			}
		A1F1 H 1 A_ZoomFactor(1.0)
		TNT1 A 0 A_Refire
		Goto Ready3
	
      Fire2:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetCrosshair(5);
			}
	    TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
        Goto Reload
		A1F3 E 1 BRIGHT {
			A_AlertMonsters;
			A_PlaySoundEx("ZSpecOps/MGun", "Weapon");
			A_FireBullets (0.1, 0.1, -1, 12, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 3, 0, 0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_FireCustomMissile("Mp40CaseSpawnRight",-2,0,2,0);
			A_Takeinventory("SMGAmmo",1);
			A_ZoomFactor(1.19);
			
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.1);
				A_SetAngle(angle+0.09);
				}
			else {
				A_SetPitch(pitch-0.2);
				A_SetAngle(angle+0.18);
				}
			}
		A1F3 F 1 {
			A_ZoomFactor(1.195);
			
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.1);
				A_SetAngle(angle+0.09);
				}
			else {
				A_SetPitch(pitch-0.2);
				A_SetAngle(angle+0.18);
				}
			}
		A1F3 G 1 A_ZoomFactor(1.2)
   		A1F3 G 0 A_Refire
		Goto Ready2
		
	FireDualWield:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetCrosshair(0);
			}
        TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
        Goto Reload
	FireRight:
		TNT1 A 0 A_JumpIfInventory("PB_SMGFireAnimation1", 1, "FireLeft")
		A2F1 A 1 BRIGHT {
			A_AlertMonsters;
			A_PlaySoundEx("ZSpecOps/MGun", "SoundSlot6");
			A_FireBullets (0.1, 0.1, -1, 12, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,4,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 4, 0, 0, 0);
			A_FireCustomMissile("Mp40CaseSpawnRight",5,0,8,2);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_Takeinventory("SMGAmmo",1);
			A_Giveinventory("PB_SMGFireAnimation1",1);
			A_ZoomFactor(0.985);
			
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.55);
				A_SetAngle(angle-0.38);
				}
			else {
				A_SetPitch(pitch-1.1);
				A_SetAngle(angle-0.76);
				}
			}
		A2F1 B 1 {
			A_ZoomFactor(0.99);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.55);
				A_SetAngle(angle+0.38); //Not a typo, recoil pattern should pull heavily to the left
				}
			else {
				A_SetPitch(pitch-1.1);
				A_SetAngle(angle+0.76); //Not a typo, recoil pattern should pull heavily to the left
				}
			}
		TNT1 A 0 A_Refire
		A2F1 G 1 A_ZoomFactor(1.0)
		Goto ReadyToFireDualWield
	FireLeft:
		A2F1 C 1 BRIGHT {
			A_AlertMonsters;
			A_PlaySoundEx("ZSpecOps/MGun", "SoundSlot7");
			A_FireBullets (0.1, 0.1, -1, 12, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,-4,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, -4, 0, 0, 0);
			A_FireCustomMissile("Mp40CaseSpawnRight",5,0,-8,2);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_Takeinventory("PB_SMGFireAnimation1",1);
			A_ZoomFactor(0.985);
			
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.55);
				A_SetAngle(angle+0.38);
				}
			else {
				A_SetPitch(pitch-1.1);
				A_SetAngle(angle+0.76);
				}
			}
		A2F1 D 1 {
			A_ZoomFactor(0.99);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.55);
				A_SetAngle(angle+0.38);
				}
			else {
				A_SetPitch(pitch-1.1);
				A_SetAngle(angle+0.76);
				}
			}
		TNT1 A 0 A_Refire
		A2F1 G 1 A_ZoomFactor(1.0)
		Goto ReadyToFireDualWield
	
    AltFire:
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "ReadyToFireDualWield")
        TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "UnloadedReady")
		TNT1 A 0 A_PlaySound("IronSights", 0)
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Unzoom")
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_Giveinventory("Zoomed",1);
			A_ZoomFactor(1.2);
			A_Giveinventory("ADSmode",1);
		}
        A1F3 ABC 1
        Goto Ready2
		
    Unzoom:
        A1F3 CBA 1
		TNT1 A 0 {
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_ZoomFactor(1.0);
			A_SetCrosshair(0);
			}
        Goto Ready3
		
	NoAmmo:
		A1F1 E 1 A_PlaySoundEx("weapons/empty", "Weapon")
		Goto Ready3
	
	Reload:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",51,"Ready3")
        TNT1 A 0 A_JumpIfInventory("PistolBullets",1,1)
        Goto NoAmmo
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			}
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "ReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "ReloadUnloaded")
        A1F4 ABC 1
		TNT1 A 0 A_PlaySoundEx("weapons/smg_out", "Auto")
        A1F4 DEFGHI 1 A_SetRoll(roll+0.4)
        TNT1 A 0 A_FireCustomMissile("EmptyClipMP40",5,0,12,-4)
	ReloadUnloaded:
        A1F4 J 1
        A1F4 KLMNNN 1 A_SetRoll(roll-0.4)
		A1F4 OPQRS 1 A_SetRoll(roll-0.5)
		TNT1 A 0 {
			A_PlaySoundEx("weapons/smg_in", "Auto");
			A_Takeinventory("UACSMGHasUnloaded",1);
			}
		A1F4 T 1 Offset(0,32)
		A1F4 T 1 Offset(2,34)
		A1F4 T 1 Offset(4,36)
		A1F4 T 1 Offset(5,40)
		A1F4 UV 1
		A1F4 WXYZ 1 A_SetRoll(roll+0.5)
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,"InsertBullets2")//30+1 effect
		TNT1 A 0 A_GiveInventory("UACSMGWasEmpty", 1)
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",50,"Ready3")
		TNT1 A 0 A_JumpIfInventory("PistolBullets",1,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("SMGAmmo",1);
			A_Takeinventory("PistolBullets",1);
			}
		Loop
		
	InsertBullets2:
		A1F4 CBA 1
	InsertBullets2Loop:
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",51,"Ready3")
		TNT1 A 0 A_JumpIfInventory("PistolBullets",1,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("SMGAmmo",1);
			A_Takeinventory("PistolBullets",1);
			}
		Loop
		
	LoadChamber:
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("UACSMGWasEmpty",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("IronSights", "Auto");
			}
		A2F4 NMLKJ 1
		A2F4 ABCDEF 1 A_SetRoll(roll+0.2)
		TNT1 A 0 A_PlaySoundEx("weapons/smg_click", "Auto")
		A2F4 GH 1
		A2F4 IJKLMN 1 A_SetRoll(roll-0.2)
		Goto Ready3
		
	ReloadDualWield:
		A3F4 A 1 A_PlaySoundEx("weapons/smg_out", "Auto")
		A3F4 B 1
		A3F4 C 1 A_PlaySoundEx("weapons/smg_out", "Auto")
		A3F4 DEFGHI 1
		
		// Normal Reload Frames
		A1F4 J 1
        A1F4 KLMNNN 1
		A1F4 OPQRS 1 
		TNT1 A 0 {
			A_PlaySoundEx("weapons/smg_in", "Auto");
			A_Takeinventory("UACSMGHasUnloaded",1);
			}
		A1F4 T 1 Offset(0,32)
		A1F4 T 1 Offset(2,34)
		A1F4 T 1 Offset(4,36)
		A1F4 T 1 Offset(5,40)
		A1F4 UV 1
		A1F4 WXYZ 1 
		// Normal Reload Frames
		
		A3F4 JKLMN 1
		TNT1 A 3
		TNT1 A 0 A_PlaySoundEx("weapons/smg_up", "Auto")
		A3F4 NMLOPQ 1
		
		
		// Normal Reload Frames
		A1F4 J 1
        A1F4 KLMNNN 1
		A1F4 OPQRS 1 
		TNT1 A 0 {
			A_PlaySoundEx("weapons/smg_in", "Auto");
			A_Takeinventory("UACSMGHasUnloaded",1);
			}
		A1F4 T 1 Offset(0,32)
		A1F4 T 1 Offset(2,34)
		A1F4 T 1 Offset(4,36)
		A1F4 T 1 Offset(5,40)
		A1F4 UV 1
		A1F4 WXYZ 1 
		// Normal Reload Frames
		A1F2 EFGHI 1
	InsertBullets3:
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",51,"ReadyDualWield")
		TNT1 A 0 A_JumpIfInventory("PistolBullets",1,1)
		Goto ReadyDualWield
		TNT1 A 0 {
			A_Giveinventory("SMGAmmo",1);
			A_Takeinventory("PistolBullets",1);
			}
		Loop
		
	Unload:
		ATFG A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_JumpIfInventory("UACSMGHasUnloaded", 1, "Ready3")
        TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
        Goto Ready3
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("weapons/smg_up", "Auto");
			}
		A1F4 ZYXWVUTS 1
		A1F4 R 1 A_PlaySoundEx("weapons/smg_out", "Auto")
		A1F4 QPONMLKJ 1
		A2F4 OPQ 1
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
		Goto FinishUnload
		TNT1 A 0 {
			A_Takeinventory("SMGAmmo",1);
			A_Giveinventory("PistolBullets",1);
			}
		Loop
	
	FInishUnload:
		TNT1 A 0 {
			A_GiveInventory("UACSMGHasUnloaded", 1);
			A_Takeinventory("Unloading",1);
			}
		Goto Ready3

//////////////////////////////////////////Unloading
		
 	Spawn:
		ATFL A -1
		Stop
		
	FlashPunching:
    FlashKicking:
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "FlashKickingDW")
		A1F7 ABCDEFGHIJKLMNO 1 
		Stop
		
	FlashAirKicking:
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "FlashAirKickingDW")
		A1F7 ABCDEFGHIJKLMNOOO 1 
		Stop
		
	FlashKickingDW:
		A2F3 ABCDEFGGGFEDCBA 1 
		Stop
		
	FlashAirKickingDW:
		A2F3 ABCDEFGGGFEDCBAAA 1 
		Stop
		
	
	FlashSlideKicking:
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "FlashSlideKickingDW")
		A1F8 ABCDEFGHIJKLMLKJIHGFEDCBA 1
		A1F1 E 1
		Stop
		
	FlashSlideKickingDW:
		A2F3 ABCDEFGGGGGGGGGGGGGFEDCBA 1
		A2F3 A 1
		Stop
		
	FlashSlideKickingStop:
		TNT1 A 0 A_JumpIfInventory("DualWieldingSMGs", 1, "FlashSlideKickingStopDW")
		A1F8 FEDCA 1 
		A1F1 E 1
		Stop
		
	FlashSlideKickingStopDW:
		A2F3 FEDCA 1 
		A2F3 A 1
		Stop
	}
}

ACTOR SMGAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 51
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 51
   Inventory.Icon "ATFLA0"
}
