Actor DeagleHasUnloaded: Inventory
{
Inventory.maxamount 1
}

Actor DeagleWasEmpty: Inventory
{
Inventory.maxamount 1
}

ACTOR RespectDeagle : Inventory
{
Inventory.MaxAmount 1
}

ACTOR DualWieldingDeagles: Inventory
{
Inventory.MaxAmount 1
}

ACTOR DeagleSelected: Inventory
{
Inventory.MaxAmount 1
}

Actor PB_DeagleFireAnimation1: Inventory
{
	Inventory.maxamount 1
}

Actor PB_DeagleFireAnimation2: Inventory
{
	Inventory.maxamount 1
}

ACTOR PB_Deagle : PB_Weapon
{
	//$Title Martian Raptor
	//$Category Project Brutality - Weapons
	//$Sprite D4E0Z0
	SpawnID 9230
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 8
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	weapon.slotnumber 2
	Weapon.AmmoType1 "PB_LowCalMag"
	Weapon.AmmoType2 "DeagleAmmo"
	Inventory.MaxAmount 2
	Inventory.Amount 1
	+FLOORCLIP
	+DONTGIB
	Obituary "%o was popped by %k's .50 Caliber Hand Cannon."
    Inventory.PickupSound "weapons/deagle/equip"
	Inventory.Pickupmessage "UAC-H54 Martian Raptor .50 (Slot 2, Upgrade)"
	Tag "UAC-H54 Martian Raptor .50"
	+WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
    +WEAPON.NOAUTOFIRE
	//+POWERED_UP
	+WEAPON.CHEATNOTWEAPON
    Scale 0.48
	//Weapon.SisterWeapon PB_Revolver
	PB_WeaponBase.UnloaderToken "DeagleHasUnloaded"
	Inventory.AltHUDIcon "D4E0Z0"
	PB_WeaponBase.respectItem "RespectDeagle"
	PB_WeaponBase.TailPitch 0.6
	FloatBobStrength 0.5
	States
	{
	Steady:
		TNT1 A 1
		Goto Ready
		
	Ready:
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0 PB_RespectIfNeeded
	WeaponRespect:
		TNT1 A 0 {
			A_SetInventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("weapons/deagle/equip", "Auto");
			A_SetCrosshair(5);
			}
        D4E0 ABCD 1 A_DoPBWeaponAction
		D0E0 ABCDEFGHIIIIJKL 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D0E0 MNOPQQ 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto") 
		D0E0 RSTTUVW 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/smg_magfly1", "Auto")
		D0E0 XYZ 1 A_DoPBWeaponAction
		D0E1 ABCDEFGHIJJJJ 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magin", "Auto") 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto") 
		D0E1 KLMNNOPQRST 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/click1", "Auto") 
		D0E1 UVVV 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/click2", "Auto") 
		D0E1 WXYZ 1 A_DoPBWeaponAction
		D0E2 ABCDE 1 A_DoPBWeaponAction
		Goto Ready3
	SelectAnimation:
		TNT1 A 0 A_SetInventory("Zoomed",0)
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "SelectAnimationDualWield")
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/equip", "Auto")
        D4E0 ABCD 1
	Ready3:
		TNT1 A 0 A_JumpIfInventory("Zoomed", 1, "Ready2")
		TNT1 A 0 {
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_SetInventory("PB_LockScreenTilt",0);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "ReadyDualWield")
	ReadyToFire:
		D4E0 E 1 {
			if (PressingFire() && PressingAltfire() && CountInv("DeagleAmmo") > 0){
					return state("Fire");
			}
			if (PressingFire() && CountInv("DeagleAmmo") > 0){
					return state("Fire");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
		}
		Loop
		
    Ready2:
		TNT1 A 0 {
			A_SetRoll(0);
			A_SetCrosshair(5);
			A_SetInventory("PB_LockScreenTilt",0);
			}
	ReadyToFire2:
        D3E0 A 1 {
			//Updated code for far superior smooth gameplay
			if(GetCvar("pb_toggle_aim_hold")) {
				if(!PressingAltfire() || JustReleased(BT_ALTATTACK)){
					return state("Zoomout");
				}
				if (PressingFire() && PressingAltfire() && CountInv("DeagleAmmo") > 0){
						return state("Fire2");
				}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOSECONDARY);
			}
			else {
				if (PressingFire() && CountInv("DeagleAmmo") > 0 ){
					return state("Fire2");
				}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
			}
			return state("");
		}
		Loop
	
	SelectAnimationDualWield:
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/equip", "Auto")
		D7E0 GFDBA 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/equip", "Auto")
	ReadyDualWield:
		TNT1 A 0 {
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_SetInventory("PB_LockScreenTilt",0);
			A_SetFiringRightWeapon(False);
			A_SetFiringLeftWeapon(False);
			if(CountInv("LeftDeagleAmmo") < CountInv("DeagleAmmo")){
				A_GiveInventory("DualFiring",1);
			}
// 			if(CountInv("PB_LowCalMag")>0){
// 				if(CountInv("LeftDeagleAmmo")<=0){
// 					A_GiveInventory("DualFireReload",1);
// 				}
// 				if(CountInv("DeagleAmmo")<=0){
// 					A_GiveInventory("DualFireReload",1);
// 				}
// 			}
			A_Overlay(10, "IdleLeft_Overlay", false);
			A_Overlay(11, "IdleRight_Overlay", false);
			}
	ReadyToFireDualWield:
		TNT1 A 1 {
			if(CountInv("PB_LowCalMag")>0){
				if(CountInv("LeftDeagleAmmo")<=0 || CountInv("DeagleAmmo")<=0){
					if(CountInv("LeftDeagleAmmo")<=0 && CountInv("DeagleAmmo")<=0){
						A_SetInventory("DualFireReload",2);
					}
					else{
						A_SetInventory("DualFireReload",1);
					}
				}
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE);
		}
		Loop
		
//Jesus Christ this took a while to figure out.
//I know it's messy but sadly it has to be like this or 
//else the left mouse button fucks up. -JM
	IdleLeft_Overlay:
		D6E2 J 1 {
			if(CountInv("LeftDeagleAmmo")<=0 && CountInv("DeagleAmmo")>0){
				A_GiveInventory("DualFiring",1);
			}
			if((PressingAltfire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire")==2){
					if(CountInv("LeftDeagleAmmo") > 0){
//						A_Log("Fire Left Overlay from RMB Inverted");
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire")==1){
					if(CountInv("LeftDeagleAmmo") > 0){
//						A_Log("Fire Left Overlay from LMB");
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			if(CountInv("DualFiring")==0 || (CountInv("DualFiring")==0 && CountInv("DeagleAmmo")<=0)){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire")==0){
					if(CountInv("LeftDeagleAmmo") > 0){
//						A_Log("Fire Left Overlay from LMB Single");
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			return state("");
		}
		Loop
	IdleRight_Overlay:
		D6E2 I 1 {
			if(CountInv("LeftDeagleAmmo")>0 && CountInv("DeagleAmmo")<=0){
				A_TakeInventory("DualFiring",1);
			}
			if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringRightWeapon() && GetCvar("SingleDualFire")==2){
				if(CountInv("DeagleAmmo") > 0){
//					A_Log("Fire Right Overlay from LMB Inverted");
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			if(CountInv("DualFiring")==1 || (CountInv("DualFiring")==1 && CountInv("LeftDeagleAmmo")<=0)){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire")==0){
					if(CountInv("DeagleAmmo") > 0){
//					A_Log("Fire Right Overlay from LMB Single");
					return state("FireRight_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			if((PressingAltfire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringRightWeapon() && GetCvar("SingleDualFire")==1){
				if(CountInv("DeagleAmmo") > 0){
//					A_Log("Fire Right Overlay from RMB");
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			return state("");
		}
		Loop
		
	FireLeft_Overlay:
		D6E1 A 1 BRIGHT{
			//A_FireBullets (0.1, 0.1, -1, 75, "BulletPuff", FBF_NORANDOM,8192,"Tracer", -1,-4);
			A_FireProjectile("PB_50AE", frandom(-0.1,0.1),0,0,0, FPF_NOAUTOAIM, frandom(-0.1,0.1));
			//A_FireCustomMissile("GunFireSmoke", 0, 0, -4, 0, 0, 0);
			PB_GunSmoke(4,0,0);
			PB_SpawnCasing("EmptyBrassDeagle",26,-12,38,-frandom(1, 2),Frandom(2,6),Frandom(3,6));
			A_StartSound("weapons/deagle/fire", CHAN_Weapon, CHANF_DEFAULT, 1.0);
			PB_DynamicTail("shotgun", "pistol_mag");
			PB_LowAmmoSoundWarning("pistol", "LeftDeagleAmmo");
			A_SetInventory("PB_DeagleFireAnimation1",0);
			A_Takeinventory("LeftDeagleAmmo",1);
			A_SetInventory("PB_DeagleFireAnimation2",1); //Toggle gun animation from one pistol to the other.
			A_AlertMonsters;
			A_ZoomFactor(0.985);
			A_SetFiringLeftWeapon(True);
			A_GunFlash;
            PB_WeaponRecoil(-1.92,+1.8);
			}
		D6E1 B 1 BRIGHT {
			A_ZoomFactor(1.0);
            PB_WeaponRecoil(-1.92,+1.8);
		}
		D6E1 C 1 {
			if(CountInv("LeftDeagleAmmo")<=0 || CountInv("DeagleAmmo")>0 ){
				A_GiveInventory("DualFiring",1);
			}
		}
		D6E1 DEFGH 1
		TNT1 A 0 A_SetFiringLeftWeapon(False)
		D6E2 JJJ 1 {
			if(JustPressed(BT_ALTATTACK) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire") == 2){
				if(CountInv("LeftDeagleAmmo") > 0){
					return state("FireLeft_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			if(JustPressed(BT_ATTACK) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire") < 2){
				if(CountInv("LeftDeagleAmmo") > 0){
					return state("FireLeft_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			return state("");
		}
		TNT1 A 0 {
			if(CountInv("LeftDeagleAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleLeft_Overlay
		
	FireRight_Overlay:
		D6E0 A 1 BRIGHT {
			//A_FireBullets (0.1, 0.1, -1, 75, "BulletPuff", FBF_NORANDOM,8192,"Tracer", -1,4);
			A_FireProjectile("PB_50AE", frandom(-0.1,0.1),0,0,0, FPF_NOAUTOAIM, frandom(-0.1,0.1));
			//A_FireCustomMissile("GunFireSmoke", 0, 0, 4, 0, 0, 0);
			PB_GunSmoke(-4,0,0);
			PB_SpawnCasing("EmptyBrassDeagle",26,25,38,-frandom(1, 2),Frandom(2,6),Frandom(3,6));
			A_StartSound("weapons/deagle/fire", CHAN_Weapon, CHANF_DEFAULT, 1.0);
			PB_DynamicTail("shotgun", "pistol_mag");
			PB_LowAmmoSoundWarning("pistol");
			A_ZoomFactor(0.985);
			A_Takeinventory("DeagleAmmo",1);
			A_SetInventory("PB_DeagleFireAnimation1",1); //Toggle gun animation from one pistol to the other.
			A_AlertMonsters;
			A_SetFiringRightWeapon(True);
			A_GunFlash;
            PB_WeaponRecoil(-1.92,-1.8);
			}
		D6E0 B 1 BRIGHT {
			A_ZoomFactor(1.0);
            PB_WeaponRecoil(-1.92,-1.8);
			}
		D6E0 C 1 {
			if(CountInv("LeftDeagleAmmo")>0 || CountInv("DeagleAmmo")<=0 ){
				A_TakeInventory("DualFiring",1);
			}
		}
		D6E0 DEFGH 1
		TNT1 A 0 A_SetFiringRightWeapon(False)
		D6E2 III 1 {
			if(JustPressed(BT_ATTACK) && !A_IsFiringRightWeapon() && GetCvar("SingleDualFire") == 2){
				if(CountInv("DeagleAmmo") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			if(JustPressed(BT_ALTATTACK) && !A_IsFiringRightWeapon() && GetCvar("SingleDualFire") < 2){
				if(CountInv("DeagleAmmo") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			return state("");
		}
		TNT1 A 0 {
			if(CountInv("DeagleAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleRight_Overlay
		
	WeaponSpecial:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "IdleBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "IdleFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "IdleIceBarrel")
		TNT1 A 0 {
			A_SetInventory("GoWeaponSpecialAbility",0);
			A_SetInventory("Zoomed",0);
			A_SetInventory("ADSmode",0);
			A_SetInventory("PB_LockScreenTilt",1);
			PB_HandleCrosshair(42);
			A_ZoomFactor(1.0);
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "StopDualWield")
		TNT1 A 0 A_JumpIfInventory("PB_Deagle", 2,"SwitchToDualWield")
		TNT1 A 0 A_Print("You need two Deagles to dual wield!")
		Goto Ready3
	SwitchToDualWield:
		TNT1 A 0 A_SetInventory("CantDoAction", 1)
		TNT1 A 0 {
			A_SetAkimbo(True);
        	A_SetInventory("DualWieldingDeagles", 1);
			A_PlaySoundEx("weapons/deagle/equip", "Auto");
		}
		D6E2 ABCDEF 1 A_Setroll(roll-0.5, SPF_INTERPOLATE)
		D6E2 GHH 1 A_Setroll(roll+1.0, SPF_INTERPOLATE)
		TNT1 A 0 A_SetInventory("CantDoAction", 0)
		Goto ReadyDualWield
	StopDualWield:
		TNT1 A 0 A_SetInventory("CantDoAction", 1)
		TNT1 A 0 {
			A_SetAkimbo(False);
			A_SetInventory("DualWieldingDeagles", 0);
			A_PlaySoundEx("weapons/deagle/equip", "Auto");
		}
		D6E2 HHGFED 1 A_Setroll(roll+0.5, SPF_INTERPOLATE)
		D6E2 CBA 1 A_Setroll(roll-1.0, SPF_INTERPOLATE)
		TNT1 A 0 A_SetInventory("CantDoAction", 0)
		Goto Ready3
	
	Deselect:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "PlaceBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "PlaceFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "PlaceIceBarrel")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_SetInventory("PB_LockScreenTilt",0);
			A_ClearOverlays(10,11);
		}
		TNT1 A 0 A_SetInventory("Unloading",0)
		TNT1 A 0 A_SetInventory("Zoomed",0)
		TNT1 A 0 A_SetInventory("ADSmode",0)
		TNT1 A 0 A_Overlay(-9, "Null")
        TNT1 A 0 A_ZoomFactor(1.0)
		
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "DeselectDualWield")
        D4E0 DCBA 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectDualWield:
		D7E0 ABD 1 A_Setroll(roll+0.5, SPF_INTERPOLATE)
		D7E0 FG 1 A_Setroll(roll-1.0, SPF_INTERPOLATE)
		TNT1 A 0 A_Lower(120)
		Wait

	Select:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_SetInventory("PB_LockScreenTilt",0);
			A_ClearOverlays(10,11);
		}
		
		TNT1 A 0 PB_ResetBarrelTokens
		D4E0 E 0 PB_TakeIfUpgrade("PB_Revolver")
		
		Goto SelectFirstPersonLegs
		SelectContinue:
		TNT1 A 0
		TNT1 A 0 PB_WeapTokenSwitch("DeagleSelected")
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait
		
    Fire:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "ThrowBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "ThrowFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "ThrowIceBarrel")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_SetInventory("PB_LockScreenTilt",0);
		}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FireDualWield")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Fire2")
        TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,1)
        Goto Reload
		D2E0 A 1 BRIGHT {	
				A_StartSound("weapons/deagle/fire", CHAN_Weapon, CHANF_DEFAULT, 1.0);
			PB_DynamicTail("shotgun", "pistol_mag");
				PB_LowAmmoSoundWarning("pistol");
				//A_FireBullets (0.1, 0.1, -1, 75, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
				A_FireProjectile("PB_50AE", frandom(-0.1,0.1),0,0,0, FPF_NOAUTOAIM, frandom(-0.1,0.1));
				A_AlertMonsters;
				A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0);
				//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				PB_GunSmoke(0,0,0);
				A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
				PB_SpawnCasing("EmptyBrassDeagle",30,0,31,-frandom(1, 2),Frandom(2,6),Frandom(3,6));
				A_Takeinventory("DeagleAmmo",1);
				A_ZoomFactor(0.96);
				A_GunFlash;
				PB_WeaponRecoil(-1.15,-0.26);
			}
		D2E0 B 1 BRIGHT {
				//A_ZoomFactor(0.98);
				PB_WeaponRecoil(-1.15,-0.26);
			}
		D2E0 CDE 1 {
				A_ZoomFactor(0.98);
				//PB_WeaponRecoil(-2.15,-1.26);
			}
		D2E0 FG 1 {
				A_ZoomFactor(1.0);
				//PB_WeaponRecoil(1.00,1.00);
			}
		D4E0 EE 1 {
			if (JustPressed(BT_ATTACK)){
					return state("Fire");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOPRIMARY);
		}
		Goto Ready3
	
      Fire2:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetCrosshair(5);
			}
	    TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,1)
        Goto Reload
		D3E0 B 1 BRIGHT {	
				A_StartSound("weapons/deagle/fire", CHAN_Weapon, CHANF_DEFAULT, 1.0);
			PB_DynamicTail("shotgun", "pistol_mag");
				PB_LowAmmoSoundWarning("pistol");
				//A_FireBullets (0.1, 0.1, -1, 75, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
				A_FireProjectile("PB_50AE", frandom(-0.1,0.1),0,0,0, FPF_NOAUTOAIM, frandom(-0.1,0.1));
				A_AlertMonsters;
				A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0);
				//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				PB_GunSmoke(0,0,0);
				A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
				PB_SpawnCasing("EmptyBrassDeagle",26,0,38,-frandom(1, 2),Frandom(2,6),Frandom(3,6));
				A_Takeinventory("DeagleAmmo",1);
				A_ZoomFactor(1.19);
				PB_WeaponRecoil(-0.90,-0.22);
			}
		D3E0 C 1 BRIGHT {
				//A_ZoomFactor(1.18);
                PB_WeaponRecoil(-0.90,-0.22);
			}
		D3E0 DEF 1 {
			A_ZoomFactor(1.2);
			//PB_WeaponRecoil(-1.90,-1.22);
			}
		D3E0 GH 1 {
			A_ZoomFactor(1.18);
			//PB_WeaponRecoil(1,1);
			}
		D3E0 AAA 1 {
			if(GetCvar("pb_toggle_aim_hold")) {
				if(JustReleased(BT_ALTATTACK)){
					return state("Zoomout");
				}
		
				if (JustPressed(BT_ATTACK) && PressingAltfire()){
						return state("Fire2");
				}
			}
			else {
				if(PressingAltfire()){
					return state("Zoomout");
				}
				
				if (JustPressed(BT_ATTACK)){
						return state("Fire2");
				}
				PB_ReFire("Fire2");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE);
		}
		Goto Ready2
/*	
	FireDualWield:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			PB_HandleCrosshair(42);
			}
        TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,1)
        Goto Reload
	FireRight:
		TNT1 A 0 A_JumpIfInventory("PB_DeagleFireAnimation1", 1, "FireLeft")
		D6E0 A 1 BRIGHT {
			//A_FireBullets (0.1, 0.1, -1, 90, "BulletPuff", FBF_NORANDOM,8192,"Tracer", -1,-4);
			A_FireProjectile("PB_50AE", frandom(-0.1,0.1),0,0,0, FPF_NOAUTOAIM, frandom(-0.1,0.1));
			A_FireCustomMissile("GunFireSmoke", 0, 0, 4, 0, 0, 0);
			A_FireCustomMissile("DeagleCasingRight",0,0,12,-5);
			A_StartSound("weapons/deagle/fire", CHAN_Weapon, CHANF_DEFAULT, 1.0);
			PB_DynamicTail("shotgun", "pistol_mag");
			PB_LowAmmoSoundWarning("pistol");
			A_ZoomFactor(0.985);
			A_Takeinventory("DeagleAmmo",1);
			A_GiveInventory("PB_DeagleFireAnimation1",1); //Toggle gun animation from one pistol to the other.
			A_AlertMonsters;
			A_GunFlash;
			PB_WeaponRecoil(-0.92,-0.8);
			}
		D6E0 B 1 BRIGHT {
			A_ZoomFactor(1.0);
			PB_WeaponRecoil(-0.92,-0.8);
			}
		D6E0 CDEFGH 1
		D6E0 E 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
		TNT1 A 0 PB_ReFire
		Goto ReadyToFireDualWield
	FireLeft:
		D6E1 A 1 BRIGHT{
			//A_FireBullets (0.1, 0.1, -1, 90, "BulletPuff", FBF_NORANDOM,8192,"Tracer", -1,-4);
			A_FireProjectile("PB_50AE", frandom(-0.1,0.1),0,0,0, FPF_NOAUTOAIM, frandom(-0.1,0.1));
			A_FireCustomMissile("GunFireSmoke", 0, 0, -4, 0, 0, 0);
			A_FireCustomMissile("DeagleCasingLeft",0,0,-12,-5);
			A_StartSound("weapons/deagle/fire", CHAN_Weapon, CHANF_DEFAULT, 1.0);
			PB_DynamicTail("shotgun", "pistol_mag");
			A_TakeInventory("PB_DeagleFireAnimation1",1);
			A_GiveInventory("PB_DeagleFireAnimation2",1); //Toggle gun animation from one pistol to the other.
			A_AlertMonsters;
			A_ZoomFactor(0.985);
			A_GunFlash;
			PB_WeaponRecoil(-0.92,+0.8);
			}
		D6E1 B 1 BRIGHT {
			A_ZoomFactor(1.0);
            PB_WeaponRecoil(-0.92,+0.8);
		}
		D6E1 CDEFGH 1
		D6E1 E 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)//(WRF_NOSECONDARY)
		TNT1 A 0 PB_ReFire
		Goto ReadyToFireDualWield
*/
	
    AltFire:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "PlaceBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "PlaceFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "PlaceIceBarrel")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_SetInventory("PB_LockScreenTilt",0);
		}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "ReadyToFireDualWield")
		TNT1 A 0 A_PlaySound("IronSights", 0)
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Zoomout")
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_SetInventory("Zoomed",1);
			A_ZoomFactor(1.2);
			A_SetInventory("ADSmode",1);
		}
		D3E1 ABCDEFG 1
        Goto Ready2
		
    Zoomout:
		TNT1 A 0 {
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
			A_ZoomFactor(1.0);
			PB_HandleCrosshair(42);
			}
		D3E1 GFEDCBA 1
        Goto Ready3
		
	Reload2:
		D0E0 ABCDEFGHIIIIJKL 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D0E0 MNO 1 
		TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto") 
		//D1E1 LKJIHGFE 1
		//D1E2 VUTSRQ 1
		//TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		D1E2 NNNPPP 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magin", "Auto") 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto") 
		D1E2 QRSTUV 1
		D1E1 EFGHIJKL 1
		TNT1 A 0 A_SetInventory("DeagleHasUnloaded",0)
		TNT1 A 0 A_SetInventory("DeagleWasEmpty", 1)
		goto InsertBullets2
	
	Reload:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "IdleBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "IdleFlameBarrel")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "IdleIceBarrel")
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "ReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("DeagleAmmo",8,"Ready3")
        TNT1 A 0 A_JumpIfInventory("PB_LowCalMag",2,2)
        TNT1 A 0 A_PlaySoundEx("weapons/empty", "Weapon")
        Goto Ready3
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
			A_SetInventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("Ironsights", "Auto");
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "ReloadDualWield")
		TNT1 A 0 A_JumpIf(Countinv("DeagleAmmo") >= 1,"Reload2")

		D1E0 ABCDEFG 1
		TNT1 A 0 A_JumpIf(Countinv("DeagleHasUnloaded") >= 1, 10)
        TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D1E0 HIJKLM 1 
        TNT1 A 0 A_FireCustomMissile("EmptyDeagleMag",5,0,12,-4)
		D1E0 NNNOP 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magin", "Auto") 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto") 
		D1E0 QRSTUVWXY 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/click1", "Auto") 
		D1E0 Z 1
        D1E1 ABCD 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/click2", "Auto") 
		D1E1 DEFGHIJKL 1
		TNT1 A 0 A_SetInventory("DeagleHasUnloaded",0)
		TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,"InsertBullets2")//30+1 effect
		TNT1 A 0 A_SetInventory("DeagleWasEmpty", 1)
	InsertBullets:
		TNT1 A 0
		TNT1 A 0 PB_AmmoIntoMag("DeagleAmmo","PB_LowCalMag",7,2)
		Goto Ready3
		
	InsertBullets2:
		TNT1 A 0
		TNT1 A 0 PB_AmmoIntoMag("DeagleAmmo","PB_LowCalMag",8,2)
		Goto Ready3
		
	LoadChamber:
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
			A_SetInventory("DeagleWasEmpty",0);
			A_SetInventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("IronSights", "Auto");
			}
		TNT1 A 0 A_PlaySoundEx("weapons/smg_click", "Auto")
		Goto Ready3
	
	// Fixes crash when there is only 1 round in reserve
	NoAmmoDualWield:
		D62H H 1
		Goto Ready3
		
	ReloadDualWield:
		TNT1 A 0 {
			if (CountInv("DeagleAmmo") >= 8 && CountInv("LeftDeagleAmmo") >= 8) {
				return state("Ready3");
			}
			return state("");
		}
		
		
        TNT1 A 0 A_JumpIfInventory("PB_LowCalMag",2,2)
		TNT1 A 0 A_PlaySoundEx("weapons/empty", "Auto")
        Goto NoAmmoDualWield
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
			A_SetInventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("Ironsights", "Auto");
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 A_JumpIfInventory("DeagleHasUnloaded", 1, "ReloadDualWieldUnloaded")
		D6E3 ABCDEFGHI 1
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D6E3 JK 1 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
        TNT1 A 0 A_FireCustomMissile("EmptyDeagleMag",5,0,12,-4)
        TNT1 A 0 A_FireCustomMissile("EmptyDeagleMag",5,0,-12,-4)
	ReloadDualWieldUnloaded:
		D6E3 LMMMNOPQ 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magin", "Auto") 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto") 
		D6E3 RSTUVWXY 1 
		TNT1 A 0 A_PlaySoundEx("weapons/smg_magfly1", "Auto")
		D6E3 Z 1
		D6E4 ABCDEFGHIJKL 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magin", "Auto") 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto") 
		D6E4 MNOPPPQQ 1 
		TNT1 A 0 A_PlaySoundEx("weapons/smg_magfly1", "Auto")
		D6E4 RSTUVWXYZ 1
		TNT1 A 0 A_PlaySoundEx("IronSights", "Auto")
		D6E5 ABCDEFG 1
		TNT1 A 0 A_SetInventory("DeagleHasUnloaded",0)
		Goto InsertBullets3_Right
		
	InsertBullets3_Right:
		TNT1 A 0 
		{
			PB_AmmoIntoMag("DeagleAmmo","PB_LowCalMag",8,2);
			PB_AmmoIntoMag("LeftDeagleAmmo","PB_LowCalMag",8,2);
		}
		Goto FinishInsertBullets		
		
	FinishInsertBullets:
		TNT1 A 0 {
			A_SetInventory("PB_DeagleFireAnimation1",0);
			A_SetInventory("PB_DeagleFireAnimation2",0);
			A_SetInventory("DeagleWasEmpty",0);
			A_SetInventory("DeagleHasUnloaded",0);
		}
		Goto Ready3
		
	Unload:
		ATFG A 0 A_SetInventory("Unloading",0)
		TNT1 A 0 A_JumpIfInventory("DeagleHasUnloaded", 1, "Ready3")
        TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,1)
        Goto Ready3
		TNT1 A 0 {
			A_ClearOverlays(10,11);
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_SetInventory("ADSmode",0);
			A_SetInventory("Zoomed",0);
			A_SetInventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("Ironsights", "Auto");
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "UnloadDualWield")
        D1E0 ABCDEFG 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D1E0 HIJKLM 1 
		D1E0 NNO 1 
		D1E1 EE 1
		D1E1 FGHIJKL 1
		Goto RemoveBullets
		
	UnloadDualWield:
		D6E3 ABCDEFGHI 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D6E3 JK 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D6E3 LMMIHGFEDCBA 1
		Goto RemoveBullets_Right
		
	RemoveBullets:
		TNT1 A 0
		TNT1 A 0 PB_UnloadMag("DeagleAmmo","PB_LowCalMag",2)
		Goto FinishUnload
	RemoveBullets_Right:
		TNT1 A 0 {
			PB_UnloadMag("DeagleAmmo","PB_LowCalMag",2);
			PB_UnloadMag("LeftDeagleAmmo","PB_LowCalMag",2);
			}
		Goto FInishUnloadDualWield
		
	FInishUnload:
		TNT1 A 0 {
			A_SetInventory("DeagleHasUnloaded", 1);
			A_SetInventory("Unloading",0);
			}
		Goto Ready3

	FInishUnloadDualWield:
		TNT1 A 0 {
			A_SetInventory("DeagleHasUnloaded", 1);
			A_SetInventory("Unloading",0);
		}
		Goto Ready3
		
	Spawn:
		V4E0 Z 0 NoDelay
		D4E0 Z 10 A_PbvpFramework("V4E0")
		"####" Z 0 A_PbvpInterpolate()
		LOOP

		
	FlashPunching:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelPunching")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelPunching")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelPunching")
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FlashPunchingDW")
		D8E0 ABCDEFGGFEDCBA 1
		Stop
	FlashPunchingDW:
		TNT1 A 15
		Stop
    FlashKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelKicking")
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FlashKickingDW")
		D5E0 ABCDEFGHGFEDCBA 1 A_DoPBWeaponAction
		Goto Ready3
		
	FlashAirKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelAirKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelAirKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelAirKicking")
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FlashAirKickingDW")
		D5E0 ABCDEFGHHHGFEDCBA 1 A_DoPBWeaponAction
		Goto Ready3
		
	FlashKickingDW:
		TNT1 A 0 {
		if (CountInv("GoFatality")>=1 || PB_executingEnemy())
		{
			return state("Steady");
		}
			return state("");
		}
		D7E0 ABCDEFGHGFEDCBA 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Goto Ready3
		
	FlashAirKickingDW:
		D7E0 ABCDEFGHHHGFEDCBA 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Goto Ready3
		
	
	FlashSlideKicking:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelSlideKicking")
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FlashSlideKickingDW")
		D5E0 ABCDEFGHHHHHHHHHHHHGFEDCBAA 1 A_DoPBWeaponAction
		Goto Ready3
		
	FlashSlideKickingDW:
		D7E0 ABCDEFGHHHHHHHHHHHGFEDCBAA 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		D6E3 A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Goto Ready3
		
	FlashSlideKickingStop:
		TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelSlideKickingStop")
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FlashSlideKickingStopDW")
		D5E0 GFEDCBA 1 A_DoPBWeaponAction
		Goto Ready3
		
	FlashSlideKickingStopDW:
		D7E0 GFEDCBA 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Goto Ready3
	}
}

ACTOR DeagleAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 8
   Inventory.Amount 0
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 8
   +INVENTORY.IGNORESKILL
   Inventory.Icon "D4E0Z0"
}

ACTOR LeftDeagleAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 8
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 8
   +INVENTORY.IGNORESKILL
   Inventory.Icon "D4E0Z0"
}
