Actor DeagleHasUnloaded: Inventory
{
Inventory.maxamount 1
}

Actor DeagleWasEmpty: Inventory
{
Inventory.maxamount 1
}

ACTOR RespectDeagle : Inventory
{
Inventory.MaxAmount 1
}

ACTOR DualWieldingDeagles: Inventory
{
Inventory.MaxAmount 1
}

ACTOR DeagleSelected: Inventory
{
Inventory.MaxAmount 1
}

Actor PB_DeagleFireAnimation1: Inventory
{
	Inventory.maxamount 1
}

Actor PB_DeagleFireAnimation2: Inventory
{
	Inventory.maxamount 1
}

ACTOR Deagle : PB_Weapon
{
	SpawnID 9230
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 12
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "PistolBullets"
	Weapon.AmmoType2 "DeagleAmmo"
	Inventory.MaxAmount 3
	Inventory.Amount 1
	+FLOORCLIP
	+DONTGIB
	Obituary "%o was popped by %k's .50 Caliber Hand Cannon."
    Inventory.PickupSound "CLIPIN"
	Inventory.Pickupmessage "You found the UAC .50 Caliber Hand Cannon! (Slot 2, Upgrade)"
	Tag "Desert Eagle .50"
    +WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
    +WEAPON.NOAUTOFIRE
	+POWERED_UP
	+WEAPON.CHEATNOTWEAPON
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
    Scale 0.48
	Weapon.SisterWeapon Revolver
	PB_WeaponBase.UnloaderToken "DeagleHasUnloaded"
	Inventory.AltHUDIcon "D4E0Z0"
	States
	{
	Steady:
		TNT1 A 1
		Goto Ready
		
	Ready:
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0 A_JumpIfInventory("RespectDeagle", 1, "SelectAnimation")
	WeaponRespect:
		TNT1 A 0 {
			A_Giveinventory("RespectDeagle",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("weapons/deagle/equip", "Auto");
			A_SetCrosshair(5);
			}
        D4E0 ABCD 1 A_DoPBWeaponAction
		D0E0 ABCDEFGHIIIIJKL 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D0E0 MNOPQQ 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto") 
		D0E0 RSTTUVW 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/smg_magfly1", "Auto")
		D0E0 XYZ 1 A_DoPBWeaponAction
		D0E1 ABCDEFGHIJJJJ 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magin", "Auto") 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto") 
		D0E1 KLMNNOPQRST 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/click1", "Auto") 
		D0E1 UVVV 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/click2", "Auto") 
		D0E1 WXYZ 1 A_DoPBWeaponAction
		D0E2 ABCDE 1 A_DoPBWeaponAction
		Goto Ready3
	SelectAnimation:
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "SelectAnimationDualWield")
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/equip", "Auto")
        D4E0 ABCD 1
	Ready3:
		TNT1 A 0 A_JumpIfInventory("Zoomed", 1, "Ready2")
		TNT1 A 0 {
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_TakeInventory("PB_LockScreenTilt",1);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "ReadyDualWield")
	ReadyToFire:
		D4E0 E 1 {
			if (PressingFire() && PressingAltfire() && CountInv("DeagleAmmo") > 0){
					return state("Fire");
			}
			if (PressingFire() && CountInv("DeagleAmmo") > 0){
					return state("Fire");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
		}
		Loop
		
    Ready2:
		TNT1 A 0 {
			A_SetRoll(0);
			A_SetCrosshair(5);
			A_TakeInventory("PB_LockScreenTilt",1);
			}
	ReadyToFire2:
        D3E0 A 1 {
			//Updated code for far superior smooth gameplay
			if(GetCvar("pb_toggle_aim_hold")) {
				if(!PressingAltfire() || JustReleased(BT_ALTATTACK)){
					return state("Zoomout");
				}
				if (PressingFire() && PressingAltfire() && CountInv("DeagleAmmo") > 0){
						return state("Fire2");
				}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOSECONDARY);
			}
			else {
				if (PressingFire() && CountInv("DeagleAmmo") > 0 ){
					return state("Fire2");
				}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
			}
			return state("");
		}
		Loop
	
	SelectAnimationDualWield:
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/equip", "Auto")
		D7E0 GFEDCBA 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/equip", "Auto")
	ReadyDualWield:
		TNT1 A 0 {
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_SetFiringRightWeapon(False);
			A_SetFiringLeftWeapon(False);
			if(CountInv("LeftDeagleAmmo") < CountInv("DeagleAmmo")){
				A_GiveInventory("DualFiring",1);
			}
			if(CountInv("PistolBullets")>0){
				if(CountInv("LeftDeagleAmmo")<=0){
					A_GiveInventory("DualFireReload",1);
				}
				if(CountInv("DeagleAmmo")<=0){
					A_GiveInventory("DualFireReload",1);
				}
			}
			A_Overlay(10, "IdleLeft_Overlay", true);
			A_Overlay(11, "IdleRight_Overlay", true);
			}
	ReadyToFireDualWield:
		TNT1 A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Loop
		
	
	IdleLeft_Overlay:
		D6E2 J 1 {
			if(CountInv("LeftDeagleAmmo")<=0 && CountInv("DeagleAmmo")>0){
				A_GiveInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==0 || (CountInv("DualFiring")==0 && CountInv("DeagleAmmo")<=0) || GetCvar("SingleDualFire")==1){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon()){
					if(CountInv("LeftDeagleAmmo") > 0){
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			return state("");
		}
		Loop
	IdleRight_Overlay:
		D6E2 I 1 {
			if(CountInv("LeftDeagleAmmo")>0 && CountInv("DeagleAmmo")<=0){
				A_TakeInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==1 || (CountInv("DualFiring")==1 && CountInv("LeftDeagleAmmo")<=0)){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire")==0){
					if(CountInv("DeagleAmmo") > 0){
					return state("FireRight_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			if((PressingAltfire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringRightWeapon() && GetCvar("SingleDualFire")==1){
				if(CountInv("DeagleAmmo") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			return state("");
		}
		Loop
		
	FireLeft_Overlay:
		D6E1 A 1 BRIGHT{
			A_FireBullets (0.1, 0.1, -1, 75, "BulletPuff", FBF_NORANDOM,8192,"Tracer", -1,-4);
			A_FireCustomMissile("GunFireSmoke", 0, 0, -4, 0, 0, 0);
			A_FireCustomMissile("DeagleCasingLeft",0,0,-12,-5);
			A_PlaySoundEx("weapons/deagle/fire", "Weapon");
			A_PlaySoundEx("weapons/deagle/afire", "Body");
			A_TakeInventory("PB_DeagleFireAnimation1",1);
			A_Takeinventory("LeftDeagleAmmo",1);
			A_GiveInventory("PB_DeagleFireAnimation2",1); //Toggle gun animation from one pistol to the other.
			A_AlertMonsters;
			A_ZoomFactor(0.985);
			A_SetFiringLeftWeapon(True);
			A_GunFlash;
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.46, SPF_INTERPOLATE);
				A_SetAngle(angle+0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.92, SPF_INTERPOLATE);
				A_SetAngle(angle+0.8, SPF_INTERPOLATE);
				}
			}
		D6E1 B 1 BRIGHT {
			A_ZoomFactor(1.0);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.46, SPF_INTERPOLATE);
				A_SetAngle(angle+0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.92, SPF_INTERPOLATE);
				A_SetAngle(angle+0.8, SPF_INTERPOLATE);
				}
		}
		D6E1 C 1 {
			if(CountInv("LeftDeagleAmmo")<=0 || CountInv("DeagleAmmo")>0 ){
				A_GiveInventory("DualFiring",1);
			}
		}
		D6E1 DE 1
		TNT1 A 0 A_SetFiringLeftWeapon(False)
		D6E2 JJJ 1 {
			if(JustPressed(BT_ATTACK) && !A_IsFiringLeftWeapon()){
				if(CountInv("LeftDeagleAmmo") > 0){
					return state("FireLeft_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			return state("");
		}
		TNT1 A 0 {
			if(CountInv("LeftDeagleAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleLeft_Overlay
		
	FireRight_Overlay:
		D6E0 A 1 BRIGHT {
			A_FireBullets (0.1, 0.1, -1, 75, "BulletPuff", FBF_NORANDOM,8192,"Tracer", -1,4);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 4, 0, 0, 0);
			A_FireCustomMissile("DeagleCasingRight",0,0,12,-5);
			A_PlaySoundEx("weapons/deagle/fire", "Weapon");
			A_PlaySoundEx("weapons/deagle/afire", "Body");
			A_ZoomFactor(0.985);
			A_Takeinventory("DeagleAmmo",1);
			A_GiveInventory("PB_DeagleFireAnimation1",1); //Toggle gun animation from one pistol to the other.
			A_AlertMonsters;
			A_SetFiringRightWeapon(True);
			A_GunFlash;
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.46, SPF_INTERPOLATE);
				A_SetAngle(angle-0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.92, SPF_INTERPOLATE);
				A_SetAngle(angle-0.8, SPF_INTERPOLATE);
				}
			}
		D6E0 B 1 BRIGHT {
			A_ZoomFactor(1.0);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.46, SPF_INTERPOLATE);
				A_SetAngle(angle-0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.92, SPF_INTERPOLATE);
				A_SetAngle(angle-0.8, SPF_INTERPOLATE);
				}
			}
		D6E0 C 1 {
			if(CountInv("LeftDeagleAmmo")>0 || CountInv("DeagleAmmo")<=0 ){
				A_TakeInventory("DualFiring",1);
			}
		}
		D6E0 DE 1
		TNT1 A 0 A_SetFiringRightWeapon(False)
		D6E2 III 1 {
			if(JustPressed(BT_ALTATTACK) && !A_IsFiringRightWeapon()){
				if(CountInv("DeagleAmmo") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			return state("");
		}
		TNT1 A 0 {
			if(CountInv("DeagleAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleRight_Overlay
		
	WeaponSpecial:
		TNT1 A 0 {
			A_Takeinventory("GoWeaponSpecialAbility",1);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_GiveInventory("PB_LockScreenTilt",1);
			PB_HandleCrosshair(42);
			A_ZoomFactor(1.0);
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "StopDualWield")
		TNT1 A 0 A_JumpIfInventory("Deagle", 2,"SwitchToDualWield")
		TNT1 A 0 A_Print("You need two Deagles to dual wield!")
		Goto Ready3
	SwitchToDualWield:
		TNT1 A 0 {
			A_GiveInventory("DualWieldingDeagles", 1);
			A_PlaySoundEx("weapons/deagle/equip", "Auto");
			}
		D6E2 ABCDEF 1 A_Setroll(roll-0.5, SPF_INTERPOLATE)
		D6E2 GHH 1 A_Setroll(roll+1.0, SPF_INTERPOLATE)
		Goto ReadyDualWield
	StopDualWield:
		TNT1 A 0 {
			A_TakeInventory("DualWieldingDeagles", 1);
			A_PlaySoundEx("weapons/smg_up", "Auto");
			}
		D6E2 HHGFED 1 A_Setroll(roll+0.5, SPF_INTERPOLATE)
		D6E2 CBA 1 A_Setroll(roll-1.0, SPF_INTERPOLATE)
		Goto Ready3
	
	Deselect:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
		}
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_Overlay(-9, "Null")
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("GotMeatShield", 1, "GrabEnemy")
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "DeselectDualWield")
        D4E0 DCBA 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectDualWield:
		D7E0 ABCDE 1 A_Setroll(roll+0.5, SPF_INTERPOLATE)
		D7E0 FGH 1 A_Setroll(roll-1.0, SPF_INTERPOLATE)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait

	Select:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
		}
		Goto SelectFirstPersonLegs
		SelectContinue:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_TakeInventory("RifleSelected",1)
		TNT1 A 0 A_TakeInventory("CarbineSelected",1)
		TNT1 A 0 A_TakeInventory("FistsSelected",1)
		TNT1 A 0 A_TakeInventory("SawSelected",1)
		TNT1 A 0 A_TakeInventory("SledgeSelected",1)
		TNT1 A 0 A_TakeInventory("HandgunSelected",1)
		TNT1 A 0 A_TakeInventory("DualHandgunSelected",1)
		TNT1 A 0 A_TakeInventory("RevolverSelected",1)
		TNT1 A 0 A_TakeInventory("ShotgunSelected",1)
		TNT1 A 0 A_TakeInventory("SSGSelected",1)
		TNT1 A 0 A_TakeInventory("ASGSelected",1)
		TNT1 A 0 A_TakeInventory("QSGSelected",1)
		TNT1 A 0 A_TakeInventory("MinigunSelected",1)
		TNT1 A 0 A_TakeInventory("NewChaingunSelected",1)	
		TNT1 A 0 A_TakeInventory("RocketLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("GrenadeLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("SGLSelected",1)
		TNT1 A 0 A_TakeInventory("PlasmaGunSelected",1)
		TNT1 A 0 A_TakeInventory("M2Selected",1)	
		TNT1 A 0 A_TakeInventory("FreezerSelected",1)
		TNT1 A 0 A_TakeInventory("RailGunSelected",1)
		TNT1 A 0 A_TakeInventory("BFGSelected",1)
		TNT1 A 0 A_TakeInventory("BFGBeamSelected",1)
		TNT1 A 0 A_TakeInventory("BHCSelected",1)
		TNT1 A 0 A_TakeInventory("RevenantLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("FlameCannonSelected",1)
		TNT1 A 0 A_TakeInventory("HellRifleSelected",1)
		TNT1 A 0 A_TakeInventory("LandMineSelected",1)
		TNT1 A 0 A_TakeInventory("SubMachineGunSelected",1)
		TNT1 A 0 A_TakeInventory("LostSoulSelected",1)
		TNT1 A 0 A_TakeInventory("HasBarrel",1)
		TNT1 A 0 A_TakeInventory("HasBurningBarrel",1)
		TNT1 A 0 A_TakeInventory("HasIncendiaryWeapon", 1)
		TNT1 A 0 A_TakeInventory("RifleWasEmpty",1)
		TNT1 A 0 A_GiveInventory("DeagleSelected",1)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait
		
    Fire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FireDualWield")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Fire2")
        TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,1)
        Goto Reload
		D2E0 A 1 BRIGHT {	
				A_PlaySoundEx("weapons/deagle/fire", "Weapon");
				A_PlaySoundEx("weapons/deagle/afire", "Body");
				A_FireBullets (0.1, 0.1, -1, 75, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
				A_AlertMonsters;
				A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
				A_FireCustomMissile("DeagleCasingRight", 0,0,-2,-1);
				A_Takeinventory("DeagleAmmo",1);
				A_ZoomFactor(0.96);
				A_GunFlash;
				
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.57, SPF_INTERPOLATE);
				A_SetAngle(angle-0.13, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-1.15, SPF_INTERPOLATE);
				A_SetAngle(angle-0.26, SPF_INTERPOLATE);
				}
				
			}
		D2E0 B 1 BRIGHT {
				A_ZoomFactor(0.98);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.57, SPF_INTERPOLATE);
				A_SetAngle(angle-0.13, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-1.15, SPF_INTERPOLATE);
				A_SetAngle(angle-0.26, SPF_INTERPOLATE);
				}
			}
		D2E0 C 1 {
				A_ZoomFactor(1.0);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.57, SPF_INTERPOLATE);
				A_SetAngle(angle-0.13, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-1.15, SPF_INTERPOLATE);
				A_SetAngle(angle-0.26, SPF_INTERPOLATE);
				}
			}
		D2E0 D 1
		D4E0 EEEE 1 {
			if (JustPressed(BT_ATTACK)){
					return state("Fire");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOPRIMARY);
		}
		Goto Ready3
	
      Fire2:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetCrosshair(5);
			}
	    TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,1)
        Goto Reload
		D3E0 B 1 BRIGHT {	
				A_PlaySoundEx("weapons/deagle/fire", "Weapon");
				A_PlaySoundEx("weapons/deagle/afire", "Body");
				A_FireBullets (0.1, 0.1, -1, 75, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
				A_AlertMonsters;
				A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
				A_FireCustomMissile("DeagleCasingRight",0,0,-2,1);
				A_Takeinventory("DeagleAmmo",1);
				A_ZoomFactor(1.19);
				
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.37, SPF_INTERPOLATE);
				A_SetAngle(angle-0.11, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.74, SPF_INTERPOLATE);
				A_SetAngle(angle-0.22, SPF_INTERPOLATE);
				}
				
			}
		D3E0 C 1 BRIGHT {
				A_ZoomFactor(1.18);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.37, SPF_INTERPOLATE);
				A_SetAngle(angle-0.11, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.74, SPF_INTERPOLATE);
				A_SetAngle(angle-0.22, SPF_INTERPOLATE);
				}
			}
		D3E0 D 1 A_ZoomFactor(1.2)
		D3E0 EE 1
		D3E0 AA 1 {
			if(GetCvar("pb_toggle_aim_hold")) {
				if(JustReleased(BT_ALTATTACK)){
					return state("Zoomout");
				}
		
				if (JustPressed(BT_ATTACK) && PressingAltfire()){
						return state("Fire2");
				}
			}
			else {
				if(PressingAltfire()){
					return state("Zoomout");
				}
				
				if (JustPressed(BT_ATTACK)){
						return state("Fire2");
				}
				A_Refire("Fire2");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE);
		}
		Goto Ready2
		
	FireDualWield:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			PB_HandleCrosshair(42);
			}
        TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,1)
        Goto Reload
	FireRight:
		TNT1 A 0 A_JumpIfInventory("PB_DeagleFireAnimation1", 1, "FireLeft")
		D6E0 A 1 BRIGHT {
			A_FireBullets (0.1, 0.1, -1, 75, "BulletPuff", FBF_NORANDOM,8192,"Tracer", -1,-4);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 4, 0, 0, 0);
			A_FireCustomMissile("DeagleCasingRight",0,0,12,-5);
			A_PlaySoundEx("weapons/deagle/fire", "Weapon");
			A_PlaySoundEx("weapons/deagle/afire", "Body");
			A_ZoomFactor(0.985);
			A_Takeinventory("DeagleAmmo",1);
			A_GiveInventory("PB_DeagleFireAnimation1",1); //Toggle gun animation from one pistol to the other.
			A_AlertMonsters;
			A_GunFlash;
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.46, SPF_INTERPOLATE);
				A_SetAngle(angle-0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.92, SPF_INTERPOLATE);
				A_SetAngle(angle-0.8, SPF_INTERPOLATE);
				}
			}
		D6E0 B 1 BRIGHT {
			A_ZoomFactor(1.0);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.46, SPF_INTERPOLATE);
				A_SetAngle(angle-0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.92, SPF_INTERPOLATE);
				A_SetAngle(angle-0.8, SPF_INTERPOLATE);
				}
			}
		D6E0 CD 1
		D6E0 E 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto ReadyToFireDualWield
	FireLeft:
		D6E1 A 1 BRIGHT{
			A_FireBullets (0.1, 0.1, -1, 75, "BulletPuff", FBF_NORANDOM,8192,"Tracer", -1,-4);
			A_FireCustomMissile("GunFireSmoke", 0, 0, -4, 0, 0, 0);
			A_FireCustomMissile("DeagleCasingLeft",0,0,-12,-5);
			A_PlaySoundEx("weapons/deagle/fire", "Weapon");
			A_PlaySoundEx("weapons/deagle/afire", "Body");
			A_TakeInventory("PB_DeagleFireAnimation1",1);
			A_GiveInventory("PB_DeagleFireAnimation2",1); //Toggle gun animation from one pistol to the other.
			A_AlertMonsters;
			A_ZoomFactor(0.985);
			A_GunFlash;
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.46, SPF_INTERPOLATE);
				A_SetAngle(angle+0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.92, SPF_INTERPOLATE);
				A_SetAngle(angle+0.8, SPF_INTERPOLATE);
				}
			}
		D6E1 B 1 BRIGHT {
			A_ZoomFactor(1.0);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.46, SPF_INTERPOLATE);
				A_SetAngle(angle+0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.92, SPF_INTERPOLATE);
				A_SetAngle(angle+0.8, SPF_INTERPOLATE);
				}
		}
		D6E1 CD 1
		D6E1 E 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)//(WRF_NOSECONDARY)
		TNT1 A 0 A_Refire
		Goto ReadyToFireDualWield
	
    AltFire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "ReadyToFireDualWield")
		TNT1 A 0 A_PlaySound("IronSights", 0)
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Zoomout")
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_Giveinventory("Zoomed",1);
			A_ZoomFactor(1.2);
			A_Giveinventory("ADSmode",1);
		}
		D3E1 ABCDEFG 1
        Goto Ready2
		
    Zoomout:
		TNT1 A 0 {
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_ZoomFactor(1.0);
			PB_HandleCrosshair(42);
			}
		D3E1 GFEDCBA 1
        Goto Ready3
		
	Reload2:
		D0E0 ABCDEFGHIIIIJKL 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D0E0 MNO 1 
		TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto") 
		//D1E1 LKJIHGFE 1
		//D1E2 VUTSRQ 1
		//TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		D1E2 NNNPPP 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magin", "Auto") 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto") 
		D1E2 QRSTUV 1
		D1E1 EFGHIJKL 1
		TNT1 A 0 A_Takeinventory("DeagleHasUnloaded",1)
		TNT1 A 0 A_GiveInventory("DeagleWasEmpty", 1)
		goto InsertBullets2
	
	Reload:
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "ReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("DeagleAmmo",12,"Ready3")
        TNT1 A 0 A_JumpIfInventory("PistolBullets",2,2)
        TNT1 A 0 A_PlaySoundEx("weapons/empty", "Weapon")
        Goto Ready3
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("Ironsights", "Auto");
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "ReloadDualWield")
		TNT1 A 0 A_JumpIf(Countinv("DeagleAmmo") >= 1,"Reload2")

        D1E0 ABCDEFG 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D1E0 HIJKLM 1 
        TNT1 A 0 A_FireCustomMissile("EmptyClipMP40",5,0,12,-4)
		D1E0 NNNOP 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magin", "Auto") 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto") 
		D1E0 QRSTUVWXY 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/click1", "Auto") 
		D1E0 Z 1
        D1E1 ABCD 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/click2", "Auto") 
		D1E1 DEFGHIJKL 1
		TNT1 A 0 A_Takeinventory("DeagleHasUnloaded",1)
		TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,"InsertBullets2")//30+1 effect
		TNT1 A 0 A_GiveInventory("DeagleWasEmpty", 1)
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("DeagleAmmo",11,"Ready3")
		TNT1 A 0 A_JumpIfInventory("PistolBullets",2,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("DeagleAmmo",1);
			A_Takeinventory("PistolBullets",2);
			}
		Loop
		
	InsertBullets2:
	InsertBullets2Loop:
		TNT1 A 0 A_JumpIfInventory("DeagleAmmo",12,"Ready3")
		TNT1 A 0 A_JumpIfInventory("PistolBullets",2,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("DeagleAmmo",1);
			A_Takeinventory("PistolBullets",2);
			}
		Loop
		
	LoadChamber:
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("UACSMGWasEmpty",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("IronSights", "Auto");
			}
		TNT1 A 0 A_PlaySoundEx("weapons/smg_click", "Auto")
		Goto Ready3
		
	ReloadDualWield:
		TNT1 A 0 {
			if (CountInv("DeagleAmmo") >= 12 && CountInv("LeftDeagleAmmo") >= 12) {
				return state("Ready3");
			}
			return state("");
		}
		
		
        TNT1 A 0 A_JumpIfInventory("PistolBullets",2,2)
		TNT1 A 0 A_PlaySoundEx("weapons/empty", "Auto")
        Goto Ready3
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("Ironsights", "Auto");
			A_ClearOverlays(10,11);
			}
		D6E3 ABCDEFGHI 1
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D6E3 JK 1 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
        TNT1 A 0 A_FireCustomMissile("EmptyClipMP40",5,0,12,-4)
        TNT1 A 0 A_FireCustomMissile("EmptyClipMP40",5,0,-12,-4)
		D6E3 LMMMNOPQ 1 
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magin", "Auto") 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto") 
		D6E3 RSTUVWXY 1 
		TNT1 A 0 A_PlaySoundEx("weapons/smg_magfly1", "Auto")
		D6E3 Z 1
		D6E4 ABCDEFGHIJKL 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magin", "Auto") 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto") 
		D6E4 MNOPPPQQ 1 
		TNT1 A 0 A_PlaySoundEx("weapons/smg_magfly1", "Auto")
		D6E4 RSTUVWXYZ 1
		TNT1 A 0 A_PlaySoundEx("IronSights", "Auto")
		D6E5 ABCDEFG 1
		TNT1 A 0 A_Takeinventory("DeagleHasUnloaded",1)
		Goto InsertBullets3_Right
		TNT1 A 0 {
			A_Takeinventory("DeagleHasUnloaded",1);
			}
		Goto InsertBullets3_Right
		
	InsertBullets3_Right:
		TNT1 A 0 A_JumpIfInventory("DeagleAmmo",12,"InsertBullets3_Left")
		TNT1 A 0 A_JumpIfInventory("PistolBullets",2,1)
		Goto InsertBullets3_Left
		TNT1 A 0 {
			A_Giveinventory("DeagleAmmo",1);
			A_Takeinventory("PistolBullets",2);
		}
		Goto InsertBullets3_Right
	InsertBullets3_Left:
		TNT1 A 0 A_JumpIfInventory("LeftDeagleAmmo",12,"FinishInsertBullets")
		TNT1 A 0 A_JumpIfInventory("PistolBullets",2,1)
		Goto FinishInsertBullets
		TNT1 A 0 {
			A_Giveinventory("LeftDeagleAmmo",1);
			A_Takeinventory("PistolBullets",2);
		}
		Goto InsertBullets3_Left
		
	
		
	FinishInsertBullets:
		TNT1 A 0 {
			A_Takeinventory("PB_DeagleFireAnimation1",1);
			A_Takeinventory("PB_DeagleFireAnimation2",1);
			A_Takeinventory("DeagleWasEmpty",1);
		}
		Goto Ready3
		
	Unload:
		ATFG A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_JumpIfInventory("DeagleHasUnloaded", 1, "Ready3")
        TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,1)
        Goto Ready3
		TNT1 A 0 {
			A_ClearOverlays(10,11);
			A_ZoomFactor(1.0);
			A_SetCrosshair(5);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("Ironsights", "Auto");
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "UnloadDualWield")
        D1E0 ABCDEFG 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D1E0 HIJKLM 1 
		D1E0 NNO 1 
		D1E1 EE 1
		D1E1 FGHIJKL 1
		Goto RemoveBullets
		
	UnloadDualWield:
		D6E3 ABCDEFGHI 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D6E3 JK 1
		TNT1 A 0 A_PlaySoundEx("weapons/deagle/magout", "Auto") 
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto") 
		D6E3 LMMIHGFEDCBA 1
		Goto RemoveBullets_Right
		
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,1)
		Goto FinishUnload
		TNT1 A 0 {
			A_Takeinventory("DeagleAmmo",1);
			A_Giveinventory("PistolBullets",2);
			}
		Loop
	RemoveBullets_Right:
		TNT1 A 0 A_JumpIfInventory("DeagleAmmo",1,1)
		Goto RemoveBullets_Left
		TNT1 A 0 {
			A_Takeinventory("DeagleAmmo",1);
			A_Giveinventory("PistolBullets",2);
			}
		Loop
	RemoveBullets_Left:
		TNT1 A 0 A_JumpIfInventory("LeftDeagleAmmo",1,1)
		Goto FInishUnloadDualWield
		TNT1 A 0 {
			A_Takeinventory("LeftDeagleAmmo",1);
			A_Giveinventory("PistolBullets",2);
			}
		Loop
	FInishUnload:
		TNT1 A 0 {
			A_GiveInventory("DeagleHasUnloaded", 1);
			A_Takeinventory("Unloading",1);
			}
		Goto Ready3

	FInishUnloadDualWield:
		TNT1 A 0 {
			A_GiveInventory("DeagleHasUnloaded", 1);
			A_Takeinventory("Unloading",1);
			}
		Goto Ready3
		
	Spawn.V5:
		V4E0 Z 10 {
			IF( A_CHECKRANGE( V5_MDist , "NULL", TRUE) ) {A_SetSpawnSprite("D4E0");}
			}
	Spawn:
		TNT1 A 0 NoDelay A_jumpif( V5_MODELS == 1 && V5_Cull == 1, "Spawn.V5")
		D4E0 Z 10 {
				if ( GetCvar( "V5_MODELS" ) == 1 && GetCVar( "V5_Cull" ) == 0 ) {A_SetSpawnSprite("V4E0");}
		}
		LOOP

		
	FlashPunching:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FlashPunchingDW")
		D8E0 ABCDEFGHIJKLMN 1
		Stop
	FlashPunchingDW:
		TNT1 A 15
		Stop
    FlashKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FlashKickingDW")
		D5E0 ABCDEFGHGFEDCBA 1 
		Stop
		
	FlashAirKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FlashAirKickingDW")
		D5E0 ABCDEFGHHHGFEDCBA 1 
		Stop
		
	FlashKickingDW:
		D7E0 ABCDEFGHGFEDCBA 1 
		Stop
		
	FlashAirKickingDW:
		D7E0 ABCDEFGHHHGFEDCBA 1 
		Stop
		
	
	FlashSlideKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FlashSlideKickingDW")
		D5E0 ABCDEFGHHHHHHHHHHHHGFEDCBAA 1 
		Stop
		
	FlashSlideKickingDW:
		D7E0 ABCDEFGHHHHHHHHHHHGFEDCBAA 1 
		D6E3 A 1
		Stop
		
	FlashSlideKickingStop:
		TNT1 A 0 A_JumpIfInventory("DualWieldingDeagles", 1, "FlashSlideKickingStopDW")
		D5E0 GFEDCBA 1 
		Stop
		
	FlashSlideKickingStopDW:
		D7E0 GFEDCBA 1
		Stop
	}
}

ACTOR DeagleAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 12
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 12
   Inventory.Icon "D4E0Z0"
}

ACTOR LeftDeagleAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 12
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 12
   Inventory.Icon "D4E0Z0"
}
