//// --------------------------------------------------------------------------
//
// Revolver
//
// --------------------------------------------------------------------------

Actor RevolverHasUnloaded: Inventory
{
Inventory.maxamount 1
}

ACTOR RespectRevolver : Inventory
{
Inventory.MaxAmount 1
}

ACTOR SwitchingFromDualWieldRevolver : Inventory
{
Inventory.MaxAmount 1
}

ACTOR SwitchingToDualWieldRevolver : Inventory
{
Inventory.MaxAmount 1
}

ACTOR DualWieldingRevolver: Inventory
{
Inventory.MaxAmount 1
}

ACTOR PB_Revolver : PB_Weapon
{
	//$Title Revolver
	//$Category Weapons
	//$Sprite RVICA0
	SpawnID 9210
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 6
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "PistolBullets"
	Weapon.AmmoType2 "RevolverAmmo"
	Inventory.MaxAmount 3
	Inventory.Amount 1
	//Weapon.Kickback 100
	+FLOORCLIP
	+DONTGIB
	Obituary "%o was shot down by %k's revolver."
    AttackSound "None"
	Inventory.PickupSound "REVOUP"
	Inventory.Pickupmessage "You got the UAC .500 Magnum! (Slot 2)"
    +WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
    +WEAPON.NOAUTOFIRE
	//+WEAPON.NO_AUTO_SWITCH
	Tag "Revolver"
    Scale 0.4
	PB_WeaponBase.UnloaderToken "RevolverHasUnloaded"
	//Weapon.SisterWeapon DualMagnums
	Inventory.AltHUDIcon "RVICA0"
	PB_WeaponBase.respectItem "RespectRevolver"
	FloatBobStrength 0.5
	States
	{  
	
	Steady:
	TNT1 A 1
	TNT1 A 0 A_ClearOverlays(10,11)
	Goto Ready
	
	FastSwitchFromDualWield2:
		TNT1 A 0 A_TakeInventory("SwitchingFromDualWieldRevolver", 1)
		TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1)
		R3V2 ABCD 1 A_SetRoll(roll-.8, SPF_INTERPOLATE)
		R3V2 E 1
		R3V2 FGHI 1 A_SetRoll(roll+.8, SPF_INTERPOLATE)
		Goto Ready3
	
	Ready:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0 A_JumpIfInventory("SwitchingFromDualWieldRevolver", 1, "FastSwitchFromDualWield2")
		TNT1 A 0 PB_RespectIfNeeded
	WeaponRespect:
		TNT1 A 0 {
			A_Giveinventory("RespectRevolver",1);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("REVOUP", "Auto");
			A_SetCrosshair(5);
			}
		R2V1 ABCDEFGHIJ 1{
			A_DoPBWeaponAction;
			A_SetRoll(roll+0.1, SPF_INTERPOLATE);
			}
		R2V1 KLMNOPQRST 1 {
			A_DoPBWeaponAction;
			A_SetRoll(roll-0.1, SPF_INTERPOLATE);
			}
		R2V1 U 1 A_DoPBWeaponAction
		R2V1 VWXYZ 1 {
			A_DoPBWeaponAction;
			A_SetRoll(roll+0.2, SPF_INTERPOLATE);
			}
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Open", "Auto")
		R2V2 AB 1 {
			A_DoPBWeaponAction;
			A_SetRoll(roll+0.2, SPF_INTERPOLATE);
			}
		R2V2 CDEFGHI 1{
			A_DoPBWeaponAction;
			A_SetRoll(roll-0.2, SPF_INTERPOLATE);
			}
		R2V2 JKLMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
		R2V3 ABCDEFGH 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySound("Weapons/Revolver/Click1")
		R2V3 IJK 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySound("Weapons/Revolver/Click1")
		R2V3 LMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
		R2V4 ABCDEFGHIJKL 1 A_DoPBWeaponAction
		TNT1 A 0 A_Playsound("CYLNSPIN")
		R2V4 MNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
		R2V5 ABCDEFGHIJKL 1 A_DoPBWeaponAction 
		R2V5 MNOP 1 {
			A_DoPBWeaponAction;
			A_SetRoll(roll-0.4, SPF_INTERPOLATE);
			}
		TNT1 A 0 A_PlaySound("Weapons/Revolver/Close")
		R2V5 QRSTUVWX 1 {
			A_DoPBWeaponAction;
			A_SetRoll(roll+0.2, SPF_INTERPOLATE);
			}
		R2V5 YZ 1 A_DoPBWeaponAction
		R2V6 ABCDEFGHIJKLM 1 A_DoPBWeaponAction
		TNT1 A 0 A_SetRoll(0)
		Goto Ready3
		
	SelectAnimation:
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "SelectAnimationDualWield")
        TNT1 A 0 A_PlaySound("REVOUP")
		R1V1 ABCD 1
	Ready3:
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "ReadyDualWield")
		TNT1 A 0 {
			A_SetRoll(0, SPF_INTERPOLATE);
			PB_HandleCrosshair(42);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
	ReadyLoop:
		R1V1 E 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD, CheckUnloaded("RevolverHasUnloaded"))
		Loop
		
	
	SelectAnimationDualWield:
        TNT1 A 0 A_PlaySoundEx("REVOUP","Auto")
		40V1 ABCD 1
        TNT1 A 0 A_PlaySoundEx("REVOUP","Auto")
	ReadyDualWield:
		TNT1 A 0 {
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_SetFiringRightWeapon(False);
			A_SetFiringLeftWeapon(False);
			if(CountInv("LeftRevolverAmmo") < CountInv("RevolverAmmo")){
				A_GiveInventory("DualFiring",1);
			}
			if(CountInv("PistolBullets")>0){
				if(CountInv("LeftRevolverAmmo")<=0){
					A_GiveInventory("DualFireReload",1);
				}
				if(CountInv("RevolverAmmo")<=0){
					A_GiveInventory("DualFireReload",1);
				}
			}
			A_Overlay(10, "IdleLeft_Overlay", false);
			A_Overlay(11, "IdleRight_Overlay", false);
			}
	ReadyToFireDualWield:
		TNT1 A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Loop
		
	IdleLeft_Overlay:
		40V1 J 1 {
			if(CountInv("LeftRevolverAmmo")<=0 && CountInv("RevolverAmmo")>0){
				A_GiveInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==0 || (CountInv("DualFiring")==0 && CountInv("RevolverAmmo")<=0) || GetCvar("SingleDualFire")==1){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon()){
					if(CountInv("LeftRevolverAmmo") > 0){
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			return state("");
		}
		Loop
	IdleRight_Overlay:
		40V1 K 1 {
			if(CountInv("LeftRevolverAmmo")>0 && CountInv("RevolverAmmo")<=0){
				A_TakeInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==1 || (CountInv("DualFiring")==1 && CountInv("LeftRevolverAmmo")<=0)){
				if((PressingFire() || JustPressed(BT_ATTACK)) && !A_IsFiringLeftWeapon() && GetCvar("SingleDualFire")==0){
					if(CountInv("RevolverAmmo") > 0){
						return state("FireRight_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			if((PressingAltfire() || JustPressed(BT_ALTATTACK)) && !A_IsFiringRightWeapon() && GetCvar("SingleDualFire")==1){
				if(CountInv("RevolverAmmo") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			return state("");
		}
		Loop
		
	FireLeft_Overlay:
			41V1 A 1 BRIGHT {	
				A_FireBullets (0.1, 0.1, -1, 85, "BulletPuff", FBF_NORANDOM,8192,"Tracer", -1,-4);
				A_FireCustomMissile("GunFireSmoke", 0, 0, -5, 0, 0, 0);
				A_Takeinventory("LeftRevolverAmmo",1);
				A_ZoomFactor(0.99);
				A_PlaySoundEx("revolver/fire", "Weapon");
				A_AlertMonsters;
				A_SetFiringLeftWeapon(True);
				A_GunFlash;
                PB_WeaponRecoil(-1.9,+1.8);
			}
			
			41V1 B 1 BRIGHT {
				A_ZoomFactor(1.0);
                PB_WeaponRecoil(-1.9,+1.8);
			}
			41V1 C 1 {
                PB_WeaponRecoil(-1.9,+1.8);
			}
			41V1 D 1
			41V1 E 1
			{
				A_SetFiringLeftWeapon(False);
				if(CountInv("LeftRevolverAmmo")<=0 || CountInv("RevolverAmmo")>0 ){
					A_GiveInventory("DualFiring",1);
				}
			}
			41V1 F 1
			41V1 GGG 1 {
				if(JustPressed(BT_ATTACK) && !A_IsFiringLeftWeapon()){
					if(CountInv("LeftRevolverAmmo") > 0){
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
				return state("");
			}
			TNT1 A 0 {
				if(CountInv("LeftRevolverAmmo")<=0){
					A_GiveInventory("DualFireReload",1);
				}
			}
			Goto IdleLeft_Overlay
	
	FireRight_Overlay:
		41V1 I 1 BRIGHT 
				{	
					A_FireBullets (0.1, 0.1, -1, 85, "BulletPuff", FBF_NORANDOM,8192,"Tracer", -1,4);
					A_FireCustomMissile("GunFireSmoke", 0, 0, 5, 0, 0, 0);
					A_Takeinventory("RevolverAmmo",1);
					A_ZoomFactor(0.99);
					A_PlaySoundEx("revolver/fire", "Weapon");
					A_AlertMonsters;
					A_SetFiringRightWeapon(True);
					A_GunFlash;
				PB_WeaponRecoil(-1.9,-1.8);
			}
		41V1 J 1 BRIGHT {
				A_ZoomFactor(1.0);
				PB_WeaponRecoil(-1.9,-1.8);
		}
		41V1 K 1 {
				PB_WeaponRecoil(-1.9,-1.8);
		}
		41V1 L 1
		41V1 M 1 {
			A_SetFiringRightWeapon(False);
			if(CountInv("LeftRevolverAmmo")>0 || CountInv("RevolverAmmo")<=0 ){
				A_TakeInventory("DualFiring",1);
			}
		}
		41V1 N 1
		41V1 OOO 1 {
			if(JustPressed(BT_ALTATTACK) && !A_IsFiringRightWeapon()){
				if(CountInv("RevolverAmmo") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			return state("");
		}
		TNT1 A 0 {
			if(CountInv("RevolverAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleRight_Overlay
		
	WeaponSpecial:
		TNT1 A 0 {
			A_GiveInventory("PB_LockScreenTilt",1);
			A_Takeinventory("GoWeaponSpecialAbility",1);
			PB_HandleCrosshair(42);
			A_ZoomFactor(1.0);
			A_ClearOverlays(10,11);
			A_PlaySoundEx("Ironsights", "Auto");
		}
		TNT1 A 0 A_JumpIfInventory("PB_Revolver", 2,"SwitchToDualWield")
		TNT1 A 0 A_print("You need two revolvers to dual wield!")
		Goto Ready3
	SwitchToDualWield:
        	TNT1 A 0 {
        	    if (A_CheckAkimbo()) {
        	        A_SetAkimbo(False);
        	        A_Takeinventory("DualWieldingRevolver",1); // this
        	        return state("SwitchFromDualWield");
        	    }
        	    else {
        	        A_SetAkimbo(True);
        	        A_Giveinventory("DualWieldingRevolver",1); // and this
        	        return state("");
        	    }
        	}
		R3V1 ABCDE 1 A_SetRoll(roll+.8, SPF_INTERPOLATE)
		R3V1 E 1
		R3V1 EFGHI 1 A_SetRoll(roll-.8, SPF_INTERPOLATE)
		Goto ReadyDualWield
	SwitchFromDualWield:
		R3V2 ABCDE 1 A_SetRoll(roll-.8, SPF_INTERPOLATE)
		R3V2 E 1
		R3V2 EFGHI 1 A_SetRoll(roll+.8, SPF_INTERPOLATE)
		Goto Ready3
	
	Deselect:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_Takeinventory("Unloading",1);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
		}
		TNT1 A 0 A_JumpIfInventory("GotMeatShield", 1, "GrabEnemy")
		TNT1 A 0 A_JumpIfInventory("PB_Deagle",1,"DeselectUpgrade")
		40V1 FGHI 0 
		R1V1 FGHI 1 {
			if (A_CheckAkimbo()) {
				A_SetWeaponSprite("40V1");
			}
		}
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
		
	DeselectUpgrade:
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_LOWER
		Wait	
	Select:
		TNT1 A 0
		Goto SelectFirstPersonLegs
		SelectContinue:
		TNT1 A 0 A_SelectWeapon("PB_Deagle")
		TNT1 A 0 PB_WeapTokenSwitch("RevolverSelected")
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("SwitchingFromDualWieldRevolver", 1, "FastSwitchFromDualWield1")
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait
	FastSwitchFromDualWield1:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		Wait
		
	Fire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
        TNT1 A 0 A_JumpIfInventory("RevolverAmmo",1,1)
        Goto Reload
		R4V1 A 1 BRIGHT {	
				A_PlaySoundEx("revolver/fire", "Weapon");
				A_FireBullets (0.1, 0.1, -1, 85, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
				A_AlertMonsters;
				A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
				A_Takeinventory("RevolverAmmo",1);
				A_ZoomFactor(0.96);
				A_GunFlash;
                PB_WeaponRecoil(-1.15,-0.26);
			}
		R4V1 B 1 BRIGHT {
				A_ZoomFactor(0.98);
                PB_WeaponRecoil(-1.15,-0.26);
			}
		R4V1 C 1 {
				A_ZoomFactor(1.0);
				PB_WeaponRecoil(-1.15,-0.26);
			}
			
		R4V1 DEFGH 1 
		TNT1 A 0 A_ZoomFactor(1.0)
		R1V1 EE 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto Ready3
    AltFire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(42);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
        TNT1 A 0 A_JumpIfInventory("RevolverAmmo",1,1)
        Goto Reload
		R5V1 A 1 BRIGHT {	
				A_PlaysoundEx("revolver/fire","Weapon");
				A_FireBullets (0.1, 0.1, -1, 85, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
				A_AlertMonsters;
				A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
				A_Takeinventory("RevolverAmmo",1);
				A_ZoomFactor(0.96);
				A_GunFlash;
                PB_WeaponRecoil(-1.15,-0.35);
			}
		R5V1 B 1 BRIGHT {
				A_ZoomFactor(0.98);
				PB_WeaponRecoil(-1.15,-0.35);
			}
		R5V1 C 1 {
				A_ZoomFactor(1.0);
				A_PlaysoundEx("Weapons/Revolver/Click1","Auto");
				PB_WeaponRecoil(-1.15,-0.35);
			}
		R5V1 DEFG 1 
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Refire("AltHold")
		R5V1 UVWX 1 
		Goto Ready3
	AltHold:
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 0 A_JumpIfInventory("RevolverAmmo",1,1)
        Goto Reload
		R5V1 I 1 BRIGHT {	
				A_PlaysoundEx("revolver/fire","Weapon");
				A_FireBullets (0.1, 0.1, -1, 85, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
				A_AlertMonsters;
				A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0);
				A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
				A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
				A_Takeinventory("RevolverAmmo",1);
				A_ZoomFactor(0.96);
				A_GunFlash;
				PB_WeaponRecoil(-1.2,-0.36);
			}
		R5V1 J 1 BRIGHT {
				A_ZoomFactor(0.98);
				PB_WeaponRecoil(-1.2,-0.36);
			}
		R5V1 K 1 {
				A_ZoomFactor(1.0);
				A_PlaysoundEx("Weapons/Revolver/Click1","Auto");
				PB_WeaponRecoil(-1.2,-0.36);
			}
		R5V1 LMNO 1 
		TNT1 A 0 A_Refire("AltHold")
		R5V1 UVWX 1 
		Goto Ready3

	NoAmmo:
		R1V1 E 1 A_PlaySound("weapons/empty")
		Goto Ready3
    Reload: 
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "ReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("RevolverAmmo",6,"Ready3")
        TNT1 A 0 A_JumpIfInventory("PistolBullets",2,1)
		Goto NoAmmo
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySound("Ironsights");
		}
		R6V1 ABCDDE 1 A_SetRoll(roll+0.2, SPF_INTERPOLATE)
		TNT1 A 0 A_JumpIfInventory("RevolverHasUnloaded",1,"ReloadEmpty")
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Open", "Auto")
		R6V1 FGHI 1 A_SetRoll(roll-0.3, SPF_INTERPOLATE)
		R6V1 J 1 
		R6V1 KLMNO 1
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Click2", "Auto")
		R6V1 PQRS 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		TNT1 AAAAAA 0 A_FireCustomMissile("MagnumCasing",-5,0,0,-30)
		R6V1 T 1 
		R6V1 U 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		ReloadEmpty:
		TNT1 A 0 A_TakeInventory("RevolverHasUnloaded", 1)
		R6V1 VWXYZ 1 A_SetRoll(roll-0.5, SPF_INTERPOLATE)
		R6V2 A 1 A_PlaySoundEx("Weapons/Revolver/Load", "Auto")
		R6V2 BCDEFF 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("CYLNSPIN", "Auto")
		TNT1 A 0 PB_SpawnCasing("RevolverSpeedLoader", 45.6, 9, 18.75,frandom(-1,1),frandom(-1.2, -0.6), frandom(1,-1))
		R6V2 GHI 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Close", "Auto")
		R6V2 JKLMNOPQQ 1 A_SetRoll(roll-0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_SetRoll(0)
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("RevolverAmmo",6,"Ready3")
		TNT1 A 0 A_JumpIfInventory("PistolBullets",2,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("RevolverAmmo",1);
			A_Takeinventory("PistolBullets",2);
		}
		Goto InsertBullets
	
	// Fixes crash when there is only 1 round in reserve
	NoAmmoDualWield:
		40V1 E 1
		Goto Ready3
		
	ReloadDualWield: 
		TNT1 A 0 {
			if (CountInv("RevolverAmmo") >= 6 && CountInv("LeftRevolverAmmo") >= 6) {
				return state("Ready3");
			}
			return state("");
		}
		TNT1 A 0 A_JumpIfInventory("PistolBullets",2,1)
        Goto NoAmmoDualWield
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySound("Ironsights");
			A_ClearOverlays(10,11);
		}
		TNT1 A 0 A_JumpIfInventory("RevolverHasUnloaded",1,"ReloadEmpty1")
		42V1 ABC 1 A_SetRoll(roll-0.3, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Open", "Auto")
		42V1 DEF 1 A_SetRoll(roll+0.3, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Click2", "Auto")
		42V1 GHIJKLM 1 A_SetRoll(roll+0.4, SPF_INTERPOLATE)
		42V1 NO 1
		TNT1 AAAAAA 0 A_FireCustomMissile("MagnumCasing",-5,0,0,-30)
		42V1 PQ 1 A_SetRoll(roll-0.4, SPF_INTERPOLATE)
	ReloadEmpty1:
		42V1 RSTUV 1 A_SetRoll(roll-0.4, SPF_INTERPOLATE)
		42V1 W 1 A_PlaySoundEx("Weapons/Revolver/Load", "Auto")
		42V1 XYZ 1
		TNT1 A 0 A_PlaySoundEx("CYLNSPIN", "Auto")
		42V2 ABBC 1	A_SetRoll(roll+0.6, SPF_INTERPOLATE)
		TNT1 A 0 PB_SpawnCasing("RevolverSpeedLoader", 45.6, 11, 18.75,frandom(-1,1),frandom(-1.2, -0.6), frandom(1,-1))
		42V2 DEFG 1	A_SetRoll(roll-0.6, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Close", "Auto")
		TNT1 A 2
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Open", "Auto")
		TNT1 A 0 A_JumpIfInventory("RevolverHasUnloaded",1,"ReloadEmpty2")
		42V2 HIJ 1 A_SetRoll(roll-0.3, SPF_INTERPOLATE)
		42V2 KLM 1 A_SetRoll(roll+0.3, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Click2", "Auto")
		42V2 NOPQRST 1 A_SetRoll(roll+0.4, SPF_INTERPOLATE)
		TNT1 AAAAAA 0 A_FireCustomMissile("MagnumCasing",-5,0,0,-30)
		42V2 UV 1 A_SetRoll(roll-0.4, SPF_INTERPOLATE)
	ReloadEmpty2:
		42V2 WXYZ 1 A_SetRoll(roll-0.4, SPF_INTERPOLATE)
		42V3 A 1 A_SetRoll(roll-0.4, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Load", "Auto")
		42V3 BCDE 1 A_SetRoll(roll+0.6, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("CYLNSPIN", "Auto")
		42V3 FGGH 1 A_SetRoll(roll-0.6, SPF_INTERPOLATE)
		TNT1 A 0 PB_SpawnCasing("RevolverSpeedLoader", 45.6, 11, 18.75,frandom(-1,1),frandom(-1.2, -0.6), frandom(1,-1))
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Close", "Auto")
		42V3 IJKL 1
		TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto")
		42V3 MNOPQRSTUV 1
		TNT1 A 0 A_Takeinventory("RevolverHasUnloaded", 1)
	InsertBulletsDualWield_Right:
		TNT1 A 0 A_JumpIfInventory("RevolverAmmo",6,"InsertBulletsDualWield_Left")
		TNT1 A 0 A_JumpIfInventory("PistolBullets",2,1)
		Goto InsertBulletsDualWield_Left
		TNT1 A 0 {
			A_Giveinventory("RevolverAmmo",1);
			A_Takeinventory("PistolBullets",2);
		}
		Goto InsertBulletsDualWield_Right
	InsertBulletsDualWield_Left:
		TNT1 A 0 A_JumpIfInventory("LeftRevolverAmmo",6,"Ready3")
		TNT1 A 0 A_JumpIfInventory("PistolBullets",2,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("LeftRevolverAmmo",1);
			A_Takeinventory("PistolBullets",2);
		}
		Goto InsertBulletsDualWield_Left
	
	AlreadyUnloaded:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto Ready3
	Unload:
		TNT1 A 0 A_JumpIfInventory("RevolverHasUnloaded", 1, "AlreadyUnloaded")
		TNT1 A 0 A_JumpIF(A_CheckAkimbo(), "DualUnload")
        TNT1 A 0 A_JumpIfInventory("RevolverAmmo",1,1)
        Goto AlreadyUnloaded
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_PlaySound("Ironsights");
			A_ClearOverlays(10,11);
		}
		R7V1 ABCDDE 1 A_SetRoll(roll+0.2, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Open", "Auto")
		R7V1 FGHI 1 A_SetRoll(roll-0.3, SPF_INTERPOLATE)
		R7V1 J 1 
		R7V1 KLMNO 1
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Click2", "Auto")
		R7V1 PQRST 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		R7V1 U 1 
		R7V1 VWXYZ 1 A_SetRoll(roll-0.5, SPF_INTERPOLATE)
		R7V2 ABCCDE 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Close", "Auto")
		R7V2 EFGHII 1 A_SetRoll(roll-0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_SetRoll(0)
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("RevolverAmmo",1,1)
		Goto FinishUnload
		TNT1 A 0 {
			A_Takeinventory("RevolverAmmo",1);
			A_Giveinventory("PistolBullets",2);
		}
		Goto RemoveBullets
	FInishUnload:
		TNT1 A 0 A_GiveInventory("RevolverHasUnloaded", 1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto Ready3
		
	
	DualUnload:
		TNT1 A 0 A_JumpIfInventory("RevolverHasUnloaded", 1, "AlreadyUnloaded")
        TNT1 A 0 A_JumpIf((CountInv("RevolverAmmo") > 0 || CountInv("LeftRevolverAmmo") > 0),1)
        Goto AlreadyUnloaded
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
		}
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Open", "Auto")
		43V1 ABCDEFGHIJ 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Click2", "Auto")
		43V1 KLMNOPQRST 1 A_SetRoll(roll-0.5, SPF_INTERPOLATE)
		43V1 UVW 1 A_SetRoll(roll+0.6, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Close", "Auto")
		43V1 XYZ 1 A_SetRoll(roll-0.6, SPF_INTERPOLATE)
		43V2 ABCDEF 1
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Open", "Auto")
		43V2 GHIJKL 1 A_SetRoll(roll+0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Click2", "Auto")
		43V2 MNOPQR 1 A_SetRoll(roll-0.5, SPF_INTERPOLATE)
		43V2 STUVWXYZ 1
		TNT1 A 0 A_Giveinventory("RevolverHasUnloaded",1)
		TNT1 A 0 A_PlaySoundEx("Weapons/Revolver/Close", "Auto")
		43V3 ABCD 1 A_SetRoll(roll+0.4, SPF_INTERPOLATE)
		43V3 EFGH 1 A_SetRoll(roll-0.4, SPF_INTERPOLATE)
	RemoveBullets_Right:
		TNT1 A 0 A_JumpIfInventory("RevolverAmmo",1,1)
		Goto RemoveBullets_Left
		TNT1 A 0 {
			A_Takeinventory("RevolverAmmo",1);
			A_Giveinventory("PistolBullets",2);
		}
		Goto RemoveBullets_Right
	RemoveBullets_Left:
		TNT1 A 0 A_JumpIfInventory("LeftRevolverAmmo",1,1)
		Goto FinishUnloadDual
		TNT1 A 0 {
			A_Takeinventory("LeftRevolverAmmo",1);
			A_Giveinventory("PistolBullets",2);
		}
		Goto RemoveBullets_Left
	FInishUnloadDual:
		TNT1 A 0 A_GiveInventory("RevolverHasUnloaded", 1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto Ready3	
		
	Spawn:
		VVIC A 0 NoDelay
		RVIC A 10 A_PbvpFramework("VVIC")
		"####" A 0 A_PbvpInterpolate()
		LOOP
		
    FlashKicking:
	FlashAirKicking:
		TNT1 A 0 A_JumpIF(A_CheckAkimbo(), "DualFlashKicking")
		R8V1 ABCDEFGHHIJKLMN 1 A_DoPBWeaponAction
		Goto Ready3
	FlashPunching:
		TNT1 A 0 A_JumpIF(A_CheckAkimbo(), "DualFlashPunching")
		R0V1 ABCDEFGHHIJKLMN 1 
		Stop
	FlashSlideKicking:
		TNT1 A 0 A_JumpIF(A_CheckAkimbo(), "DualFlashSlideKicking")
		R9V1 ABCDEFGHIJKLMNONOPQRSTUVWX 1 A_DoPBWeaponAction
		Goto Ready3
	FlashSlideKickingStop:
		TNT1 A 0 A_JumpIF(A_CheckAkimbo(), "DualFlashSlideKickingStop")
		R9V1 RSTUVWX 1 A_DoPBWeaponAction
		Goto Ready3
		
	
    DualFlashKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		44V1 ABCDEFGHHIJKLMN 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Goto Ready3
	DualFlashPunching:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 AAAAAAAAAAAAAAA 1
		Goto Ready3
	DualFlashSlideKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		44V2 ABCDEFGHIJKLMNONOPQRSTUVWX 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Goto Ready3
	DualFlashSlideKickingStop:
		TNT1 A 0 A_ClearOverlays(10,11)
		44V2 RSTUVWX 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Goto Ready3
	}
}

ACTOR LeftRevolverAmmo : Ammo //Your weapon's magazine ammo.
{
   Inventory.Amount 0
   Inventory.MaxAmount 6 //Your weapon's magazine ammo limit. Always leave one more bullet, so you can do the 12+1 effect.
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 6
   +INVENTORY.IGNORESKILL
   Inventory.Icon "RVICA0"
}

ACTOR RevolverAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 6
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 6
   +INVENTORY.IGNORESKILL
   Inventory.Icon "RVICA0"
}
