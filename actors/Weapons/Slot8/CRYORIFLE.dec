ACTOR FireModeCryoRifleMissile : Inventory { Inventory.MaxAmount 1 }
ACTOR FireModeCryoRifleBeam : Inventory { Inventory.MaxAmount 1 }
ACTOR FireModeCryoRifleSpear : Inventory { Inventory.MaxAmount 1 }
ACTOR FireModeCryoRifleFlak : Inventory { Inventory.MaxAmount 1 }
ACTOR FireModeCryoRifleMissile_WW : Inventory { Inventory.MaxAmount 1 }
ACTOR FireModeCryoRifleBeam_WW : Inventory { Inventory.MaxAmount 1 }
ACTOR FireModeCryoRifleSpear_WW : Inventory { Inventory.MaxAmount 1 }
ACTOR FireModeCryoRifleFlak_WW : Inventory { Inventory.MaxAmount 1 }
ACTOR RespectCryoRifle : Inventory { Inventory.MaxAmount 1 }
ACTOR HasUnloadedCryoRifle : Inventory { Inventory.MaxAmount 1 }
Actor CryoRifleOverCooling: Inventory { Inventory.maxamount 420 }
Actor CryoRifleAmmo : PB_WeaponAmmo {
inventory.maxamount 60
+INVENTORY.IGNORESKILL
Inventory.Icon "FRPKA0"
}

// The LNDD - Mark III, colloquially known as the Cryo Rifle is an experimental weapon 
//developed by the TeiTenga Armaments corporation. It was developed to combat the more
//flammable denizens of hell. Powered by plasma energy, it injects fuel into a powerful
//vaccum chamber, where the superheated plasma quickly burns both its own fuel, and
//all oxygen in the chamber. This drops the chamber to temperatures below that of the
//vaccum of space. The projectiles fired by the weapon are actually comprised of 
//moisture from the air, as suddenly releasing the chamber's pressure flash freezes all
//liquid in a nearby area. Anything outside of this radius will be coated with 
//supercooled particulate at enough volume to generate ice crystals on flesh. The weapon
//comes equipped with an adjustable lens on the barrel to release the supercooled plasma 
//gas in a number of different orientations, allowing the user to create projectiles
//from ice when focussed enough, or the spray a beam of freezing gasses when unfocussed.
Actor PB_CryoRifle: PB_Weapon
{
	//$Category Project Brutality - Weapons
	//$Sprite FRPKA0
	SpawnID 9700
	Weapon.SelectionOrder 100
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 60
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Inventory.PickupSound "weapons/CryoRifle/respect1"
	Weapon.AmmoType1 "PB_Cell"
	Weapon.AmmoType2 "CryoRifleAmmo"
		+WEAPON.NOAUTOAIM
// 	+WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	DamageType "Ice"
	Inventory.PickupMessage "UAC-31 Cryo Rifle (Slot 8)"
	Obituary "%o was iced by %k"
	Tag "TeiTenga Arms Cryo Rifle"
	Scale 0.48
	PB_WeaponBase.respectItem "RespectCryoRifle"
	PB_WeaponBase.OffsetRecoilX 1.5
	PB_WeaponBase.OffsetRecoilY 1.8
	PB_WeaponBase.UsesWheel 1
	PB_WeaponBase.WheelInfo "PB_CryoRifleWheel"
		States {
  	
			Spawn:
				FRPK A 1
				Loop
		
			Steady: 
				TNT1 A 1
				Goto Ready
				
			Ready:
				TNT1 A 0 A_JumpIfInventory("GoFatality",1,"steady")
				TNT1 A 0 PB_RespectIfNeeded
			WeaponRespect:
				TNT1 A 0 {
					A_SetCrosshair(5);
					A_SetInventory("RespectCryoRifle",1);
					A_SetInventory("FireModeCryoRifleBeam",1);
					A_SetInventory("FireModeCryoRifleFlak",1);
				}
				TNT1 A 0 A_StartSound("weapons/CryoRifle/up", CHAN_AUTO, CHANF_OVERLAP)
				FR00 ABCDEFGHIJKLMNO 1 A_DoPBWeaponAction
				TNT1 A 0 A_StartSound("weapons/CryoRifle/respect2", CHAN_AUTO, CHANF_OVERLAP)
				FR00 PQRSTUVWXYZ 1 A_DoPBWeaponAction
				FR01 ABCD 1 A_DoPBWeaponAction
				TNT1 A 0 {
					A_StartSound("weapons/CryoRifle/respect3", CHAN_AUTO, CHANF_OVERLAP);
					A_StartSound("weapons/CryoRifle/respect1", CHAN_AUTO, CHANF_OVERLAP);
				}
				FR01 EFGHIJKLMNOPQRSTUVWXYZ 1 {
					A_FireCustomMissile("GunFireSmoke", 0, 0, -2, -5, 0, 0);
					return A_DoPBWeaponAction;
				}
				FR02 ABCDEFGH 1 {
					A_FireCustomMissile("GunFireSmoke", 0, 0, -2, -5, 0, 0);
					return A_DoPBWeaponAction;
				}
				FR02 IJKL 1 A_DoPBWeaponAction
				TNT1 A 0 {
					A_StartSound("weapons/CryoRifle/idle", CHAN_6, CHANF_LOOPING|CHANF_OVERLAP);
					A_StartSound("PLSIDLE", CHAN_7, CHANF_LOOPING|CHANF_OVERLAP);
				}
				FR02 MNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
				FR60 ABCDEFGHIJKLMNOPQ 1 A_DoPBWeaponAction
				Goto Ready3
				
			SelectAnimation:
				TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
				TNT1 A 0 A_StartSound("weapons/CryoRifle/respect1", CHAN_AUTO, CHANF_OVERLAP)
				FR03 ABCD 1
			Ready3:
				TNT1 A 0 {
					A_TakeInventory("PB_LockScreenTilt",1);
					PB_HandleCrosshair(79);
					A_StartSound("weapons/CryoRifle/idle", CHAN_6, CHANF_LOOPING|CHANF_OVERLAP);
					A_StartSound("PLSIDLE", CHAN_7, CHANF_LOOPING|CHANF_OVERLAP);
					}
				TNT1 A 0 A_JumpIfInventory("HasUnloadedCryoRifle", 1, "GunEmpty")
			ReadyToFire1:
				FR03 EFGHIJKLMMLKJIHGFE 1 {
					if(CountInv("CryoRifleAmmo") == 0) {
						return state("GunEmpty");
					}
					
					A_Overlay(7, "BigTubeGlow");
					A_Overlay(8, "SmallTubeGlow");
					A_Overlay(9, "MuzzleGlow");
					A_TakeInventory("CryoRifleOverCooling",1);
					A_FireCustomMissile("TinyGunSmoker", 0, 0, 0, -3, 0, 0);
					
					return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
				}
				FR03 EFGHIJKLMMLKJIHGFE 1 {
					if(CountInv("CryoRifleAmmo") == 0) {
						return state("GunEmpty");
					}
					A_Overlay(7, "BigTubeGlow");
					A_Overlay(8, "SmallTubeGlow");
					A_Overlay(9, "MuzzleGlow");
					A_TakeInventory("CryoRifleOverCooling",1);
					A_FireCustomMissile("TinyGunSmoker", 0, 0, 0, -3, 0, 0);
					return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
				}
				Loop
			GunEmpty:
				TNT1 A 0 A_StopSound(CHAN_6)
				TNT1 A 0 A_StopSound(CHAN_7)
				FR03 EE 1 {
					A_Overlay(7, "BigTubeGlow");
					A_Overlay(8, "SmallTubeGlow");
					A_Overlay(9, "MuzzleGlow");
					A_TakeInventory("CryoRifleOverCooling",1);
					return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
				}
				Goto GunEmpty+3
			
			WeaponSpecial:
				TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "IdleBarrel")
				TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "IdleFlameBarrel")
				TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "IdleIceBarrel")
				TNT1 A 0 {
					A_StopSound(2);
					A_SetInventory("ADSmode",0);
					A_SetInventory("GoWeaponSpecialAbility",0);
					PB_HandleCrosshair(5);
					A_ZoomFactor(1.0);
					A_ClearOverlays(10,11);
				}
			SwitchMode:
				TNT1 A 0 {
					
					
					if(CountInv("FireModeCryoRifleMissile")==1 && CountInv("FireModeCryoRifleMissile_WW")==1) {
						return state("CancelModeSwap");
					}
					if(CountInv("FireModeCryoRifleBeam")==1 && CountInv("FireModeCryoRifleBeam_WW")==1) {
						return state("CancelModeSwap");
					}
					if(CountInv("FireModeCryoRifleSpear")==1 && CountInv("FireModeCryoRifleSpear_WW")==1) {
						return state("CancelModeSwap");
					}
					if(CountInv("FireModeCryoRifleFlak")==1 && CountInv("FireModeCryoRifleFlak_WW")==1) {
						return state("CancelModeSwap");
					}
				
					A_StartSound("weapons/CryoRifle/up", CHAN_AUTO, CHANF_OVERLAP);
					A_StartSound("weapons/CryoRifle/reload1", CHAN_AUTO, CHANF_OVERLAP);
					
					// Missile
					if(CountInv("FireModeCryoRifleMissile_WW")==1) {
						
						A_Print("Primary Mode: Ice Missile");
						A_SetInventory("FireModeCryoRifleMissile", 1);
						A_SetInventory("FireModeCryoRifleBeam", 0);
					}
					
					// Beam
					if(CountInv("FireModeCryoRifleBeam_WW")==1) {
						A_Print("Primary Mode: Ice Beam");
						A_SetInventory("FireModeCryoRifleMissile", 0);
						A_SetInventory("FireModeCryoRifleBeam", 1);
					}
					
					// Spike
					if(CountInv("FireModeCryoRifleSpear_WW")==1) {
						A_Print("Secondary Mode: Ice Spear");
						A_SetInventory("FireModeCryoRifleSpear", 1);
						A_SetInventory("FireModeCryoRifleFlak", 0);
					}
					
					// Flak
					if(CountInv("FireModeCryoRifleFlak_WW")==1) {
						A_Print("Secondary Mode: Ice Flak");
						A_SetInventory("FireModeCryoRifleSpear", 0);
						A_SetInventory("FireModeCryoRifleFlak", 1);
					}
					return state("");
				}
				FR20 ABCDEDFGHIJKL 1
				TNT1 A 0 A_StartSound("weapons/CryoRifle/respect2", CHAN_AUTO, CHANF_OVERLAP)
				FR20 MNOPQRSTUU 1 A_FireCustomMissile("GunFireSmoke", 0, 0, -5, -5, 0, 0)
				TNT1 A 0 A_StartSound("weapons/CryoRifle/respect3", CHAN_AUTO, CHANF_OVERLAP)
				FR20 VWXYZ 1 A_FireCustomMissile("GunFireSmoke", 0, 0, -5, -5, 0, 0)
				FR21 ABCDEDFGHIJKLMNOPQRS 1
				TNT1 A 0 { 
					A_SetInventory("FireModeCryoRifleMissile_WW", 0);
					A_SetInventory("FireModeCryoRifleBeam_WW", 0);
					A_SetInventory("FireModeCryoRifleSpear_WW", 0);
					A_SetInventory("FireModeCryoRifleFlak_WW", 0);
				}
				Goto Ready3
			CancelModeSwap:
				TNT1 A 0 { 
					A_Print("Fire Mode Already Selected");
					A_SetInventory("FireModeCryoRifleMissile_WW", 0);
					A_SetInventory("FireModeCryoRifleBeam_WW", 0);
					A_SetInventory("FireModeCryoRifleSpear_WW", 0);
					A_SetInventory("FireModeCryoRifleFlak_WW", 0);
				}
				Goto Ready3

			Select:
				TNT1 A 0 {
					A_WeaponOffset(0,32);
					A_SetRoll(0);
					PB_HandleCrosshair(79);
					A_SetInventory("PB_LockScreenTilt",0);
					PB_ResetBarrelTokens;
				}
				
				
				Goto SelectFirstPersonLegs
			SelectContinue:
				TNT1 A 0 PB_WeapTokenSwitch("CryoRifleSelected")
				TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
				TNT1 AAAAAAAA 1 A_Raise
				Wait

			Deselect:
				TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "PlaceBarrel")
				TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "PlaceFlameBarrel")
				TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "PlaceIceBarrel")
				TNT1 A 0 {
					A_WeaponOffset(0,32);
					A_SetRoll(0);
					PB_HandleCrosshair(79);
					A_Setinventory("PB_LockScreenTilt",0);
					A_Setinventory("HasUnloadedCryoRifle",0);
					A_Setinventory("Unloading",0);
					A_StopSound(1);
					A_StopSound(2);
					A_StopSound(CHAN_6);
					A_StopSound(CHAN_7);
				}
				FR93 NOPQ 0
				FR03 NOPQ 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR93");}
				}
				TNT1 A 0 A_Takeinventory("CryoRifleOverCooling",420)
				TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
				Wait

			Fire:
				TNT1 A 0 {
					A_WeaponOffset(0,32);
					A_SetRoll(0);
					PB_HandleCrosshair(79);
					A_TakeInventory("PB_LockScreenTilt",1);
				}
				TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "ThrowBarrel")
				TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "ThrowFlameBarrel")
				TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "ThrowIceBarrel")
				TNT1 A 0 A_JumpIfInventory("FireModeCryoRifleBeam",1,"FireBeam")
			FireMissile:
				TNT1 A 0  {
					if (CountInv("FireModeCryoRifleMissile") == 1  && CountInv("CryoRifleAmmo") < 5) {
						return state("Reload");
					}
					A_StopSound(CHAN_6);
					A_StopSound(CHAN_7);
					A_StartSound("weapons/CryoRifle/powerup", CHAN_AUTO, CHANF_OVERLAP);
					return state("");
				}
				FR10 HGFEDCB 1 { 
					PB_FireOffset;
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				}
			HoldMissile:
				FR11 ABCDEFGHI 0 // Initialize sprites into virtual memory
				
				FR10 A 1 Bright {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
					
					//Shoot Ice Missile
					if(CountInv("FireModeCryoRifleMissile") == 1) {
						PB_WeaponRecoil(-0.32,-0.16);
						A_FireCustomMissile("IceMissile", 0, 0, 0, 0);
						A_Takeinventory("CryoRifleAmmo",5);
						A_StartSound("weapons/CryoRifle/missile", CHAN_WEAPON, CHANF_OVERLAP);
					}
					
					PB_FireOffset;
					A_GunFlash;
					A_AlertMonsters;
					A_Giveinventory("CryoRifleOverCooling",60);
					PB_GunSmoke(0,0,0);//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
					PB_GunSmoke(0,0,0);//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
					PB_GunSmoke(0,0,0);//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
					A_ZoomFactor(0.98);
				}
				FR10 BCD 1 Bright {
					PB_FireOffset;
					A_ZoomFactor(1.0);
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				}
				FR10 EFGHI 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				}
				TNT1 A 0 {
					if (CountInv("FireModeCryoRifleMissile") == 1 && CountInv("CryoRifleAmmo") < 5) {
						return state("EndMissile");
					}
					PB_ReFire("HoldMissile");
					A_Startsound("weapons/CryoRifle/powerdown", CHAN_AUTO, CHANF_OVERLAP);
					return state("");
				}
			EndMissile:
// 				TNT1 A 0 A_Startsound("weapons/CryoRifle/powerdown", CHAN_AUTO, CHANF_OVERLAP)
				TNT1 A 0 A_JumpIfInventory("CryoRifleAmmo",1,"Ready3")
				Goto Reload
				
			FireBeam:
				TNT1 A 0 A_JumpIfInventory("CryoRifleAmmo",1,1)
				Goto Reload
				TNT1 A 0 {
					A_StopSound(CHAN_6);
					A_StopSound(CHAN_7);
					A_StartSound("weapons/CryoRifle/powerup", CHAN_5, CHANF_OVERLAP);
				}
				FR10 HGFEDCB 1 { 
					PB_FireOffset;
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				}
			HoldBeam:
				TNT1 A 0 A_JumpIfInventory("CryoRifleAmmo",1,1)
				Goto EndBeam
				FR14 ABC 0 
				FR13 ABCB 1 BRIGHT {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR14");}
					PB_FireCryoRifleBeam();
					PB_FireOffset;
					PB_WeaponRecoil(-0.20,-0.08);
					
					A_GunFlash;
					A_Overlay(-2, "BeamMuzzleFlash");
					A_StartSound("weapons/cryobowflyby",CHAN_WEAPON, CHANF_LOOPING);
					A_AlertMonsters;
					PB_GunSmoke(0,0,0);//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
					A_Giveinventory("CryoRifleOverCooling",8);
				}
				TNT1 A 0 {
					A_Takeinventory("CryoRifleAmmo",1);
					PB_ReFire ("HoldBeam");
				}
			EndBeam:
				TNT1 A 0 {
					A_StopSound(CHAN_WEAPON);
					A_StopSound(CHAN_5);
					A_StartSound("weapons/CryoRifle/powerdown", CHAN_5, CHANF_OVERLAP);
				}
				FR10 CDEFGHI 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				}
				TNT1 A 0 A_JumpIfInventory("CryoRifleAmmo",1,"Ready3")
				Goto Reload
				
			BeamMuzzleFlash:
				TNT1 A 0 A_Jump(256, "Muzzle1", "Muzzle2", "Muzzle3", "Muzzle4", "Muzzle5", "Muzzle6", "Muzzle7")
			Muzzle1:
				FR12 A 1 BRIGHT
				Stop
			Muzzle2:
				FR12 B 1 BRIGHT
				Stop
			Muzzle3:
				FR12 C 1 BRIGHT
				Stop
			Muzzle4:
				FR12 D 1 BRIGHT
				Stop
			Muzzle5:
				FR12 E 1 BRIGHT
				Stop
			Muzzle6:
				FR12 F 1 BRIGHT
				Stop
			Muzzle7:
				FR12 G 1 BRIGHT
				Stop
				
			AltFire: //No moar pistol
				TNT1 A 0 {
					A_WeaponOffset(0,32);
					A_SetRoll(0);
					PB_HandleCrosshair(79);
					A_Setinventory("PB_LockScreenTilt",0);
				}
				TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "PlaceBarrel")
				TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "PlaceFlameBarrel")
				TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "PlaceIceBarrel")
			AltFireMissile:
				TNT1 A 0  {
					if (CountInv("FireModeCryoRifleSpear") == 1  && CountInv("CryoRifleAmmo") < 10) {
						return state("Reload");
					}
					if (CountInv("FireModeCryoRifleFlak") == 1  && CountInv("CryoRifleAmmo") < 2) {
						return state("Reload");
					}
					A_StopSound(CHAN_6);
					A_StopSound(CHAN_7);
					A_StartSound("weapons/CryoRifle/powerup", CHAN_AUTO, CHANF_OVERLAP);
					return state("");
				}
				FR10 HGFEDCB 1 { 
					PB_FireOffset;
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				}
			AltHoldMissile:
				
					TNT1 A 0  {
					//Shoot Ice Spear
					if(CountInv("FireModeCryoRifleSpear") == 1) {
						PB_WeaponRecoil(-0.32,-0.16);
						A_FireCustomMissile("IceSpear", 0, 0, 0, 0);
						A_Takeinventory("CryoRifleAmmo",10);
						A_StartSound("weapons/CryoRifle/spearfire", CHAN_WEAPON, CHANF_OVERLAP);
					}
					
					//Shoot Ice Flak
					if(CountInv("FireModeCryoRifleFlak") == 1) {
						PB_WeaponRecoil(-0.32,-0.16);
						A_FireCustomMissile("IceFlak1",random(-4,4),0,0,random(0,-1));
						A_FireCustomMissile("IceFlak2",random(-2,2),0,0,random(0,-1));
						A_FireCustomMissile("IceFlak3",random(-3,3),0,0,random(0,-1));
						A_FireCustomMissile("IceFlak2",random(-2,2),0,0,random(0,-1));
						A_FireCustomMissile("IceFlak3",random(-3,3),0,0,random(0,-1));
						A_Takeinventory("CryoRifleAmmo",2);
						A_StartSound("weapons/CryoRifle/flak", CHAN_WEAPON, CHANF_OVERLAP);
					}
					PB_FireOffset;
					A_GunFlash;
					A_AlertMonsters;
					A_Giveinventory("CryoRifleOverCooling",60);
					PB_GunSmoke(0,0,0);//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
					PB_GunSmoke(0,0,0);//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
					PB_GunSmoke(0,0,0);//A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 1, 0, 0);
					A_ZoomFactor(0.98);
				}
				FR10 BCD 1 Bright {
					PB_FireOffset;
					A_ZoomFactor(1.0);
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				}
				FR10 EFGH 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR11");}
				}
				TNT1 A 0 {
						if ((CountInv("FireModeCryoRifleFlak") == 1) && CountInv("CryoRifleAmmo") == 0) {
							A_Jump(256,"EndAltMissile");
						}
				}
				TNT1 A 0 A_JumpIfInventory("FireModeCryoRifleFlak",1,"AltRefire")
				FR14 ABCCBA 1 BRIGHT {
					PB_GunSmoke(frandom(-2,2),frandom(-2,2),frandom(-2,2));
				}
				FR14 BCA 1 { 
					PB_GunSmoke(frandom(-2,2),frandom(-2,2),frandom(-2,2));
					PB_FireOffset;
				}
				FR13 CBA 1 { 
					PB_GunSmoke(frandom(-2,2),frandom(-2,2),frandom(-2,2));
					PB_FireOffset;
				}
			AltRefire:
				TNT1 A 0 {
					if (CountInv("FireModeCryoRifleSpear") == 1 && CountInv("CryoRifleAmmo") < 10) {
						return state("EndAltMissile");
					}
					if (CountInv("FireModeCryoRifleFlak") == 1  && CountInv("CryoRifleAmmo") < 2) {
						return state("EndAltMissile");
					}
					return state("");
					}
				TNT1 A 0 {
					PB_ReFire("AltHoldMissile");
					A_Startsound("weapons/CryoRifle/powerdown", CHAN_AUTO, CHANF_OVERLAP);
					return state("");
				}
			
			EndAltMissile:
// 				TNT1 A 0 A_Startsound("weapons/CryoRifle/powerdown", CHAN_AUTO, CHANF_OVERLAP)
				TNT1 A 0 A_JumpIfInventory("CryoRifleAmmo",1,"Ready3")
				Goto Reload
//		 A1F3 CBA 1
//		 Goto Ready3
			Reload:
				TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "IdleBarrel")
				TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "IdleFlameBarrel")
				TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "IdleIceBarrel")
				TNT1 A 0 A_JumpIfInventory("CryoRifleAmmo",60,"Ready3")
				TNT1 A 0 A_JumpIfInventory("HasUnloadedCryoRifle",1,"ReloadUnloaded")
				TNT1 A 0 A_JumpIfInventory("PB_Cell",1,1)
				Goto Ready3
				FR94 ABCDEFGHIJKLMNOPQRSTUVWXYZ 0
				FR95 ABCDEFGHIJKLMNOPQRSTUVWXYZ 0
				FR96 ABCDEFGHIJKLMN 0
				//TNT1 A 0 A_TakeInventory("CryoRifleOverCooling", 420)
				TNT1 A 0 {
					A_StopSound(1);
					A_StopSound(6);
					A_StartSound("weapons/CryoRifle/reload1", CHAN_AUTO, CHANF_OVERLAP);
				}
				FR04 ABCDEFGHIJ 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR94");}
				}
				FR04 KLM 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR94");}
					A_FireCustomMissile("GunFireSmoke", 0, 0, 5, -5, 0, 0);
				}
				TNT1 A 0 A_StartSound("weapons/CryoRifle/reload3", CHAN_AUTO, CHANF_OVERLAP)
				
				FR04 NOPQRSTUVW 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR94");}
					A_FireCustomMissile("GunFireSmoke", 0, 0, 5, -5, 0, 0);
				}
				TNT1 A 0 A_StartSound("weapons/CellEject", CHAN_AUTO, CHANF_OVERLAP)
				TNT1 A 0 {
					if (CountInv("CryoRifleAmmo") <= 0) {
						PB_SpawnCasing("EmptyCell",10,-10,12,Frandom(-2,2),Frandom(-9,-6),Frandom(3,6));
					}
				}
				FR04 XYZ 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR94");}
					A_FireCustomMissile("GunFireSmoke", 0, 0, 5, -6, 0, 0);
				}
				FR05 ABCDEFGHIJKLM 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR95");}
					A_FireCustomMissile("GunFireSmoke", 0, 0, 5, -5, 0, 0);
				}
				TNT1 A 0 A_StartSound("weapons/CryoRifle/reload2", CHAN_AUTO, CHANF_OVERLAP)
				FR05 NOOOPQRS 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR95");}
				}
				TNT1 A 0 A_StartSound("weapons/plasma/cellin", CHAN_AUTO, CHANF_OVERLAP)
				FR05 TUVWXYZ 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR95");}
				}
				TNT1 A 0 A_StartSound("weapons/nailgun/inspect4", CHAN_AUTO, CHANF_OVERLAP)
				FR06 ABCDEFGHI 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR96");}
				}
				TNT1 A 0 A_StartSound("weapons/CryoRifle/up", CHAN_AUTO, CHANF_OVERLAP)
				FR06 JKLMN 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR96");}
				}
				TNT1 A 0 A_Setinventory("HasUnloadedCryoRifle", 0)
				Goto InsertBullets
			ReloadUnloaded:
				FR94 ABCDEFGHIJKLMNOPQRSTUVWXYZ 0
				FR95 ABCDEFGHIJKLMNOPQRSTUVWXYZ 0
				FR96 ABCDEFGHIJKLMN 0
				//TNT1 A 0 A_TakeInventory("CryoRifleOverCooling", 420)
				TNT1 A 0 {
					A_StopSound(1);
					A_StopSound(6);
					A_StartSound("weapons/CryoRifle/reload1", CHAN_AUTO, CHANF_OVERLAP);
				}
				FR04 ABCDEFGHI 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR94");}
				}
				TNT1 A 0 A_StartSound("weapons/CryoRifle/reload3", CHAN_AUTO, CHANF_OVERLAP)
				FR05 GHIJKLM 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR95");}
				}
				TNT1 A 0 A_StartSound("weapons/CryoRifle/reload2", CHAN_AUTO, CHANF_OVERLAP)
				FR05 NOOOPQRS 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR95");}
				}
				TNT1 A 0 A_StartSound("weapons/plasma/cellin", CHAN_AUTO, CHANF_OVERLAP)
				FR05 TUVWXYZ 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR95");}
				}
				TNT1 A 0 A_StartSound("weapons/nailgun/inspect4", CHAN_AUTO, CHANF_OVERLAP)
				FR06 ABCDEFGHI 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR96");}
				}
				TNT1 A 0 A_StartSound("weapons/CryoRifle/up", CHAN_AUTO, CHANF_OVERLAP)
				FR06 JKLMN 1 {
					if (CountInv("CryoRifleOverCooling") >= 100 ) { A_SetWeaponSprite("FR96");}
				}
				TNT1 A 0 A_Setinventory("HasUnloadedCryoRifle", 0)
			InsertBullets:
				TNT1 A 0 A_JumpIfInventory("CryoRifleAmmo",60,"Ready3")
				TNT1 A 0 A_JumpIfInventory("PB_Cell",1,1)
				Goto Ready3
				TNT1 A 0 {
					A_Giveinventory("CryoRifleAmmo",1);
					A_Takeinventory("PB_Cell",1);
					}
				Goto InsertBullets	
			Unload:
				TNT1 A 0 A_Setinventory("ADSmode",0)
				TNT1 A 0 A_Setinventory("Unloading",0)
				FR03 E 1 A_JumpIfInventory("HasUnloadedCryoRifle", 1, "Ready3")
				TNT1 A 0 A_StopSound(6)
				TNT1 A 0 A_StopSound(1)
				TNT1 A 0 A_JumpIfInventory("CryoRifleAmmo",1,1)
				Goto Ready3
				TNT1 A 0 A_PlaySoundEx("weapons/CryoRifle/reload1", "Auto")
				FR06 NMLKJIHGFEDCBA 1
				TNT1 A 0 A_PlaySoundEx("weapons/CryoRifle/reload3", "Auto")
				FR05 ZYXWVUTSTRQPONMLKJIHG 1
				FR04 IHGFEDCBA 1
			RemoveBullets:
				TNT1 A 0 A_JumpIfInventory("CryoRifleAmmo",1,1)
				Goto FinishUnload
				TNT1 A 0 A_Takeinventory("CryoRifleAmmo",1)
				TNT1 A 0 A_Giveinventory("PB_Cell",1)
				Goto RemoveBullets
			FInishUnload:
				TNT1 A 0 A_Setinventory("HasUnloadedCryoRifle", 1)
				TNT1 A 0 A_Setinventory("Unloading",0)
				Goto Ready3

			Flash:
				TNT1 A 1
				Goto LightDone
			FlashPunching:
				TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelPunching")
				TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelPunching")
				TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelPunching")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling", 100, "FlashPunchingFrost")
				FR42 ABCDEFGGHIJKLM 1
				Stop
			FlashPunchingFrost:
				FR72 ABCDEFGGHIJKLM 1
				Stop
			FlashKicking:
				TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelKicking")
				TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelKicking")
				TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelKicking")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling", 100, "FlashKickingFrost")
				FR40 ABCDEFGGHIJKLM 1
				Goto Ready3
			FlashKickingFrost:
				FR70 ABCDEFGGHIJKLM 1
				Goto Ready3
			FlashAirKicking:
				TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelAirKicking")
				TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelAirKicking")
				TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelAirKicking")	
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling", 100, "FlashAirKickingFrost")
				FR40 ABCDEFGGGHIJKLM 1
				Goto Ready3
			FlashAirKickingFrost:
				FR70 ABCDEFGGGHIJKLM 1
				Goto Ready3
			FlashSlideKicking:
				TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelSlideKicking")
				TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelSlideKicking")
				TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelSlideKicking")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling", 100, "FlashSlideKickingFrost")
				FR41 ABCDEFGHIJKLMNOPQRSSSTUVWX 1
				Goto Ready3
			FlashSlideKickingFrost:
				FR71 ABCDEFGHIJKLMNOPQRRRRRSTUV 1
				Goto Ready3
			FlashSlideKickingStop:
				TNT1 A 0 A_JumpIfInventory ("GrabbedBarrel", 1, "FlashBarrelSlideKickingStop")
				TNT1 A 0 A_JumpIfInventory ("GrabbedFlameBarrel", 1, "FlashBarrelSlideKickingStop")
				TNT1 A 0 A_JumpIfInventory ("GrabbedIceBarrel", 1, "FlashBarrelSlideKickingStop")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling", 100, "FlashSlideKickingStopFrost")
				FR41 TTTUVWX 1 
				Goto Ready3
			FlashSlideKickingStopFrost:
				FR71 RRRSTUV 1 
				Goto Ready3
				
/////COMPONENTS AND EFFECTS/////////////////////////////////////////////////////////////////////////////////////
			BigTubeGlow:
				TNT1 A 0
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",360, "BigTubeGlow7")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",300, "BigTubeGlow6")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",240, "BigTubeGlow5")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",180, "BigTubeGlow4")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",120, "BigTubeGlow3")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",60, "BigTubeGlow2")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",40, "BigTubeGlow1")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",35, "BigTubeGlowFade1")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",30, "BigTubeGlowFade2")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",25, "BigTubeGlowFade3")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",20, "BigTubeGlowFade4")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",15, "BigTubeGlowFade5")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",10, "BigTubeGlowFade6")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",5, "BigTubeGlowFade7")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",3, "BigTubeGlowFade8")
				FR50 A 1
				Stop
			BigTubeGlow1:
				FR50 B 1 BRIGHT //BRIGHT
				Stop
			BigTubeGlow2:
				FR50 B 1 BRIGHT //BRIGHT
				Stop
			BigTubeGlow3:
				FR50 C 1 BRIGHT
				Stop
			BigTubeGlow4:
				FR50 D 1 BRIGHT
				Stop
			BigTubeGlow5:
				FR50 E 1 BRIGHT
				Stop
			BigTubeGlow6:
				FR50 F 1 BRIGHT
				Stop
			BigTubeGlow7:
				FR50 G 1 BRIGHT
				Stop
			BigTubeGlowFade1:
				FR50 O 1 BRIGHT //BRIGHT
				Stop
			BigTubeGlowFade2:
				FR50 N 1 BRIGHT //BRIGHT
				Stop
			BigTubeGlowFade3:
				FR50 M 1 BRIGHT //BRIGHT
				Stop
			BigTubeGlowFade4:
				FR50 L 1 BRIGHT //BRIGHT
				Stop
			BigTubeGlowFade5:
				FR50 K 1 BRIGHT //BRIGHT
				Stop
			BigTubeGlowFade6:
				FR50 J 1 BRIGHT //BRIGHT
				Stop
			BigTubeGlowFade7:
				FR50 I 1 BRIGHT //BRIGHT
				Stop
			BigTubeGlowFade8:
				FR50 H 1 BRIGHT //BRIGHT
				Stop
			SmallTubeGlow:
				TNT1 A 0
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",360, "SmallTubeGlow7")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",300, "SmallTubeGlow6")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",240, "SmallTubeGlow5")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",180, "SmallTubeGlow4")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",120, "SmallTubeGlow3")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",60, "SmallTubeGlow2")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",40, "SmallTubeGlow1")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",35, "SmallTubeGlowFade1")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",30, "SmallTubeGlowFade2")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",25, "SmallTubeGlowFade3")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",20, "SmallTubeGlowFade4")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",15, "SmallTubeGlowFade5")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",10, "SmallTubeGlowFade6")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",5, "SmallTubeGlowFade7")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",3, "SmallTubeGlowFade8")
				FR51 A 1
				Stop
			SmallTubeGlow1:
				FR51 B 1 BRIGHT //BRIGHT
				Stop
			SmallTubeGlow2:
				FR51 B 1 BRIGHT //BRIGHT
				Stop
			SmallTubeGlow3:
				FR51 C 1 BRIGHT
				Stop
			SmallTubeGlow4:
				FR51 D 1 BRIGHT
				Stop
			SmallTubeGlow5:
				FR51 E 1 BRIGHT
				Stop
			SmallTubeGlow6:
				FR51 F 1 BRIGHT
				Stop
			SmallTubeGlow7:
				FR51 G 1 BRIGHT
				Stop
			SmallTubeGlowFade1:
				FR51 O 1 BRIGHT //BRIGHT
				Stop
			SmallTubeGlowFade2:
				FR51 N 1 BRIGHT //BRIGHT
				Stop
			SmallTubeGlowFade3:
				FR51 M 1 BRIGHT //BRIGHT
				Stop
			SmallTubeGlowFade4:
				FR51 L 1 BRIGHT //BRIGHT
				Stop
			SmallTubeGlowFade5:
				FR51 K 1 BRIGHT //BRIGHT
				Stop
			SmallTubeGlowFade6:
				FR51 J 1 BRIGHT //BRIGHT
				Stop
			SmallTubeGlowFade7:
				FR51 I 1 BRIGHT //BRIGHT
				Stop
			SmallTubeGlowFade8:
				FR51 H 1 BRIGHT //BRIGHT
				Stop
			MuzzleGlow:
				TNT1 A 0
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",360, "MuzzleGlow7")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",300, "MuzzleGlow6")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",240, "MuzzleGlow5")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",180, "MuzzleGlow4")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",120, "MuzzleGlow3")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",60, "MuzzleGlow2")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",40, "MuzzleGlow1")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",35, "MuzzleGlowFade1")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",30, "MuzzleGlowFade2")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",25, "MuzzleGlowFade3")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",20, "MuzzleGlowFade4")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",15, "MuzzleGlowFade5")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",10, "MuzzleGlowFade6")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",5, "MuzzleGlowFade7")
				TNT1 A 0 A_JumpIfInventory("CryoRifleOverCooling",3, "MuzzleGlowFade8")
				FR52 A 1
				Stop
			MuzzleGlow1:
				FR52 B 1 BRIGHT //BRIGHT
				Stop
			MuzzleGlow2:
				FR52 B 1 BRIGHT //BRIGHT
				Stop
			MuzzleGlow3:
				FR52 C 1 BRIGHT
				Stop
			MuzzleGlow4:
				FR52 D 1 BRIGHT
				Stop
			MuzzleGlow5:
				FR52 E 1 BRIGHT
				Stop
			MuzzleGlow6:
				FR52 F 1 BRIGHT
				Stop
			MuzzleGlow7:
				FR52 G 1 BRIGHT
				Stop
			MuzzleGlowFade1:
				FR52 O 1 BRIGHT //BRIGHT
				Stop
			MuzzleGlowFade2:
				FR52 N 1 BRIGHT //BRIGHT
				Stop
			MuzzleGlowFade3:
				FR52 M 1 BRIGHT //BRIGHT
				Stop
			MuzzleGlowFade4:
				FR52 L 1 BRIGHT //BRIGHT
				Stop
			MuzzleGlowFade5:
				FR52 K 1 BRIGHT //BRIGHT
				Stop
			MuzzleGlowFade6:
				FR52 J 1 BRIGHT //BRIGHT
				Stop
			MuzzleGlowFade7:
				FR52 I 1 BRIGHT //BRIGHT
				Stop
			MuzzleGlowFade8:
				FR52 H 1 BRIGHT //BRIGHT
				Stop	}}

/////PROJECTILES////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
ACTOR IceMissile : FastProjectile {
	Radius 4
	Height 8
	Speed 52
	Damage 0
	Projectile
	DamageType "Freeze"
	RenderStyle Add
	Alpha 0.95
	+NODAMAGETHRUST
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+BLOODLESSIMPACT
	Decal "FreezerBurn"
	
	DeathSound "weapons/CryoRifle/freezeobject"
		States {
			Spawn:
				FRPJ ABC 1 Bright A_SpawnItemEx("CryoRifleTrailSparksSmall", random(5,-5), random(5,-5), random(5,-5), 0, 0, 0, 0, 128, 0) 
				TNT1 A 0 A_CustomMissile("BlueFlareSpawn",0,0,0,0)
				TNT1 A 0 A_CustomMissile ("Icetracer", 0, 0, random (0, 360), 2, random (0, 360))
				Loop
			Death:
				TNT1 A 0
				TNT1 A 0 Bright A_StopSound(CHAN_BODY)
				TNT1 A 0 Bright A_ChangeFlag("ICEDAMAGE", 1)
				TNT1 A 0 Bright A_ChangeFlag("NODAMAGETHRUST", 0)
				TNT1 A 0 Bright A_Explode(100,80, 0)  
				TNT1 A 0 Bright A_Explode(50, 50, 0)
				TNT1 A 0 A_CheckFloor("DeathFloor")
				TNT1 A 0 A_CheckCeiling("DeathCeiling")
				Goto DeathEffects
			DeathFloor:
				TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx ("DetectFloorIce",random(-30,30), random(-30,30),1,0,0,0,0,SXF_NOCHECKPOSITION,0)
				Goto DeathEffects
			DeathCeiling:
				TNT1 AAAA 0 A_SpawnItemEx ("DetectCeilIce",random(-15,15), random(-15,15),1,0,0,0,0,SXF_NOCHECKPOSITION,0)
				Goto DeathEffects
			DeathEffects:
				TNT1 AAAAAAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke", 0, 0, 0, random(10, 30)*0.1, 0, random(0, 10)*0.1, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
				TNT1 AAAAA 0 Bright A_SpawnItemEx("CryoSmoke3", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
				TNT1 AAAAAAA 0 Bright A_SpawnItemEx("CryoRifleTrailSparksSmall", random(5,-5), random(5,-5), random(5,-5), random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
				TNT1 AAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke2", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
				TNT1 A 0 Bright A_SpawnItemEx("IceExplosionImpact", 0, 0, 0, 0, 0, 0, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
				TNT1 A 0 A_Playsound("weapons/CryoRifle/missiledeath")
				BXPL ABCDEFGH 1 Bright
				BXPL IJKLLM 1 Bright A_FadeOut(0.1)
				Stop	}}
actor CryoBeamDamage {
	+NOBLOCKMAP
	+MISSILE
	Radius 5
	Height 5
	Damagetype "Freeze"
		States {
			Spawn:
			TNT1 A 1
			TNT1 A 0 Bright A_ChangeFlag("ICEDAMAGE", 1)
			TNT1 A 0 Bright A_ChangeFlag("NODAMAGETHRUST", 0)
			TNT1 A 0 A_Explode(10,10)
			TNT1 A 0 A_Explode(10,30)
				Stop	}}
Actor IceSpear {
	+BloodSplatter
	+Ripper
	-NOGRAVITY
//	+RollSprite //This looks terrible from the other angles. The front and back look okay, but not the sides. -JM
	Projectile
	Radius 4
	Height 8
	Speed 110
	Damage 75	
	Damagetype "Blast"
	Scale 1.25
	RipperLevel 1
	+MISSILE	
	SeeSound ""
	DeathSound "weapons/CryoRifle/speardeath"
	Gravity 0.1
	Decal "FreezerBurnSmall"
		States {
			Spawn:
				ISPR A 1 BRIGHT {
					A_CustomMissile ("Icetracer", 0, 0, random (0, 360), 2, random (0, 360));
//					A_SetRoll(roll-15);
				}
				Loop
			Crash:
			Death:
				TNT1 A 0 { 
					A_ChangeFlag("NOGRAVITY",1);
					A_SpawnItem("HitPuff");
					A_Stop;
					A_SetRoll(0);
					}
				TNT1 AAAAAAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke", 0, 0, 0, random(10, 30)*0.1, 0, random(0, 10)*0.1, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
				TNT1 AAAAA 0 Bright A_SpawnItemEx("CryoSmoke3", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
				TNT1 AAAAAAA 0 Bright A_SpawnItemEx("CryoRifleTrailSparksSmall", random(5,-5), random(5,-5), random(5,-5), random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
				TNT1 AAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke2", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
				ISPR A 70 Bright
				ISPR AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 Bright A_FadeOut(0.02)
				Stop
			Disappear:
				TNT1 A 0 A_SpawnItem("HitPuff")
				Stop
			XDeath:
				NLPJ B 0 A_PlaySound("Weapons/NailHitBleed")
				Stop	}}
ACTOR IceFlak1 {
	Radius 3
	Height 4
	Speed 45
	Damage 7
	Mass 200
	Scale 0.75
	Damagetype "Cutless"
	+MISSILE
	+DOOMBOUNCE
	+BLOODSPLATTER
	+ROLLSPRITE
	BounceFactor 0.65
	BounceSound "IceShardBounce"
	BounceSound "IceShardBounce"
	MissileType "FlakTrail"
	Decal "FreezerBurnSmall"
		States {
			Spawn:
				CSC2 C 1 Bright {
					A_CustomMissile ("SmallIcetracer", 0, 0, random (0, 360), 2, random (0, 360));
					A_SetRoll(Roll+45);
				}
				loop
			Death:
				EXPL A 0 A_CustomMissile ("IceDust", 4, 0, random (0, 360), 2, random (0, 360))
				
				Stop	}}
ACTOR IceFlak2 : IceFlak1 {
	Damagetype "Saw"
		States {
			Spawn:
				CSD8 B 1 Bright {
					A_CustomMissile ("SmallIcetracer", 0, 0, random (0, 360), 2, random (0, 360));
					A_SetRoll(Roll+45);
				}
				loop	}}
ACTOR IceFlak3 : IceFlak1 {
	Damagetype "Blast"
		States {
			Spawn:
				CSD9 C 1 Bright {
					A_CustomMissile ("SmallIcetracer", 0, 0, random (0, 360), 2, random (0, 360));
					A_SetRoll(Roll+45);
				}
				loop	}}
ACTOR IceFlak4 : IceFlak1 {
	Damagetype "Shotgun"
		States {
			Spawn:
				ICC5 D 1 Bright {
					A_CustomMissile ("SmallIcetracer", 0, 0, random (0, 360), 2, random (0, 360));
					A_SetRoll(Roll+45);
				}
				loop	}}

/////EFFECTS///////////////////////////////////////////////////////////////////////////////////////////////////////////////
ACTOR CryoSmoke {
	Radius 2
	Height 2
	+NOINTERACTION
	+FORCEXYBILLBOARD
	RenderStyle Shaded
	StencilColor "A0 FF FF"
	Alpha 0.8
	Scale 0.85
		States {
			Spawn:
				SMKO A 0
				SMK2 A 2 A_FadeOut (0.05)
				Wait	}}
Actor CryoSmoke2 : CryoSmoke { StencilColor "DD DD DD" Scale 0.7 Alpha 0.5 VSpeed 2.5
		States {
			Spawn:
				TNT1 A 0
				SMK2 A 6 A_FadeOut(0.04)
				Wait	}}
Actor CryoSmoke3 : CryoSmoke { StencilColor "AA FF FF" Scale 0.7 Alpha 0.4 VSpeed 2.25
		States {
			Spawn:
				TNT1 A 0
				TNT1 A 0 A_PlaySound("Weapons/CryoRifleRecharge")
				SMK2 A 6 A_FadeOut(0.04)
				Wait	}}
Actor MiniCryoSmoke1 : CryoSmoke { StencilColor "AA FF FF" Scale 0.7 Alpha 0.4 VSpeed 2.25
	Scale 0.25
		States {
			Spawn:
				TNT1 A 0
				TNT1 A 0 A_PlaySound("Weapons/CryoRifleRecharge")
				SMK2 A 6 A_FadeOut(0.04)
				Wait	}}
Actor IceTracer {
	Height 0
	Radius 0
	Mass 0
	+RollSprite
	+Missile
	+NoBlockMap
	+LowGravity
	+DontSplash
	+DoomBounce
	+FORCEXYBILLBOARD
	BounceFactor 0.2
	Gravity 0.1
	RenderStyle Add
	Alpha 0.9
	StencilColor "A0 FF FF"
	Scale 0.4
	Speed 0
	-DOOMBOUNCE
		States {
			Spawn:
			Death:
				FRPJ HHHEEEFFFGGG 1 Bright {
					A_FadeOut(0.04);
					A_SetRoll(roll-frandom(-15,15));
				}
				Stop	}
	}
				
Actor SmallIceTracer : IceTracer {
		States {
			Spawn:
			Death:
				SHEX AABBCCDDEE 1 Bright {
					A_FadeOut(0.04);
					A_SetRoll(roll-frandom(-15,15));
				}
				Stop	}
	}

ACTOR CryoTrail {
	+NOINTERACTION
	Radius 4
	Height 8
	Renderstyle Add
	Alpha 0.5
	YScale 0.3
	XScale 0.6
		States {
		Spawn:
			X027 ABCDEFGHIJK 1 Bright
			Stop	}}