class PB_SuperBFGBall : Actor replaces BFGBall
{
	Default
    {
		Projectile;
		Speed 24;
		RenderStyle "Normal";
		//Alpha 0.99;
		Radius 4;
		Height 8;
		Damage 500;
		Decal "BFGLightning";
		DeathSound "BFGEXPLO";
		DamageType "Disintegrate";
		Scale 0.55;
		Species "Marines";
		+THRUSPECIES;
		+MTHRUSPECIES;
		+DONTHARMSPECIES;
		+FORCEXYBILLBOARD;
	}
	States
	{
		Spawn:
			TNT1 A 0;
			TNT1 A 0 A_StartSound("Bfgfly1", CHAN_BODY, CHANF_OVERLAP|CHANF_LOOPING );
		Flying:
			098G ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT Light("BFGBALL") 
			{		
				//A_SpawnItemEx("BFGFOG",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
				A_SpawnItemEx("BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(7, 22), Random(-8, 8), Random(-3, 3), (0.1)*Random(-10, 10), Random(-20, 20), 128);
				A_SpawnItemEx("BFGLightningTrial", 15, random(-1,1), random(-6,-4), 20);
				//A_SpawnItemEx("NewBFGTrailGreen", 15, random(8,-8), random(8,-8), 0, 0, 0, 0, 128, 0);
			}
			099G ABCD 1 BRIGHT Light("BFGBALL") 
			{
				//A_SpawnItemEx("BFGFOG",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
				//A_SpawnItemEx("BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128);
				A_SpawnItemEx("BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(7, 22), Random(-8, 8), Random(-3, 3), (0.1)*Random(-10, 10), Random(-20, 20), 128);
				A_SpawnItemEx("BFGLightningTrial", 15, random(-1,1), random(-6,-4), 20);
				//A_SpawnItemEx("NewBFGTrailGreen", 15, random(8,-8), random(8,-8), 0, 0, 0, 0, 128, 0);
			}
			Loop;
		Death:
			TNT1 A 0;
			TNT1 A 0 A_StopSound(6);
			TNT1 A 0 Bright A_SpawnItem("GreenShockWave",0,0,0);
			EXPL A 0 Radius_Quake (5, 16, 0, 20, 0);//(intensity, duration, damrad, tremrad, tid)
			EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile ("BFGBIGFOG", 0, 0, random (0, 360), 2, random (0, 360));
			EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile ("BFGDeathParticleFast", 0, 0, random (0, 360), 2, random (0, 360));
			EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile ("BFGDeathParticleSuperFast", 0, 0, random (0, 360), 2, random (0, 360));
			EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile ("BFGDeathParticleFast", 0, 0, random (0, 360), 2, random (0, 360));
			EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile ("BFGDeathParticleFast", 0, 0, random (0, 360), 2, random (0, 360));
			TNT1 A 0 A_SpawnItem("MegaGibRemoving");
			TNT1 A 0 A_StartSound("BFGAR", 3);
			TNT1 A 0 A_Explode(400,500,0,0,true);
			BFGB ABCDE 1 Bright Light("BFGBALL") A_SpawnItem("GreenFlare",0,0) ;
			BFE1 F 1 Bright Light("BFGBALL") A_BFGSpray("SuperBFGExtra", 40, 35);
			TNT1 A 0 A_SpawnItem("SuperBFGExtraGiant");
			BFE1 GGHHIIJJKK 1 Bright Light("BFGBALL") A_SpawnItem("GreenFlare",0,0);
			Stop;
	}

	override void Tick()
	{
		Super.Tick();
		if (isFrozen() || InStateSequence(curstate, ResolveState("Death")))
			return;

		if (level.time % 3 == 0)
		{
			A_AttackRailTargets(1024);
		}
	}

	void A_AttackRailTargets(double dist = 1024)
	{
		let itr = BlockThingsIterator.Create(self, dist);
		while (itr.Next())
		{        
			let victim = itr.thing;
			if (!victim)
				return;
			
			// check it's a valid victim (a shootable, hostile, alive monster):
			if (victim == target || victim == self || !victim.bISMONSTER || !victim.bSHOOTABLE || !victim.isHostile(target) || victim.health <= 0)
				continue;
			
			// check distance:
			double victimDist = Distance3D(victim);
			if (victimDist > dist)
				continue;
			
			// face the victim:
			A_Face(victim, flags: FAF_MIDDLE);

			// check the victim can be reached:
			FLineTraceData ltrace;
			LineTrace(angle, victimDist, pitch, offsetz: height * 0.5, data: ltrace);
			// abort the attack if LineTrace hit a different actor that is also shootable
			// but is the same Species "Marines" or the hit actor is friends with the Shooter.
			if (ltrace.HitType == TRACE_HitActor)
			{
				let act = ltrace.hitActor;
				if (act && act != victim && act.bSHOOTABLE && (act.Species == self.Species || act.IsFriend(target)))
					continue;
			}
			
			// proceed with attack:
			A_CustomRailgun(15, 0, "", "Green", 
				flags: RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING, 
				aim: 0, 
				pufftype: "None", 
				range: victimdist, 
				duration: 1, 
				sparsity: 60, 
				spawnclass: "BFGLightningTrial_Small"
			);
		}
	}
}
