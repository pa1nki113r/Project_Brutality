Actor SemiFire : Inventory
{Inventory.MaxAmount 1}

Actor BurstFire : Inventory
{Inventory.MaxAmount 1}

Actor CarbineBurstCounter : Inventory
{Inventory.MaxAmount 2}

Actor CarbineFullAuto : Inventory
{Inventory.MaxAmount 1}

Actor CarbineHasUnloaded : Inventory
{Inventory.MaxAmount 1}

Actor RespectCarbine : Inventory
{Inventory.MaxAmount 1}

Actor CarbineMuzzleFlash1 : Inventory
{Inventory.MaxAmount 1}

Actor CarbineMuzzleFlash2 : Inventory
{Inventory.MaxAmount 1}

Actor CarbineMuzzleFlash3 : Inventory
{Inventory.MaxAmount 1}

Actor CarbineBarrelHeat: Inventory
{Inventory.maxamount 200}

Actor SelectCarbine_DualWield : Inventory
{Inventory.MaxAmount 1}
Actor SelectCarbine_FullAutoFire : Inventory
{Inventory.MaxAmount 1}
Actor SelectCarbine_SemiFire : Inventory
{Inventory.MaxAmount 1}
Actor SelectCarbine_BurstFire : Inventory
{Inventory.MaxAmount 1}


Actor DualWieldingCarbines : Inventory
{Inventory.MaxAmount 1}


ACTOR PB_Carbine : PB_Weapon
{
	//$Title Carbine
	//$Category Weapons
	//$Sprite CB00Z0
	SpawnID 9450
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 20
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "NewClip"
	Weapon.AmmoType2 "XRifleAmmo"
	+FLOORCLIP
	+DONTGIB
	Obituary "%o was shot down by %k's Combat Rifle."
    AttackSound "None"
    Inventory.PickupSound "CLIPIN"
	Inventory.Pickupmessage "You got the UAC41 Carbine! (Slot 4)"
	Inventory.MaxAmount 3
	Inventory.Amount 1
	Tag "UAC-41 Carbine"
	+WEAPON.WIMPY_WEAPON
	//+WEAPON.NO_AUTO_SWITCH
    +WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
    +WEAPON.NOAUTOFIRE
    Scale 0.51
	PB_WeaponBase.UnloaderToken "CarbineHasUnloaded"
	Inventory.AltHUDIcon "CB00Z0"
	PB_WeaponBase.respectItem "RespectCarbine"
	FloatBobStrength 0.5
	States
	{
		Steady:
			TNT1 A 1
			Goto Ready
		
		Ready:
			TNT1 A 0 A_SelectWeapon("PB_LMG")
			TNT1 A 0 PB_RespectIfNeeded
		WeaponRespect:
			TNT1 A 0 {
				A_SetCrosshair(5);
				A_GiveInventory("RespectCarbine");
				A_PlaySoundEx("weapons/carbine/up", "Auto");
			}
			4A11 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			4A12 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			4A13 ABCDEFGHIJ 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("weapons/carbine/magout", "Auto")
			4A13 KLMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			4A14 ABCDEFGHIJKL 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("IronSights", "Auto")
			4A14 MNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("LIGHTON", "Auto")
			4A15 ABCDEFGHIJ 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("LIGHTON", "Auto")
			4A15 KLMNOPQRSTU 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("LIGHTON", "Auto")
			4A15 VWXYZ 1 A_DoPBWeaponAction
			4A16 ABCDE 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("IronSights", "Auto")
			4A16 FGHIJKLMNOPQ 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("weapons/carbine/fancybutton", "Auto")
			4A16 RSTUVWXYZ 1 A_DoPBWeaponAction
			4A17 ABCDEFGHIJKLMNOPQRSTU 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("weapons/carbine/magin", "Auto")
			4A17 VWXYZ 1 A_DoPBWeaponAction
			4A18 AB 1 A_DoPBWeaponAction
			TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
			4A18 CDEFGHIJKLMNOP 1 A_DoPBWeaponAction
			TNT1 A 0 {
				A_PlaySoundEx("weapons/carbine/down", "Auto");
				A_Setroll(0);
				}
			4A18 QRSTUVWX 1 A_DoPBWeaponAction
			Goto Ready3
		
		SelectAnimation:
			TNT1 A 0 A_JumpIfInventory("GoFatality",1,"Steady")
			TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "SelectAnimationDualWield")
			TNT1 A 0 A_PlaySoundEx("weapons/carbine/up", "Auto")
			4A1S ABCDE 1 A_JumpIfInventory("GoFatality", 1, "Steady")
		Ready3:
			TNT1 A 0 {
				A_TakeInventory("PB_LockScreenTilt",1);
				PB_HandleCrosshair(59);
				}
			TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "ReadyDualWield")
		ReadyToFire:
		TNT1 A 0 A_JumpIfInventory("RifleWasEmpty",1,"LoadChamber")
		4AFG A 1 
		{
			A_SetInventory("CarbineBurstCounter",0);
			if (CountInv("CarbineBarrelHeat") >= 60) {A_Gunflash("BarrelSmoke1");}
				
			if(GetCvar("pb_toggle_aim_hold")) 
			{
					if ((PressingFire() && PressingAltfire() && CountInv("XRifleAmmo") > 0))
					{
						if(CountInv("CarbineFullAuto") >= 1) 
							{Return State("Fire2");}
					}
					if (PressingFire() && CountInv("XRifleAmmo") > 0)
					{
						if(CountInv("CarbineFullAuto") >= 1) 
							{Return State("Fire");}
						if(CountInv("BurstFire") >= 1) 
							{
								if(CountInv("CarbineBurstCounter") >= 2) 
								{A_SetInventory("CarbineBurstCounter",0);Return State("Ready3");}
							}
					}
			}
			else 
			{
				if (PressingFire() && CountInv("XRifleAmmo") > 0 )
					{
						if(CountInv("CarbineFullAuto") >= 1) 
							{Return State("Fire");}
						
						if(CountInv("BurstFire") >= 1) 
							{
								if(CountInv("CarbineBurstCounter") >= 2) 
								{A_SetInventory("CarbineBurstCounter",0); Return State("Ready3");}
							}
					}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
		}
			Loop
		
	SelectAnimationDualWield:
		//Stuff
		TNT1 A 0 A_PlaySoundEx("weapons/carbine/up", "Auto")
		CB01 EDCBA 1
		TNT1 A 0 A_PlaySoundEx("weapons/carbine/up", "Auto")
	ReadyDualWield:	
		TNT1 A 0 {
			A_SetRoll(0);
			PB_HandleCrosshair(59);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_SetFiringRightWeapon(False);
			A_SetFiringLeftWeapon(False);
			A_TakeInventory("DualFiring",1);
			if(CountInv("LeftXRifleAmmo") < CountInv("XRifleAmmo")){
				A_GiveInventory("DualFiring",1);
			}
			if(CountInv("NewClip")>0){
				if(CountInv("LeftXRifleAmmo")<=0){
					A_GiveInventory("DualFireReload",1);
				}
				if(CountInv("XRifleAmmo")<=0){
					A_GiveInventory("DualFireReload",1);
				}
			}
			A_Overlay(10, "IdleLeft_Overlay", false);
			A_Overlay(11, "IdleRight_Overlay", false);
			}
	ReadyToFireDualWield:
		TNT1 A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Loop
		
	IdleLeft_Overlay:
		CB01 F 1 {
			// Full-Auto
			if(CountInv("LeftXRifleAmmo")<=0 && CountInv("XRifleAmmo")>0){
				A_GiveInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==0 || (CountInv("DualFiring")==0 && CountInv("XRifleAmmo")<=0) || GetCvar("SingleDualFire")==1){
				if(PressingFire() || JustPressed(BT_ATTACK)){
					if(CountInv("LeftXRifleAmmo") > 0){
						return state("FireLeft_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			return state("");
		}
		Loop
	IdleRight_Overlay:
		CB01 G 1 {
			// Full-Auto
			if(CountInv("LeftXRifleAmmo")>0 && CountInv("XRifleAmmo")<=0){
				A_TakeInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==1 || (CountInv("DualFiring")==1 && CountInv("LeftXRifleAmmo")<=0) || GetCvar("SingleDualFire")==1){
				if((PressingFire() || JustPressed(BT_ATTACK)) && GetCvar("SingleDualFire")==0){
					if(CountInv("XRifleAmmo") > 0){
						return state("FireRight_Overlay");
					}
					else {
						A_PlaySoundEx("weapons/empty", "Auto");
						return state("");
					}
				}
			}
			if((PressingAltFire() || JustPressed(BT_ALTATTACK)) && GetCvar("SingleDualFire")==1){
				if(CountInv("XRifleAmmo") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_PlaySoundEx("weapons/empty", "Auto");
					return state("");
				}
			}
			return state("");
		}
		Loop
	
	FireLeft_Overlay:
		CB02 A 1 BRIGHT {
			A_AlertMonsters;
			A_PlaySoundEx("Weapons/Rifle/SFire", "SoundSlot7");
			A_FireBullets (0.1, 0.1, -1, 20, "HitPuff", FBF_NORANDOM,8192,"Tracer", -4,-8);
			A_FireCustomMissile("YellowFlareSpawn",0,0,-4,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, -4, 0, 0, 0);
			A_FireCustomMissile("RifleCaseSpawn",5,0,-14,-6);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_Takeinventory("LeftXRifleAmmo",1);
			A_ZoomFactor(0.985);
			A_GunFlash;
            PB_WeaponRecoil(-1.6,+1.2);
			}
		CB02 B 1 {
			A_ZoomFactor(0.99);
            PB_WeaponRecoil(-1.6,+1.2);
			}
		TNT1 A 0 {
			if(CountInv("LeftXRifleAmmo")<=0 || CountInv("XRifleAmmo")>0 ){
				A_GiveInventory("DualFiring",1);
			}
		}
		CB02 C 1
		TNT1 A 0 {
			if(CountInv("LeftXRifleAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleLeft_Overlay
		
	FireRight_Overlay:
		CB03 A 1 BRIGHT {
			A_AlertMonsters;
			A_PlaySoundEx("Weapons/Rifle/SFire", "SoundSlot6");
			A_FireBullets (0.1, 0.1, -1, 20, "HitPuff", FBF_NORANDOM,8192,"Tracer", -4, 8);
			A_FireCustomMissile("YellowFlareSpawn",0,0,4,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 4, 0, 0, 0);
			A_FireCustomMissile("RifleCaseSpawn",5,0,16,-5);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_Takeinventory("XRifleAmmo",1);
			A_ZoomFactor(0.985);
			A_GunFlash;
            PB_WeaponRecoil(-1.6,-1.2);
			}
		CB03 B 1 {
			A_ZoomFactor(0.99);
            PB_WeaponRecoil(-1.6,-1.2);
			}
		TNT1 A 0 {
			if(CountInv("LeftXRifleAmmo")>0 || CountInv("XRifleAmmo")<=0 ){
				A_TakeInventory("DualFiring",1);
			}
		}
		CB03 C 1
		TNT1 A 0 {
			if(CountInv("XRifleAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleRight_Overlay
	
	////////////Smoking Gun Effects////////////
	BarrelSmoke1:
		TNT1 A 1 {
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 0, 0, 0);
			A_Takeinventory("CarbineBarrelHeat",1);
		}
		Stop
	BarrelSmoke2:
		TNT1 A 1 {
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 25, 0, 0);
			A_Takeinventory("CarbineBarrelHeat",1);
		}
		Stop
	////////////Smoking Gun Effects////////////
		
    Ready2:
		TNT1 A 0 {
			A_TakeInventory("PB_LockScreenTilt",1);
			A_SetCrosshair(5);
			}
	ReadyToFire2:
		4A2G A 1 {
			//Updated code for far superior smooth gameplay
			A_SetInventory("CarbineBurstCounter",0);
			if (CountInv("CarbineBarrelHeat") >= 60) {
				A_Gunflash("BarrelSmoke2");
			}
			
			if(GetCvar("pb_toggle_aim_hold")) {
				if(!PressingAltfire() || JustReleased(BT_ALTATTACK)){
					return state("Zoomout");
				}
				if (PressingFire() && PressingAltfire() && CountInv("XRifleAmmo") > 0){
						if(CountInv("CarbineFullAuto") >= 1) 
							{Return State("Fire2");}
						
						if(CountInv("BurstFire") >= 1) 
							{
								if(CountInv("CarbineBurstCounter") >= 2) 
								{A_SetInventory("CarbineBurstCounter",0); Return State("ReadyToFire2");}
								//else{Return State("Fire2");}
							}
				
				}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOSECONDARY);
			}
			else {
				if (PressingFire() && CountInv("XRifleAmmo") > 0 ){
						if(CountInv("CarbineFullAuto") >= 1) 
							{Return State("Fire2");}
						
						if(CountInv("BurstFire") >= 1) 
							{
								if(CountInv("CarbineBurstCounter") >= 2) 
								{A_SetInventory("CarbineBurstCounter",0); Return State("ReadyToFire2");}
							}

				}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
		}
		Loop
		
	Deselect:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(59);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
		}
        TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_Takeinventory("Unloading",1);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
		}
		TNT1 A 0 A_JumpIfInventory("GotMeatShield", 1, "GrabEnemy")
		TNT1 A 0 A_JumpIfInventory("PB_LMG",1,"DeselectUpgrade")
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "DeselectAkimbo")
        4A1S EDCBA 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectAkimbo:
		CB01 ABCDE 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectUpgrade:
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	WheelCancel:
		TNT1 A 0 {
			A_SetInventory("SelectCarbine_DualWield",0);
			A_SetInventory("SelectCarbine_SemiFire",0);
			A_SetInventory("SelectCarbine_BurstFire",0);
			A_SetInventory("SelectCarbine_FullAutoFire",0);
			A_SetInventory("CantWeaponSpecial",0);
			}
		Stop
			
	WeaponSpecial:
		TNT1 A 0 {
			A_Giveinventory("PB_LockScreenTilt",1);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("GoWeaponSpecialAbility",1);
			PB_HandleCrosshair(59);
			A_ZoomFactor(1.0);
			A_ClearOverlays(10,11);
		}
	
	TNT1 A 0{
		if(CountInv("SelectCarbine_DualWield") >=1)
			{A_Overlay(-19,"WheelCancel"); Return State("SwitchToDualWield");}
		if(CountInv("SelectCarbine_SemiFire") && CountInv("SemiFire") >=1)
			{A_Overlay(-19,"WheelCancel"); A_Log("Fire control already selected: Semi-Auto"); Return State("Ready3");}
		if(CountInv("SelectCarbine_BurstFire") && CountInv("BurstFire") >=1)
			{A_Overlay(-19,"WheelCancel"); A_Log("Fire control already selected: Burst-Fire"); Return State("Ready3");}
		if(CountInv("SelectCarbine_FullAutoFire") && CountInv("CarbineFullAuto") >=1)
			{A_Overlay(-19,"WheelCancel"); A_Log("Fire control already selected: Full-Auto"); Return State("Ready3");}
		
		if(CountInv("SelectCarbine_FullAutoFire") || CountInv("SelectCarbine_BurstFire") || CountInv("SelectCarbine_SemiFire") >=1)
			{Return State("ToggleFireMode");}
			
		A_Overlay(-19,"WheelCancel");
		Return State("");
		}

	SwitchToDualWield:
		TNT1 A 0 {
			if(CountInv("PB_Carbine") < 2) {
			A_Print("You need two carbines to dual wield!"); Return State("Ready3");}
			Return State("");
			}
		TNT1 A 0 {
			if (A_CheckAkimbo()) {
				A_Takeinventory("DualWieldingCarbines",1);
				A_SetAkimbo(False);
				return state("SwitchFromDualWield");
			}
			else {
				A_Giveinventory("DualWieldingCarbines",1);
				A_SetAkimbo(True);
				return state("");
			}
		}
		CB00 ABCDE 1 A_SetRoll(roll+.8, SPF_INTERPOLATE)
		CB00 E 1
		CB00 EFGHI 1 A_SetRoll(roll-.8, SPF_INTERPOLATE)
		Goto ReadyDualWield
	SwitchFromDualWield:
		CB00 IHGFE 1 A_SetRoll(roll-.8, SPF_INTERPOLATE)
		CB00 E 1
		CB00 EDCBA 1 A_SetRoll(roll+.8, SPF_INTERPOLATE)
		Goto Ready3
		
		
 	ToggleFireMode:
		TNT1 A 0 {
			if (A_CheckAkimbo())
				{
				A_Log("Fire control not available while dual-wielding");
				A_Overlay(-19,"WheelCancel");
				return state("");
				}
			if(CountInv("SelectCarbine_SemiFire") >= 1) {
				A_Setinventory("CarbineFullAuto",0);
 				A_Setinventory("BurstFire",0);
 				A_SetInventory("SemiFire",1);
				A_Overlay(-19,"WheelCancel");
				A_Log("Fire control mode selected: Semi-Auto");
 				return state("FiremodeAnimation");
				}
 			if(CountInv("SelectCarbine_BurstFire") >= 1) {
				A_Setinventory("CarbineFullAuto",0);
 				A_Setinventory("BurstFire",1);
 				A_SetInventory("SemiFire",0);
				A_Overlay(-19,"WheelCancel");
				A_Log("Fire control mode selected: Burst-Fire");
 				return state("FiremodeAnimation");
				}
			if(CountInv("SelectCarbine_FullAutoFire") >= 1) {
				A_Setinventory("CarbineFullAuto",1);
 				A_Setinventory("BurstFire",0);
 				A_SetInventory("SemiFire",0);
				A_Overlay(-19,"WheelCancel");
				A_Log("Fire control mode selected: Full-Auto");
 				return state("FiremodeAnimation");
				}
 			return state("");	
 		}
 		Goto Ready3
 	FiremodeAnimation:
 		4A1W ABCDEF 1 A_Setroll(roll+0.8, SPF_INTERPOLATE)
 		TNT1 A 0 A_PlaySound("LIGHTON")
 		4A1W FEDCBA 1 A_Setroll(roll-0.8, SPF_INTERPOLATE)
 		Goto Ready3
		
	Select:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(59);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		Goto SelectFirstPersonLegs
		SelectContinue:
		TNT1 A 0 A_SelectWeapon("PB_LMG")
		TNT1 A 0 PB_WeapTokenSwitch("CarbineSelected")
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait
		
    Fire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(59);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
		}
	// Load sprites into memory
		4AFF AB 0
		4BFF AB 0
		4CFF AB 0
		
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Fire2")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			PB_HandleCrosshair(59);
			A_SetRoll(0);
			}
    
        TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,1)
        Goto Reload
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Fire2")
		
		4AFF A 1 {
			if (CountInv("CarbineMuzzleFlash1") >= 1 ) { A_SetWeaponSprite("4AFF");}
			if (CountInv("CarbineMuzzleFlash2") >= 1 ) { A_SetWeaponSprite("4BFF");}
			if (CountInv("CarbineMuzzleFlash3") >= 1 ) { A_SetWeaponSprite("4CFF");}
			A_GunFlash;
			A_FireBullets (0.1, 0.1, -1, 20, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			PB_SpawnCasing("EmptyBrass",32,-2,30,Frandom(4,7),Frandom(6,9),Frandom(0,5));
			A_Takeinventory("XRifleAmmo",1);
			A_Giveinventory("CarbineBarrelHeat",10);
			A_PlaySoundEx("Weapons/Rifle/SFire", "Weapon");
			A_AlertMonsters;
			A_ZoomFactor(0.98);
			//A_WeaponReady(WRF_NOPRIMARY);
            PB_WeaponRecoil(-0.48,-0.18);
			}
		4AFF B 1 {
			if (CountInv("CarbineMuzzleFlash1") >= 1 ) { A_SetWeaponSprite("4AFF");}
			if (CountInv("CarbineMuzzleFlash2") >= 1 ) { A_SetWeaponSprite("4BFF");}
			if (CountInv("CarbineMuzzleFlash3") >= 1 ) { A_SetWeaponSprite("4CFF");}
			
			A_ZoomFactor(0.99);
			//A_WeaponReady(WRF_NOPRIMARY);
            PB_WeaponRecoil(-0.48,-0.18);
		}
		4AFF C 1 {
			A_ZoomFactor(1.0);
			//A_WeaponReady(WRF_NOPRIMARY);
		}
		TNT1 A 0 {
			if (CountInv("CarbineMuzzleFlash1") >= 1 ) {
				A_TakeInventory("CarbineMuzzleFlash1",1);
				A_GiveInventory("CarbineMuzzleFlash2",1);
			}
			else if (CountInv("CarbineMuzzleFlash2") >= 1 ) {
				A_TakeInventory("CarbineMuzzleFlash2",1);
				A_GiveInventory("CarbineMuzzleFlash3",1);
			}
			else if (CountInv("CarbineMuzzleFlash3") >= 1 ) {
				A_TakeInventory("CarbineMuzzleFlash3",1);
				A_GiveInventory("CarbineMuzzleFlash1",1);
			}
			else {
				A_GiveInventory("CarbineMuzzleFlash1",1);
			}
		}
		TNT1 A 0 {
			if(CountInv("SemiFire") >= 1) {
 				return state("ReadyToFire");
				}
 			if(CountInv("BurstFire") >= 1) {
				if(CountInv("CarbineBurstCounter") <=1)
					{A_GiveInventory("CarbineBurstCounter",1); A_Refire;}
 				Else
					{Return State("ReadyToFire");}
				}
			if(CountInv("CarbineFullAuto") >= 1) {A_Refire;}
			Return State ("");
		}
		Goto ReadyToFire
	
      Fire2:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetCrosshair(5);
			}
        TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,1)
        Goto Reload
		4A2F A 0 A_Jump(256,"Fire2_Flash1","Fire2_Flash2")
		Fire2_Flash1:
		4A2F A 1 BRIGHT {
			A_GunFlash;
			A_FireBullets (0.1, 0.1, -1, 20, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			PB_SpawnCasing("EmptyBrass",25,0,32,Frandom(1,3),Frandom(3,6),Frandom(2,4));
			A_Takeinventory("XRifleAmmo",1);
			A_Giveinventory("CarbineBarrelHeat",10);
			A_PlaySoundEx("Weapons/Rifle/SFire", "Weapon");
			A_AlertMonsters;
			A_ZoomFactor(1.485);
            PB_WeaponRecoil(-0.30,-0.12);
			}
		Fire2Continue:
        4A2F B 1 BRIGHT {
			//A_WeaponReady(WRF_NOPRIMARY);
			A_ZoomFactor(1.495);
            PB_WeaponRecoil(-0.30,-0.12);
			}
		4A2F C 1 A_ZoomFactor(1.5)
		TNT1 A 0
		{
			if(GetCvar("pb_toggle_aim_hold")) 
			{
				if(JustReleased(BT_ALTATTACK))
					{return state("Zoomout");}
		
				if ((JustPressed(BT_ATTACK) || PressingFire())  && PressingAltfire())
				{
					if(CountInv("SemiFire") >= 1) 
						{return state("Ready2");}
					if(CountInv("BurstFire") >= 1) 
						{
							if(CountInv("CarbineBurstCounter") <=1)
								{A_GiveInventory("CarbineBurstCounter",1); A_Refire("Fire2");}
							Else
								{Return State("ReadyToFire2");}
						}
					if(CountInv("CarbineFullAuto") >= 1) { A_Refire("Fire2");}
				}
			
			Return State ("");
			}
			
			else 
			{
				if(PressingAltfire())
					{return state("Zoomout");}
				

				if(CountInv("SemiFire") >= 1) 
						{return state("Ready2");}
				if(CountInv("BurstFire") >= 1) 
							{
								if(CountInv("CarbineBurstCounter") <=1)
									{A_GiveInventory("CarbineBurstCounter",1); A_Refire("Fire2");}
								Else
									{Return State("ReadyToFire2");}
							}
				if(CountInv("CarbineFullAuto") >= 1) {A_Refire("Fire2");}
					
			Return State ("");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE);
		}
		//TNT1 A 0 A_Refire
		Goto ReadyToFire2
		
		Fire2_Flash2:
		4A3F A 1 BRIGHT {
			A_GunFlash;
			A_FireBullets (0.1, 0.1, -1, 20, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			PB_SpawnCasing("EmptyBrass",25,0,32,Frandom(1,3),Frandom(3,6),Frandom(2,4));
			A_Takeinventory("XRifleAmmo",1);
			A_Giveinventory("CarbineBarrelHeat",10);
			A_PlaySoundEx("Weapons/Rifle/SFire", "Weapon");
			A_AlertMonsters;
			A_ZoomFactor(1.485);
            PB_WeaponRecoil(-0.30,-0.12);
			}
		Goto Fire2Continue
    AltFire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(59);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ZoomOut")
	ZoomIn:
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_ZoomFactor(1.5);
			A_Giveinventory("Zoomed",1);
			A_Giveinventory("GoSpecial",1);
			A_Giveinventory("ADSmode",1);
		}
        4AFZ ABCDE 1
		TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY)
        Goto Ready2
		
   ZoomOut:
		TNT1 A 0 {
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_ZoomFactor(1.0);
			PB_HandleCrosshair(59);
			}
        4AFZ EDCBA 1
		TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY)
        Goto Ready3
		
	NoAmmo:
	4AFG A 1 {
			A_ZoomFactor(1.0);
			A_PlaySoundEx("weapons/empty", "Auto");
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
		}
	Goto Ready3

	BarrelSmoke3:
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 0, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2, 0, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 3, 0, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2, -1, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 4, 0, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 6, 1, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 1, 4, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 1, 5, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 5, 0, 0)
			TNT1 AAAAAAAAAAAAAA 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 6, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 1, 6, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2, 6, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2, 6, 0, 0) //z
			TNT1 AAAAAA 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2, 5, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 1, 5, 0, 0) //G
			TNT1 AAAAAAAAA 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 5, 0, 0) //H
			TNT1 AAAAAA 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 1, 5, 0, 0)
			TNT1 AAAA 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 5, 0, 0)
			Stop

	ReloadDualWield:
		TNT1 A 0 {
			if (CountInv("XRifleAmmo") >= 41 && CountInv("LeftXRifleAmmo") >= 41) {
				return state("ReadyDualWield");
			}
			return state("");
		}
        TNT1 A 0 A_JumpIfInventory("NewClip",1,1)
        Goto ReadyDualWield
		TNT1 A 0 {
			A_Giveinventory("PB_LockScreenTilt",1);
			A_SetCrosshair(5);
			A_PlaySoundEx("Ironsights", "Auto");
		}
		CB01 ABCDE 1 
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",41,"ReloadLeftCarbine")
		Goto ReloadCheckPoint1
		
	InsertBulletsRight:
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",41,"ReloadLeftCarbine")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto ReloadLeftCarbine
		TNT1 A 0 {
			A_Giveinventory("XRifleAmmo",1);
			A_Takeinventory("NewCLip",1);
			}
		Goto InsertBulletsRight
	ReloadLeftCarbine:
		TNT1 A 0 A_Takeinventory("CarbineHasUnloaded",1)
		TNT1 A 0 A_JumpIfInventory("LeftXRifleAmmo",41,"SelectAnimationDualWield")
		4B20 G 1 A_SetRoll(roll+0.8)
		TNT1 A 0 A_JumpIfInventory("LeftCarbineHasUnloaded",1,"ReloadLeftUnloadedStart")
        4B20 H 1
        4B20 IJKLMNO 1 A_SetRoll(roll-0.8)
		
		4B20 PQRST 1
		4B20 UVWXY 1 A_SetRoll(roll-0.6)
		TNT1 A 0 A_PlaySoundEx("weapons/carbine/magout", "Auto")
		4B20 Z 1 A_SetRoll(roll-0.6)
	ContinueLeftReload:
        4B21 AB 1 A_SetRoll(roll-0.6)
		4B21 CDEF 1
		TNT1 A 0 A_JumpIfInventory("LeftXRifleAmmo",1,3)
		TNT1 A 0 A_JumpIfInventory("LeftCarbineHasUnloaded",1,2)
		TNT1 A 0 PB_SpawnCasing("EmptyCarbineMag",38,-16,13,frandom(0, 3.5),frandom(3.3, 7.2),frandom(3,7))
		4B21 GHIJKLMN 1 A_SetRoll(roll+0.6)
		TNT1 A 0 A_PlaySoundEx("weapons/carbine/magin", "Auto")
		4B21 OPQRS 1 A_SetRoll(roll+1.5)
		
		4B21 TUV 1 A_SetRoll(roll-0.5)
		TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
		4B21 WXYZ 1 A_SetRoll(roll+1.5)
        4B22 ABCDEFGH 1 A_SetRoll(roll-1.5)
		TNT1 A 0 A_TakeInventory("LeftCarbineHasUnloaded",1)
	InsertBulletsLeft:
		TNT1 A 0 A_JumpIfInventory("LeftXRifleAmmo",41,"SelectAnimationDualwield")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto SelectAnimationDualwield
		TNT1 A 0 {
			A_Giveinventory("LeftXRifleAmmo",1);
			A_Takeinventory("NewCLip",1);
			}
		Goto InsertBulletsLeft
	ReloadLeftUnloadedStart:
		4B21 A 1 Offset(4,34) A_SetRoll(roll-0.6)
		4B21 A 0 A_WeaponOffset(0,32)
		4B21 A 1 A_SetRoll(roll-0.1)
		GOTO ContinueLeftReload	

	Reload:
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_ClearOverlays(10,11);
		}
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "ReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",41,"Ready3")
        TNT1 A 0 A_JumpIfInventory("NewClip",1,1)
        Goto NoAmmo
		TNT1 A 0 {
			A_Giveinventory("PB_LockScreenTilt",1);
			A_SetCrosshair(5);
			A_PlaySoundEx("Ironsights", "Auto");
			
			if (CountInv("CarbineBarrelHeat") >= 60) {
					A_Gunflash("BarrelSmoke3");
			}
			
			return state("");
		}
        4A20 ABCDEF 1 A_SetRoll(roll+0.8)
	ReloadCheckPoint1:
        4A20 G 1 A_SetRoll(roll+0.8)
        4A20 H 1
		TNT1 A 0 A_JumpIfInventory("CarbineHasUnloaded",1,"ReloadUnloadedStart")
        4A20 IJKLMNO 1 A_SetRoll(roll-0.8)
		
		4A20 PQRST 1
		4A20 UVWXY 1 A_SetRoll(roll-0.6)
		TNT1 A 0 A_PlaySoundEx("weapons/carbine/magout", "Auto")
		4A20 Z 1 A_SetRoll(roll-0.6)
	ReloadFromUnload:	
        4A21 AB 1 A_SetRoll(roll-0.6)
		4A21 CDEF 1
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,3)
		TNT1 A 0 A_JumpIfInventory("CarbineHasUnloaded",1,2)
		TNT1 A 0 PB_SpawnCasing("EmptyCarbineMag",38,20,13,frandom(0, 3.5),frandom(-7.2, -3.3),frandom(3,7))
		4A21 GHIJKLMN 1 A_SetRoll(roll+0.6)
		TNT1 A 0 A_PlaySoundEx("weapons/carbine/magin", "Auto")
		4A21 OPQRS 1 A_SetRoll(roll+1.5)
		
		4A21 TUV 1 A_SetRoll(roll-0.5)
		TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
		4A21 WXYZ 1 A_SetRoll(roll+1.5)
        4A22 ABCDEFGH 1 A_SetRoll(roll-1.5)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "InsertBulletsRight")
        4A22 IJ 1 A_SetRoll(roll-1.5)
		TNT1 A 0 A_SetRoll(0)
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,5)//30+1 effect
		4A22 KLMN 1
		TNT1 A 0 A_Takeinventory("CarbineHasUnloaded",1)
		TNT1 A 0 A_SetCrosshair(0)
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,"InsertBullets2")//30+1 effect
		TNT1 A 0 A_GiveInventory("RifleWasEmpty", 1)
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",40,"Ready3")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("XRifleAmmo",1);
			A_Takeinventory("NewCLip",1);
			}
		Goto InsertBullets
		
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",41,"Ready3")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("XRifleAmmo",1);
			A_Takeinventory("NewCLip",1);
			}
		Goto InsertBullets2
	
	LoadChamber:
		TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_SetCrosshair(5)
		TNT1 A 0 A_Takeinventory("RifleWasEmpty",1)
		TNT1 A 0 A_PlaySoundEx("weapons/magpull", "Auto")
		4AFA RSTTU 1 A_SetRoll(roll-.6, SPF_INTERPOLATE)
		4AFR BA 1 A_SetRoll(roll+1.5, SPF_INTERPOLATE)
		TNT1 A 0 A_SetCrosshair(0)
		Goto Ready3
		
	ReloadUnloadedStart:
		4A21 A 1 Offset(-4,34) A_SetRoll(roll-0.6)
		4A21 A 0 A_WeaponOffset(0,32)
		4A21 A 1 A_SetRoll(roll-0.1)
		GOTO ReloadFromUnload
	AlreadyUnloaded:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto Ready3
		
	UnLoadDualWieldStart:
		CB01 ABCDE 1 
		Goto UnloadCheckPoint
		
	
	Unload:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 {	
			A_ZoomFactor(1.0);
			A_Takeinventory("Unloading",1);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 A_JumpIfInventory("CarbineHasUnloaded", 1, "AlreadyUnloaded")
        TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,1)
        Goto NoAmmo
		TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_SetCrosshair(5)
		TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto")
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "UnLoadDualWieldStart")
		 4A20 ABCDEF 1 A_SetRoll(roll+0.8)
	UnloadCheckPoint:
        4A20 G 1 A_SetRoll(roll+0.8)
        4A20 H 1
        4A20 IJKLMNO 1 A_SetRoll(roll-0.8)
		
		4A20 PQRST 1
		4A20 UVWXY 1 A_SetRoll(roll-0.6)
		TNT1 A 0 A_PlaySoundEx("weapons/carbine/magout", "Auto")
		4A20 Z 1 A_SetRoll(roll-0.6)
        4A21 AB 1 A_SetRoll(roll-0.6)
		4A21 CDEF 1
		4A21 GHIJK 1 A_SetRoll(roll+0.6)
		4A21 K 5
		4A22 F 1 Offset(-10,32) A_SetRoll(roll-1.5)
		4A22 G 0 A_WeaponOffset(0,32) 
		4A22 G 1 A_SetRoll(roll-1.5)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "RemoveBullets")
		4A22 HIJ 1 A_SetRoll(roll-1.5)
		4A22 KLMN 1
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,1)
		Goto FinishUnload
		TNT1 A 0 {
			A_Takeinventory("XRifleAmmo",1);
			A_Giveinventory("NewClip",1);
			}
		Goto RemoveBullets
	
	FInishUnload:
		TNT1 A 0 A_GiveInventory("CarbineHasUnloaded", 1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "UnloadLeftCarbine")
		Goto Ready3
	
	UnloadLeftCarbine:
		//TNT1 A 0 A_Takeinventory("CarbineHasUnloaded",1)
		
		
		4A22 H 1 A_SetRoll(roll-1.5)
		4A22 H 1 Offset(-18,44) A_SetRoll(roll-1.5)
		4A22 H 1 Offset(-30,56) A_SetRoll(roll-1.5)
		TNT1 A 5 A_WeaponOffset(0,32)
		TNT1 A 0 A_JumpIf(CountInv("LeftXRifleAmmo") < 1,"SelectAnimationDualWield")
		4B20 G 1 A_SetRoll(roll+0.8)
        4B20 H 1
        4B20 IJKLMNO 1 A_SetRoll(roll-0.8)
		
		4B20 PQRST 1
		4B20 UVWXY 1 A_SetRoll(roll-0.6)
		TNT1 A 0 A_PlaySoundEx("weapons/carbine/magout", "Auto")
		4B20 Z 1 A_SetRoll(roll-0.6)
        4B21 AB 1 A_SetRoll(roll-0.6)
		4B21 CDEF 1
		4B21 GHIJK 1
		4B21 K 5
		4B22 F 1 Offset(10,32) A_SetRoll(roll-1.5)
		4A22 G 0 A_WeaponOffset(0,32) 
		4B22 G 1 A_SetRoll(roll-1.5)
		4B22 H 1 A_SetRoll(roll-1.5)
		4B22 H 1 Offset(18,44) A_SetRoll(roll-1.5)
		4B22 H 1 Offset(30,56) A_SetRoll(roll-1.5)
		TNT1 A 5 A_WeaponOffset(0,32)
		RemoveBullets_Left:
		TNT1 A 0 A_JumpIfInventory("LeftXRifleAmmo",1,1)
		Goto FInishDualUnload
		TNT1 A 0 {
			A_Takeinventory("LeftXRifleAmmo",1);
			A_Giveinventory("NewClip",1);
			}
		Goto RemoveBullets_Left
	
	FInishDualUnload:
		TNT1 A 0 A_GiveInventory("CarbineHasUnloaded", 1)
		TNT1 A 0 A_GiveInventory("LeftCarbineHasUnloaded",1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto SelectAnimationDualWield
		
	Spawn:
		VB00 Z 0 NoDelay
		CB00 Z 10 A_PbvpFramework("VB00")
		"####" "#" 0 A_PbvpInterpolate()
		LOOP
		
    
	FlashAirKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FlashAirKickingDW")
    FlashKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FlashKickingDW")
		4A1K ABCDEFGHIJKLMNNN 1 
		Goto Ready3
	FlashKickingDW:
		CB01 ABCDEEEEEEDCBA 1
		Goto Ready3
	FlashAirKickingDW:
		CB01 ABCDEEEEEEEEDCBA 1
		Goto Ready3
		
	FlashSlideKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FlashSlideKickingDW")
		4A1K ABCDEFGPQRSTUTSRQGHIJKLMNNN 1
		Goto Ready3
	FlashSlideKickingDW:
		CB01 ABCDEEEEEEEEEEEEEEEEEEEDCBA 1
		Goto Ready3
	FlashSlideKickingStop:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FlashSlideKickingStopDW")
		4A1K GHIJKLM 1 
		Goto Ready3
	FlashSlideKickingStopDW:
		CB01 EEEDCBA 1
		Goto Ready3
		
	FlashPunching:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FlashPunchingDW")
		4A1Q ABCDEFGHFEDCBA 1
		Stop
	FlashPunchingDW:
		TNT1 A 15
		Stop
		
	}
}

ACTOR XRifleAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 41
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 40
   +INVENTORY.IGNORESKILL
   Inventory.Icon "CB00Z0"
}

ACTOR LeftXRifleAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 41
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 40
   +INVENTORY.IGNORESKILL
   Inventory.Icon "CB00Z0"
}

Actor LeftCarbineHasUnloaded: HasUnloaded{}