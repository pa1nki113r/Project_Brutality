Actor SemiFire : Inventory
{Inventory.MaxAmount 1}

Actor BurstFire : Inventory
{Inventory.MaxAmount 1}

Actor CarbineBurstCounter : Inventory
{Inventory.MaxAmount 2}

Actor CarbineFullAuto : Inventory
{Inventory.MaxAmount 1}

Actor CarbineHasUnloaded : Inventory
{Inventory.MaxAmount 1}

Actor RespectCarbine : Inventory
{Inventory.MaxAmount 1}

Actor CarbineMuzzleFlash1 : Inventory
{Inventory.MaxAmount 1}

Actor CarbineMuzzleFlash2 : Inventory
{Inventory.MaxAmount 1}

Actor CarbineMuzzleFlash3 : Inventory
{Inventory.MaxAmount 1}

Actor CarbineBarrelHeat: Inventory
{Inventory.maxamount 200}

Actor SelectCarbine_DualWield : Inventory
{Inventory.MaxAmount 1}
Actor SelectCarbine_FullAutoFire : Inventory
{Inventory.MaxAmount 1}
Actor SelectCarbine_SemiFire : Inventory
{Inventory.MaxAmount 1}
Actor SelectCarbine_BurstFire : Inventory
{Inventory.MaxAmount 1}


Actor DualWieldingCarbines : Inventory
{Inventory.MaxAmount 1}


ACTOR Carbine : PB_Weapon
{
	SpawnID 9450
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 20
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "NewClip"
	Weapon.AmmoType2 "XRifleAmmo"
	+FLOORCLIP
	+DONTGIB
	Obituary "%o was shot down by %k's Combat Rifle."
    AttackSound "None"
    Inventory.PickupSound "CLIPIN"
	Inventory.Pickupmessage "You got the UAC41 Carbine! (Slot 4)"
	Inventory.MaxAmount 3
	Inventory.Amount 1
	Tag "UAC-41 Carbine"
	+WEAPON.WIMPY_WEAPON
	//+WEAPON.NO_AUTO_SWITCH
    +WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
    +WEAPON.NOAUTOFIRE
    Scale 0.51
	PB_WeaponBase.UnloaderToken "CarbineHasUnloaded"
	Inventory.AltHUDIcon "CB00Z0"
	States
	{
		Steady:
			TNT1 A 1
			Goto Ready
		
		Ready:
			TNT1 A 0 A_SelectWeapon("LMG")
			TNT1 A 0 A_JumpIfInventory("RespectCarbine", 1, "SelectAnimation")
		WeaponRespect:
			TNT1 A 0 {
				A_SetCrosshair(5);
				A_GiveInventory("RespectCarbine");
				A_StartSound("weapons/carbine/up", CHAN_AUTO);
			}
			4A11 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			4A12 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			4A13 ABCDEFGHIJ 1 A_DoPBWeaponAction
			TNT1 A 0 A_StartSound("weapons/carbine/magout", CHAN_AUTO)
			4A13 KLMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			4A14 ABCDEFGHIJKL 1 A_DoPBWeaponAction
			TNT1 A 0 A_StartSound("IronSights", CHAN_AUTO)
			4A14 MNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
			TNT1 A 0 A_StartSound("LIGHTON", CHAN_AUTO)
			4A15 ABCDEFGHIJ 1 A_DoPBWeaponAction
			TNT1 A 0 A_StartSound("LIGHTON", CHAN_AUTO)
			4A15 KLMNOPQRSTU 1 A_DoPBWeaponAction
			TNT1 A 0 A_StartSound("LIGHTON", CHAN_AUTO)
			4A15 VWXYZ 1 A_DoPBWeaponAction
			4A16 ABCDE 1 A_DoPBWeaponAction
			TNT1 A 0 A_StartSound("IronSights", CHAN_AUTO)
			4A16 FGHIJKLMNOPQ 1 A_DoPBWeaponAction
			TNT1 A 0 A_StartSound("weapons/carbine/fancybutton", CHAN_AUTO)
			4A16 RSTUVWXYZ 1 A_DoPBWeaponAction
			4A17 ABCDEFGHIJKLMNOPQRSTU 1 A_DoPBWeaponAction
			TNT1 A 0 A_StartSound("weapons/carbine/magin", CHAN_AUTO)
			4A17 VWXYZ 1 A_DoPBWeaponAction
			4A18 AB 1 A_DoPBWeaponAction
			TNT1 A 0 A_StartSound("weapons/riflemagslap", CHAN_AUTO)
			4A18 CDEFGHIJKLMNOP 1 A_DoPBWeaponAction
			TNT1 A 0 {
				A_StartSound("weapons/carbine/down", CHAN_AUTO);
				A_Setroll(0);
				}
			4A18 QRSTUVWX 1 A_DoPBWeaponAction
			Goto Ready3
		
		SelectAnimation:
			TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "SelectAnimationDualWield")
			TNT1 A 0 A_StartSound("weapons/carbine/up", CHAN_AUTO)
			4A1S ABCDE 1 A_JumpIfInventory("GoFatality", 1, "Steady")
		Ready3:
			TNT1 A 0 {
				A_TakeInventory("PB_LockScreenTilt",1);
				PB_HandleCrosshair(59);
				}
			TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "ReadyDualWield")
		ReadyToFire:
		TNT1 A 0 A_JumpIfInventory("RifleWasEmpty",1,"LoadChamber")
		4AFG A 1 
		{
			A_SetInventory("CarbineBurstCounter",0);
			if (CountInv("CarbineBarrelHeat") >= 60) {A_Gunflash("BarrelSmoke1");}
				
			if(GetCvar("pb_toggle_aim_hold")) 
			{
					if ((PressingFire() && PressingAltfire() && CountInv("XRifleAmmo") > 0))
					{
						if(CountInv("CarbineFullAuto") >= 1) 
							{Return State("Fire2");}
					}
					if (PressingFire() && CountInv("XRifleAmmo") > 0)
					{
						if(CountInv("CarbineFullAuto") >= 1) 
							{Return State("Fire");}
						if(CountInv("BurstFire") >= 1) 
							{
								if(CountInv("CarbineBurstCounter") >= 2) 
								{A_SetInventory("CarbineBurstCounter",0);Return State("Ready3");}
							}
						//if (!PressingFire())
						//	{A_SetInventory("CarbineBurstCounter",0);}
					}
			}
			else 
			{
				if (PressingFire() && CountInv("XRifleAmmo") > 0 )
					{
						if(CountInv("CarbineFullAuto") >= 1) 
							{Return State("Fire");}
						
						if(CountInv("BurstFire") >= 1) 
							{
								if(CountInv("CarbineBurstCounter") >= 2) 
								{A_SetInventory("CarbineBurstCounter",0); Return State("Ready3");}
							}
					}
				//if (!PressingFire())
				//	{A_SetInventory("CarbineBurstCounter",0);}
				
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
		}
			Loop
		
	SelectAnimationDualWield:
		//Stuff
		TNT1 A 0 A_StartSound("weapons/carbine/up", CHAN_AUTO)
		CB01 EDCBA 1
		TNT1 A 0 A_StartSound("weapons/carbine/up", CHAN_AUTO)
	ReadyDualWield:	
		TNT1 A 0 {
			A_SetRoll(0);
			PB_HandleCrosshair(59);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_SetFiringRightWeapon(False);
			A_SetFiringLeftWeapon(False);
			A_TakeInventory("DualFiring",1);
			if(CountInv("LeftXRifleAmmo") < CountInv("XRifleAmmo")){
				A_GiveInventory("DualFiring",1);
			}
			if(CountInv("NewClip")>0){
				if(CountInv("LeftXRifleAmmo")<=0){
					A_GiveInventory("DualFireReload",1);
				}
				if(CountInv("XRifleAmmo")<=0){
					A_GiveInventory("DualFireReload",1);
				}
			}
			A_Overlay(10, "IdleLeft_Overlay", true);
			A_Overlay(11, "IdleRight_Overlay", true);
			}
	ReadyToFireDualWield:
		TNT1 A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE)
		Loop
		
	IdleLeft_Overlay:
		CB01 F 1 {
			// Full-Auto
			if(CountInv("LeftXRifleAmmo")<=0 && CountInv("XRifleAmmo")>0){
				A_GiveInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==0 || (CountInv("DualFiring")==0 && CountInv("XRifleAmmo")<=0) || GetCvar("SingleDualFire")==1){
				if(PressingFire() || JustPressed(BT_ATTACK)){
					if(CountInv("LeftXRifleAmmo") > 0){
						return state("FireLeft_Overlay");
					}
					else {
						A_StartSound("weapons/empty", CHAN_AUTO);
						return state("");
					}
				}
			}
			return state("");
		}
		Loop
	IdleRight_Overlay:
		CB01 G 1 {
			// Full-Auto
			if(CountInv("LeftXRifleAmmo")>0 && CountInv("XRifleAmmo")<=0){
				A_TakeInventory("DualFiring",1);
			}
			if(CountInv("DualFiring")==1 || (CountInv("DualFiring")==1 && CountInv("LeftXRifleAmmo")<=0) || GetCvar("SingleDualFire")==1){
				if((PressingFire() || JustPressed(BT_ATTACK)) && GetCvar("SingleDualFire")==0){
					if(CountInv("XRifleAmmo") > 0){
						return state("FireRight_Overlay");
					}
					else {
						A_StartSound("weapons/empty", CHAN_AUTO);
						return state("");
					}
				}
			}
			if((PressingAltFire() || JustPressed(BT_ALTATTACK)) && GetCvar("SingleDualFire")==1){
				if(CountInv("XRifleAmmo") > 0){
					return state("FireRight_Overlay");
				}
				else {
					A_StartSound("weapons/empty", CHAN_AUTO);
					return state("");
				}
			}
			return state("");
		}
		Loop
	
	FireLeft_Overlay:
		CB02 A 1 BRIGHT {
			A_AlertMonsters;
			A_StartSound("Weapons/Rifle/SFire", CHAN_7);
			A_FireBullets (0.1, 0.1, -1, 19, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,-4,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, -4, 0, 0, 0);
			A_FireCustomMissile("RifleCaseSpawn",5,0,-14,-6);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_Takeinventory("LeftXRifleAmmo",1);
			A_ZoomFactor(0.985);
			A_GunFlash;
            PB_WeaponRecoil(-1.6,+1.2);
			}
		CB02 B 1 {
			A_ZoomFactor(0.99);
            PB_WeaponRecoil(-1.6,+1.2);
			}
		TNT1 A 0 {
			if(CountInv("LeftXRifleAmmo")<=0 || CountInv("XRifleAmmo")>0 ){
				A_GiveInventory("DualFiring",1);
			}
		}
		CB02 C 1
		TNT1 A 0 {
			if(CountInv("LeftXRifleAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleLeft_Overlay
		
	FireRight_Overlay:
		CB03 A 1 BRIGHT {
			A_AlertMonsters;
			A_StartSound("Weapons/Rifle/SFire", CHAN_6);
			A_FireBullets (0.1, 0.1, -1, 19, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,4,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 4, 0, 0, 0);
			A_FireCustomMissile("RifleCaseSpawn",5,0,16,-5);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_Takeinventory("XRifleAmmo",1);
			A_ZoomFactor(0.985);
			A_GunFlash;
            PB_WeaponRecoil(-1.6,-1.2);
			}
		CB03 B 1 {
			A_ZoomFactor(0.99);
            PB_WeaponRecoil(-1.6,-1.2);
			}
		TNT1 A 0 {
			if(CountInv("LeftXRifleAmmo")>0 || CountInv("XRifleAmmo")<=0 ){
				A_TakeInventory("DualFiring",1);
			}
		}
		CB03 C 1
		TNT1 A 0 {
			if(CountInv("XRifleAmmo")<=0){
				A_GiveInventory("DualFireReload",1);
			}
		}
		Goto IdleRight_Overlay
	
	////////////Smoking Gun Effects////////////
	BarrelSmoke1:
		TNT1 A 1 {
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 0, 0, 0);
			A_Takeinventory("CarbineBarrelHeat",1);
		}
		Stop
	BarrelSmoke2:
		TNT1 A 1 {
			A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 25, 0, 0);
			A_Takeinventory("CarbineBarrelHeat",1);
		}
		Stop
	////////////Smoking Gun Effects////////////
		
    Ready2:
		TNT1 A 0 {
			A_TakeInventory("PB_LockScreenTilt",1);
			A_SetCrosshair(5);
			}
	ReadyToFire2:
		4A2G A 1 {
			//Updated code for far superior smooth gameplay
			A_SetInventory("CarbineBurstCounter",0);
			if (CountInv("CarbineBarrelHeat") >= 60) {
				A_Gunflash("BarrelSmoke2");
			}
			
			if(GetCvar("pb_toggle_aim_hold")) {
				if(!PressingAltfire() || JustReleased(BT_ALTATTACK)){
					return state("Zoomout");
				}
				if (PressingFire() && PressingAltfire() && CountInv("XRifleAmmo") > 0){
						if(CountInv("CarbineFullAuto") >= 1) 
							{Return State("Fire2");}
						
						if(CountInv("BurstFire") >= 1) 
							{
								if(CountInv("CarbineBurstCounter") >= 2) 
								{A_SetInventory("CarbineBurstCounter",0); Return State("ReadyToFire2");}
								//else{Return State("Fire2");}
							}
				
				}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOSECONDARY);
			}
			else {
				if (PressingFire() && CountInv("XRifleAmmo") > 0 ){
						if(CountInv("CarbineFullAuto") >= 1) 
							{Return State("Fire2");}
						
						if(CountInv("BurstFire") >= 1) 
							{
								if(CountInv("CarbineBurstCounter") >= 2) 
								{A_SetInventory("CarbineBurstCounter",0); Return State("ReadyToFire2");}
							}

				}
				return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD);
		}
		Loop
		
	Deselect:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(59);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
		}
        TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_Takeinventory("Unloading",1);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
		}
		TNT1 A 0 A_JumpIfInventory("GotMeatShield", 1, "GrabEnemy")
		TNT1 A 0 A_JumpIfInventory("LMG",1,"DeselectUpgrade")
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "DeselectAkimbo")
        4A1S EDCBA 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectAkimbo:
		CB01 ABCDE 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	DeselectUpgrade:
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	WheelCancel:
		TNT1 A 0 {
			A_SetInventory("SelectCarbine_DualWield",0);
			A_SetInventory("SelectCarbine_SemiFire",0);
			A_SetInventory("SelectCarbine_BurstFire",0);
			A_SetInventory("SelectCarbine_FullAutoFire",0);
			A_SetInventory("CantWeaponSpecial",0);
			}
		Stop
			
	WeaponSpecial:
		TNT1 A 0 {
			A_Giveinventory("PB_LockScreenTilt",1);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("GoWeaponSpecialAbility",1);
			PB_HandleCrosshair(59);
			A_ZoomFactor(1.0);
			A_ClearOverlays(10,11);
		}
	
	TNT1 A 0{
		if(CountInv("SelectCarbine_DualWield") >=1)
			{A_Overlay(-19,"WheelCancel"); Return State("SwitchToDualWield");}
		if(CountInv("SelectCarbine_SemiFire") && CountInv("SemiFire") >=1)
			{A_Overlay(-19,"WheelCancel"); A_Log("Fire control already selected: Semi-Auto"); Return State("Ready3");}
		if(CountInv("SelectCarbine_BurstFire") && CountInv("BurstFire") >=1)
			{A_Overlay(-19,"WheelCancel"); A_Log("Fire control already selected: Burst-Fire"); Return State("Ready3");}
		if(CountInv("SelectCarbine_FullAutoFire") && CountInv("CarbineFullAuto") >=1)
			{A_Overlay(-19,"WheelCancel"); A_Log("Fire control already selected: Full-Auto"); Return State("Ready3");}
		
		if(CountInv("SelectCarbine_FullAutoFire") || CountInv("SelectCarbine_BurstFire") || CountInv("SelectCarbine_SemiFire") >=1)
			{Return State("ToggleFireMode");}
			
		A_Overlay(-19,"WheelCancel");
		A_Log("You should not be this far down the state");
		Return State("");
		}
	
	//ToggleDualWield:
	//	TNT1 A 0 {
	//			A_Takeinventory("SelectCarbine_DualWield",1);
 	//			A_Takeinventory("SelectCarbine_SemiFire",1);
	//			A_Takeinventory("SelectCarbine_BurstFire",1);
	//			A_Takeinventory("SelectCarbine_FullAutoFire",1);
	//	}
	//	TNT1 A 0 A_JumpIfInventory("Carbine", 2, "SwitchToDualWield")
	//	Goto Ready3
	SwitchToDualWield:
		TNT1 A 0 {
			if(CountInv("Carbine") < 2) {
			A_Print("You need two carbines to dual wield!"); Return State("Ready3");}
			Return State("");
			}
		TNT1 A 0 {
			if (A_CheckAkimbo()) {
				A_Takeinventory("DualWieldingCarbines",1);
				A_SetAkimbo(False);
				return state("SwitchFromDualWield");
			}
			else {
				A_Giveinventory("DualWieldingCarbines",1);
				A_SetAkimbo(True);
				return state("");
			}
		}
		CB00 ABCDE 1 A_SetRoll(roll+.8, SPF_INTERPOLATE)
		CB00 E 1
		CB00 EFGHI 1 A_SetRoll(roll-.8, SPF_INTERPOLATE)
		Goto ReadyDualWield
	SwitchFromDualWield:
		CB00 IHGFE 1 A_SetRoll(roll-.8, SPF_INTERPOLATE)
		CB00 E 1
		CB00 EDCBA 1 A_SetRoll(roll+.8, SPF_INTERPOLATE)
		Goto Ready3
		
		
 	ToggleFireMode:
		TNT1 A 0 {
			if (A_CheckAkimbo())
				{
				A_Log("Fire control not available while dual-wielding");
				A_Overlay(-19,"WheelCancel");
				return state("");
				}
			if(CountInv("SelectCarbine_SemiFire") >= 1) {
				A_Setinventory("CarbineFullAuto",0);
 				A_Setinventory("BurstFire",0);
 				A_SetInventory("SemiFire",1);
				A_Overlay(-19,"WheelCancel");
				A_Log("Fire control mode selected: Semi-Auto");
 				return state("FiremodeAnimation");
				}
 			if(CountInv("SelectCarbine_BurstFire") >= 1) {
				A_Setinventory("CarbineFullAuto",0);
 				A_Setinventory("BurstFire",1);
 				A_SetInventory("SemiFire",0);
				A_Overlay(-19,"WheelCancel");
				A_Log("Fire control mode selected: Burst-Fire");
 				return state("FiremodeAnimation");
				}
			if(CountInv("SelectCarbine_FullAutoFire") >= 1) {
				A_Setinventory("CarbineFullAuto",1);
 				A_Setinventory("BurstFire",0);
 				A_SetInventory("SemiFire",0);
				A_Overlay(-19,"WheelCancel");
				A_Log("Fire control mode selected: Full-Auto");
 				return state("FiremodeAnimation");
				}
 			return state("");	
 		}
 		Goto Ready3
 	FiremodeAnimation:
 		4A1W ABCDEF 1 A_Setroll(roll+0.8, SPF_INTERPOLATE)
 		TNT1 A 0 A_StartSound("LIGHTON")
 		4A1W FEDCBA 1 A_Setroll(roll-0.8, SPF_INTERPOLATE)
 		Goto Ready3
		
	Select:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(59);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		Goto SelectFirstPersonLegs
		SelectContinue:
		TNT1 A 0 A_SelectWeapon("LMG")
		TNT1 A 0 A_TakeInventory("FistsSelected",1)
		TNT1 A 0 A_TakeInventory("SawSelected",1)
		TNT1 A 0 A_TakeInventory("SledgeSelected",1)
		TNT1 A 0 A_TakeInventory("HandgunSelected",1)
		TNT1 A 0 A_TakeInventory("DualHandgunSelected",1)
		TNT1 A 0 A_TakeInventory("RevolverSelected",1)
		TNT1 A 0 A_TakeInventory("ShotgunSelected",1)
		TNT1 A 0 A_TakeInventory("RifleSelected",1)
		TNT1 A 0 A_TakeInventory("SSGSelected",1)
		TNT1 A 0 A_TakeInventory("ASGSelected",1)
		TNT1 A 0 A_TakeInventory("QSGSelected",1)
		TNT1 A 0 A_TakeInventory("MinigunSelected",1)
		TNT1 A 0 A_TakeInventory("NewChaingunSelected",1)	
		TNT1 A 0 A_TakeInventory("RocketLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("GrenadeLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("SGLSelected",1)
		TNT1 A 0 A_TakeInventory("PlasmaGunSelected",1)
		TNT1 A 0 A_TakeInventory("M2Selected",1)	
		TNT1 A 0 A_TakeInventory("FreezerSelected",1)
		TNT1 A 0 A_TakeInventory("RailGunSelected",1)
		TNT1 A 0 A_TakeInventory("BFGSelected",1)
		TNT1 A 0 A_TakeInventory("BFGBeamSelected",1)
		TNT1 A 0 A_TakeInventory("BHCSelected",1)
		TNT1 A 0 A_TakeInventory("RevenantLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("FlameCannonSelected",1)
		TNT1 A 0 A_TakeInventory("HellRifleSelected",1)
		TNT1 A 0 A_TakeInventory("LandMineSelected",1)
		TNT1 A 0 A_TakeInventory("UACSMGSelected",1)
		TNT1 A 0 A_TakeInventory("SubMachineGunSelected",1)
		TNT1 A 0 A_TakeInventory("LostSoulSelected",1)
		TNT1 A 0 A_TakeInventory("HasBarrel",1)
		TNT1 A 0 A_TakeInventory("HasBurningBarrel",1)
		TNT1 A 0 A_TakeInventory("HasIncendiaryWeapon", 1)
		TNT1 A 0 A_TakeInventory("LMGSelected",1)
		TNT1 A 0 A_GiveInventory("CarbineSelected",1)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait
		
    Fire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(59);
			A_TakeInventory("PB_LockScreenTilt",1);
			A_ClearOverlays(10,11);
		}
	// Load sprites into memory
		4AFF AB 0
		4BFF AB 0
		4CFF AB 0
		
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Fire2")
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			PB_HandleCrosshair(59);
			A_SetRoll(0);
			}
      //  TNT1 A 0 A_JumpIfInventory("SemiFire",1,"FireSemi")
      //  TNT1 A 0 A_JumpIfInventory("BurstFire",1,"FireBurst")
        TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,1)
        Goto Reload
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Fire2")
		
		4AFF A 1 {
			if (CountInv("CarbineMuzzleFlash1") >= 1 ) { A_SetWeaponSprite("4AFF");}
			if (CountInv("CarbineMuzzleFlash2") >= 1 ) { A_SetWeaponSprite("4BFF");}
			if (CountInv("CarbineMuzzleFlash3") >= 1 ) { A_SetWeaponSprite("4CFF");}
			A_GunFlash;
			A_FireBullets (0.1, 0.1, -1, 19, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			A_FireCustomMissile("RifleCaseSpawn",5,0,8,-9);
			A_Takeinventory("XRifleAmmo",1);
			A_Giveinventory("CarbineBarrelHeat",10);
			A_StartSound("Weapons/Rifle/SFire", CHAN_WEAPON);
			A_AlertMonsters;
			A_ZoomFactor(0.98);
			//A_WeaponReady(WRF_NOPRIMARY);
            PB_WeaponRecoil(-0.48,-0.18);
			}
		4AFF B 1 {
			if (CountInv("CarbineMuzzleFlash1") >= 1 ) { A_SetWeaponSprite("4AFF");}
			if (CountInv("CarbineMuzzleFlash2") >= 1 ) { A_SetWeaponSprite("4BFF");}
			if (CountInv("CarbineMuzzleFlash3") >= 1 ) { A_SetWeaponSprite("4CFF");}
			
			A_ZoomFactor(0.99);
			//A_WeaponReady(WRF_NOPRIMARY);
            PB_WeaponRecoil(-0.48,-0.18);
		}
		4AFF C 1 {
			A_ZoomFactor(1.0);
			//A_WeaponReady(WRF_NOPRIMARY);
		}
		TNT1 A 0 {
			if (CountInv("CarbineMuzzleFlash1") >= 1 ) {
				A_TakeInventory("CarbineMuzzleFlash1",1);
				A_GiveInventory("CarbineMuzzleFlash2",1);
			}
			else if (CountInv("CarbineMuzzleFlash2") >= 1 ) {
				A_TakeInventory("CarbineMuzzleFlash2",1);
				A_GiveInventory("CarbineMuzzleFlash3",1);
			}
			else if (CountInv("CarbineMuzzleFlash3") >= 1 ) {
				A_TakeInventory("CarbineMuzzleFlash3",1);
				A_GiveInventory("CarbineMuzzleFlash1",1);
			}
			else {
				A_GiveInventory("CarbineMuzzleFlash1",1);
			}
		}
		TNT1 A 0 {
			if(CountInv("SemiFire") >= 1) {
 				return state("ReadyToFire");
				}
 			if(CountInv("BurstFire") >= 1) {
				if(CountInv("CarbineBurstCounter") <=1)
					{A_GiveInventory("CarbineBurstCounter",1); A_Refire;}
 				Else
					{Return State("ReadyToFire");}
				}
			if(CountInv("CarbineFullAuto") >= 1) {A_Refire;}
			Return State ("");
		}
		Goto ReadyToFire
	
      Fire2:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetCrosshair(5);
			}
        TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,1)
        Goto Reload
		4A2F A 0 A_Jump(256,"Fire2_Flash1","Fire2_Flash2")
		Fire2_Flash1:
		4A2F A 1 BRIGHT {
			A_GunFlash;
			A_FireBullets (0.1, 0.1, -1, 19, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			A_FireCustomMissile("RifleCaseSpawn",5,0,8,-9);
			A_Takeinventory("XRifleAmmo",1);
			A_Giveinventory("CarbineBarrelHeat",10);
			A_StartSound("Weapons/Rifle/SFire", CHAN_WEAPON);
			A_AlertMonsters;
			A_ZoomFactor(1.485);
            PB_WeaponRecoil(-0.30,-0.12);
			}
		Fire2Continue:
        4A2F B 1 BRIGHT {
			//A_WeaponReady(WRF_NOPRIMARY);
			A_ZoomFactor(1.495);
            PB_WeaponRecoil(-0.30,-0.12);
			}
		4A2F C 1 A_ZoomFactor(1.5)
		TNT1 A 0
		{
			if(GetCvar("pb_toggle_aim_hold")) 
			{
				if(JustReleased(BT_ALTATTACK))
					{return state("Zoomout");}
		
				if ((JustPressed(BT_ATTACK) || PressingFire())  && PressingAltfire())
				{
					if(CountInv("SemiFire") >= 1) 
						{return state("Ready2");}
					if(CountInv("BurstFire") >= 1) 
						{
							if(CountInv("CarbineBurstCounter") <=1)
								{A_GiveInventory("CarbineBurstCounter",1); A_Refire("Fire2");}
							Else
								{Return State("ReadyToFire2");}
						}
					if(CountInv("CarbineFullAuto") >= 1) { A_Refire("Fire2");}
				}
			
			Return State ("");
			}
			
			else 
			{
				if(PressingAltfire())
					{return state("Zoomout");}
				

				if(CountInv("SemiFire") >= 1) 
						{return state("Ready2");}
				if(CountInv("BurstFire") >= 1) 
							{
								if(CountInv("CarbineBurstCounter") <=1)
									{A_GiveInventory("CarbineBurstCounter",1); A_Refire("Fire2");}
								Else
									{Return State("ReadyToFire2");}
							}
				if(CountInv("CarbineFullAuto") >= 1) {A_Refire("Fire2");}
					
			Return State ("");
			}
			return A_DoPBWeaponAction(WRF_ALLOWRELOAD|WRF_NOFIRE);
		}
		//TNT1 A 0 A_Refire
		Goto ReadyToFire2
		
		Fire2_Flash2:
		4A3F A 1 BRIGHT {
			A_GunFlash;
			A_FireBullets (0.1, 0.1, -1, 19, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			A_FireCustomMissile("RifleCaseSpawn",5,0,8,-9);
			A_Takeinventory("XRifleAmmo",1);
			A_Giveinventory("CarbineBarrelHeat",10);
			A_StartSound("Weapons/Rifle/SFire", CHAN_WEAPON);
			A_AlertMonsters;
			A_ZoomFactor(1.485);
            PB_WeaponRecoil(-0.30,-0.12);
			}
		Goto Fire2Continue
    AltFire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			PB_HandleCrosshair(59);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 A_StartSound("Ironsights", CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ZoomOut")
	ZoomIn:
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_ZoomFactor(1.5);
			A_Giveinventory("Zoomed",1);
			A_Giveinventory("GoSpecial",1);
			A_Giveinventory("ADSmode",1);
		}
        4AFZ ABCDE 1
		TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY)
        Goto Ready2
		
   ZoomOut:
		TNT1 A 0 {
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_ZoomFactor(1.0);
			PB_HandleCrosshair(59);
			}
        4AFZ EDCBA 1
		TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY)
        Goto Ready3
		
	NoAmmo:
	4AFG A 1 {
			A_ZoomFactor(1.0);
			A_StartSound("weapons/empty", CHAN_AUTO);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
		}
	Goto Ready3

	BarrelSmoke3:
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 0, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2, 0, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 3, 0, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2, -1, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 4, 0, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 6, 1, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 1, 4, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 1, 5, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 5, 0, 0)
			TNT1 AAAAAAAAAAAAAA 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 6, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 1, 6, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2, 6, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2, 6, 0, 0) //z
			TNT1 AAAAAA 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 2, 5, 0, 0)
			TNT1 A 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 1, 5, 0, 0) //G
			TNT1 AAAAAAAAA 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 5, 0, 0) //H
			TNT1 AAAAAA 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 1, 5, 0, 0)
			TNT1 AAAA 1 A_FireCustomMissile("GunBarrelSmoke", 0, 0, 0, 5, 0, 0)
			Stop

	ReloadDualWield:
		TNT1 A 0 {
			if (CountInv("XRifleAmmo") >= 41 && CountInv("LeftXRifleAmmo") >= 41) {
				return state("ReadyDualWield");
			}
			return state("");
		}
        TNT1 A 0 A_JumpIfInventory("NewClip",1,1)
        Goto ReadyDualWield
		TNT1 A 0 {
			A_Giveinventory("PB_LockScreenTilt",1);
			A_SetCrosshair(5);
			A_StartSound("Ironsights", CHAN_AUTO);
		}
		CB01 ABCDE 1 
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",41,"ReloadLeftCarbine")
		Goto ReloadCheckPoint1
		
	InsertBulletsRight:
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",41,"ReloadLeftCarbine")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto ReloadLeftCarbine
		TNT1 A 0 {
			A_Giveinventory("XRifleAmmo",1);
			A_Takeinventory("NewCLip",1);
			}
		Goto InsertBulletsRight
	ReloadLeftCarbine:
		TNT1 A 0 A_Takeinventory("CarbineHasUnloaded",1)
		TNT1 A 0 A_JumpIfInventory("LeftXRifleAmmo",41,"SelectAnimationDualWield")
		4B20 G 1 A_SetRoll(roll+0.8)
        4B20 H 1
        4B20 IJKLMNO 1 A_SetRoll(roll-0.8)
		
		4B20 PQRST 1
		4B20 UVWXY 1 A_SetRoll(roll-0.6)
		TNT1 A 0 A_StartSound("weapons/carbine/magout", CHAN_AUTO)
		4B20 Z 1 A_SetRoll(roll-0.6)
        4B21 AB 1 A_SetRoll(roll-0.6)
		4B21 CDEF 1
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,-25,-4)
		4B21 GHIJKLMN 1 A_SetRoll(roll+0.6)
		TNT1 A 0 A_StartSound("weapons/carbine/magin", CHAN_AUTO)
		4B21 OPQRS 1 A_SetRoll(roll+1.5)
		
		4B21 TUV 1 A_SetRoll(roll-0.5)
		TNT1 A 0 A_StartSound("weapons/riflemagslap", CHAN_AUTO)
		4B21 WXYZ 1 A_SetRoll(roll+1.5)
        4B22 ABCDEFGH 1 A_SetRoll(roll-1.5)
	InsertBulletsLeft:
		TNT1 A 0 A_JumpIfInventory("LeftXRifleAmmo",41,"SelectAnimationDualwield")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto SelectAnimationDualwield
		TNT1 A 0 {
			A_Giveinventory("LeftXRifleAmmo",1);
			A_Takeinventory("NewCLip",1);
			}
		Goto InsertBulletsLeft
		

	Reload:
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_ClearOverlays(10,11);
		}
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "ReloadDualWield")
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",41,"Ready3")
        TNT1 A 0 A_JumpIfInventory("NewClip",1,1)
        Goto NoAmmo
		TNT1 A 0 {
			A_Giveinventory("PB_LockScreenTilt",1);
			A_SetCrosshair(5);
			A_StartSound("Ironsights", CHAN_AUTO);
			
			if (CountInv("CarbineBarrelHeat") >= 60) {
					A_Gunflash("BarrelSmoke3");
			}
			
			return state("");
		}
        4A20 ABCDEF 1 A_SetRoll(roll+0.8)
	ReloadCheckPoint1:
        4A20 G 1 A_SetRoll(roll+0.8)
        4A20 H 1
        4A20 IJKLMNO 1 A_SetRoll(roll-0.8)
		
		4A20 PQRST 1
		4A20 UVWXY 1 A_SetRoll(roll-0.6)
		TNT1 A 0 A_StartSound("weapons/carbine/magout", CHAN_AUTO)
		4A20 Z 1 A_SetRoll(roll-0.6)
        4A21 AB 1 A_SetRoll(roll-0.6)
		4A21 CDEF 1
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,-25,-4)
		4A21 GHIJKLMN 1 A_SetRoll(roll+0.6)
		TNT1 A 0 A_StartSound("weapons/carbine/magin", CHAN_AUTO)
		4A21 OPQRS 1 A_SetRoll(roll+1.5)
		
		4A21 TUV 1 A_SetRoll(roll-0.5)
		TNT1 A 0 A_StartSound("weapons/riflemagslap", CHAN_AUTO)
		4A21 WXYZ 1 A_SetRoll(roll+1.5)
        4A22 ABCDEFGH 1 A_SetRoll(roll-1.5)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "InsertBulletsRight")
        4A22 IJ 1 A_SetRoll(roll-1.5)
		TNT1 A 0 A_SetRoll(0)
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,5)//30+1 effect
		4A22 KLMN 1
		TNT1 A 0 A_Takeinventory("CarbineHasUnloaded",1)
		TNT1 A 0 A_SetCrosshair(0)
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,"InsertBullets2")//30+1 effect
		TNT1 A 0 A_GiveInventory("RifleWasEmpty", 1)
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",40,"Ready3")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("XRifleAmmo",1);
			A_Takeinventory("NewCLip",1);
			}
		Goto InsertBullets
		
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",41,"Ready3")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("XRifleAmmo",1);
			A_Takeinventory("NewCLip",1);
			}
		Goto InsertBullets2
	
	LoadChamber:
		TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_SetCrosshair(5)
		TNT1 A 0 A_Takeinventory("RifleWasEmpty",1)
		TNT1 A 0 A_StartSound("weapons/magpull", CHAN_AUTO)
		4AFA RSTTU 1 A_SetRoll(roll-.6, SPF_INTERPOLATE)
		4AFR BA 1 A_SetRoll(roll+1.5, SPF_INTERPOLATE)
		TNT1 A 0 A_SetCrosshair(0)
		Goto Ready3

	AlreadyUnloaded:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto Ready3
		
	UnLoadDualWieldStart:
		CB01 ABCDE 1 
		Goto UnloadCheckPoint
		
	
	Unload:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 {	
			A_ZoomFactor(1.0);
			A_Takeinventory("Unloading",1);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("Zoomed",1);
			A_ClearOverlays(10,11);
			}
		TNT1 A 0 A_JumpIfInventory("CarbineHasUnloaded", 1, "AlreadyUnloaded")
        TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,1)
        Goto NoAmmo
		TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_SetCrosshair(5)
		TNT1 A 0 A_StartSound("Ironsights", CHAN_AUTO)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "UnLoadDualWieldStart")
	UnloadCheckPoint:
        4AFR ABCDE 1
        4AFR FGHIJ 1 A_SetRoll(roll+0.8, SPF_INTERPOLATE)
		TNT1 A 0 A_StartSound("weapons/carbine/magout", CHAN_AUTO)
		4AFR KLMNO 1 A_SetRoll(roll-0.8, SPF_INTERPOLATE)
		4AFR PPQRST 1 A_SetRoll(roll-0.9, SPF_INTERPOLATE)
		4AFA AAA 1 A_SetRoll(roll+1.8, SPF_INTERPOLATE)
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("XRifleAmmo",1,1)
		Goto FinishUnload
		TNT1 A 0 {
			A_Takeinventory("XRifleAmmo",1);
			A_Giveinventory("NewClip",1);
			}
		Goto RemoveBullets
	
	FInishUnload:
		4AFR CBA 1
		TNT1 A 0 A_GiveInventory("CarbineHasUnloaded", 1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto Ready3
	

	Spawn.V5:
		VB00 Z 10 {
			IF( A_CHECKRANGE( V5_MDist , "NULL", TRUE) ) {A_SetSpawnSprite("CB00");}
			}
	Spawn:
		TNT1 A 0 NoDelay A_jumpif( V5_MODELS == 1 && V5_Cull == 1, "Spawn.V5")
		CB00 Z 10 {
				if ( GetCvar( "V5_MODELS" ) == 1 && GetCVar( "V5_Cull" ) == 0 ) {A_SetSpawnSprite("VB00");}
		}
		LOOP
		
    
	FlashAirKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FlashAirKickingDW")
    FlashKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FlashKickingDW")
		4A1K ABCDEFGHIJKLMNNN 1 
		Stop
	FlashKickingDW:
		CB01 ABCDEEEEEEDCBA 1
		Stop
	FlashAirKickingDW:
		CB01 ABCDEEEEEEEEDCBA 1
		Stop
		
	FlashSlideKicking:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FlashSlideKickingDW")
		4A1K ABCDEFGPQRSTUTSRQGHIJKLMNNN 1
		Stop
	FlashSlideKickingDW:
		CB01 ABCDEEEEEEEEEEEEEEEEEEEDCBA 1
		Stop
	FlashSlideKickingStop:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FlashSlideKickingStopDW")
		4A1K GHIJKLM 1 
		Stop
	FlashSlideKickingStopDW:
		CB01 EEEDCBA 1
		Stop
		
	FlashPunching:
		TNT1 A 0 A_ClearOverlays(10,11)
		TNT1 A 0 A_JumpIf(A_CheckAkimbo(), "FlashPunchingDW")
		4AFG A 1 
		4AFG B 1 
		4AFG BB 1 
		4AFG C 2 
		4AFG D 3
		4AFG C 2 
		4AFG BB 1 
		4AFG B 1 
		4AFG A 1 
		4AFG A 1 
		Stop
	FlashPunchingDW:
		TNT1 A 15
		Stop
		
	}
}

ACTOR XRifleAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 41
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 40
   Inventory.Icon "CB00Z0"
}


ACTOR LeftXRifleAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 41
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 40
   Inventory.Icon "CB00Z0"
}
