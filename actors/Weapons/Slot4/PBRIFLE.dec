Actor Reloading : Inventory
{
inventory.maxamount 1
}

Actor ADSMode : Inventory
{
inventory.maxamount 1
}

Actor RifleWasEmpty : Inventory
{
inventory.maxamount 1
}

Actor Unloading : Inventory
{
inventory.maxamount 1
}

Actor HasUnloaded : Inventory
{
inventory.maxamount 1
}

Actor HasUnloadedDMR : Inventory
{
inventory.maxamount 1
}

Actor HasUnderBarrelGrenadeLauncher : Inventory
{
inventory.maxamount 1
}

Actor UnderBarrelGrenadeLoaded : Inventory
{
inventory.maxamount 1
}


Actor DualWieldingDMRs : Inventory
{
inventory.maxamount 1
}

Actor PB_DMRFireAnimation1: Inventory
{
Inventory.maxamount 1
}
Actor PB_DMRFireAnimation2: Inventory
{
Inventory.maxamount 1
}


Actor RifleUpgrade: Custominventory
{
//$Category Ammunition
	Game Doom
	//SpawnID 9410
	Height 24
	//-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+COUNTITEM
	Inventory.Pickupsound "misc/rockboxa"
	Inventory.PickupMessage "You got the DMR Grenade Launcher Upgrade!!!"
	States
	{
	Spawn:
		RWGL ABCDEDCB 2 BRIGHT Light("WeaponUpgradeSpawner")
		Loop
	
    Pickup:
	  TNT1 A 0
	  TNT1 A 0 A_GiveInventory("UnderBarrelGrenadeLoaded", 1)
	  TNT1 A 0 A_GiveInventory("HasUnderBarrelGrenadeLauncher", 1)
      Stop
	}
}

//Placing the helmet on Doomguy's head
Actor IntroductionSequence : Inventory
{
inventory.maxamount 1
}
Actor RespectRifle : Inventory
{
inventory.maxamount 1
}

ACTOR HalfTicCounter : Inventory { Inventory.MaxAmount 1 }

// --------------------------------------------------------------------------

// UAC DMR

// --------------------------------------------------------------------------
ACTOR Rifle : PB_Weapon
{
	SpawnID 9420
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 10
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "NewClip"
	Weapon.AmmoType2 "RifleAmmo"
	Inventory.MaxAmount 3
	Inventory.Amount 1
	+FLOORCLIP
	+DONTGIB
	Obituary "%o was shot down by %k's DMR."
    AttackSound "None"
    Inventory.PickupSound "CLIPIN"
	Inventory.Pickupmessage "You got the UAC30 DMR! (Slot 4)"
	+WEAPON.WIMPY_WEAPON
    +WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
    +WEAPON.NOAUTOFIRE
	+WEAPON.NO_AUTO_SWITCH
    Scale 0.99 
	Weapon.SisterWeapon DualRifles
	Tag "UAC-30 DMR"
	
	States
	{
	Steady:
	TNT1 A 1
	Goto Ready
	
	Ready:
		TNT1 A 0
		TNT1 A 2 A_JumpIf(ACS_NamedExecuteWithResult("ToggleHelmetAnimation",0,0,0)==1,"WeaponRespect")
		TNT1 A 0 
		TNT1 A 0 A_JumpIfInventory("IntroductionSequence",1,"WeaponRespect")
		TNT1 A 0 {
			A_GiveInventory("sae_extcam", 1);
			A_GiveInventory("sae_deathcam", 1);
			A_GiveInventory("IntroductionSequence",1);
			A_GiveInventory("RifleSelected",1);
			A_SetCrosshair(5);
			}
        TNT1 A 20 A_PlaySoundEx("IronSights","Auto")
		TNT1 A 1 ACS_NamedExecute("ToggleHelmetAnimation", 0)
		HLMT ABC 2
		HLMT DEFGH 3
		HLMT I 4 A_PlaySoundEx("HLMTPUT","Auto")
		HLMT JKLMN 2
		TNT1 A 0 A_PlaySoundEx("HLMTBEP","Auto")
		HLMT O 3 A_PlaySoundEx("HLMTBPP","Auto")
		TNT1 A 0 {
			A_TakeInventory("sae_extcam", 1);
			A_TakeInventory("sae_deathcam", 1);
			}
		HLMT PQRS 2
	WeaponRespect:
		TNT1 A 0 A_JumpIfInventory("RespectRifle",1,"SelectAnimation")
		TNT1 A 0 {
			A_SetCrosshair(5);
			A_GiveInventory("RespectRifle");
			A_GiveInventory("PB_LockScreenTilt",1);
			A_PlaySoundEx("weapons/carbine/up", "Auto");
		}
        R0F0 ABCDEF 1 {
			A_DoPBWeaponAction;
			A_Setroll(roll-0.5, SPF_INTERPOLATE);
			}
        R0F0 GHI 1 {
			A_DoPBWeaponAction;
			A_Setroll(roll+1.0, SPF_INTERPOLATE);
			}
		TNT1 A 0 A_PlaySoundEx("LIGHTON", "Auto")
		R0F0 JKLMNOPQRSTUVWXYZ 1 A_DoPBWeaponAction
        R0F1 ABCDE 1 {
			A_DoPBWeaponAction;
			A_Setroll(roll-1.0, SPF_INTERPOLATE);
			}
		TNT1 A 0 A_PlaySoundEx("IronSights", "Auto")
		R0F1 FGHIJK 1 A_DoPBWeaponAction
		TNT1 A 0 A_PlaySoundEx("RIFCL_CK", "Auto")
		R0F1 LMNOP 1 {
			A_DoPBWeaponAction;
			A_Setroll(roll+2.0, SPF_INTERPOLATE);
			}
		R0F1 QRSTU 1 {
			A_DoPBWeaponAction;
			A_Setroll(roll-1.0, SPF_INTERPOLATE);
			}
		R0F1 VWX 1 A_DoPBWeaponAction
		TNT1 A 0 A_SetRoll(0)
		Goto Ready3
	SelectAnimation:
		TNT1 A 0 A_PlaySoundEx("weapons/rifle/up", "Auto")
		TNT1 A 0 A_JumpIfInventory("DualWieldingDMRs", 1, "SelectAnimationDualWield")
        RIFG EDCB 1 A_JumpIfInventory("GoFatality", 1, "Steady")
	Ready3:
		TNT1 A 0 {
			A_TakeInventory("PB_LockScreenTilt",1);
			A_SetCrosshair(0);
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDMRs", 1, "ReadyDualWield")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Ready2")
	ReallyReady:
		RIFG A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
		Loop
    Ready2:
		RI2G A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
		Loop
		
	SelectAnimationDualWield:
		R2F1 EFGHIJK 1
		TNT1 A 0 A_PlaySoundEx("weapons/rifle/up", "Auto")
	ReadyDualWield:
		TNT1 A 0 {
			A_SetRoll(0);
			A_SetCrosshair(0);
			A_TakeInventory("PB_LockScreenTilt",1);
			}
	ReadyToFireDualWield:
		P2SG A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
		Loop
		
	Deselect:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			A_SetCrosshair(0);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 {	
			A_Takeinventory("HasUnloadedDMR",1);
			A_Takeinventory("Unloading",1);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_ZoomFactor(1.0);
			A_PlaySoundEx("weapons/changing", "Auto");
			}
		TNT1 A 0 A_JumpIfInventory("GotMeatShield", 1, "GrabEnemy")
        RIFG BCDE 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
		
	WeaponSpecial:
		TNT1 A 0 {
			A_Takeinventory("GoWeaponSpecialAbility",1);
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_GiveInventory("PB_LockScreenTilt",1);
			A_SetCrosshair(0);
			A_ZoomFactor(1.0);
			A_PlaySoundEx("Ironsights", "Auto");
			}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDMRs", 1,"StopDualWield")
		TNT1 A 0 A_JumpIfINventory("Rifle", 2, "SwitchToDualWield")
		Goto Ready3
	SwitchToDualWield:
		R2F1 ABCDEF 1 A_Setroll(roll-0.5, SPF_INTERPOLATE)
		R2F1 GHI 1 A_Setroll(roll+1.0, SPF_INTERPOLATE)
		R2F1 JK 1
		TNT1 A 0 A_GiveInventory("DualWieldingDMRs",1)
		Goto ReadyDualWield
	StopDualWield:
		R2F1 KJ 1
		R2F1 IHGFED 1 A_Setroll(roll+0.5, SPF_INTERPOLATE)
		R2F1 CBA 1 A_Setroll(roll-1.0, SPF_INTERPOLATE)
		TNT1 A 0 A_TakeInventory("DualWieldingDMRs", 1)
		Goto Ready3
		
	Select:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			A_SetCrosshair(0);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 A_Overlay(-10, "FirstPersonLegsStand")
		TNT1 A 1 ACS_NamedExecuteAlways("ToggleCarbineUpgraded",0) //For people who want this weapon to replace another
		TNT1 A 0 A_Takeinventory("HasUnloadedDMR",1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_TakeInventory("FistsSelected",1)
		TNT1 A 0 A_TakeInventory("SawSelected",1)
		TNT1 A 0 A_TakeInventory("SledgeSelected",1)
		TNT1 A 0 A_TakeInventory("CarbineSelected",1)
		TNT1 A 0 A_TakeInventory("HandgunSelected",1)
		TNT1 A 0 A_TakeInventory("DualHandgunSelected",1)
		TNT1 A 0 A_TakeInventory("RevolverSelected",1)
		TNT1 A 0 A_TakeInventory("ShotgunSelected",1)
		TNT1 A 0 A_TakeInventory("SSGSelected",1)
		TNT1 A 0 A_TakeInventory("ASGSelected",1)
		TNT1 A 0 A_TakeInventory("QSGSelected",1)
		TNT1 A 0 A_TakeInventory("MinigunSelected",1)
		TNT1 A 0 A_TakeInventory("NewChaingunSelected",1)		
		TNT1 A 0 A_TakeInventory("RocketLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("GrenadeLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("SGLSelected",1)
		TNT1 A 0 A_TakeInventory("PlasmaGunSelected",1)
		TNT1 A 0 A_TakeInventory("M2Selected",1)	
		TNT1 A 0 A_TakeInventory("FreezerSelected",1)
		TNT1 A 0 A_TakeInventory("RailGunSelected",1)
		TNT1 A 0 A_TakeInventory("BFGSelected",1)
		TNT1 A 0 A_TakeInventory("BFGBeamSelected",1)
		TNT1 A 0 A_TakeInventory("BHCSelected",1)
		TNT1 A 0 A_TakeInventory("RevenantLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("FlameCannonSelected",1)
		TNT1 A 0 A_TakeInventory("HellRifleSelected",1)
		TNT1 A 0 A_TakeInventory("LandMineSelected",1)
		TNT1 A 0 A_TakeInventory("UACSMGSelected",1)
		TNT1 A 0 A_TakeInventory("SubMachineGunSelected",1)
		TNT1 A 0 A_TakeInventory("LostSoulSelected",1)
		TNT1 A 0 A_GiveInventory("RifleSelected",1)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait
		
	//This code will be reworked since the new equipment system breaks it
	ThrowGrenade:
		TNT1 AA 0
		TNT1 A 0 A_JumpIfInventory("HasUnderBarrelGrenadeLauncher",1,1)
		Goto Super::ThrowGrenade
		TNT1 A 0
	FireGrenadeLauncher:
		TNT1 A 0 A_WeaponOffset(0,32)
        TNT1 A 0 A_JumpIfInventory("UnderBarrelGrenadeLoaded",1,2)
        Goto ReloadGrenadeLauncher
        TNT1 AAAA 0
		R1GF ABC 1
		TNT1 A 0 A_PlaySound("RFGLLCH")
		RIFF A 0 A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0)
		TNT1 A 0 A_FireCustomMissile("YellowFlareSpawn",0,0,0,0)
		TNT1 AAAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-16,16), 0, -1, random(-9,9))
		TNT1 A 0 A_ZoomFactor(0.985)
		TNT1 A 0 A_FireCustomMissile("BouncingGrenadeHE", 0, 0, 0, 0)
		TNT1 A 0 A_TakeInventory("UnderBarrelGrenadeLoaded", 1)
		R1GF DDEE 1 BRIGHT
        TNT1 A 0 ACS_NamedExecute("Recoil282", 0, 0, 0, 0)
		TNT1 A 0 A_ZoomFactor(0.980)
		R1GF FG 1
		TNT1 A 0 A_ZoomFactor(0.985)
		R1GF CB 1
		TNT1 A 0 A_ZoomFactor(0.99)
		R1GF A 1 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("DoGrenade", 1)
        TNT1 A 0 A_JumpIfInventory("HandGrenade",1,2)
		Goto ReallyReady
		TNT1 A 0 A_TakeInventory("DoGrenade", 1)
        Goto ReloadGrenadeLauncher
		
    Fire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			A_SetCrosshair(0);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDMRs", 1, "FireDualWield")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Fire2")
        TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,2)
		TNT1 A 0 A_PlaySoundEx("weapons/empty", "Auto")
        Goto Reload
		RIFF A 1 BRIGHT {
			A_FireBullets (0.1, 0.1, -1, 22, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_PlaySoundEx("weapons/rifle", "Weapon");
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			A_FireCustomMissile("RifleCaseSpawn",5,0,8,-9);
			A_Takeinventory("RifleAmmo",1);
			A_AlertMonsters;
			A_ZoomFactor(0.98);
			A_WeaponReady(WRF_NOPRIMARY);
			if (CountInv("PowerStrength") >= 1 ) {

				A_SetPitch(pitch-0.18, SPF_INTERPOLATE);
				A_SetAngle(angle-0.01, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.36, SPF_INTERPOLATE);
				A_SetAngle(angle-0.02, SPF_INTERPOLATE);
				}
			}
		RIFF B 1 BRIGHT {
			A_ZoomFactor(0.99);
			A_WeaponReady(WRF_NOPRIMARY);
			if (CountInv("PowerStrength") >= 1 ) {

				A_SetPitch(pitch-0.18, SPF_INTERPOLATE);
				A_SetAngle(angle-0.01, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.36, SPF_INTERPOLATE);
				A_SetAngle(angle-0.02, SPF_INTERPOLATE);
				}
			}
		RIFF C 1{
			A_ZoomFactor(1.0);
			A_WeaponReady(WRF_NOPRIMARY);
			}
		RIFF DE 1 A_WeaponReady(WRF_NOPRIMARY)
		TNT1 A 0 A_Refire
		Goto ReallyReady
		
	
	 Fire2:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetCrosshair(5);
			}
        TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,2)
		TNT1 A 0 A_PlaySoundEx("weapons/empty", "Auto")
        Goto Reload
		RI2F A 1 BRIGHT {
			A_WeaponReady(WRF_NOPRIMARY);
			A_FireBullets (0.1, 0.1, -1, 22, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_PlaySoundEx("weapons/rifle", "Weapon");
			A_AlertMonsters;
			A_Takeinventory("RifleAmmo",1);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 3, 0, 0);
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			A_FireCustomMissile("RifleCaseSpawn",1,0,8,0);
			A_ZoomFactor(2.485);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.1, SPF_INTERPOLATE);
				A_SetAngle(angle-0.01, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.2, SPF_INTERPOLATE);
				A_SetAngle(angle-0.02, SPF_INTERPOLATE);
				}
		}
		RI2F B 1 {
			A_ZoomFactor(2.49);
			A_WeaponReady(WRF_NOPRIMARY);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.1, SPF_INTERPOLATE);
				A_SetAngle(angle-0.01, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-0.2, SPF_INTERPOLATE);
				A_SetAngle(angle-0.02, SPF_INTERPOLATE);
				}
			}
        RI2F C 1 {
			A_ZoomFactor(2.5);
			A_WeaponReady(WRF_NOPRIMARY);
			}
		RI2G AA 1 A_WeaponReady(WRF_NOPRIMARY)
		TNT1 A 0 A_Refire("Fire2")
		Goto Ready2
		
	FireDualWield:
        TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,2)
		TNT1 A 0 A_PlaySoundEx("weapons/empty", "Auto")
        Goto Reload
	FireRight:
		TNT1 A 0 A_JumpIfInventory("PB_DMRFireAnimation1", 1, "FireLeft")
		P2W1 A 1 BRIGHT {
			A_FireBullets (0.1, 0.1, -1, 22, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_PlaySoundEx("weapons/rifle", "Weapon");
			A_FireCustomMissile("GunFireSmoke", 0, 0, 4, 0, 0, 0);
			A_FireCustomMissile("RifleCaseSpawn",0,0,6,4);
			A_AlertMonsters;
			A_ZoomFactor(0.98);
			A_Takeinventory("RifleAmmo",1);
			A_Giveinventory("PB_DMRFireAnimation1",1);
			if (CountInv("PowerStrength") >= 1 ) {

				A_SetPitch(pitch-0.7, SPF_INTERPOLATE);
				A_SetAngle(angle-0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-1.4, SPF_INTERPOLATE);
				A_SetAngle(angle-0.8, SPF_INTERPOLATE);
				}
			}
		P2W1 B 1 BRIGHT {
			A_ZoomFactor(0.99);
			if (CountInv("PowerStrength") >= 1 ) {

				A_SetPitch(pitch-0.7, SPF_INTERPOLATE);
				A_SetAngle(angle-0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-1.4, SPF_INTERPOLATE);
				A_SetAngle(angle-0.8, SPF_INTERPOLATE);
				}
			}
		P2W1 C 1 A_ZoomFactor(1.0)
		P2W1 D 1 A_Refire("FireDualWield")
		Goto ReadyToFireDualWield
	FireLeft:
		P2W2 A 1 BRIGHT {
			A_FireBullets (0.1, 0.1, -1, 22, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_PlaySoundEx("weapons/rifle", "Weapon");
			A_FireCustomMissile("GunFireSmoke", 0, 0, -4, 0, 0, 0);
			A_FireCustomMissile("RifleCaseSpawn",0,0,-6,4);
			A_AlertMonsters;
			A_ZoomFactor(0.98);
			A_Takeinventory("PB_DMRFireAnimation1",1);
			A_Giveinventory("PB_DMRFireAnimation2",1);
			if (CountInv("PowerStrength") >= 1 ) {

				A_SetPitch(pitch-0.7, SPF_INTERPOLATE);
				A_SetAngle(angle+0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-1.4, SPF_INTERPOLATE);
				A_SetAngle(angle+0.8, SPF_INTERPOLATE);
				}
			}
		P2W2 B 1 BRIGHT {
			A_ZoomFactor(0.99);
			if (CountInv("PowerStrength") >= 1 ) {

				A_SetPitch(pitch-0.7, SPF_INTERPOLATE);
				A_SetAngle(angle+0.4, SPF_INTERPOLATE);
				}
			else {
				A_SetPitch(pitch-1.4, SPF_INTERPOLATE);
				A_SetAngle(angle+0.8, SPF_INTERPOLATE);
				}
			}
		P2W2 C 1 A_ZoomFactor(1.0)
		P2W2 D 1 
		TNT1 A 0 A_Refire("FireDualWield")
		Goto ReadyToFireDualWield
		
    AltFire:
		TNT1 A 0 {
			A_WeaponOffset(0,32);
			A_SetRoll(0);
			A_SetCrosshair(0);
			A_TakeInventory("PB_LockScreenTilt",1);
		}
		TNT1 A 0 A_JumpIfInventory("DualWieldingDMRs", 1, "ReadyToFireDualWield")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Zoomout")
		TNT1 A 0 {
			A_Giveinventory("Zoomed",1);
			A_Giveinventory("ADSmode",1);
			A_SetCrosshair(5);
			A_ZoomFactor(2.5);
			}
        RI2Z CDEEF 1
		TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY)
        Goto Ready2
	Zoomout:
        RI2Z FEEDC 1
		TNT1 A 0 {	
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_SetCrosshair(0);
			A_ZoomFactor(1.0);
			}
		TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY)
        Goto Ready3
	
	NoAmmo:
		TNT1 A 0 {	
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_SetCrosshair(0);
			A_ZoomFactor(1.0);
			}
		Goto Ready3
		
	
	ReloadGrenadeLauncher:
		TNT1 A 0 A_WeaponReady
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
        TNT1 A 0 A_JumpIfInventory("UnderBarrelGrenadeLoaded",1,"ReallyReady")
        RIGR BATS 1  A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_GiveInventory("UnderBarrelGrenadeLoaded", 1)
		RGLR A 4
		RGLR BCD 2
		TNT1 A 0 A_PlaySound("Weapons/GrenadeOpen", 1)
		TNT1 AAAA 0 A_FireCustomMissile("SmokeSpawner",4,0,0,2)	
		TNT1 A 0 A_FireCustomMissile("GrenadeCaseSpawn",5,0,-10,-5)
		RGLR EFG 2
		RGLR HIJKLMMMMM 1
		RGLR NOP 2
		TNT1 A 0 A_PlaySound("Weapons/GrenadeLoad", 2)
		RGLR QQRRSSS 1
		RGLR TUVWXY 1
		TNT1 A 0 A_TakeInventory("DoGrenade", 1)
		RGLR A 1
		TNT1 A 0 A_TakeInventory("DoGrenade", 1)
        RIGR STBA 1  A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_TakeInventory("DoGrenade", 1)
		Goto ReallyReady
	Reload2:
        RIFR ABCDE 1  A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_PlaySound("RIFCL_OU", 2)
        RIFR F 1 A_JumpIfInventory("HasUnloadedDMR", 1, 2)
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-4)
        RIFR GGGGGGGG 1
		RIFR HIKL 1
		RIFR MMM 1
		TNT1 A 0 A_PlaySound("RIFCL_IN", 3)
		RIFR NOPQRST 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_PlaySound("RIFCL_CL", 4)
		TNT1 A 0 A_Takeinventory("HasUnloadedDMR",1)
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"InsertBullets2")//30+1 effect
		TNT1 A 0 A_GiveInventory("RifleWasEmpty", 1)
		Goto InsertBullets
		
    Reload:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",31,"ReallyReady")
        TNT1 A 0 A_JumpIfInventory("NewClip",1,1)
        Goto NoAmmo
		TNT1 A 0 A_JumpIfInventory("DualWieldingDMRs", 1, "ReloadDualWield")
		TNT1 A 0 {
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_ZoomFactor(1.0);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_SetCrosshair(5);
			A_PlaySoundEx("Ironsights", "Auto");
			}
		R1F0 ABCDEF 1 A_Setroll(roll-1.2)
		TNT1 A 0 A_JumpIfInventory("HasUnloadedDMR", 1, "ReloadFromUnloaded")
		R1F0 GHIJKL 1 A_Setroll(roll+1.2)
		R1F0 MNOPQR 1 A_Setroll(roll+0.2)
		TNT1 A 0 A_PlaySoundEx("RIFCL_OU", "Auto")
		R1F0 STU 1 A_Setroll(roll-0.4) 
		TNT1 A 0 A_Jump(256, 7)
	ReloadFromUnloaded:
		TNT1 A 0 A_Takeinventory("HasUnloadedDMR",1)
		R1F0 UUUUU 1 A_Setroll(roll+1.2)
		R1F0 VWXYZ 1
		R1F1 ABCDEF 1 A_Setroll(roll+0.2)
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto")
		R1F1 GHI 1 A_Setroll(roll+0.4)
		R1F1 JKLMNO 1 A_Setroll(roll-0.4)
		R1F1 OP 1
		TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
		R1F1 QRST 1 A_Setroll(roll+1.2) 
		R1F1 UVWX 1 A_Setroll(roll-1.2)
		R1F1 YZ 1 A_Setroll(roll-0.4)
		R1F2 ABCD 1 A_Setroll(roll+0.2)
		R1F2 E 1
		TNT1 A 0 A_SetRoll(0)
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"InsertBullets2")
		TNT1 A 0 A_GiveInventory("RifleWasEmpty", 1)
		Goto InsertBullets
	ReloadDualWield:
		TNT1 A 0 {
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_ZoomFactor(1.0);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_SetCrosshair(5);
			A_PlaySoundEx("Ironsights", "Auto");
			}
		R3F0 ABCDEFGHIJKLMNOPQR 1
		TNT1 A 0 A_PlaySoundEx("RIFCL_OU", "Auto")
		R3F0 STUVWXYZ 1
		R3F1 ABCDEF 1 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto")
		R3F1 GHIJKLMNOP 1
		TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
		R3F1 QRSTUVWXYZ 1
		R3F2 ABCDE 1 
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"ReloadDualWieldNotEmpty1")
		TNT1 A 0 A_GiveInventory("RifleWasEmpty", 1)
		R3F2 FGHIJKLM 1 
		TNT1 A 0 A_PlaySoundEx("RIFCL_CK", "auto")
		R3F2 NOPQRSTU 1
	ReloadDualWieldNotEmpty1:
		R3F2 VWXYZ 1
		TNT1 A 0 A_JumpIf(CountInv("PB_DMRFireAnimation2") == 1,8)
		P2SS FEDCBA 1
		TNT1 A 0 A_JumpIfInventory("RifleWasEmpty",1,"InsertBullets3")
		Goto InsertBullets4
		TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto")
		R3F3 ABCDEFGHIJKLMNOPQR 1 
		TNT1 A 0 A_PlaySoundEx("RIFCL_OU", "Auto")
		R3F3 STUVWXYZ 1
		R3F4 ABCDEF 1 
		TNT1 A 0 A_PlaySoundEx("RIFCL_IN", "Auto")
		R3F4 GHIJKLMN 1 
		TNT1 A 0 A_PlaySoundEx("weapons/riflemagslap", "Auto")
		R3F4 OPQRSTUVWXYZ 1
		R3F5 AB 1
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"ReloadDualWieldNotEmpty2")
		R3F5 CDEFGHIJ 1 
		TNT1 A 0 A_PlaySoundEx("RIFCL_CK", "auto")
		R3F5 KLMNOPQR 1
	ReloadDualWieldNotEmpty2:
		TNT1 A 0 A_PlaySoundEx("Ironsights", "Auto")
		R3F5 STUVWXYZ 1
		R3F6 A 1
		TNT1 A 0 A_JumpIfInventory("RifleWasEmpty",1,"InsertBullets3")
		Goto InsertBullets4
		
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",30,"Ready3")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto Ready3
		TNT1 A 0 {
			A_Giveinventory("RifleAmmo",1);
			A_Takeinventory("NewCLip",1);
			}
		Goto InsertBullets
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",31,"Ready3")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto Ready3 
		TNT1 A 0 {
			A_Giveinventory("RifleAmmo",1);
			A_Takeinventory("NewCLip",1);
		}
		Goto InsertBullets2
		
	InsertBullets3:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",30,"FinishInsertBullets")
		TNT1 A 0 A_JumpIfInventory("NewCLip",2,1)
		Goto FinishInsertBullets
		TNT1 A 0 {
			if(CountInv("PB_DMRFireAnimation2")==1)
			{
				A_Takeinventory("NewCLip",1);
			}
			A_Giveinventory("RifleAmmo",1);
			A_Takeinventory("NewCLip",1);
		}
		Goto InsertBullets3
		
	InsertBullets4:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",31,"FinishInsertBullets")
		TNT1 A 0 A_JumpIfInventory("NewCLip",1,1)
		Goto FinishInsertBullets
		TNT1 A 0 {
			if(CountInv("PB_DMRFireAnimation2")==1)
			{
				A_Takeinventory("NewCLip",1);
			}
			A_Giveinventory("RifleAmmo",1);
			A_Takeinventory("NewCLip",1);
		}
		Goto InsertBullets4
	FinishInsertBullets:
		TNT1 A 0 {
			A_Takeinventory("PB_DMRFireAnimation1",1);
			A_Takeinventory("PB_DMRFireAnimation2",1);
			A_Takeinventory("RifleWasEmpty",1);
		}
		Goto Ready3
	
	
	LoadChamber:
		TNT1 A 0 {
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_Takeinventory("RifleWasEmpty",1);
			A_ZoomFactor(1.0);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_SetCrosshair(5);
			A_PlaySoundEx("Ironsights", "Auto");
			}
		R1F2 FGHIJK 1 A_Setroll(roll-0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySoundEx("RIFCL_CK", "auto")
		R1F2 LMN 1 A_Setroll(roll+0.5, SPF_INTERPOLATE)
		R1F2 OPQRST 1 A_Setroll(roll+1.1, SPF_INTERPOLATE)
		R1F2 UVWXYZ 1 A_Setroll(roll+-.1, SPF_INTERPOLATE)
		TNT1 A 0 A_SetRoll(0)
		Goto Ready3

	AlreadyUnloaded:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto ReallyReady
	
	Unload:
		TNT1 A 0 A_JumpIfInventory("HasUnloadedDMR", 1, "AlreadyUnloaded")
        TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,1)
        Goto NoAmmo
		TNT1 A 0 {
			A_Takeinventory("Zoomed",1);
			A_Takeinventory("ADSmode",1);
			A_ZoomFactor(1.0);
			A_Giveinventory("PB_LockScreenTilt",1);
			A_SetCrosshair(5);
			A_PlaySoundEx("Ironsights", "Auto");
			}
		R1F0 ABCDEF 1 A_Setroll(roll-1.2)
		R1F0 GHIJKL 1 A_Setroll(roll+1.2)
		R1F0 MNOPQR 1 A_Setroll(roll+0.2)
		TNT1 A 0 A_PlaySoundEx("RIFCL_OU", "Auto")
		R1F0 STU 1 A_Setroll(roll-0.4) 
		R1F0 VWX 1
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,1)
		Goto FinishUnload
		TNT1 A 0 
		{
			A_Takeinventory("RifleAmmo",1);
			A_Takeinventory("DoubleRifleAmmo",1);
			A_Giveinventory("NewClip",1);
		}
		Goto RemoveBullets
	
	FInishUnload:
		R1F0 YZ 1 A_Setroll(roll-0.4)
		R1F2 ABCD 1 A_Setroll(roll+0.2)
		R1F2 E 1
		TNT1 A 0 A_GiveInventory("HasUnloadedDMR", 1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto Ready3

 	Spawn:
		RIFL A -1
		Stop
			
	FlashKicking:
		TNT1 A 0 A_JumpIfInventory("DualWieldingDMRs", 1, "FlashKicking2")
		R2F0 ABCDEFGHHGFEDCBA 1
		RIFG AA 1 
		Stop
	FlashKicking2:
		P2SS ABCDEFFFFEDCBA 1
		P2SG A 1 
		Stop

	FlashAirKicking:
		TNT1 A 0 A_JumpIfInventory("DualWieldingDMRs", 1, "FlashAirKicking2")
		R2F0 ABCDEFGHHGFEDCBA 1
		RIFG AAA 1 
		Stop
	FlashAirKicking2:
		P2SS ABCDEFFFFFFEDCBA 1
		P2SG AAA 1 
		Stop
		
	
	FlashSlideKicking:
		TNT1 A 0 A_JumpIfInventory("DualWieldingDMRs",1,"FlashSlideKicking2")
		R2F0 ABCDEFGHIJKLMNHGFEDCBA 1
		Stop
	FlashSlideKicking2:
		P2SS ABCDEFFFFFFFFFFFFFFFFEDCBA 1
		Stop
		
	FlashSlideKickingStop:
		TNT1 A 0 A_JumpIfInventory("DualWieldingDMRs", 1, "FlashSlideKickingStop2")
		R2F0 FEDCBA 1 
		Stop
	FlashSlideKickingStop2:
		P2SS EDCBA 1 
		P2SG AAAAAAA 1 
		Stop
		
	FlashPunching:
		TNT1 A 0 
		Stop
	}
}



Actor Zoomed : Inventory
{
inventory.maxamount 1
}

ACTOR RifleAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 31
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 30
   Inventory.Icon "RIFLA0"
}

ACTOR MeatshieldGunAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 30
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 30
   Inventory.Icon "RIFLA0"
}

ACTOR MeatshieldPistolAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 15
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 15
   Inventory.Icon "DEGTA0"
}

ACTOR MeatshieldASGAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 12
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 12
   Inventory.Icon "AUSCA0"
}

ACTOR MeatshieldCarbineAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 40
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 40
   Inventory.Icon "4AFLA0"
}

ACTOR MeatshieldPlasmaAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 50
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 50
   Inventory.Icon "PLASA0"
}

ACTOR MeatAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 100
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 100
   Inventory.Icon "5DUNA0"
}
