Actor BaronSpawner replaces BaronOfHell
{
	//$Title Random Baron Spawner
	//$Sprite UDBGB0
	Scale 0.6 //So the editor sprite isn't massive.
	RenderStyle "Translucent"
	+LOOKALLAROUND
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("BaronPackSpawner",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
		TNT1 A 1 ACS_NamedExecuteAlways("SpawnerScript",0)
	Deciding:
		TNT1 A 1
		loop
	DiceRandom:
		TNT1 A 0 A_RadiusGive("IsPlayingChaoticRandom", 290, RGF_GIVESELF | RGF_MONSTERS | RGF_ITEMS | RGF_NOSIGHT, 1)
		Stop
	DiceMain:	// Default spawn preset with a slight difficulty curve. Tier 1-4 Monster presets are contained here to provide more variety.
		EarlyLevelMob:
			TNT1 A 0 A_Jump(24,"BelphegorPack")
			TNT1 A 0 A_Jump(32,"InfernusPack")	
			TNT1 A 0 A_Jump(64,"CyberBaronPack")
			TNT1 A 0 A_Jump(256,"BaronPack")
		LowLevelMob:
			TNT1 A 0 A_Jump(32,"BelphegorPack")
			TNT1 A 0 A_Jump(64,"InfernusPack")	
			TNT1 A 0 A_Jump(70,"CyberBaronPack")
			TNT1 A 0 A_Jump(256,"BaronPack")
		MidLevelMob:
			TNT1 A 0 A_Jump(48,"BelphegorPack")
			TNT1 A 0 A_Jump(72,"InfernusPack")
			TNT1 A 0 A_Jump(80,"CyberBaronPack")
			TNT1 A 0 A_Jump(256,"BaronPack")
		HighLevelMob:
			TNT1 A 0 A_Jump(50,"BelphegorPack")
			TNT1 A 0 A_Jump(82,"InfernusPack")
			TNT1 A 0 A_Jump(88,"CyberBaronPack")
			TNT1 A 0 A_Jump(256,"BaronPack")
	DiceProg:	// Progressive spawn preset enforces strict spawning rules by excluding monsters until they are presented in later levels.
		DiceTier1:
		DiceTier2:	
			TNT1 A 0 A_Jump(256,"BaronPack")
		DiceTier3:
			TNT1 A 0 A_Jump(80,"CyberBaronPack")
			TNT1 A 0 A_Jump(12,"InfernusPack")
			TNT1 A 0 A_Jump(256,"BaronPack")
		DiceTier4:
			TNT1 A 0 A_Jump(50,"BelphegorPack")
			TNT1 A 0 A_Jump(82,"InfernusPack")
			TNT1 A 0 A_Jump(88,"CyberBaronPack")
			TNT1 A 0 A_Jump(256,"BaronPack")
		DiceDeathWish:
			TNT1 A 0 A_Jump(54,"BelphegorPack")
			TNT1 A 0 A_Jump(86,"InfernusPack")
			TNT1 A 0 A_Jump(92,"CyberBaronPack")
			TNT1 A 0 A_Jump(256,"BaronPack")
	BelphegorPack:	// This is where the pack tokens are distributed
		TNT1 A 0 A_RadiusGive("IsBelphegor", 386, RGF_GIVESELF | RGF_MONSTERS | RGF_ITEMS | RGF_NOSIGHT, 1)
		Stop
	InfernusPack:
		TNT1 A 0 A_RadiusGive("IsInfernus", 386, RGF_GIVESELF | RGF_MONSTERS | RGF_ITEMS | RGF_NOSIGHT, 1)
		Stop
	CyberBaronPack:
		TNT1 A 0 A_RadiusGive("IsCyberBaron", 386, RGF_GIVESELF | RGF_MONSTERS | RGF_ITEMS | RGF_NOSIGHT, 1)
		Stop
	BaronPack:
		ReplaceVanilla:
		ReplaceVanilla:
		
		TNT1 A 0 A_RadiusGive("IsBaron", 386, RGF_GIVESELF | RGF_MONSTERS | RGF_ITEMS | RGF_NOSIGHT, 1)
		Stop
	}
}

Actor BaronPackSpawner : Demonpickup
{
	+LOOKALLAROUND
	States
	{
	Spawn:	// Checks for the tokens distributed above and spawns the correct monster accordingly
		TNT1 A 3
		TNT1 A 0 A_JumpIfInventory("IsPlayingChaoticRandom", 1, "RandomSpawn")
		TNT1 A 0 A_JumpIfInventory("IsInfernus", 1, "SpawnInfernus")
		TNT1 A 0 A_JumpIfInventory("IsBelphegor", 1, "SpawnBelphegor")
		TNT1 A 0 A_JumpIfInventory("IsCyberBaron", 1, "SpawnCyberBaron")
		TNT1 A 0 A_JumpIfInventory("IsBaron", 1, "SpawnBaron")
		Loop
	RandomSpawn:
		TNT1 A 0 A_Jump(256, "SpawnBaron", "SpawnCyberBaron", "SpawnBelphegor", "SpawnInfernus")
	SpawnBaron:
		ReplaceToggle:
		TNT1 A 0 A_SpawnItemEx("PB_Baron",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
		Stop
	SpawnCyberBaron:
		TNT1 A 1 ACS_NamedExecuteAlways("ToggleCyberBaron",0)
		TNT1 A 0 A_SpawnItemEx("PB_CyberBaron",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
		Stop
	SpawnBelphegor:
		TNT1 A 1 ACS_NamedExecuteAlways("ToggleBelphegor",0)
		TNT1 A 0 A_SpawnItemEx("PB_Belphegor",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
		Stop
	SpawnInfernus:
		TNT1 A 1 ACS_NamedExecuteAlways("ToggleInfernus",0)
		TNT1 A 0 A_SpawnItemEx("PB_Infernus",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS | 288,0,tid)
		Stop
	}
}